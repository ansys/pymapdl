/COM,ANSYS MEDIA REL. 150 (11/8/2013) REF. VERIF. MANUAL: REL. 150
/VERIFY,VM220
/TITLE,VM220, EDDY CURRENT LOSS IN THICK STEEL PLATE
JPGPRF,500,100,1             ! MACRO TO SET PREFS FOR JPEG PLOTS
/SHOW,JPEG
/NOPR
!
/PREP7
DD=2.5E-3		! 1/2 HEIGTH OF PLATE
HM=2644.1		! IMPOSED H FIELD
SIGMA=5.0E6		! MATERIAL CONDUCTIVITY
FF=50			! FREQUENCY (Hz.)
DX=DD/6			! MODELED PLATE WIDTH
DY=DD/6			! COIL HEIGTH
!
ET,1,PLANE53
MP,MURX,1,1.		! COIL RELATIVE PERMEABILITY
TB,BH,2,,25		! PLATE B-H CURVE
TBPT,,100,.46512
TBPT,,200,.72993
TBPT,,300,.90090
TBPT,,400,1.0204
TBPT,,500,1.1086
TBPT,,600,1.1765
TBPT,,700,1.2302
TBPT,,800,1.2739
TBPT,,900,1.3100
TBPT,,1000,1.3405
TBPT,,1400,1.4257
TBPT,,1800,1.4778
TBPT,,2200,1.5131
TBPT,,2600,1.5385
TBPT,,3000,1.5576
TBPT,,3400,1.5726
TBPT,,3800,1.5847
TBPT,,4200,1.5945
TBPT,,4600,1.6028
TBPT,,5000,1.6098
TBPT,,7000,1.6332
TBPT,,9000,1.6465
TBPT,,11000,1.6551
TBPT,,13000,1.6611
TBPT,,15000,1.6656
/NUM,1
TBPLOT			!PLOT B-H CURVE
MP,RSVX,2,1/SIGMA

RECT,0,DX,0,DD		! PLATE
RECT,0,DX, DD,DD+DY	! COIL
AGLUE,ALL


ASEL,S,LOC,Y,DD,DD+DY
AATT,1,0,1
ASEL,S,LOC,Y,0,DD
AATT,2,0,1
ASEL,ALL
AMESH,ALL
!
NSEL,S,LOC,Y,
D,ALL,AZ,0.		! FLUX-PARALLEL
NSEL,S,LOC,Y,DD+DY
CP,1,AZ,ALL		! COUPLE FOR FLUX-PARALLEL
NSEL,ALL
/PBC,ALL,1
/VIEW,1,,,1
/ANG,1
/PNUM,AREA,1
/NUM,1
APLOT
/PNUM,NODE,1
/PNUM,MAT,1
EPLOT			!PLOT ELEMENTS
FINISH

/SOLU
ANTYPE,HARMIC
ESEL,S,MAT,,1
BFE,ALL,JS,, 0.,0.,HM/DY,0. !APPLY COIL CURRENT DENSITY
ESEL,ALL
HMAGSOLV,FF,,1E-2 	! SOLVE NONLINEAR PROBLEM	
FINISH

/POST1
!
! COMPUTE LOSSES
!
SET,LAST,1,,0
ESEL,S,MAT,,2
POWERH			! EXTRACT LOSSES IN PLATE (W/m)
PAVG=PAVG*2/DX		! CONVERT TO (W/m**2)
*VWRITE,PAVG
(/'COMPUTED EDDY CURRENT LOSS:', F9.4, 'Watts/m**2')

/VIEW,1,1,1,1
/ANG,1
PLVECT,H,,,,VECT,ELEM,ON,0
*DIM,LABEL,CHAR,1
*DIM,VALUE,,1,3
LABEL(1) = 'CUR LOSS'
*VFILL,VALUE(1,1),DATA,1342.323
*VFILL,VALUE(1,2),DATA,PAVG
*VFILL,VALUE(1,3),DATA,PAVG/1342.323
/OUT,vm220,vrt
/COM
/COM,------------------- VM220 RESULTS COMPARISON ---------------------
/COM,
/COM,         |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
*VWRITE,LABEL(1),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,'   ',F10.3,'  ',F14.3,'   ',1F15.3)
/COM,
/COM,***TARGET SOLUTION BASED ON GRAPHICAL RESULTS***
/COM,------------------------------------------------------------------
/OUT

FINISH
*LIST,vm220,vrt

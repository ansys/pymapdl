/COM,ANSYS MEDIA REL. 2023R1 (11/04/2022) REF. VERIF. MANUAL: REL. 2023R1
/VERIFY,VM220   
/TITLE,VM220,ELECTROMIGRATION DIFFUSION WITH PERFECTLY BLOCKING DIFFUSION BARRIER   
/COM,   
/COM,  REFERENCE: J.J.CLEMENT AND J.R.LLOYD,"NUMERICAL INVESTIGATIONS OF
/COM,             THE ELECTROMIGATION BOUNDARY VALUE PROBLEM",J.APPL.PHYSICS
/COM,             71 (4),15 FEBRUARY 1992   
/COM,   
/COM,  USING PURE DIFFUSION ELEMENTS
/PREP7  
ET,1,PLANE238                ! 2D 8 NODE DIFFUSION ELEMENT  
ALPHA=1                      ! ELECTROMIGRATION DRIVING FORCE, 1/m
    
LENGTH=2                     ! LENGTH, m 
HEIGHT=0.2                   ! HEIGHT, m   
    
C0=1                         ! CONCENTRATION
D=1.                         ! DIFFUSIVITY, m^2/s   
    
MP,DXX,1,D  
    
RECTNG,0,-LENGTH,0,HEIGHT,  
ESIZE,HEIGHT
AMESH,1                      ! MESH AREAS WITH PLANE238 ELEMENTS
    
NSEL,S,LOC,X,-LENGTH
D,ALL,CONC,C0                ! CONCENTRATION AT X=-2
NSEL,ALL
    
BF,ALL,VELO,D*ALPHA          ! VELOCITY, m/s
FINISH  
    
/SOLUTION   
ANTYPE,TRANS                 ! TRANSIENT ANALYSIS   
DELTIM,.05,.05,.05           ! TIME INCREMENT = 0.05
KBC,1                        ! STEPPED LOADING  
IC,ALL,CONC,C0               ! INITIAL CONDITION TO SPECIFY CONCENTRATION   
TINTP,,,,0.5                 ! SPECIFYING THE 1ST ORDER TRANSIENT INTEGRATION PARAMETER
OUTRES,ALL,ALL  
TIME,50                      ! END TIME 
/OUT,SCRATCH
SOLVE   
FINISH  

/POST1
SET,LAST
PLNSOL,CONC                  ! NODAL CONCENTRATION PLOT AT TIME=50seconds
FINISH
    
/POST26 
NPRINT,10   
NSOL,3,NODE(0,0,0),CONC      ! CONCENTRATION VALUES AT NODE(0,0,0)  
PROD,2,1,,,,,,ALPHA**2*D     ! X-AXIS, NORMALIZED TIME  
/XRANGE,0,100   
/GROPT,LOGX,ON  
/GROPT,LOGY,ON  
/GROPT,DIG2,1   
/AXLAB,X,NORMALIZED TIME,TAU=ALPHA**2*D*t  
/AXLAB,Y,CONCENTRATION (C/C0)@ X=0  
XVAR,2  
/OUT,
PRVAR,3 
/SHOW,PNG,REV   
PLVAR,3 
/SHOW,CLOSE 
/OUT,SCRATCH
*GET,CONC1,VARI,3,REAL,0.01
*GET,CONC2,VARI,3,REAL,1
*GET,CONC3,VARI,3,REAL,10
*GET,CONC4,VARI,3,REAL,39.5
*DIM,LABEL,CHAR,4,1
*DIM,VALUE,,4,3
LABEL(1,1) = '0.01   ','1.00   ','10.00   ','39.50   '
*VFILL,VALUE(1,1),DATA,1.12,2.70,6.69,7.35          ! VALUES RETRIEVED FROM REFERENCE USING DIGITIZER
*VFILL,VALUE(1,2),DATA,CONC1,CONC2,CONC3,CONC4      ! MAPDL VALUES
*VFILL,VALUE(1,3),DATA,ABS(CONC1/1.11),ABS(CONC2/2.70),ABS(CONC3/6.69),ABS(7.35/CONC4)
SAVE,TABLE_1
FINISH  
/CLEAR,NOSTART  
/TITLE,VM220,ELECTROMIGRATION DIFFUSION WITH PERFECTLY BLOCKING DIFFUSION BARRIER   
/OUT,
/COM,   
/COM,  USING COUPLED ELECTRIC DIFFUSION ELEMENTS
/COM,   
/PREP7  

ET,1,PLANE223,100100    ! DIFFUSION-ELECTRIC ELEMENTS

ALPHA=1.                ! ELECTROMIGRATION DRIVING FORCE, 1/m
LENGTH=2                ! LENGTH, m
HEIGHT=0.2              ! HEIGHT, m 

D=1.                    ! DIFFUSIVITY, m^2/s
RSV=1e-7                ! RESISTIVITY, Ohm-m
T=200                   ! TEMPERATURE, deg C
Toff=273                ! TEMPERATURE OFFSET FROM ABSOLUTE ZERO, deg C

MP,DXX,1,D
MP,RSVX,1,RSV           

Ze_k=1.16045e4          ! EFFECTIVE CHARGE/BOLTZMANN CONSTANT, C-K/J

TB,MIGR,1
TBDATA,1,,,,Ze_k

EF=ALPHA*(T+273)/Ze_k   ! ELECTRIC FIELD, V/m
V0=EF*LENGTH            ! VOLTAGE , V

RECTNG,0,-LENGTH,0,HEIGHT   
ELEMSIZE=HEIGHT
ESIZE,ELEMSIZE
AMESH,1

NSEL,S,LOC,X,0
D,ALL,VOLT,0
NSEL,S,LOC,X,-LENGTH
D,ALL,CONC,1
CP,1,VOLT,ALL
ND=NDNEXT(0)
ALLSEL,ALL

D,ND,VOLT,V0

TOFFST,Toff
BF,ALL,TEMP,T
FINISH

/SOLUTION
ANTYPE,TRANS
DELTIM,.05,.05,.05
KBC,1
IC,ALL,CONC,1
OUTRES,ALL,ALL, 
TIME,50
/OUT,SCRATCH
SOLVE
FINISH

/POST1
SET,LAST
PLNSOL,CONC                  ! NODAL CONCENTRATION PLOT AT TIME=50seconds
FINISH

/POST26 
NPRINT,10   
NSOL,3,NODE(0,0,0),CONC      ! CONCENTRATION VALUES AT NODE(0,0,0)  
PROD,2,1,,,,,,ALPHA**2*D     ! X-AXIS, NORMALIZED TIME  
/XRANGE,0,100   
/GROPT,LOGX,ON  
/GROPT,LOGY,ON  
/GROPT,DIG2,1   
/AXLAB,X,NORMALIZED TIME,TAU=ALPHA**2*D*t  
/AXLAB,Y,CONCENTRATION (C/C0)@ X=0  
XVAR,2  
/OUT,   
PRVAR,3 
/OUT,SCRATCH
/SHOW,PNG,REV   
PLVAR,3 
/SHOW,CLOSE 
/OUT,SCRATCH
*GET,CONC1,VARI,3,REAL,0.01
*GET,CONC2,VARI,3,REAL,1
*GET,CONC3,VARI,3,REAL,10
*GET,CONC4,VARI,3,REAL,39.5
*DIM,LABEL,CHAR,4,1
*DIM,VALUE,,4,3
LABEL(1,1) = '0.01   ','1.00   ','10.00   ','39.50   '
*VFILL,VALUE(1,1),DATA,1.12,2.70,6.69,7.35          ! VALUES RETRIEVED FROM REFERENCE USING DIGITIZER
*VFILL,VALUE(1,2),DATA,CONC1,CONC2,CONC3,CONC4      ! MAPDL VALUES
*VFILL,VALUE(1,3),DATA,ABS(CONC1/1.11),ABS(CONC2/2.70),ABS(CONC3/6.69),ABS(7.35/CONC4)
SAVE,TABLE_2
FINISH
RESUME,TABLE_1
/COM,
/OUT,vm220,vrt
/COM,
/COM, -------------------------VM220 RESULTS COMPARISON------------------
/COM,
/COM,
/COM,                       | TARGET | Mechanical APDL | RATIO
/COM,
/COM, 
/COM,  USING PURE DIFFUSION ELEMENTS - PLANE238
/COM,
*VWRITE,LABEL(1,1),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A14,'   ',F12.2,'    ',F12.2,'    ',F12.2)
/COM,
/COM,
/NOPR
RESUME,TABLE_2
/GOPR
/COM,
/COM, USING COUPLED ELECTRIC DIFFUION ELEMENT - PLANE223
/COM,
*VWRITE,LABEL(1,1),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A14,'   ',F12.2,'    ',F12.2,'    ',F12.2)
/COM,
/COM,
/COM, --------------------------------------------------------------------
/OUT,
*list,vm220,vrt
FINISH  

! Copyright (C) 2024 ANSYS, Inc. and/or its affiliates.
! SPDX-License-Identifier: MIT
! 
!
! Permission is hereby granted, free of charge, to any person obtaining a copy
! of this software and associated documentation files (the "Software"), to deal
! in the Software without restriction, including without limitation the rights
! to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
! copies of the Software, and to permit persons to whom the Software is
! furnished to do so, subject to the following conditions:
!
! The above copyright notice and this permission notice shall be included in all
! copies or substantial portions of the Software.
!
! THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
! IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
! FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
! AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
! LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
! OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
! SOFTWARE.

/COM,ANSYS MEDIA REL. 2023R2 (05/12/2023) REF. VERIF. MANUAL: REL. 2023R2
/VERIFY,vmr049-pl5a-182
/TITLE, vmr049-pl5a-182, PRESSURISED CYLINDER PLASTICITY BENCHMARK
/COM, REFERENCE: NAFEMS REPORT: R0049. TEST PL-5
/SHOW,JPEG
JPGPRF,500,100,1

/PREP7
R = 2.5E-5
ET,1,182,,,
KEYOPT,1,3,1     
N,1,100,0,,
N,2,100,50,,
N,3,100,100,,
N,4,125,0,,
N,5,125,50,,
N,6,125,100,,
N,7,150,0,,
N,8,150,50,,
N,9,150,100,,,
N,10,175,0,,
N,11,175,50,,
N,12,175,100,,
N,13,200,0,,
N,14,200,50,,
N,15,200,100,,
E, 1,4,5,2
E, 2,5,6,3
E, 4,7,8,5
E, 5,8,9,6
E, 7,10,11,8
E, 8,11,12,9
E, 10,13,14,11
E, 11,14,15,12
MP,EX,1,21E3,		
MP,NUXY,1,0.3,
TB,PLAS,1,,,BISO
TBDATA,1,24.0,0.0		
NSEL,U,LOC,Y,50		
D,ALL,UY,0.0
NSEL,ALL,
FINISH

/SOLU
NLGEOM,ON
NSEL,S,LOC,X,100	
SF,ALL,PRES,10.0
NSEL,ALL,
NSUBST,10,,,
OUTRES,ALL,ALL
/OUT,SCRATCH
SOLVE
NSEL,S,LOC,X,100	
SF,ALL,PRES,14.0
NSEL,ALL,
NSUBST,10,,,
SOLVE
NSEL,S,LOC,X,100	
SF,ALL,PRES,16.6
NSEL, ALL,
NSUBST,10,,,
SOLVE
NSEL,S,LOC,X,100	
SF,ALL,PRES,19.1
NSEL,ALL,
NSUBST,10,,,
SOLVE

/POST26
/XRANGE,0,4
/YRANGE,-40,60
/AXLAB,X,STEP
/AXLAB,Y,STRESS
/OUT,
ESOL,2,1,1,S,X,
ESOL,3,1,1,S,Y,
ESOL,4,1,1,S,Z,
ESOL,5,1,1,S,EQV,
PLVAR,2,3,4,5
PRVAR,2,3,4,5

*DIM,VALUEX,ARRAY,4,1
*DO,JJ,1,4,1
*GET,VALUEX(JJ,1),VARI,2,RTIME,JJ
*ENDDO
R1=VALUEX(1,1)/(-8.603)
R2=VALUEX(2,1)/(-10.642)
R3=VALUEX(3,1)/(-13.319)
R4=VALUEX(4,1)/(-15.774)
*DIM,VALUEY,ARRAY,4,1
*DO,JJ,1,4,1
*GET,VALUEY(JJ,1),VARI,3,RTIME,JJ
*ENDDO
R5=VALUEY(1,1)/1.599
R6=VALUEY(2,1)/1.810
R7=VALUEY(3,1)/0.303
R8=VALUEY(4,1)/(-1.879)
*DIM,VALUEZ,ARRAY,4,1
*DO,JJ,1,4,1
*GET,VALUEZ(JJ,1),VARI,4,RTIME,JJ
*ENDDO
R9=VALUEZ(1,1)/15.706
R10=VALUEZ(2,1)/17.019
R11=VALUEZ(3,1)/14.386
R12=VALUEZ(4,1)/11.934
*DIM,VALUEEF,ARRAY,4,1
*DO,JJ,1,4,1
*GET,VALUEEF(JJ,1),VARI,5,RTIME,JJ
*ENDDO
R13=VALUEEF(1,1)/21.153
R14=VALUEEF(2,1)/24.0
R15=VALUEEF(3,1)/24.0
R16=VALUEEF(4,1)/24.0
*DIM,STEP,CHAR,10
*DIM,TARGETX,CHAR,10
*DIM,TARGETY,CHAR,10
*DIM,TARGETZ,CHAR,10
*DIM,TARGETEF,CHAR,10
*DIM,RATIOX,,4,1
*DIM,RATIOY,,4,1
*DIM,RATIOZ,,4,1
*DIM,RATIOEF,,4,1
STEP(1)='1.0','2.0','3.0','4.0'
TARGETX(1)='-8.603','-10.642','-13.319','-15.774'
TARGETY(1)='1.599','1.810','0.303','-1.879'
TARGETZ(1)='15.706','17.019','14.386','11.934'
TARGETEF(1)='21.153','24.000','24.000','24.000'
*VFILL,RATIOX,DATA,R1,R2,R3,R4
*VFILL,RATIOY,DATA,R5,R6,R7,R8
*VFILL,RATIOZ,DATA,R9,R10,R11,R12
*VFILL,RATIOEF,DATA,R13,R14,R15,R16
/COM,
/COM, ----------- vmr049-pl5a-182 RESULTS COMPARISON --------------
/COM,
/COM, vmr049-pl5a-182.jpeg RESULTS SHOULD MATCH R0049 NAFEMS MANUAL
/COM, GRAPH RESULTS ON PAGE 61 FIGURE 2.23(A).
/COM, THE RESULTS DISPLAYED ARE INCREMENTED FOR THIS PURPOSE.
/COM,
/COM,
/COM, ------ vmr049-pl5a-182 RADIAL STRESS RESULTS -------
/COM,
/COM, |  STEP  |  TARGET  |  Mechanical APDL  |  RATIO
/COM,
*VWRITE,STEP(1),TARGETX(1),VALUEX(1,1),RATIOX(1,1)
(1X,A8,'   ',1x,A8,'   ',F14.3,'   ',F12.3,'   ')
/COM,
/COM, ------ vmr049-pl5a-182 AXIAL STRESS RESULTS -------
/COM,
/COM, |  STEP  |  TARGET  |  Mechanical APDL  |  RATIO
/COM,
*VWRITE,STEP(1),TARGETY(1),VALUEY(1,1),RATIOY(1,1)
(1X,A8,'   ',1x,A8,'   ',F14.3,'   ',F12.3,'   ')
/COM,
/COM,
/COM, ------ vmr049-pl5a-182 HOOP STRESS RESULTS -------
/COM,
/COM, |  STEP  |  TARGET  |  Mechanical APDL  |  RATIO
/COM,
*VWRITE,STEP(1),TARGETZ(1),VALUEZ(1,1),RATIOZ(1,1)
(1X,A8,'   ',1x,A8,'   ',F14.3,'   ',F12.3,'   ')
/COM,
/COM,
/COM, ------ vmr049-pl5a-182 EFFECTIVE STRESS RESULTS -------
/COM,
/COM, |  STEP  |  TARGET  |  Mechanical APDL  |  RATIO
/COM,
*VWRITE,STEP(1),TARGETEF(1),VALUEEF(1,1),RATIOEF(1,1)
(1X,A8,'   ',1x,A8,'   ',F14.3,'   ',F12.3,'   ')
/COM,
/COM,
FINISH

/POST26
*DIM,LABEL1,CHAR,4
*DIM,VALUE1,,4,3
LABEL1(1) = ' SRAD ',' SAXI ',' SHOOP ',' SEFF '
*VFILL,VALUE1(1,1),DATA,VALUEX(4,1),VALUEY(4,1),VALUEZ(4,1),VALUEEF(4,1)
*VFILL,VALUE1(1,2),DATA,R4,R8,R12,R16
*DIM,LABEL2,CHAR,2
LABEL2(1) = 'vmr049-','pl5a-182'

/OUT,vmr049-pl5a-182,vrt
/COM
/COM,------------------- vmr049-pl5a RESULTS COMPARISON ---------------------
/COM,
/COM,            |   Mechanical APDL   |   RATIO  |         INPUT         |
/COM,
/COM, PLANE182
*VWRITE,LABEL1(1),VALUE1(1,1),VALUE1(1,2),LABEL2(1),LABEL2(2)
(1X,A8,'      ',F14.4,'      ',F9.4,'     ',A7,A8)
*VWRITE,LABEL1(2),VALUE1(2,1),VALUE1(2,2),LABEL2(1),LABEL2(2)
(1X,A8,'      ',F14.4,'      ',F9.4,'     ',A7,A8)
*VWRITE,LABEL1(3),VALUE1(3,1),VALUE1(3,2),LABEL2(1),LABEL2(2)
(1X,A8,'      ',F14.4,'      ',F9.4,'     ',A7,A8)
*VWRITE,LABEL1(4),VALUE1(4,1),VALUE1(4,2),LABEL2(1),LABEL2(2)
(1X,A8,'      ',F14.4,'      ',F9.4,'     ',A7,A8)
/COM,
/COM,----------------------------------------------------------------
/OUT

FINISH
*LIST,vmr049-pl5a-182,vrt

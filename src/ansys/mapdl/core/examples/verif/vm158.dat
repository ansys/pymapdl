/COM,ANSYS MEDIA REL. 2023R1 (11/04/2022) REF. VERIF. MANUAL: REL. 2023R1
/VERIFY,VM158
/PREP7   
MP,PRXY,,0.3
JPGPRF,500,100,1             ! MACRO TO SET PREFS FOR JPEG PLOTS
/SHOW,JPEG
/TITLE, VM158, MOTION OF A BOBBING BUOY
C***            ELEMENTARY THEORETICAL FLUID MECHANICS, BRENKERT, PAGE 37
ANTYPE,TRANS
NLGEOM,ON                     ! LARGE DISPLACEMENTS
ET,1,PIPE288,,,,2
SECTYPE,1,PIPE
SECDATA,1,.03                  ! DIAMETER, WALL THICKNESS
MP,EX,1,21E10
MP,DENS,1,8000
MP,PRXY,1,0.3
MP,DENS,2,1000
N,1,,,-9
N,7,,,1
FILL
E,1,2
EGEN,6,1,1
FINISH
/SOLU    
NSUBST,20                     ! 20 SUBSTEPS
CNVTOL,U                      ! CONVERGENCE BASED ON DISPLACEMENTS
CNVTOL,F                      ! CONVERGENCE BASED ON FORCES
OUTPR,BASIC,LAST
OUTRES,NSOL,1
KBC,1
ALPHAD,3                      ! MASS DAMPING FOR SLOW DYNAMICS
ACEL,,,9.807
OCTYPE,BASIC,one
OCDATA,30,2,,1                ! DEPTH, WATER DENSITY
OCTABLE,,,.3,.3,.3,0          ! DRAG COEFFICIENT
D,1,UX,,,7,,UY,ROTX,ROTY,ROTZ ! CONSTRAIN ALL BUT UZ DOF
TIME,30
/OUT,SCRATCH
SOLVE
FINISH
/POST26
NSOL,2,1,U,Z
/OUT,
PRVAR,2
/GRID,1
/AXLAB,Y,DISP
PLVAR,2                       ! DISPLAY TOP DISPLACEMENT VS. TIME
*GET,DISP,VARI,2,RTIME,30
*DIM,LABEL,CHAR,1,2
*DIM,VALUE,,1,3
LABEL(1,1) = 'DISP '
LABEL(1,2) = 'm '
*VFILL,VALUE(1,1),DATA,-.312
*VFILL,VALUE(1,2),DATA,DISP
*VFILL,VALUE(1,3),DATA,ABS(DISP/.312)
/COM
/OUT,vm158,vrt
/COM,------------------- VM158 RESULTS COMPARISON -------------
/COM,
/COM,              |    TARGET   |    ANSYS   |    RATIO
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.3,'  ',F10.3,'   ',1F6.3)
/COM,----------------------------------------------------------
/OUT
FINISH
*LIST,vm158,vrt

/COM,ANSYS MEDIA REL. 2023R1 (11/04/2022) REF. VERIF. MANUAL: REL. 2023R1
/VERIFY,VM68

/PREP7
/TITLE, VM68, PSD RESPONSE OF TWO DOF SPRING-MASS SYSTEM
!     "VIBRATION ANALYSIS" R.K. VIERK ,2ND EDITION ,(CHAPTER 7)
ET,1,COMBIN40            ! DISPLACEMENT ALONG X AXIS, MASS AT NODE I
R,1,42832.,,0.50  
R,2,32416.,,1.0   
MP,EX,1,1                ! NOT USED, DUMMY PROPERTY
N,1                      ! DEFINE MODEL
N,2,1  
N,3,2
E,2,1
REAL,2   
E,3,2
D,1,UX,0.0               ! CONSTRAINT THE BASE
OUTPR,ALL,ALL
FINISH

/SOLU    
ANTYPE,MODAL             ! PERFORM A MODAL ANALYSIS
MODOPT,LANB,2            ! LANB EXTRACTION METHOD
                         ! EXTRACT 2 MODES FROM ENTIRE FREQUENCY RANGE
MXPAND,2,,,YES           ! EXPAND 2 MODES, DO ELEMENT STRESS CALCULATIONS
/OUT,vm68_SCRATCH
SOLVE
/OUT 
*GET,FREQ1,MODE,1,FREQ
*GET,FREQ2,MODE,2,FREQ
FINISH

/SOLU    
ANTYPE,SPECTR            ! PERFORM SPECTRUM ANALYSIS
SPOPT,PSD,2,ON           ! USE FIRST 2 MODES FROM MODAL ANALYSIS

PSDUNIT,1,ACCG           ! USE G**2/HZ FOR PSD AND DIMENSION IN INCHES
D,1,UX,1.0               ! APPLY SPECTRUM AT THE SUPPORT POINT
PSDFRQ,1,1,10.0,100.0    ! FREQUENCY RANGE OF 10 TO 100 HERTZ
PSDVAL,1,.1,.1           ! WHITE NOISE PSD, VALUES IN  G**2/HZ
/OUT,vm68_SCRATCH,,,APPEND
PFACT,1,BASE             ! BASE EXCITATION
/OUT

DMPRAT,0.02              ! 2% DAMPING
PSDCOM                   ! COMBINE MODES FOR PSD, USE DEFAULT SIGNIFICANCE LEVEL

PSDRES,ACEL,REL          ! CALCULATE RELATIVE ACCELERATION SOLUTIONS
/OUT,vm68_SCRATCH,,,APPEND
SOLVE
/OUT 
FINISH

/POST1
LCDEF,6,5,1              ! DEFINE LOAD STEP AND SUBSTEP FOR LOAD FACTOR OPERATION
LCFACT,ALL,(1/386.4)     ! CONVERT ACCEL. RESULT TO G
LCASE,6
PRNSOL,U,COMP            ! PRINT NODAL SOLUTION RESULTS
*GET,M1STD,NODE,2,U,X
*GET,M2STD,NODE,3,U,X
*DIM,LABEL,CHAR,4,2
*DIM,VALUE,,4,3
LABEL(1,1) = 'f1,     ','f2,     ','MASS1,1S','MASS2,1S'
LABEL(1,2) = '      Hz','      Hz','IG.STDEV','IG.STDEV'
*VFILL,VALUE(1,1),DATA,20.57,64.88,9.059,10.63
*VFILL,VALUE(1,2),DATA,FREQ1,FREQ2,M1STD,M2STD
*VFILL,VALUE(1,3),DATA,ABS(FREQ1/20.57),ABS(FREQ2/64.88),ABS(M1STD/9.059),ABS(M2STD/10.63)
/COM
/OUT,vm68,vrt
/COM,------------------- VM68 RESULTS COMPARISON --------------
/COM,
/COM,                 |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.3,'  ',F14.3,'   ',1F15.3)
/COM,----------------------------------------------------------
/OUT
FINISH
*LIST,vm68,vrt

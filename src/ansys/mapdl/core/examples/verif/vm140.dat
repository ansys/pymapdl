/COM,ANSYS MEDIA REL. 2023R1 (11/04/2022) REF. VERIF. MANUAL: REL. 2023R1
/VERIFY,VM140
/PREP7
/TITLE, VM140, STRETCHING, TWISTING AND BENDING OF A LONG SOLID SHAFT
C***    STR. OF MATL., TIMOSHENKO, PART 1, 3RD ED., PAGE 296
ET,1,PLANE83,,,,2,,1
MP,EX,1,30E6
MP,NUXY,1,0                ! ZERO POISSONS RATIO
N,1
N,25,,24
FILL
NGEN,3,25,1,25,1,.25
E,51,53,3,1,52,28,2,26
EGEN,12,2,1
MODE,0,1                   ! AXIAL + TORSION MODE
D,1,ALL,,,51,25            ! FIX SUPPORT
D,2,UZ,,,25                ! CENTERLINE CONSTRAINTS AGAINST TORSION
F,75,FY,100                ! APPLY AXIAL FORCE
F,75,FZ,400                ! APPLY TORSION
FINISH
/SOLU         
OUTPR,BASIC,LAST           ! PRINTOUT SOLUTION
/OUT,SCRATCH
SOLVE
MODE,1,1                   ! BENDING MODE
DDELE,2,UZ,25              ! DELETE PREVIOUS UZ CONSTRAINTS
FDEL,75,ALL                ! DELETE PREVIOUS FORCES
D,2,UY,,,25                ! CENTERLINE CONSTRAINTS AGAINST BENDING
F,75,FX,-50                ! APPLY VERTICAL FORCE
SOLVE
FINISH
/POST1
/OUT,
SET,1,1,,,,0.0             ! GET LOAD STEP 1 AT 0.0 DEGREES
LCWRITE,1                  ! WRITE OUT AS LOAD CASE 1
SET,2,1,,,,0.0             ! GET LOAD STEP 2 AT 0.0 DEGREES
LCOPER,ADD,1               ! ADD LOAD CASE 1 TO LOAD CASE 2
ESEL,S,ELEM,,1             ! SELECT ELEMENT 1
NSLE
PRNSOL,S,COMP
PRNSOL,S,PRIN              ! PRINT PRINCIPLE STRESSES
*GET,TOR,NODE,51,S,YZ
*GET,AXBND,NODE,51,S,Y
*GET,COMB,NODE,51,S,1
*DIM,LABEL,CHAR,3,2
*DIM,VALUE,,3,3
LABEL(1,1) = 'TORSION ','AXIAL+BEND ','COMBINED '
LABEL(1,2) = ' psi',' psi',' psi'
*VFILL,VALUE(1,1),DATA,1018.6,6238.9,6401
*VFILL,VALUE(1,2),DATA,TOR,AXBND,COMB
*VFILL,VALUE(1,3),DATA,ABS(TOR/1018.6),ABS(AXBND/6238.9),ABS(COMB/6401)
/COM
/OUT,vm140,vrt
/COM,------------------- VM140 RESULTS COMPARISON -------------
/COM,
/COM,   LOAD STEP 3   |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.1,'  ',F12.1,'   ',1F15.3)
/COM,-----------------------------------------------------------
*status,parm
/OUT
FINISH
*LIST,vm140,vrt


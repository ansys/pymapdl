! Copyright (C) 2024 ANSYS, Inc. and/or its affiliates.
! SPDX-License-Identifier: MIT
! 
!
! Permission is hereby granted, free of charge, to any person obtaining a copy
! of this software and associated documentation files (the "Software"), to deal
! in the Software without restriction, including without limitation the rights
! to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
! copies of the Software, and to permit persons to whom the Software is
! furnished to do so, subject to the following conditions:
!
! The above copyright notice and this permission notice shall be included in all
! copies or substantial portions of the Software.
!
! THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
! IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
! FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
! AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
! LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
! OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
! SOFTWARE.

/COM,ANSYS MEDIA REL. 2023R2 (05/12/2023) REF. VERIF. MANUAL: REL. 2023R2
/VERIFY,VM128
/PREP7
MP,PRXY,,0.3
/TITLE, VM128, BUCKLING OF A BAR WITH HINGED SOLVES (AREA ELEMENTS)
C***    STR. OF MATL., TIMOSHENKO, PART 2, 3RD ED., PAGE 148
ET,1,PLANE182,2,,3               ! 2-D SOLID
R,1,.5                           ! THICKNESS (SQUARE CROSS-SECTION)
MP,EX,1,30E6
MP,PRXY,1,0.3
N,1
N,11,,100
FILL
NGEN,2,11,1,11,,.5
E,12,13,2,1
EGEN,10,1,1
FINISH
/SOLU 
ANTYPE,STATIC   
RESCONTROL,LINEAR,ALL,1         ! RESTART FILES FOR SUBSEQUENT LINEAR PERTURBATION 
D,1,ALL,,,12,11                 ! FIX SYMMETRY SOLVE
F,11,FY,-.5,,22,11              ! UNIT LOAD AT FREE END DIVIDED BETWEEN NODES
OUTPR,,1
/OUT,SCRATCH
SOLVE
FINISH
/SOLU 
ANTYPE,STATIC,RESTART,,,PERTURB ! PERFORM A PERTURBATION ANALYSIS
PERTURB,BUCKLE,,,ALLKEEP        ! PERTURBED BUCKLING SOLVE 
SOLVE,ELFORM                    ! REFORM ELEMENT MATRICES
              
BUCOPT,SUBSPACE,1               ! USE SUBSPACE EXTRACTION METHOD, EXTRACT 1 MODE
MXPAND,1                        ! EXPAND 1 MODE SHAPE
SOLVE
/OUT,
*GET,FCR,MODE,1,FREQ
*STATUS,PARM
*DIM,LABEL,CHAR,1,2
*DIM,VALUE,,1,3
LABEL(1,1) = 'Fcr '
LABEL(1,2) = 'lb '
*VFILL,VALUE(1,1),DATA,38.553
*VFILL,VALUE(1,2),DATA,FCR
*VFILL,VALUE(1,3),DATA,ABS(FCR/38.553)
/OUT,vm128,vrt
/COM,--------------------- vm128 RESULTS COMPARSION ---------------
/COM,
/COM, BLOCK LANCZOS SOLUTION
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.3,'  ',F10.3,'   ',1F5.3)
/COM,
/COM,
/COM,---------------------------------------------------------------
/OUT
FINISH
*LIST,vm128,vrt

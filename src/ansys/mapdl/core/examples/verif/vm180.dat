/COM,ANSYS MEDIA REL. 150 (11/8/2013) REF. VERIF. MANUAL: REL. 150
/VERIFY,VM180
JPGPRF,500,100,1             ! MACRO TO SET PREFS FOR JPEG PLOTS
/SHOW,JPEG
/PREP7
smrt,off
/TITLE, VM180, BENDING OF A CURVED BEAM
C***    THEORY OF ELASTICITY, TIMOSHENKO & GOODIER, 3RD ED., P. 78
ANTYPE,STATIC        ! STATIC ANALYSIS
ET,1,PLANE183
ET,2,BEAM188
KEYOPT,2,3,3
SECT,2,BEAM,ASEC
SECD,1,1,,1,,1E-6
MP,EX,1,30E6
MP,NUXY,1,0
CSYS,1
K,1,3.5
K,2,3.5,90
KGEN,2,1,2,1,1.0
L,2,4                ! CREATE STIFFENING BEAM ELEMENTS
LESIZE,1,,,4
TYPE,2
SECN,2
LMESH,1
L,1,2                ! CREATE CURVED BEAM
LESIZE,2,,,25
L,3,4
LESIZE,3,,,25
ESIZE,,4
A,1,2,4,3
AATT,1,1,1
MSHAPE,1,2D
MSHKEY,0
AMESH,1
NROTAT,ALL
ESEL,S,TYPE,,2
NSLE
D,ALL,UZ,,,,,ROTX,ROTY
ALLS
DK,1,ALL,,,1         ! DEFINE KEYPOINT CONSTRAINTS AND FORCES
DK,3,UY,,,1          ! IN GLOBAL CYLINDRICAL COORDINATE SYSTEM
FK,2,FY,100
FK,4,FY,-100
FINISH
/SOLU    
SOLVE
FINISH
/POST1
RSYS,1
NSEL,R,LOC,X,3.5     ! SELECT INNER RADIUS NODES
NSEL,U,LOC,Y,90
PRNSOL,S,COMP
NSORT,S,Y
*GET,SI,SORT,,MAX
NSEL,ALL
NSEL,R,LOC,X,4.5     ! SELECT OUTER RADIUS NODES
NSEL,U,LOC,Y,90
PRNSOL,S,COMP
NSORT,S,Y
*GET,SO,SORT,,MIN
*DIM,LABEL,CHAR,2,2
*DIM,VALUE,,2,3
LABEL(1,1) = 'INR STR ','OTR STR '
LABEL(1,2) = 'psi ','psi '
*VFILL,VALUE(1,1),DATA,655,-555
*VFILL,VALUE(1,2),DATA,SI,SO
*VFILL,VALUE(1,3),DATA,ABS(SI/655),ABS(SO/555)
/COM
/OUT,vm180,vrt
/COM,------------------- VM180 RESULTS COMPARISON -------------
/COM,
/COM,                 |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.1,'  ',F14.1,'   ',1F15.3)
/COM,----------------------------------------------------------
/OUT
FINISH
*LIST,vm180,vrt

! Copyright (C) 2024 ANSYS, Inc. and/or its affiliates.
! SPDX-License-Identifier: MIT
! 
!
! Permission is hereby granted, free of charge, to any person obtaining a copy
! of this software and associated documentation files (the "Software"), to deal
! in the Software without restriction, including without limitation the rights
! to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
! copies of the Software, and to permit persons to whom the Software is
! furnished to do so, subject to the following conditions:
!
! The above copyright notice and this permission notice shall be included in all
! copies or substantial portions of the Software.
!
! THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
! IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
! FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
! AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
! LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
! OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
! SOFTWARE.

/COM,ANSYS MEDIA REL. 2023R2 (05/12/2023) REF. VERIF. MANUAL: REL. 2023R2
/verify,vm-nr1677-2-1a-a
/title,vm-nr1677-2-1a-a,NRC piping benchmarks problems,Volume II,Problem 1a

/com, ***************************************************************************
/com, Reference: Piping benchmark problems,Dynamic analysis independant support
/com,            motion response spectrum method, P. Bezler, M. Subudhi and
/com,            M.Hartzman, NUREG/CR--1677-Vol.2, August 1985.
/com, 
/com, 
/com,  Elements used: Pipe16, Pipe18, Combin14
/com,
/com, Results comparsion:
/com, The following results are compared against NRC piping benchmark values
/com, 1. Frequencies obtained from modal solution.
/com, 2. Maximum nodal displacements and rotations obtained from spectrum solution.
/com, 3. Element forces/moments obtained from spectrum solution.
/com, 4. Reaction forces obtained from spectrum solution.
/com,
/com, *******************************************************************************

/out,scratch

/PREP7
YoungModulus1 = .258e+8                         ! Young's Modulus
Nu = 0.3                                        ! Poissons ratio
ShearModulus1 = YoungModulus1/(2*(1+Nu))        ! Shear Modulus
WMass=1.042868e-03                              ! Density
WTick=0.216                                     ! Wall Thickness
OD=3.5                                          ! Outer Diameter
RADCUR=48.003                                   ! Radius curvature
temp=60                                         ! Temperature                                             
maxm=15                                         ! Number of modes to extract


et,1,pipe16                                     ! Straight pipe elements
et,2,pipe18                                     ! Curved pipe elements
et,3,combin14	                              ! Spring-damper elements
keyopt,3,2,1                                    ! UX Degree Of Freedom      
et,4,combin14,                                  ! Spring-damper elements
keyopt,4,2,2                                    ! UY Degree Of Freedom
et,5,combin14	                              ! Spring-damper elements
keyopt,5,2,3                                    ! UZ Degree Of Freedom
et,6,combin14                                   ! Spring-damper elements
keyopt,6,2,1                                    ! UX Degree Of Freedom
et,7,combin14                                   ! Spring-damper elements
keyopt,7,2,2                                    ! UY Degree Of Freedom


/com,
/com, Real Constants
/com,****************

r,1,OD,WTick
r,2,OD,WTick,RADCUR
r,3,0.2e+8                                      ! Stiffness
r,4,0.2e+8                                      ! Stiffness
r,5,0.2e+8                                      ! Stiffness
r,6,0.2e+5                                      ! Stiffness
r,7,0.2e+5                                      ! Stiffness


/com,----------------------------------------------------------------------------

/com,
/com, Material Properties
/com,*********************

mp,ex,1,YoungModulus1					
mp,nuxy,1,Nu						
mp,gxy,1,ShearModulus1					
mp,dens,1,WMass						

mp,ex,2,YoungModulus1					
mp,nuxy,2,Nu						
mp,gxy,2,ShearModulus1					
mp,dens,2,WMass						

/com,----------------------------------------------------------------------------

/com, Nodes
/com,********

n,1,0,0,0							
n,2,0,12,0
n,3,35.687,60,32.110
n,4,55,60,49.5
n,5,74.329,60,66.882
n,6,110,12,99
n,7,110,0,99
n,8,110,-24,99,
n,9,110,-48,99,
n,10,110,-72,99
n,11,110,-96,99
n,12,110,-120,99
n,13,110,-144,99
n,14,110,-168,99
n,15,110,-198,99
n,16,110,-228,99
n,17,110,-252,99
n,18,110,-276,99
n,19,110,-300,99
n,20,110,-324,99
n,21,99.6,-349.4,99
n,22,89.2,-374.8,99
n,23,78.8,-400,99
n,24,68.4,-425.6,99
n,25,58,-451,99
n,26,58,-475,99
n,27,58,-487,99
n,28,103.537,-535,114.179
n,29,124.269,-535,121.1
n,30,145,-535,128
n,31,184.975,-535,123.615
n,32,214.8,-536,102.8
n,33,254.585,-535,81.849
n,34,279.312,-535,75
n,35,331,-535,75
n,36,383,-535,75

/com,
/com, Elastic support Nodes 
/com,***********************

n,37,10,0,0
n,38,0,10,0
n,39,0,0,10
n,40,55,70,49.5
n,41,110,0,109
n,42,120,0,99
n,43,110,-168,109
n,44,120,-168,109
n,45,110,-324,109
n,46,120,-324,99
n,47,58,-475,109
n,48,68,-475,99
n,49,103.537,-545,114.179
n,50,103.537,-535,104.179
n,51,393,-535,75
n,52,383,-545,75
n,53,383,-535,85

/com,----------------------------------------------------------------------------

/com,
/com, Straight Pipe (Tangent) Elements
/com,**********************************

mat,1								! Material ID 1
type,1							! Element Type 1
real,1							! Real Constant 1

e, 1, 2							
e, 3, 4
e, 4, 5
e, 6, 7
e, 7, 8
e, 8, 9
e, 9,10
e,10,11
e,11,12
e,12,13
e,13,14
e,14,15
e,15,16
e,16,17
e,17,18
e,18,19
e,19,20
e,20,21
e,21,22
e,22,23
e,23,24
e,24,25
e,25,26
e,26,27
e,28,29
e,29,30
e,31,32
e,32,33
e,34,35
e,35,36

/com,
/com, Pipe Bend Elements
/com,********************

mat,2
type,2
real,2

e,2,3,4
e,5,6,4
e,27,28,26
e,30,31,29
e,33,34,32


/com, Elastic supports and anchors
/com,******************************

/com, rotate nodes with less than 3 supports
/com,

wplane,,nx(4),ny(4),nz(4),nx(40),ny(40),nz(40),nx(3),ny(3),nz(3)
cswplane,11,0
nrotat,4
nrotat,40
csys,0

wplane,,nx(7),ny(7),nz(7),nx(41),ny(41),nz(41),nx(42),ny(42),nz(42)
cswplane,12,0
nrotat,7
nrotat,41,42
csys,0

wplane,,nx(14),ny(14),nz(14),nx(43),ny(43),nz(43),nx(44),ny(44),nz(44)
cswplane,13,0
nrotat,14
nrotat,43,44
csys,0

wplane,,nx(20),ny(20),nz(20),nx(45),ny(45),nz(45),nx(46),ny(46),nz(46)
cswplane,14,0
nrotat,20
nrotat,45,46
csys,0

wplane,,nx(26),ny(26),nz(26),nx(47),ny(47),nz(47),nx(48),ny(48),nz(48)
cswplane,15,0
nrotat,26
nrotat,47,48
csys,0

wplane,,nx(28),ny(28),nz(28),nx(49),ny(49),nz(49),nx(50),ny(50),nz(50)
cswplane,16,0
nrotat,28
nrotat,49,50
csys,0

type,3
real,3
e,1,37
e,36,51
e,4,40
e,7,41
e,26,47
e,28,49

type,4
real,4
e,1,38
e,36,52
e,7,42
e,26,48
e,28,50

type,5
real,5
e,1,39
e,36,53

type,6
real,6
e,14,43
e,20,45

type,7
real,7
e,14,44
e,20,46

/com,----------------------------------------------------------------------------

/com,
/com, Constraints
/com,*************

nsel,,node,,37,53
d,all,all
allsel

d,1,rotx,,,,,roty,rotz
d,36,rotx,,,,,roty,rotz
allsel,all
fini

/com,----------------------------------------------------------------------------

/com
/com,==============
/com, Modal solve
/com,==============
/com,

/solution
antype,modal							! Perform Modal Analysis
modopt,lanb,maxm							
lumpm,on								! Use Lumped Mass Approximation
mxpand,maxm,,,yes							! Expand solution with Element Calculations ON
solve									

/com,========================
/com, Frequency Comparison
/com,========================

*dim,Amode,ARRAY,maxm
*dim,Emode,ARRAY,maxm
*dim,ERmode,ARRAY,maxm
*dim,moden,ARRAY,maxm

*do,i,1,maxm
	*GET, Amode(i), MODE, i, FREQ
*enddo
*VFILL,Emode,DATA,6.042,6.256,7.76,8.943,12.444,12.83,14.303,15.486,16.371,18.543
*VFILL,Emode(11),DATA,19.499,23.243,24.105,32.636,33.837

*do,i,1,maxm
		ERmode(i)=ABS((Amode(i))/(Emode(i)))
		moden(i)=i
*enddo

save,table_1

finish

/com,----------------------------------------------------------------------------

/com,
/com,==================
/com, Spectrum solve
/com,==================
/com,

/solution
antype,spectrum							! Perform Spectrum Analysis
spopt,sprs,15							! Single Point Excitation Response Spectrum
srss,0.0								! SRSS mode combination

gval = 386.4

svtyp, 2, gval
freq, 2.5  , 5.0  ,  8.0,  12.35,  13.51,  16.95,  20.83,  23.25,  29.41
sv,,  2.275, 2.275,  1.0,    0.8,  0.925,  0.925,   0.8 ,   1.0 ,    1.0
freq,  34.48
sv,,   0.875
sed,1,0,0								! Excitation in X direction
SOLVE

svtyp, 2, gval
freq
freq, 2.5  , 5.0  ,  8.0,  12.35,  13.51,  16.95,  20.83,  23.25,  29.41
sv,, 1.517, 1.517, 0.667,  0.534,  0.617,  0.617,   0.534, 0.667,  0.667
freq,  34.48
sv,,   0.584
sed,0,1,0								! Excitation in Y direction
SOLVE
fini

/com,----------------------------------------------------------------------------

/post1
/input,,mcom

/com,----------------------------------------------------------------------------

/com,	Labels
/com,********

*dim,label2,char,1,6
*dim,label3,char,6,1
*dim,label4,char,6,1
*dim,label5,char,5,1

/com,-------------------------

label2(1,1) = 'ux_5'
label2(1,2) = 'uy_33'
label2(1,3) = 'uz_15'
label2(1,4) = 'rotx_32'
label2(1,5) = 'roty_32'
label2(1,6) = 'rotz_5'

/com,-------------------------

label3(1,1)='PX(I)'
label3(2,1)='VY(I)'
label3(3,1)='VZ(I)'
label3(4,1)='TX(I)'
label3(5,1)='MY(I)'
label3(6,1)='MZ(I)'

/com,-------------------------

label4(1,1)='PX(J)'
label4(2,1)='VY(J)'
label4(3,1)='VZ(J)'
label4(4,1)='TX(J)'
label4(5,1)='MY(J)'
label4(6,1)='MZ(J)'

/com,-------------------------

label5(1,1)='38'
label5(2,1)='40'
label5(3,1)='46'
label5(4,1)='50'
label5(5,1)='53'


/com,----------------------------------------------------------------------------


/com, *========================================================
/com, * Maximum nodal displacements and rotations comparison
/com, *==========================================================

/com, Solution obtained from Mechanical APDL
/com, ****************************

*GET,AdisX,NODE,5,U,X
*GET,AdisY,NODE,33,U,Y
*GET,AdisZ,NODE,15,U,Z
*GET,ArotX,NODE,32,ROT,X
*GET,ArotY,NODE,32,ROT,Y
*GET,ArotZ,NODE,5,ROT,Z

/com, Expected results from NRC manual
/com, *********************************

*SET,EdisX,5.86080e-02
*SET,EdisY,1.12744e-01
*SET,EdisZ,1.02848e-02
*SET,ErotX,1.46853e-03
*SET,ErotY,1.09722e-03
*SET,ErotZ,1.27548e-03

/com, Error computation
/com, *********************

ERdisX=ABS((AdisX)/(EdisX))
ERdisY=ABS((AdisY)/(EdisY))
ERdisZ=ABS((AdisZ)/(EdisZ))
ERrotX=ABS((ArotX)/(ErotX))
ERrotY=ABS((ArotY)/(ErotY))
ERrotZ=ABS((ArotZ)/(ErotZ))

*dim,value,,6,3

*vfill,value(1,1),data,EdisX
*vfill,value(1,2),data,AdisX
*vfill,value(1,3),data,ERdisX

*vfill,value(2,1),data,EdisY
*vfill,value(2,2),data,AdisY
*vfill,value(2,3),data,ERdisY

*vfill,value(3,1),data,EdisZ
*vfill,value(3,2),data,AdisZ
*vfill,value(3,3),data,ERdisZ

*vfill,value(4,1),data,ErotX
*vfill,value(4,2),data,ArotX
*vfill,value(4,3),data,ERrotX

*vfill,value(5,1),data,ErotY
*vfill,value(5,2),data,ArotY
*vfill,value(5,3),data,ERrotY

*vfill,value(6,1),data,ErotZ
*vfill,value(6,2),data,ArotZ
*vfill,value(6,3),data,ERrotZ

save,table_2

/com,-----------------------------------------------------------

/com, *========================================================
/com, * Element Forces and Moments Comparison
/com, *==========================================================

/com, Solution obtained from Mechanical APDL
/com, ****************************

*dim,elem_res_I,,2,6
*dim,elem_res_J,,2,6

*dim,pxi,,2
*dim,vyi,,2
*dim,vzi,,2
*dim,txi,,2
*dim,myi,,2
*dim,mzi,,2

*dim,pxj,,2
*dim,vyj,,2
*dim,vzj,,2
*dim,txj,,2
*dim,myj,,2
*dim,mzj,,2

esel,s,ename,,16
esel,a,ename,,18

/com,==========
/com,	 Node I
/com,==========

/com, Element #30 
/com,************************

*get,pxi(1,1),elem,30,smisc,1
*get,vyi(1,1),elem,30,smisc,2
*get,vzi(1,1),elem,30,smisc,3
*get,txi(1,1),elem,30,smisc,4
*get,myi(1,1),elem,30,smisc,5
*get,mzi(1,1),elem,30,smisc,6

*vfill,elem_res_I(1,1),data,pxi(1,1)
*vfill,elem_res_I(1,2),data,vyi(1,1)
*vfill,elem_res_I(1,3),data,vzi(1,1)
*vfill,elem_res_I(1,4),data,txi(1,1)
*vfill,elem_res_I(1,5),data,myi(1,1)
*vfill,elem_res_I(1,6),data,mzi(1,1)

/com, Element #33 
/com,*************************

*get,pxi(2,1),elem,33,smisc,1
*get,vyi(2,1),elem,33,smisc,2
*get,vzi(2,1),elem,33,smisc,3
*get,txi(2,1),elem,33,smisc,4
*get,myi(2,1),elem,33,smisc,5
*get,mzi(2,1),elem,33,smisc,6

*vfill,elem_res_I(2,1),data,pxi(2,1)
*vfill,elem_res_I(2,2),data,vyi(2,1)
*vfill,elem_res_I(2,3),data,vzi(2,1)
*vfill,elem_res_I(2,4),data,txi(2,1)
*vfill,elem_res_I(2,5),data,myi(2,1)
*vfill,elem_res_I(2,6),data,mzi(2,1)


/com,==========
/com,  Node J
/com,==========

/com, Element #30 
/com,************************

*get,pxj(1,1),elem,30,smisc,7
*get,vyj(1,1),elem,30,smisc,8
*get,vzj(1,1),elem,30,smisc,9
*get,txj(1,1),elem,30,smisc,10
*get,myj(1,1),elem,30,smisc,11
*get,mzj(1,1),elem,30,smisc,12

*vfill,elem_res_J(1,1),data,pxj(1,1)
*vfill,elem_res_J(1,2),data,vyj(1,1)
*vfill,elem_res_J(1,3),data,vzj(1,1)
*vfill,elem_res_J(1,4),data,txj(1,1)
*vfill,elem_res_J(1,5),data,myj(1,1)
*vfill,elem_res_J(1,6),data,mzj(1,1)

/com, Element #33 
/com,************************

*get,pxj(2,1),elem,33,smisc,7
*get,vyj(2,1),elem,33,smisc,8
*get,vzj(2,1),elem,33,smisc,9
*get,txj(2,1),elem,33,smisc,10
*get,myj(2,1),elem,33,smisc,11
*get,mzj(2,1),elem,33,smisc,12

*vfill,elem_res_J(2,1),data,pxj(2,1)
*vfill,elem_res_J(2,2),data,vyj(2,1)
*vfill,elem_res_J(2,3),data,vzj(2,1)
*vfill,elem_res_J(2,4),data,txj(2,1)
*vfill,elem_res_J(2,5),data,myj(2,1)
*vfill,elem_res_J(2,6),data,mzj(2,1)

allsel,all

/com,----------------------------------------------------------------------------

/com, Results from NRC benchmarks
/com, ***************************

*dim,exp_I,,2,6
*dim,exp_J,,2,6

/com, Element #35
/com,*************

*vfill,exp_I(1,1),data,1.199e+02
*vfill,exp_I(1,2),data,5.663e+01
*vfill,exp_I(1,3),data,5.595e+01
*vfill,exp_I(1,4),data,5.958e+02
*vfill,exp_I(1,5),data,6.750e+02
*vfill,exp_I(1,6),data,6.062e+02

*vfill,exp_J(1,1),data,1.199e+02
*vfill,exp_J(1,2),data,5.663e+01
*vfill,exp_J(1,3),data,5.595e+01
*vfill,exp_J(1,4),data,5.958e+02
*vfill,exp_J(1,5),data,2.685e+03
*vfill,exp_J(1,6),data,3.329e+03

/com, Element #27
/com,*************

*vfill,exp_I(2,1),data,1.837e+02
*vfill,exp_I(2,2),data,2.674e+01
*vfill,exp_I(2,3),data,1.204e+02
*vfill,exp_I(2,4),data,2.658e+02
*vfill,exp_I(2,5),data,1.308e+03
*vfill,exp_I(2,6),data,3.982e+02

*vfill,exp_J(2,1),data,1.204e+02
*vfill,exp_J(2,2),data,2.674e+01
*vfill,exp_J(2,3),data,1.837e+02
*vfill,exp_J(2,4),data,1.123e+03
*vfill,exp_J(2,5),data,3.095e+03
*vfill,exp_J(2,6),data,1.496e+03

/com,---------------------------------------------------------------------------

/com, Error computation
/com, *********************

*dim,elem_error_I,,2,6
*dim,elem_error_J,,2,6
*dim,elem_tab,,24,3

/com,============
/com,   Node I
/com,============

*do,i,1,2
	*do,j,1,6
		*vfill,elem_error_I(i,j),data,abs(elem_res_I(i,j)/exp_I(i,j))
	*enddo
*enddo

/com,============
/com,   Node J
/com,============

*do,i,1,2
	*do,j,1,6
		*vfill,elem_error_J(i,j),data,abs(elem_res_J(i,j)/exp_J(i,j))
	*enddo
*enddo

/com,--------------------------------------------------------------------------

*do,i,1,2
	cs=(i-1)*6
	*do,j,1,6
		n=cs+j
		*vfill,elem_tab(n,1),data,exp_I(i,j)
		*vfill,elem_tab(n,2),data,elem_res_I(i,j)
		*vfill,elem_tab(n,3),data,elem_error_I(i,j)
	*enddo

	*do,j,1,6
		m=cs+j+12
		*vfill,elem_tab(m,1),data,exp_J(i,j)
		*vfill,elem_tab(m,2),data,elem_res_J(i,j)
		*vfill,elem_tab(m,3),data,elem_error_J(i,j)
	*enddo
*enddo

save,table_3

/com,------------------------------------------------------------------------------------


/com, *****************************
/com,  Reaction forces comparision
/com, ******************************

*dim,rf_tab,,5,3

/com, Solution obtained from Mechanical APDL
/com, ****************************

*GET,RFA38,NODE,38,RF,FY
*GET,RFA40,NODE,40,RF,FX
*GET,RFA46,NODE,46,RF,FY
*GET,RFA50,NODE,50,RF,FY
*GET,RFA53,NODE,53,RF,FZ

/com, Expected results from NRC manual
/com, *********************************

*SET,RFE38,107
*SET,RFE40,234
*SET,RFE46,78
*SET,RFE50,89
*SET,RFE53,56

/com, Error computation
/com, *********************

ER38=ABS(RFA38/RFE38)
ER40=ABS(RFA40/RFE40)
ER46=ABS(RFA46/RFE46)
ER50=ABS(RFA50/RFE50)
ER53=ABS(RFA53/RFE53)

*vfill,rf_tab(1,1),data,RFE38
*vfill,rf_tab(1,2),data,RFA38
*vfill,rf_tab(1,3),data,ER38

*vfill,rf_tab(2,1),data,RFE40
*vfill,rf_tab(2,2),data,RFA40
*vfill,rf_tab(2,3),data,ER40

*vfill,rf_tab(3,1),data,RFE46
*vfill,rf_tab(3,2),data,RFA46
*vfill,rf_tab(3,3),data,ER46

*vfill,rf_tab(4,1),data,RFE50
*vfill,rf_tab(4,2),data,RFA50
*vfill,rf_tab(4,3),data,ER50

*vfill,rf_tab(5,1),data,RFE53
*vfill,rf_tab(5,2),data,RFA53
*vfill,rf_tab(5,3),data,ER53

save,table_4
/com,

/com,
/com, --------------------vm-nr1677-2-1a-a Results Verification---------------------------
/com, 

/nopr
resume,table_1
/gopr

/out,vm-nr1677-2-1a-a,vrt

/com, ===========================================
/com,  COMPARISON OF MODAL FREQUENCY 
/com,      WITH EXPECTED RESULTS
/com, ===========================================
/com,

/com,	Mode | Expected | Mechanical APDL |  Ratio
/com,

*VWRITE,moden(1),Emode(1),Amode(1),ERmode(1)
(1X,F3.0,2X,F8.4,3X,F8.4,3X,F4.2,' ')

/com,

/com,-------------------------------------------------------------------------
/com,

/nopr
resume,table_2
/gopr

/com,
/com, ============================================================
/com, 	COMPARISON OF NODAL DISPLACEMENTS AND ROTATIONS
/com,				WITH EXPECTED RESULTS
/com, ============================================================
/com,

/com,		Result_Node | Expected | Mechanical APDL |  Ratio
/com,

*vwrite,label2(1,1),value(1,1),value(1,2),value(1,3)
(1x,a8,'  ',f10.4,'  ',f10.4,'   ',f5.3)
*vwrite,label2(1,2),value(2,1),value(2,2),value(2,3)
(1x,a8,'  ',f10.4,'  ',f10.4,'   ',f5.3)
*vwrite,label2(1,3),value(3,1),value(3,2),value(3,3)
(1x,a8,'  ',f10.4,'  ',f10.4,'   ',f5.3)
*vwrite,label2(1,4),value(4,1),value(4,2),value(4,3)
(1x,a8,'  ',f10.4,'  ',f10.4,'   ',f5.3)
*vwrite,label2(1,5),value(5,1),value(5,2),value(5,3)
(1x,a8,'  ',f10.4,'  ',f10.4,'   ',f5.3)
*vwrite,label2(1,6),value(6,1),value(6,2),value(6,3)
(1x,a8,'  ',f10.4,'  ',f10.4,'   ',f5.3)

/com,

/com,--------------------------------------------------------------------------
/com,

/nopr
resume,table_4
/gopr

/com,
/com, ===========================================
/com,  COMPARISON OF REACTION FORCES 
/com,      WITH EXPECTED RESULTS
/com, ===========================================
/com,

/com,		Node| Expected |  Mechanical APDL  |  Ratio
/com,

*vwrite,label5(1,1),rf_tab(1,1),rf_tab(1,2),rf_tab(1,3)
(1x,a8,'   ',f10.4,'  ',f10.4,'   ',f5.3)

/com,

/com,--------------------------------------------------------------------------
/com,

/nopr
resume,table_3
/gopr

/com,
/com,===============================================
/com,  COMPARISON OF ELEMENT FORCES AND MOMENTS
/com,		     WITH EXPECTED RESULTS
/com,===============================================
/com,

/com,------------------------------------------------
/com,	Note: Element Forces and Moments for some elements
/com,       along Y & Z directions are flipped between Mechanical APDL
/com,		and NRC results 
/com,
/com,       Element numbers from Mechanical APDL and NRC are
/com,       different.
/com,       Element 1 (Mechanical APDL) = Element 1 (NRC)
/com,       Element 30 (Mechanical APDL) = Element 35 (NRC)
/com,       Element 33 (Mechanical APDL) = Element 27 (NRC)
/com,------------------------------------------------
/com,		Result | Expected | Mechanical APDL |  Ratio
/com,

/com,===============
/com,   Element 35
/com,===============
/com,

*vwrite,label3(1,1),elem_tab(1,1),elem_tab(1,2),elem_tab(1,3)
(1x,a8,'   ',f10.4,'  ',f10.4,'   ',f5.3)

/com,

*vwrite,label4(1,1),elem_tab(13,1),elem_tab(13,2),elem_tab(13,3)
(1x,a8,'   ',f10.4,'  ',f10.4,'   ',f5.3)

/com,
/com,

/com,===============
/com,   Element 27
/com,===============
/com,

*vwrite,label3(1,1),elem_tab(7,1),elem_tab(7,2),elem_tab(7,3)
(1x,a8,'   ',f10.4,'  ',f10.4,'   ',f5.3)

/com,

*vwrite,label4(1,1),elem_tab(19,1),elem_tab(19,2),elem_tab(19,3)
(1x,a8,'   ',f10.4,'  ',f10.4,'   ',f5.3)

/com,
/com,
/com,*******************************************************************
/com,*******************************************************************
/com,
/com,

/out,
*list,vm-nr1677-2-1a-a,vrt
finish
/exit,nosave

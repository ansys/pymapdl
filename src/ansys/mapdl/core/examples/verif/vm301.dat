/COM,ANSYS MEDIA REL. 2023R1 (11/04/2022) REF. VERIF. MANUAL: REL. 2023R1
/VERIFY,VM301
/COM,   REF: D.S. DUGDALE. "STIFFNESS OF A SPINNING DISC CLAMPED
/COM,   AT ITS CENTRE." J. MECH. PHYS. SOLIDS. 1966: VOL 14.
/COM,
/TITLE, VM301, ROTATING DISK VIBRATION

/COM, ***** SHELL281 MODEL
/TITLE, ROTATING DISK VIBRATION - SHELL

/OUT,SCRATCH_vm301
/PREP7

/COM, PARAMETERS
RI_INCH = 6/2
RO_INCH = 24/2
TH_INCH = 0.12
RATIO_ITOM = 0.0254
RI = RI_INCH*RATIO_ITOM
RO = RO_INCH*RATIO_ITOM
TH = TH_INCH*RATIO_ITOM

/COM, MATERIAL PROPERTIES - STEEL
E   = 2.03E11
RHO = 7850
NU  = 0.3

/COM, ROTATIONAL VELOCITY TABLE DEFINITION
NBSTEP = 10
SPIN   = 4000
DSPIN  = SPIN/(NBSTEP-1)
*DIM,SPINS,,NBSTEP
*VFILL,SPINS,RAMP,,DSPIN
RATIO = ACOS(-1)/30

PARSAV

/COM, GEOMETRY, MESH & BCS
ET,1,281
MP,EX,1,E
MP,DENS,1,RHO
MP,PRXY,1,NU
SECTYPE,,SHELL
SECDATA,TH
CYL4,,,RI,0,RO,90
ESIZE,(RO-RI)/6
MSHKEY,1
AMESH,1
ARSYMM,X,ALL
ARSYMM,Y,ALL
NUMMRG,ALL

CSYS,1
NSEL,,LOC,X,RI           ! CLAMP NODES ON INSIDE RADIUS
D,ALL,ALL
ALLSEL
CSYS,0

/COM, PERFORM LINEAR PERTURBATION MODAL ANALYSES
*DO,I,1,NBSTEP
   /SOLU
   ANTYPE,STATIC
   OMEGA,,,SPINS(I)*RATIO
   RESCONTROL,LINEAR
   CAMPBELL,RSTP
   SOLVE
   FINI
   /SOLU
   ANTYPE,STATIC, RESTART,,,PERTURB
   PERTURB, MODAL
   SOLVE, ELFORM
   MODOPT,LANB,12
   MXPAND,12
   SOLVE
   FINI
*ENDDO

/COM, OBTAIN CAMPBELL DIAGRAM AND CRITICAL SPEED
/POST1
FILE,,rstp
/SHOW,JPEG,REV
EPLO
PLCAMP,OFF,2,RPM
PRCAMP,OFF,2,RPM,,,,YES
SET,7,4
PLNSOL,U,SUM
*GET,VC,CAMP,4,VCRIT
*DIM,LABEL,CHAR,1,2
*DIM,VALUE,,1,3
LABEL(1,1) = '   VCRIT'
LABEL(1,2) = ', RPM   '
*VFILL,VALUE(1,1),DATA,2650
*VFILL,VALUE(1,2),DATA,VC
*VFILL,VALUE(1,3),DATA,ABS(VC/2650)
/COM,
/OUT,vm301,vrt
/COM,
/COM,                   ***** CASE 1 - SHELL MODEL *****
/COM,
/COM,--------------------- VM301 RESULTS COMPARISON -----------------
/COM,
/COM,                 |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.1,'  ',F12.1,'   ',1F15.3)
/COM,----------------------------------------------------------------
/OUT,
FINISH
/CLEAR,NOSTART

/COM, ***** SOLID186 MODEL
/TITLE, ROTATING DISK VIBRATION - SOLID

/OUT,SCRATCH_vm301,,,append

/PREP7

PARRES

/COM, GEOMETRY, MESH & BCS
ET,1,186
MP,EX,1,E
MP,DENS,1,RHO
MP,PRXY,1,NU

CYLIND,RI,RO,-TH/2,TH/2,0,90
ESIZE,(RO-RI)/6
MSHKEY,1
VMESH,1
VSYMM,X,ALL
VSYMM,Y,ALL
NUMMRG,ALL

CSYS,1
NSEL,,LOC,X,RI           ! CLAMP NODES ON INSIDE RADIUS
D,ALL,ALL
ALLSEL
CSYS,0

/COM, ROTATIONAL VELOCITY TABLE DEFINITION
*DIM,SPINS,,NBSTEP
*VFILL,SPINS,RAMP,,DSPIN

/COM, PERFORM LINEAR PERTURBATION MODAL ANALYSES
*DO,I,1,NBSTEP
   /SOLU
   ANTYPE,STATIC
   OMEGA,,,SPINS(I)*RATIO
   RESCONTROL,LINEAR
   CAMPBELL,RSTP
   SOLVE
   FINI
   /SOLU
   ANTYPE,STATIC, RESTART,,,PERTURB
   PERTURB, MODAL
   SOLVE, ELFORM
   MODOPT,LANB,12
   MXPAND,12
   SOLVE
   FINI
*ENDDO

/COM, OBTAIN CAMPBELL DIAGRAM AND CRITICAL SPEED
/POST1
FILE,,rstp
/SHOW,JPEG,REV
PLCAMP,OFF,2,RPM
PRCAMP,OFF,2,RPM,,,,YES
*GET,VC,CAMP,4,VCRIT
*DIM,LABEL,CHAR,1,2
*DIM,VALUE,,1,3
LABEL(1,1) = '   VCRIT'
LABEL(1,2) = ', RPM   '
*VFILL,VALUE(1,1),DATA,2650
*VFILL,VALUE(1,2),DATA,VC
*VFILL,VALUE(1,3),DATA,ABS(VC/2650)
/COM
/COM,
/OUT,vm301,vrt,,append
/COM,
/COM,                   ***** CASE 2 - SOLID MODEL *****
/COM,
/COM,--------------------- VM301 RESULTS COMPARISON -----------------
/COM,
/COM,                 |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.1,'  ',F12.1,'   ',1F15.3)
/COM,----------------------------------------------------------------
/OUT
*LIST,vm301,vrt
FINISH
/EXIT,NOSAVE

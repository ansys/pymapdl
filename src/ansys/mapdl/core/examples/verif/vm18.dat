/COM,ANSYS MEDIA REL. 2023R1 (11/04/2022) REF. VERIF. MANUAL: REL. 2023R1
/VERIFY,VM18
/PREP7
/TITLE, VM18, OUT-OF-PLANE BENDING OF A CURVED BAR
!    USING PIPE18    STR. OF MATL., TIMOSHENKO, PART 1, 3RD ED., PAGE 412, EQN. 241
ANTYPE,STATIC
ET,1,PIPE18,,,,,,2   ! KEYOPT(6)=2 PRINTS MEMBER FORCES
MP,EX,1,30E6
MP,NUXY,1,.3
R,1,2,1,100          ! OD = 2, WALL THICKNESS = 1, RADIUS = 100
N,1,100              ! DEFINE NODES
N,2,,100
N,10
E,1,2,10             ! DEFINE ELEMENT
D,1,ALL              ! BOUNDARY CONDITIONS + LOAD
F,2,FZ,-50
FINISH
/SOLU    
OUTPR,BASIC,1
/OUT,SCRATCH
SOLVE
FINISH 
/POST1
SET,LAST
/OUT,
*GET,DEF,NODE,2,U,Z
ETABLE,STRS_BEN,NMISC,91
ETABLE,STRS_SHR,LS,4
*GET,STRSS_B,ELEM,1,ETAB,STRS_BEN
*GET,STRSS_T,ELEM,1,ETAB,STRS_SHR

*DIM,LABEL,CHAR,3,2
*DIM,VALUE,,3,3
LABEL(1,1) = 'DEFLECTI','STRSS BE','STRSS SH'
LABEL(1,2) = 'ON (in) ','ND   psi','EAR  psi'
*VFILL,VALUE(1,1),DATA,-2.648,6366,-3183
*VFILL,VALUE(1,2),DATA,DEF,ABS(STRSS_B) ,STRSS_T 
*VFILL,VALUE(1,3),DATA,ABS(DEF/2.648) ,ABS(STRSS_B /6366 ),ABS(STRSS_T /3183 )
SAVE,TABLE_1
FINI
/CLEAR,NOSTART

!**********************************************

/SHOW,JPEG
/PREP7
!     USING PIPE289  
ANTYPE,STATIC
ET,1,PIPE289,,, ,2
MP,EX,1,30E6
MP,NUXY,1,.3
SECTYPE,1,PIPE
SECDATA,2,1,16	     ! OD = 2, WALL THICKNESS = 1
CSYS,1
N,1,100              ! DEFINE NODES
NGEN,19,1,1,,,,5
E,1,3,2
EGEN,9,2,-1
CSYS,0
D,1,ALL              ! BOUNDARY CONDITIONS + LOAD
F,19,FZ,-50
FINISH
/SOLU    
OUTPR,BASIC,1
/OUT,SCRATCH
SOLVE
FINISH 
/POST1
SET,LAST
/GRAPHICS,POWER
/ESHAPE,1
/VIEW,1,1,1,1
/OUT,
*GET,DEF,NODE,19,U,Z
ETABLE,STRS_BEN,SMISC,35
*GET,STRSS_B,ELEM,1,ETAB,STRS_BEN
PLESOL,S,XY
*GET,SHEAR,PLNSOL,0,MIN
*DIM,LABEL,CHAR,3,2
*DIM,VALUE,,3,3
LABEL(1,1) = 'DEFLECTI','STRSS BE','STRSS SH'
LABEL(1,2) = 'ON (in) ','ND   psi','EAR  psi'
*VFILL,VALUE(1,1),DATA,-2.648,6366,-3183
*VFILL,VALUE(1,2),DATA,DEF,ABS(STRSS_B) ,SHEAR 
*VFILL,VALUE(1,3),DATA,ABS(DEF/2.648) ,ABS(STRSS_B /6366 ),ABS(SHEAR /3183 )

SAVE,TABLE_2

/NOPR
/COM,
/OUT,vm18,vrt
/COM,------------------- VM18 RESULTS COMPARISON ---------------------
/COM,
/COM,                 |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
RESUME,TABLE_1
/COM,USING PIPE18
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.3,'  ',F14.3,'   ',1F15.3)
/COM,
RESUME,TABLE_2 
/COM,USING PIPE289
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.3,'  ',F14.3,'   ',1F15.3)
/COM,
/COM,-----------------------------------------------------------------
/OUT

FINISH
*LIST,vm18,vrt





/COM,ANSYS MEDIA REL. 150 (11/8/2013) REF. VERIF. MANUAL: REL. 150
/VERIFY,VM62
/PREP7
SMRT,OFF
/TITLE, VM62, VIBRATION OF A WEDGE
C***          VIBRATION PROBS. IN ENGR., TIMOSHENKO, 3RD. ED., PAGE 392, ART. 62
C***          USING SHELL63 ELEMENTS WITH BENDING STIFFNESS OPTION
ANTYPE,MODAL                    ! MODE-FREQUENCY ANALYSIS
MXPAND,2                        ! EXPAND MODES
MODOPT,LANB,2,,,                
ET,1,SHELL63,2                  ! SHELL63 WITH BENDING STIFFNESS OPTION
R,1,1                           ! UNIT THICKNESS
MP,EX,1,30E6
MP,DENS,1,.000728
MP,NUXY,1,0                     ! POISSON'S RATIO IS ZERO
K,1                             ! DEFINE MODEL GEOMETRY
K,2,16
K,3,16,2
L,2,3
LESIZE,1,,,1
A,1,2,3,3
ARSYM,Y,1                       ! SYMMETRY REFLECTION OF AREAS
NUMMRG,KPOI                     ! MERGE COINCIDENT KEYPOINTS
ESIZE,,4
AMESH,1,2
NSEL,S,LOC,X,16
D,ALL,UZ,,,,,ROTX,ROTY,UX,UY    ! CONSTRAIN DISPLACEMENTS AT BASE
NSEL,ALL
OUTPR,ALL,1
OUTRES,ALL,0
FINISH
/out,scratch
/SOLU    
SOLVE
/out
*GET,FREQ1,MODE,1,FREQ
FINISH
/PREP7
SMRT,OFF
C***      USING SHELL63 ELEMENTS WITH SHELL OPTION
ET,1,SHELL63                         ! USE BENDING AND MEMBRANE STIFFNESS
FINISH
/out,scratch
/SOLU    
SOLVE
/out
*GET,FREQ2,MODE,1,FREQ
FINISH
PARSAV
/CLEAR,NOSTART
PARRES,CHANGE
/PREP7
SMRT,OFF
C***      USING SHELL181 ELEMENTS WITH SHELL OPTION
ANTYPE,MODAL                    ! MODE-FREQUENCY ANALYSIS
MXPAND,2                        ! EXPAND MODES
MODOPT,LANB,2,,,
ET,1,SHELL181                   ! SHELL181
KEYOPT,1,3,2
SECTYPE,1,SHELL                
SECDATA,1,1,0,3
MP,EX,1,30E6
MP,DENS,1,.000728
MP,NUXY,1,0                     ! POISSON'S RATIO IS ZERO
K,1                             ! DEFINE MODEL GEOMETRY
K,2,16
K,3,16,2
L,2,3
LESIZE,1,,,1
A,1,2,3,3
ARSYM,Y,1                       ! SYMMETRY REFLECTION OF AREAS
NUMMRG,KPOI                     ! MERGE COINCIDENT KEYPOINTS
ESIZE,,4
AMESH,1,2
NSEL,S,LOC,X,16
D,ALL,UZ,,,,,ROTX,ROTY,UX,UY    ! CONSTRAIN DISPLACEMENTS AT BASE
NSEL,ALL
OUTPR,ALL,1
OUTRES,ALL,0
FINISH
/out,scratch
/SOLU    
SOLVE
/out
*GET,FREQ3,MODE,1,FREQ
FINISH
PARSAV
/CLEAR, NOSTART                ! CLEAR DATABASE FOR SECOND SOLUTION
PARRES,CHANGE
/TITLE, VM62, VIBRATION OF A FLAT PLATE
C***      USING SHELL281 ELEMENTS WITH SHELL OPTION
/PREP7
SMRT,OFF
ANTYPE,MODAL                    ! MODE-FREQUENCY ANALYSIS
MXPAND,2                        ! EXPAND MODES
MODOPT,LANB,2,,,
ET,1,SHELL281                   ! SHELL281 WITH SHELL OPTION
SECT,1,SHELL
SECD,1,1                        ! UNIT THICKNESS
MP,EX,1,30E6
MP,DENS,1,.000728
MP,NUXY,1,0                     ! POISSON'S RATIO IS ZERO
K,1                             ! DEFINE MODEL GEOMETRY
K,2,16
K,3,16,2
L,2,3
LESIZE,1,,,1
A,1,2,3,3
ARSYM,Y,1                       ! SYMMETRY REFLECTION OF AREAS
NUMMRG,KPOI                     ! MERGE COINCIDENT KEYPOINTS
ESIZE,,4
AMESH,1,2
NSEL,S,LOC,X,16
D,ALL,UZ,,,,,ROTX,ROTY,UX,UY    ! CONSTRAIN DISPLACEMENTS AT BASE
NSEL,ALL
OUTPR,ALL,1
OUTRES,ALL,0
FINISH
/out,scratch
/SOLU    
SOLVE
/out
*GET,FREQ4,MODE,1,FREQ
FINISH
*DIM,LABEL,CHAR,1,2
*DIM,VALUE_1,,1,3
*DIM,VALUE_2,,1,3
*DIM,VALUE_3,,1,3
*DIM,VALUE_4,,1,3
LABEL(1,1) = '      f,'
LABEL(1,2) = ' Hz     '
*VFILL,VALUE_1(1,1),DATA,259.16   
*VFILL,VALUE_1(1,2),DATA,FREQ1 
*VFILL,VALUE_1(1,3),DATA,ABS(FREQ1/259.16 )
*VFILL,VALUE_2(1,1),DATA,259.16   
*VFILL,VALUE_2(1,2),DATA,FREQ2 
*VFILL,VALUE_2(1,3),DATA,ABS(FREQ2/259.16 )
*VFILL,VALUE_3(1,1),DATA,259.16   
*VFILL,VALUE_3(1,2),DATA,FREQ3 
*VFILL,VALUE_3(1,3),DATA,ABS(FREQ3/259.16 )
*VFILL,VALUE_4(1,1),DATA,259.16   
*VFILL,VALUE_4(1,2),DATA,FREQ4 
*VFILL,VALUE_4(1,3),DATA,ABS(FREQ4/259.16 )
/COM
/OUT,vm62,vrt
/COM,------------------- VM62 RESULTS COMPARISON ---------------
/COM,
/COM,                 |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
/COM,RESULTS USING PLATE ELEMENTS:SHELL63
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE_1(1,1),VALUE_1(1,2),VALUE_1(1,3)
(1X,A8,A8,'   ',F10.2,'  ',F14.2,'   ',1F15.3)
/COM,
/COM,RESULTS USING SHELL ELEMENTS:SHELL63
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE_2(1,1),VALUE_2(1,2),VALUE_2(1,3)
(1X,A8,A8,'   ',F10.2,'  ',F14.2,'   ',1F15.3)
/COM,
/COM,RESULTS USING SHELL ELEMENTS:SHELL181 
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE_3(1,1),VALUE_3(1,2),VALUE_3(1,3)
(1X,A8,A8,'   ',F10.2,'  ',F14.2,'   ',1F15.3)
/COM,
/COM,RESULTS USING SHELL ELEMENTS:SHELL281 
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE_4(1,1),VALUE_4(1,2),VALUE_4(1,3)
(1X,A8,A8,'   ',F10.2,'  ',F14.2,'   ',1F15.3)
/COM,----------------------------------------------------------
/OUT
FINISH
*LIST,vm62,vrt

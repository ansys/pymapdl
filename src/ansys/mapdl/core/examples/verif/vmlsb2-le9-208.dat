! Copyright (C) 2024 ANSYS, Inc. and/or its affiliates.
! SPDX-License-Identifier: MIT
! 
!
! Permission is hereby granted, free of charge, to any person obtaining a copy
! of this software and associated documentation files (the "Software"), to deal
! in the Software without restriction, including without limitation the rights
! to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
! copies of the Software, and to permit persons to whom the Software is
! furnished to do so, subject to the following conditions:
!
! The above copyright notice and this permission notice shall be included in all
! copies or substantial portions of the Software.
!
! THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
! IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
! FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
! AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
! LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
! OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
! SOFTWARE.

/COM,ANSYS MEDIA REL. 2023R2 (05/12/2023) REF. VERIF. MANUAL: REL. 2023R2
/VERIFY,vmlsb2-le9-208
/TITLE,vmlsb2-le9-208,AXISYMMETRIC BRANCHED SHELL WITH PRESSURE LOADING
/COM, REFERENCE: NAFEMS BENCHMARKS,REPORT: LSB2,1989-01-01/1
/COM, ORIGINAL TEST NUMBER: LE9 
/PREP7
ET,1,SHELL208
KEYOPT,1,3,2			! INCLUDE EXTRA INTERNAL NODE
KEYOPT,1,8,2			! STORE DATA FOR ALL LAYERS
SECTYPE,1,SHELL
SECDATA,0.01,1,0
MP,EX,1,210E9
MP,NUXY,1,0.3
K,1,0.70710,0,0
K,2,0.70710,0.875,0
K,3,0.70710,1,0
K,4,0.70710,1.125,0
K,5,0.70710,2,0
K,6,0,0.70710,0
K,7,0.60874,0.91374,0
K,8,0,1.70710,0

L,5,4
L,4,3
LARC,3,7,8,1
LARC,7,6,8,1
L,3,2
L,2,1

KDELE,8

LESIZE,1,,,8
LESIZE,2,,,8
LESIZE,3,,,8
LESIZE,4,,,10
LESIZE,5,,,8
LESIZE,6,,,8
LMESH,ALL
ALLSEL,ALL

NSEL,S,LOC,X,0.70710
NSEL,R,LOC,Y,0
D,ALL,ALL,0
ALLSEL,ALL

LSEL,S,LINE,,3,4,1
LSEL,A,LINE,,1,2,1
NSLL,S,1
ESLN,S
SFE,ALL,2,PRES,0,1E6
ALLSEL,ALL
FINI

/SOLU
ANTYPE,STATIC
OUTRES,ALL,ALL
NSUBS,10,10,10
TIME,1
SOLVE
FINI

/POST1
SET,LAST
/GRAPHICS,POWER
/SHOW,vmfebsta-le9-208
ESEL,S,ELEM,,16
PLESOL,S,Y
*GET,SYY_MIN,PLNSOL,0,MIN
ESEL,ALL
*DIM,LABEL,CHAR,1
*DIM,VALUE,,1,3
LABEL(1,1) = 'AXIAL-STRESS'
*VFILL,VALUE(1,1),DATA,-319.9
*VFILL,VALUE(1,2),DATA,(SYY_MIN/1000000)
*VFILL,VALUE(1,3),DATA,((SYY_MIN/1000000)/(-319.9))
/OUT,vmlsb2-le9-208,vrt
/COM,-------------------vmlsb2-le9-208 RESULTS COMPARISON --------------
/COM,
/COM,           |  TARGET  |   Mechanical APDL  |  RATIO
/COM,
/COM, 
*VWRITE,LABEL(1,1),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,'   ',F10.4,'  ',F14.4,'   ',F15.3)
/COM,----------------------------------------------------------------------
/OUT,
FINISH
*LIST,vmlsb2-le9-208,vrt







/COM,ANSYS MEDIA REL. 2023R1 (11/04/2022) REF. VERIF. MANUAL: REL. 2023R1
/VERIFY,VM52
/PREP7
MP,PRXY,,0.3
/TITLE, VM52, AUTOMOBILE SUSPENSION SYSTEM VIBRATIONS
C***VIBRATION THEORY AND APPLICATIONS, THOMSON, 2ND PRINTING, PAGE 181, EX 6.7-1
ANTYPE,MODAL              ! MODE-FREQUENCY ANALYSIS
MXPAND,2                  ! EXPAND MODES
MODOPT,LANB,2            
ET,1,BEAM188              ! BEAM ELEMENT
KEYOPT,1,3,3
ET,2,COMBIN14,,,2         ! SPRING ELEMENT
ET,3,MASS21,,,3           ! MASS ELEMENT
R,1,2400                  ! SPRING STIFFNESS (K1) = 2400
R,2,100,1600              ! MASS = 100 (FROM 3220/32.2), I = 1600
R,3,2600                  ! SPRING STIFFNESS (K2) = 2600
SECT,2,BEAM,RECT          ! BEAM PROPERTIES
SECD,1,1
MP,EX,1,4E9
N,1
N,2,,1
N,3,4.5,1
N,4,10,1
N,5,10
TYPE,2
E,1,2                      ! SPRING ELEMENT
TYPE,1
SECN,2
E,2,3                      ! BEAM ELEMENT
TYPE,3
REAL,2
E,3                        ! MASS ELEMENT
TYPE,1
SECN,2
E,3,4                      ! BEAM ELEMENT
TYPE,2
REAL,3
E,4,5                      ! SPRING ELEMENT
D,1,UX,,,5,4,UY            ! BOUNDARY CONDITIONS
D,3,UX
NSEL,S,NODE,,2,4
D,ALL,UZ,,,,,ROTY,ROTX
NSEL,ALL
OUTPR,NSOL,1
FINISH
/SOLU    
/OUT,SCRATCH
SOLVE
/OUT,
*GET,FREQ1,MODE,1,FREQ
*GET,FREQ2,MODE,2,FREQ
*DIM,LABEL,CHAR,2,2
*DIM,VALUE,,2,3
LABEL(1,1) = '    f1, ','    f2, '
LABEL(1,2) = 'Hz      ','Hz      '
*VFILL,VALUE(1,1),DATA,1.0981,1.4406
*VFILL,VALUE(1,2),DATA,FREQ1,FREQ2
*VFILL,VALUE(1,3),DATA,ABS(FREQ1/1.0981 ) ,ABS( FREQ2/1.4406)
/COM
/OUT,vm52,vrt
/COM,------------------- VM52 RESULTS COMPARISON ---------------
/COM,
/COM,                 |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.4,'  ',F14.4,'   ',1F15.3)
/COM,-----------------------------------------------------------
/OUT
FINISH
*LIST,vm52,vrt



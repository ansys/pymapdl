/COM,ANSYS MEDIA REL. 2023R1 (11/04/2022) REF. VERIF. MANUAL: REL. 2023R1
/VERIFY,VM204
JPGPRF,500,100,1             ! MACRO TO SET PREFS FOR JPEG PLOTS
/SHOW,JPEG
/UNITS,BIN
/PREP7 
/TITLE, VM204, SOLID MODEL OF AN AXIAL BEARING 
/COM    ANY BASIC GEOMETRY TEXT
BASEWD=8.0                                      ! BASE WIDTH
BASEDP=4.0                                      ! BASE DEPTH                   
BASEHT=1.0                                      ! BASE HEIGHT
BRNGWD=4.0                                      ! BEARING HOUSING WIDTH
BRNGDP=3.0                                      ! BEARING HOUSING DEPTH
BRNGHT=2.0                                      ! BEARING HOUSING HEIGHT
GRVDIA=3.5                                      ! GROOVE DIAMETER 
BRCKWD=2.0                                      ! BRACKET WIDTH   
BRCKDP=0.5                                      ! BRACKET DEPTH
SPRTHT=BRNGHT/2                                 ! SUPPORT HOLE HEIGHT
SPRTRAD=0.5                                     ! SUPPORT HOLE RADIUS
BLOCK,-BASEWD/2,BASEWD/2,0,-BASEHT,0,BASEDP     ! BASE
BLOCK,-BRNGWD/2,BRNGWD/2,0,BRNGHT,0,BRNGDP      ! BEARING HOUSING
WPAVE,BRNGWD/2,0,0                              ! MOVE WORKING PLANE ORIGIN
BLOCK,0,BRCKWD,0,BRNGHT,0,BRCKDP                ! RIGHT BRACKET
WPAVE,-BRNGWD/2,0,0
BLOCK,0,-BRCKWD,0,BRNGHT,0,BRCKDP               ! LEFT BRACKET
WPAVE,0,BRNGHT,0
CYLIND,0,GRVDIA/2,0,BRNGWD
VSBV,2,5                                        ! GROOVE
LOCAL,11,1,BRNGWD/2+(BASEWD-BRNGWD)/4,SPRTHT    ! LOCAL COORD SYSTEM
WPCSYS,1,11                                     ! MOVE WP TO LOCAL CS
CYLIND,0,SPRTRAD,0,1                                        
VSBV,3,2                                        ! SUPPORT HOLE    
LOCAL,12,1,-BRNGWD/2-(BASEWD-BRNGWD)/4,SPRTHT
WPCSYS,1,12
CYLIND,0,SPRTRAD,0,1
VSBV,4,2                                        ! SUPPORT HOLE
CSYS,0                                          ! CARTESIAN COORD SYSTEM
WPAVE,BRNGWD/2+(BASEWD-BRNGWD)/4,,BASEDP*0.75
WPROTA,,90
CYLIND,0,SPRTRAD,0,1
VSBV,1,2                                        ! SUPPORT HOLE
WPOFFS,-BRNGWD-(BASEWD-BRNGWD)/2                ! OFFSET WORKING PLANE
CYLIND,0,SPRTRAD,0,1
VSBV,4,1                                        ! SUPPORT HOLE
CSYS,11
K,100,(BASEWD-BRNGWD)/4,0,0
K,101,(BASEWD-BRNGWD)/4,90,0
K,102,(BASEWD-BRNGWD)/4,90,1
K,103,(BASEWD-BRNGWD)/4,0,1
A,100,101,102,103                               ! CUTTING AREA 
VSBA,5,3,SEPO                                   ! ROUND  
VDELE,1,,,1
CSYS,12
K,105,(BASEWD-BRNGWD)/4,180,0
K,106,(BASEWD-BRNGWD)/4,90,0
K,107,(BASEWD-BRNGWD)/4,90,1
K,108,(BASEWD-BRNGWD)/4,180,1
A,105,106,107,108                               ! CUTTING AREA
VSBA,3,3,SEPO                                   ! ROUND
VDELE,1,,,1
VGLUE,ALL                                       ! GLUE VOLUMES - CONTINUITY
/TYPE,1,4
/VIEW,1,1,2,3                                   ! VIEWING ANGLE
/AUTO,1
/TRIAD,OFF 
VPLOT                                           ! DISPLAY VOLUMES
/OUT,SCRATCH
VSUM                                            ! CALCULATE TOTAL VOLUME
*GET,TVOL,VOLU,,VOLU
*status,parm
*DIM,VALUE,,1,3
*DIM,LABEL,CHAR,1,2
LABEL(1,1) = 'VOLUME '
LABEL(1,2) = 'NO UNTS '
*VFILL,VALUE(1,1),DATA,42.997
*VFILL,VALUE(1,2),DATA,TVOL
*VFILL,VALUE(1,3),DATA,ABS(TVOL/42.997)
/COM
/OUT,vm204,vrt
/COM,------------------- VM204 RESULTS COMPARISON--------------
/COM,
/COM,                  |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.3,'  ',F14.3,'   ',1F18.3)
/COM,----------------------------------------------------------
/OUT
FINISH
*LIST,vm204,vrt

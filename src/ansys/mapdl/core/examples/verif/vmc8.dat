! Copyright (C) 2024 ANSYS, Inc. and/or its affiliates.
! SPDX-License-Identifier: MIT
! 
!
! Permission is hereby granted, free of charge, to any person obtaining a copy
! of this software and associated documentation files (the "Software"), to deal
! in the Software without restriction, including without limitation the rights
! to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
! copies of the Software, and to permit persons to whom the Software is
! furnished to do so, subject to the following conditions:
!
! The above copyright notice and this permission notice shall be included in all
! copies or substantial portions of the Software.
!
! THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
! IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
! FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
! AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
! LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
! OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
! SOFTWARE.

/COM,ANSYS MEDIA REL. 2023R2 (05/12/2023) REF. VERIF. MANUAL: REL. 2023R2
/VERIFY,VMC8
/CONFIG,NLCO,0
/SHOW
/DEVICE,VECTOR,ON
OKEY=1
/TITLE, VMC8, ALUMINUM BAR IMPACTING A RIGID BOUNDARY
/COM 
/COM   REF: WILKINS M.L AND GUINAN M.W., "IMPACT OF CYLINDERS ON A RIGID 
/COM        BOUNDARY", J. APPL. PHYS., VOL. 44, NO. 3, 1973.
/COM
/UNITS,SI                       ! (KG, NE, M , SEC)

*DIM,LFA,,2,2
*DO,I,1,2
  *IF,I,EQ,1,THEN
    ATYPE=2 
    ETYP=182 
    /TITLE, VMC8, ALUMINUM BAR IMPACTING A RIGID BOUNDARY - PLANE182
  *ELSEIF,I,EQ,2,THEN
    ATYPE=2
    ETYP=183
    /TITLE, VMC8, ALUMINUM BAR IMPACTING A RIGID BOUNDARY - PLANE183
  *ENDIF

  /PREP7
  RAD = 0.00381                   ! BAR RADIUS [M]
  L = 0.02347                     ! BAR LENGTH
  DI = 0.0001                     ! INTERFACE BETWEEN THE BAR AND THE WALL
  VEL = 478                       ! INITIAL VELOCITY [M/SEC]
  CVEL= (70E9/2700)**0.5          ! ELASTIC WAVE PROPAGATION SPEED
  TEL=(RAD/4)/CVEL                ! TIME STEP INCREMENT (4 ELEMENTS ALONG RADIUS) 
  NLS=NINT(1.1*(4.5E-5/TEL))      ! MINIMUM NUMBER OF SUBSTEPS FOT TIME=4.5E-5
  ET,1,ETYP                       ! ELEMENT TYPE
  *IF,ETYP,EQ,182,THEN
    KEYOPT,1,1,1			! REDUCED INTEGRATION FOR 182
  *ENDIF
  MP,EX,1,70E9                    ! ELASTIC MODULUS [PA]
  MP,NUXY,1,0.3 
  MP,DENS,1,2700                  ! DENSITY (KG/M^3)
  TB,BISO,1                       ! BILINEAR ISOTROPIC HARDENING
  TBDAT,1,420E6,100E6             ! YEILD STRESS [PA], TANGENT MODULUS [PA] 
  K,1,0,DI                        ! SOLID MODEL 
  K,2,RAD,DI
  K,3,RAD,(DI+L)
  K,4,0,(DI+L)
  L,1,2
  L,3,4
  LESIZE,ALL,,,4
  L,1,4
  L,2,3
  LESIZE,ALL,,,12,3
  A,1,2,3,4
  AMESH,1
  EPLOT
  NSEL,S,LOC,X,0 
  D,ALL,UX                          
  NSEL,S,LOC,X,0                 
  NSEL,R,LOC,Y,L+DI
  *GET,NTOP,NODE,,NUM,MIN   
  NSEL,ALL
  NBOT=NODE(0,0,0) 
  NSEL,S,,,NBOT
  ESLN,S
  *GET,EBOT,ELEM,,NUM,MIN
  NSEL,ALL
  ESEL,ALL
  SAVE
  FINISH

  /OUT,SCRATCH
  /SOLU
  ANTYPE,TRANS
  NLGEOM,ON
  NROPT,FULL
  AUTOTS,ON
  TIMINT,OFF                      ! STATIC LOAD STEP - DEFINE INITIAL VELOCITY    
  T1=DI/VEL                       ! TIME INCREMENT 
  TIME,T1
  DELTIM,T1                      
  NSEL,S,LOC,Y,DI
  D,ALL,UY,-DI
  NSEL,ALL 

  NCNV,2
  CNVTOL,U,1,0.001
  OUTPR,ALL,NONE 
  OUTRES,ALL,10 
  SOLVE                           ! LOAD STEP 1 - STATIC
  TIMINT,ON                     
  NEQIT,40
  CNVTOL,U
  CNVTOL,F,0.01,0.001
  NSUBST,NLS,10*NLS,NLS
  TIME,(T1+4.5E-5)
  SOLVE                           ! LOAD STEP 1 - DYNAMIC
  SAVE
  FINISH

  /POST1
  SET,LAST
  /DSCAL,1,1
  PLDISP                          ! PLOT DEFORMED SHAPE
  *GET,DYTP,UY,NTOP               ! NODAL DISPLACEMENT OF TOP NODE
  LF=(L+DI)+DYTP                  ! DEFORMED LENGTH
  LFA(1,I)=LF
  LFA(2,I)=LF/(L*0.562)
  /OUT,
  *STATUS,LF
  FINISH

  /OUT,SCRATCH
  /POST26
  /GRID,1
  XVAR,1
  NSOL,2,NTOP,U,Y                 ! DISPLACEMENT OF FREE END NODE
  ESOL,3,EBOT,NBOT,EPPL,EQV       ! EQUIVALENT PLASTIC STRAIN
  ADD,2,2,,,DISP,,,-1
  /AXLAB,X,TIME [SEC]
  /AXLAB,Y,FREE END DISPLACEMENT [M]
  PLVAR,2                         ! PLOT DISPLACEMENT VS. TIME
  /AXLAB,Y,EPPL-EQV AT NODE 1       
  PLVAR,3                         ! PLOT PLASTIC STRAIN
  FINISH

  PARSAV,ALL
  /CLEAR, NOSTART 
  PARRES

*ENDDO

FINISH
*VLEN,1
/OUT,vmc8,vrt
/COM
/COM,------------------- VMC8 RESULTS LISTING ------------------------------------
/COM,
/COM,         
/COM,ETYP  |   182   |   183   |
/COM,
*VWRITE,LFA(1,1),LFA(1,2)
(' L (M)   ',F6.4,'     ',F6.4)
*VWRITE,LFA(2,1),LFA(2,2)
(' RATIO   ',F6.4,'     ',F6.4)
/COM,-----------------------------------------------------------------------------
/OUT
*LIST,vmc8,vrt
FINISH
/EXIT,NOSAVE

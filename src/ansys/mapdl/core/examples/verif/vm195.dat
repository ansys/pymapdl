! Copyright (C) 2024 ANSYS, Inc. and/or its affiliates.
! SPDX-License-Identifier: MIT
! 
!
! Permission is hereby granted, free of charge, to any person obtaining a copy
! of this software and associated documentation files (the "Software"), to deal
! in the Software without restriction, including without limitation the rights
! to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
! copies of the Software, and to permit persons to whom the Software is
! furnished to do so, subject to the following conditions:
!
! The above copyright notice and this permission notice shall be included in all
! copies or substantial portions of the Software.
!
! THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
! IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
! FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
! AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
! LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
! OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
! SOFTWARE.

/COM,ANSYS MEDIA REL. 2023R2 (05/12/2023) REF. VERIF. MANUAL: REL. 2023R2
/VERIFY,VM195
/PREP7
MP,PRXY,,0.3
/TITLE, VM195, TOGGLE MECHANISM
C*** KINEMATICS AND DYNAMICS OF MACHINES, MARTIN, 2ND ED., P 56, FIG 3-22 
ET,1,MPC184,6,,,1
ET,2,BEAM188,,,3
ET,3,COMBIN14
ET,4,LINK11
SECTYPE,1,JOINT,REVO               ! REVOLUTE JOINT
LOCAL,11,0,1.6,2.0
SECJOIN,,11,11
SECSTOP,6,-.6435,0.6435            !  ALLOW 37 DEG. ROTATION
SECT,2,BEAM,RECT                   ! BEAM PROPERTIES
SECD,0.1,0.1
R,3,166.6667                       ! SPRING STIFFNESS
SECTYPE,3,JOINT,REVO               ! REVOLUTE JOINT
LOCAL,12,0,1.6,1.2
SECJOIN,,12,12
SECLOCK,6,-1.287,1.287             ! ALLOW 74 DEG. ROTATION THEN LOCK-UP
DIST=(SQRT(12.2)-1)                ! DISTANCE TO MOVE ACTUATOR
R,4,(100/DIST)                     ! ACTUATOR STIFFNESS (F=100=KX)
MP,EX,1,1E9
N,1,,0.8
N,2,1.6,2.0
N,3,1.6,2.0
N,4,3.2,3.2
N,5
N,6,1.6,1.2
N,7,1.6,1.2
N,8,1.6,1.2
N,9,3.2
N,10,5
N,21,3.2,4.2
TYPE,1
SECN,1
E,2,3
CP,1,UX,6,7
CP,2,UY,6,7
SECN,3
E,7,8
TYPE,2
SECN,2
E,1,2
E,2,4
E,3,6
E,5,7
E,8,9
TYPE,3
REAL,3
E,9,10
TYPE,4
REAL,4
E,4,21
D,1,UX,,,5,4,UY,ROTX
D,9,UY
D,10,ALL,,,21,11
ESEL,S,TYPE,,1
NSLE,U
D,ALL,UZ
NSEL,ALL
ESEL,ALL
SFE,9,2,PRES,,102            ! ACTUATOR FORCE, INCREASE BY 2% TO ENSURE IMPACT
FINISH
/OUTPUT,SCRATCH
/SOLU
NLGEOM,ON
NSUBST,5
SOLVE
/POST1
*GET,UY,NODE,4,U,Y
*GET,UX,NODE,9,U,X
ESEL,S,ELEM,,8,8
ETABLE,FORCE,SMISC,1
ESORT,FORCE
*GET,F,SORT,,MIN
*STATUS,PARM
*DIM,VALUE,,3,3
*DIM,LABEL,CHAR,3,2
LABEL(1,1) = 'FORCE ','UY ','UX '
LABEL(1,2) = 'MAX','ND=4','ND=9'
*VFILL,VALUE(1,1),DATA,-133.33,-2.4,.8
*VFILL,VALUE(1,2),DATA,F,UY,UX
*VFILL,VALUE(1,3),DATA,ABS(F/133.33),ABS(UY/2.4),ABS(UX/.8)
/COM
/OUT,vm195,vrt
/COM,------------------- VM195 RESULTS COMPARISON --------------
/COM,
/COM,                  |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.2,'  ',F15.2,'   ',1F18.3)
/COM,-----------------------------------------------------------
/OUT
FINISH
*LIST,vm195,vrt

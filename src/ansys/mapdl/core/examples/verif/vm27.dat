/COM,ANSYS MEDIA REL. 2023R1 (11/04/2022) REF. VERIF. MANUAL: REL. 2023R1
/VERIFY,VM27
/PREP7
/TITLE, VM27, THERMAL EXPANSION TO CLOSE A GAP 
C***   INTROD. TO STRESS ANALYSIS, HARRIS, 1ST PRINTING, PAGE 58, PROB. 8
C***           CONTA178 AND LINK180 ELEMENTS (3-D)
ET,1,LINK180
ET,2,CONTA178
SECTYPE,1,LINK   
SECDATA,1			! AREA = 1 - SPAR ELEMENT
R,2,-10E10              ! STIFFNESS = 10E10
MP,EX,1,10.5E6
MP,ALPX,,12.5E-6
LOCAL,11,0,,,,45,,-45   ! LOCAL COORDINATE SYSTEM
N,1                     ! DEFINE NODES
N,2,,,3
N,3,,,3.002
E,1,2                   ! DEFINE SPAR ELEMENT
TYPE,2
REAL,2
E,2,3                   ! DEFINE GAP ELEMENT
NROTAT,ALL              ! ROTATE NODES INTO LOCAL COORDINATE SYSTEM
BFUNIF,TEMP,170         ! BOUNDARY CONDITIONS AND LOADS
TREF,70
D,1,ALL,,,3,2
D,2,UY
D,2,UX
SAVE                    ! SAVE DATABASE FOR SECOND ANALYSIS
FINISH
/SOLU    
NSUBST,5
OUTPR,,LAST
AUTOTS,ON
/OUT,SCRATCH
SOLVE
FINISH
/POST1
ETABLE,STRS,LS,1
/OUT,
*GET,STRSS,ELEM,1,ETAB,STRS
ETABLE,THST,LEPTH,1
*GET,THSTR,ELEM,1,ETAB,THST
*DIM,VALUE_C1,,2,3
*VFILL,VALUE_C1(1,1),DATA,-6125,.00125
*VFILL,VALUE_C1(1,2),DATA,STRSS,THSTR
*VFILL,VALUE_C1(1,3),DATA,ABS(STRSS/6125) ,ABS(THSTR/.00125)
*DIM,LABEL,CHAR,2,2
LABEL(1,1) = 'STRESS  ','THERMAL '
LABEL(1,2) = '(psi)   ','STRAIN  '
SAVE,TABLE_1
FINISH
RESUME,TABLE_1
/COM
/OUT,vm27,vrt
/COM,------------------- VM27 RESULTS COMPARISON ---------------------
/COM,
/COM,                 |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
/COM,RESULTS FOR 3-D ANALYSIS:
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE_C1(1,1),VALUE_C1(1,2),VALUE_C1(1,3)
(1X,A8,A8,'   ',F11.5,'  ',F15.5,'   ',1F15.3)
/NOPR    
/COM,-----------------------------------------------------------------
/OUT
FINISH
*LIST,vm27,vrt

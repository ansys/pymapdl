/COM,ANSYS MEDIA REL. 2023R1 (11/04/2022) REF. VERIF. MANUAL: REL. 2023R1
/VERIFY,VM261
JPGPRF,500,100,1
/SHOW,JPEG
/COM, VERIFICATION MANUAL FOR ROTATING BEAM WITH INTERNAL VISCOUS DAMPING
/TITLE,VM261,ROTATING BEAM WITH INTERNAL VISCOUS DAMPING
/COM,  REFERENCE: " FINITE ELEMENT SIMULATION OF ROTOR-BEARING SYSTEMS
/COM,               WITH INTERNAL DAMPING",ASME JOURNAL OF ENGINEERING 
/COM,               FOR POWER, E.S.ZORZI AND H.D.NELSON, 1976
/COM,
/COM,
/COM, ROTATING BEAM WITH UNDAMPED ISOTROPIC SUPPORTS
/COM,
/COM,
/OUT,SCRATCH
/PREP7
/COM, BEAM DIMENSIONS
LENGTH = 1.27				! LENGTH OF THE BEAM IN METERS
DIA	 = 0.1016				! DIAMETER OF THE BEAM IN METERS
PI     = ACOS(-1)
/COM, MATERIAL PROPERTIES FOR STEEL
E	 = 2.1E+11				! YOUNG'S MODULUS FOR STEEL IN PA
DENS   = 7800				! DENSITY IN KG/M^3
SHEARM = E*1000				! NO SHEAR
PROPD	 = 2.0E-04			      ! INTERNAL VISCOUS DAMPING
KB	 = 1.75E+07				! STIFFNESS FOR BEARING IN N/M
/COM, MODEL
ET,1,BEAM188
SECTYPE,1,BEAM,CSOLID
SECDATA,DIA/2
MP,EX,1,E
MP,GXY,1,SHEARM
MP,DENS,1,DENS
MP,NUXY,1,0.3
MP,BETD,1,2.0E-04
TYPE,1
SECNUM,1
MAT,1
K,1,0,0,0
K,2,LENGTH,0,0
L,1,2
LESIZE,1,,,5
LMESH,1
ALLSEL,ALL
CM,SHAFT,ELEM				! CREATING COMPONENT FOR BEAM ELEMENTS
ALLSEL,ALL
/COM, BEARING ELEMENTS
ET,2,COMBI214
KEYOPT,2,2,1				! YZ PLANE
REAL,2
R,2,KB,KB					! STIFFNESS ALONG Y AND Z DIRECTION
RMORE,,,,
N,101,0,0,0
N,102,LENGTH,0,0
TYPE,2
MAT,2
E,1,101
E,2,102
ALLSEL,ALL
/COM, CONSTRAINTS
D,101,ALL,0
D,102,ALL,0
D,ALL,UX,,,,,ROTX
ALLSEL,ALL
FINI
/COM, COMPLEX MODAL SOLVE WITH QRDAMP
/SOLU
ANTYPE,MODAL
MODOPT,QRDAMP,6,,,ON,
QRDOPT,ON                   ! REUSE OF NORMAL MODES FOR QRDAMP SOLVE
MXPAND,6
CORIOLIS,ON,,,ON,ON			! GYROSCOPIC DAMPING AND ROTATING DAMPING
CMOMEGA,SHAFT,0,0,0			! 1ST LOAD STEP
SOLVE
CMOMEGA,SHAFT,130,0,0			! 2ND LOAD STEP
SOLVE
CMOMEGA,SHAFT,261,0,0			! 3RD LOAD STEP
SOLVE
CMOMEGA,SHAFT,392,0,0			! 4TH LOAD STEP
SOLVE
CMOMEGA,SHAFT,539.25,0,0		! 5TH LOAD STEP
SOLVE
CMOMEGA,SHAFT,654,0,0			! 6TH LOAD STEP
SOLVE
CMOMEGA,SHAFT,785,0,0			! 7TH LOAD STEP
SOLVE
CMOMEGA,SHAFT,916,0,0			! 8TH LOAD STEP
SOLVE
CMOMEGA,SHAFT,1047,0,0			! 9TH LOAD STEP
SOLVE
CMOMEGA,SHAFT,1178,0,0			! 10TH LOAD STEP
SOLVE
CMOMEGA,SHAFT,1309,0,0			! 11TH LOAD STEP
SOLVE
CMOMEGA,SHAFT,1444,0,0			! 12TH LOAD STEP
SOLVE
CMOMEGA,SHAFT,1570,0,0			! 13TH LOAD STEP
SOLVE
FINI
/POST1
SET,5,1
*GET,RFREQ1,ACTIVE,0,SET,FREQ		! REAL FREQUENCY AFTER FIRST CRITICAL SPEED
SET,12,4
*GET,RFREQ2,ACTIVE,0,SET,FREQ		! REAL FREQUENCY AFTER SECOND CRITICAL SPEED
SET,5,1,,1
*GET,IFREQ1,ACTIVE,0,SET,FREQ		! IMAGINARY FREQUENCY AFTER FIRST CRITICAL SPEED
SET,12,4,,1
*GET,IFREQ2,ACTIVE,0,SET,FREQ		! IMAGINARY FREQUENCY AFTER SECOND CRITICAL SPEED
/OUT,
PRCAMP,ON,1,RPM,,SHAFT			! CRITICAL SPEEDS
*GET,VCRIC1_1,CAMP,1,VCRI
*GET,VCRIC4_1,CAMP,4,VCRI
PRCAMP,ON,,RPM,,SHAFT,2			! LOGARITHMIC DECREMENT
/OUT,SCRATCH
/COM, COMPUTING INSTABILITY FROM Mechanical APDL
UFREQ1_1 = 2*PI*(RFREQ1/IFREQ1)	! COMPUTING LOGARITHMIC DECREMENT AFTER FIRST CRITICAL SPEED
UFREQ4_1 = 2*PI*(RFREQ2/IFREQ2)	! COMPUTING LOGARITHMIC DECREMENT AFTER SECOND CRITICAL SPEED
*DIM,LABEL,CHAR,1,4
*DIM,VALUE,,4,3
LABEL(1,1) = 'VCRIC1'
LABEL(1,2) = 'VCRIC2'
LABEL(1,3) = 'UFREQ1'
LABEL(1,4) = 'UFREQ2'
/COM, CRITICAL SPEEDS OBTAINED FROM Mechanical APDL AND REFERENCE
/COM, UNSTABLE MODES ARE OBTAINED FROM 1ST FORWARD AND 2ND FORWARD MODES
/COM,
/OUT,SCRATCH
*VFILL,VALUE(1,1),DATA,4950		! 1ST FORWARD CRITICAL SPEED - REFERENCE
*VFILL,VALUE(1,2),DATA,VCRIC1_1     ! 1ST FORWARD CRITICAL SPEED - Mechanical APDL
*VFILL,VALUE(1,3),DATA,ABS(VCRIC1_1/4950)
*VFILL,VALUE(2,1),DATA,10500		! 2ND FORWARD CRITICAL SPEED - REFERENCE
*VFILL,VALUE(2,2),DATA,VCRIC4_1     ! 2ND FORWARD CRITICAL SPEED - Mechanical APDL
*VFILL,VALUE(2,3),DATA,ABS(VCRIC4_1/10500)
*VFILL,VALUE(3,1),DATA,0.001		! UNSTABLE FREQUENCY NEAR 5000 RPM - REFERENCE (FIG 3)
*VFILL,VALUE(3,2),DATA,UFREQ1_1	! FROM Mechanical APDL
*VFILL,VALUE(3,3),DATA,ABS(UFREQ1_1/0.0010)
*VFILL,VALUE(4,1),DATA,0.0103		! UNSTABLE FREQUENCY NEAR 13,500 RPM - REFERENCE (FIG 3)
*VFILL,VALUE(4,2),DATA,UFREQ4_1	! FROM Mechanical APDL
*VFILL,VALUE(4,3),DATA,ABS(UFREQ4_1/0.0103)
SAVE,TABLE_1
FINISH
RESUME,TABLE_1
/COM,
/OUT,vm261,vrt
/COM,
/COM, ----------------------VM261 RESULTS COMPARISON------------------------
/COM,
/COM,           |  TARGET  |   Mechanical APDL  |  RATIO
/COM,
/COM, CRITICAL SPEEDS
/COM, ---------------
/COM, 
*VWRITE,LABEL(1,1),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,'   ',F10.4,'  ',F14.4,'   ',F15.3)
*VWRITE,LABEL(1,2),VALUE(2,1),VALUE(2,2),VALUE(2,3)
(1X,A8,'   ',F10.4,'  ',F14.4,'   ',F15.3)
/COM,
/COM, LOGARITHMIC DECREMENT FOR UNSTABLE FREQUENCIES
/COM, -----------------------------------------------
/COM,
*VWRITE,LABEL(1,3),VALUE(3,1),VALUE(3,2),VALUE(3,3)
(1X,A8,'   ',F10.4,'  ',F14.4,'   ',F15.3)
*VWRITE,LABEL(1,4),VALUE(4,1),VALUE(4,2),VALUE(4,3)
(1X,A8,'   ',F10.4,'  ',F14.4,'   ',F15.3)
/COM, ----------------------------------------------------------------------------
/OUT,
*LIST,vm261,vrt
FINISH
/EXIT,NOSAVE


! Copyright (C) 2024 ANSYS, Inc. and/or its affiliates.
! SPDX-License-Identifier: MIT
! 
!
! Permission is hereby granted, free of charge, to any person obtaining a copy
! of this software and associated documentation files (the "Software"), to deal
! in the Software without restriction, including without limitation the rights
! to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
! copies of the Software, and to permit persons to whom the Software is
! furnished to do so, subject to the following conditions:
!
! The above copyright notice and this permission notice shall be included in all
! copies or substantial portions of the Software.
!
! THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
! IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
! FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
! AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
! LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
! OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
! SOFTWARE.

/COM,ANSYS MEDIA REL. 2023R2 (05/12/2023) REF. VERIF. MANUAL: REL. 2023R2
/verify,vm-nr1677-01-3a
/title,vm-nr1677-01-3a,NRC Piping Benchmark Problems,Volume 1,Problem 3

/com,****************************************************************************
/com,
/com, Reference: Piping Benchmark Problems
/com,        NUREC/CR--1677-Vol.1
/com,                P.Bezier, M.Hartzman, M.Reich
/com,            August 1980
/com,
/com, Elements used: Pipe289, Elbow290, Mass21
/com,
/com, Results:
/com, The following results are outputted
/com, 1. Frequencies obtained from modal solution.
/com, 2. Maximum nodal displacements and rotations obtained from spectrum solution.
/com, 3. Element forces and moments obtained from spectrum solution.
/com,
/com,****************************************************************************


/out,scratch

/prep7

et,1,pipe289,,,,2                         ! Element 1 - PIPE288 with cubic shape function

et,2,elbow290,,6                        ! Element 2 - ELBOW290

et,3,combin14                           ! Element 3 - COMBIN14
keyopt,3,2,1                            ! UX Degree of Freedom

et,4,combin14                           ! Element 4 - COMBIN14
keyopt,4,2,2                            ! UY Degree of Freedom

et,5,combin14                           ! Element 5 - COMBIN14
keyopt,5,2,3                            ! UZ Degree of Freedom

et,6,mass21                             ! Element 6 - MASS21
keyopt,6,3,2                            ! 3D Mass with Rotary Inertia

/com, Material Properties
/com,*********************

mp,ex,1,24e6
mp,prxy,1,.3
mp,dens,1,0.001057

mp,ex,2,24e6
mp,prxy,2,.3
mp,dens,2,0.001057

/com, Real Constants
/com,****************

sectype,1,pipe
secdata,7.289,0.241,24                          ! Pipe section 1

r,3,0.1e+5                                      ! Real Constant Set 3
r,4,0.1e+9                                      ! Real Constant Set 4
r,5,0.1e+11                                     ! Real Constant Set 5
r,6,1.518                                       ! Real Constant Set 6

/com, Nodes
/com,*******

k,1,0,0,0
k,2,0,54.45,0
k,3,0,108.9,0
k,301,0.675,116,0
k,101,2.76,123,0
k,302,6.13,129,0
k,4,10.632,134.568,0
k,102,22.4,142,0
k,5,36.3,145.2,0
k,6,54.15,145.2,0
k,7,72.0,145.2,0
k,701,79.1,145,0.697
k,103,85.9,145,2.76
k,702,92.2,145,6.12
k,8,97.668,145.2,10.632
k,801,102,145,16.1
k,104,106,145,22.4
k,802,108,145,29.2
k,9,108.3,145.2,36.3
k,10,108.3,145.2,56.8
k,11,108.3,145.2,77.3
k,12,108.3,145.2,97.8
k,13,108.3,145.2,118.3
k,14,108.3,145.2,188.8
k,401,108,146,196
k,201,108,148,203
k,402,108,151,209
k,105,108,156,214
k,202,108,168,222
k,15,108.3,181.5,225.1
k,16,108.3,236,225.1
k,17,108.3,290,225.1
k,18,148.3,145.2,97.8
k,19,188.3,145.2,97.8
k,203,202,145,95.1
k,106,214,145,87.2
k,601,219,145,81.7
k,204,222,145,75.4
k,602,224,145,68.6
k,20,224.6,145.2,61.5
k,21,224.6,145.2,20

/com, Elastic support Nodes
/com,***********************

k,22,1,0,0
k,23,0,1,0
k,24,0,0,1
k,25,72,145.2,-1
k,26,109.3,145.2,36.3
k,27,108.3,146.2,77.3
k,28,108.3,146.2,118.3
k,29,107.3,182.5,226.5
k,30,109.3,290,225.1
k,31,108.3,291,225.1
k,32,108.3,290,226.1
k,33,225.6,145.2,20
k,34,224.6,146.2,20
k,35,224.6,145.2,21

/com, Straight Pipe (Tangent Elements)
/com,**********************************

l,1,2
l,2,3
l,5,6
l,6,7
l,9,10
l,10,11
l,11,12
l,12,13
l,13,14
l,15,16
l,16,17
l,12,18
l,18,19
l,20,21		!Line #14

/com, Curved pipe elements
/com,**********************

larch,3,101,301	!Line #15
larch,101,4,302
larch,4,5,102
larch,7,103,701
larch,103,8,702
larch,8,104,801
larch,104,9,802
larch,14,201,401
larch,201,105,402
larch,105,15,202
larch,19,106,203
larch,106,204,601
larch,204,20,602	!Line #27


/com, **********************************
/com, Meshing for Straight pipe
/com, **********************************

type,1
secnum,1
mat,1

lsel,s,line,,1,14
allsel,below,line
lesize,all,,,2
lmesh,all

allsel,all,all

/com, **********************************
/com, Meshing for bend pipe
/com, **********************************

type,2
secnum,1
mat,1

lsel,s,,,15,27
allsel,below,line
lesize,all,,,2
lmesh,all

allsel,all,all

/com, ************************************************************
/com,  Using ELBOW, to convert some PIPE289 to ELBOW290
/com, ************************************************************

elbow,on,,,sect

/com, Elastic supports and anchors

l,11,27		!Line #28
l,13,28		!Line #29

real,3
type,4
lsel,s,,,28,29
allsel,below,line
lesize,all,,,1
lmesh,all
allsel

l,9,26		!Line #30	
l,15,29		!Line #31

real,4
type,3
lsel,s,,,30,31
allsel,below,line
lesize,all,,,1
lmesh,all
allsel


l,7,25		!Line #32
type,5
lsel,s,,,32
allsel,below,line
lesize,all,,,1
lmesh,all
allsel

l,1,22		!Line #33
l,17,30		!Line #34
l,21,33		!Line #35

real,5
type,3

lsel,s,,,33,35
allsel,below,line
lesize,all,,,1
lmesh,all
allsel

l,1,23		!Line #36
l,17,31		!Line #37	
l,21,34		!Line #38
 
type,4
lsel,s,,,36,38
allsel,below,line
lesize,all,,,1
lmesh,all
allsel

l,1,24		!Line #39
l,17,32		!Line #40
l,21,35		!Line #41

type,5
lsel,s,,,39,41
allsel,below,line
lesize,all,,,1
lmesh,all
allsel

/com,******************************
/com, **rotate nodes with less than 3 supports**

wplane,,108.3,181.5,225.1,107.3,182.5,226.5,108.3,236,225.1
cswplane,11,0
nrotat,40
nrotat,113
csys,0


/com, Mass Elements
/com,***************

type,6
real,6
e,18

/com, Constraints
/com,*************

ksel,s,kp,,22,35
nslk
d,all,all
allsel,all


ksel,s,kp,,1
nslk
d,all,rotx,,,,,roty,rotz
allsel

ksel,s,kp,,21
nslk
d,all,rotx,,,,,roty,rotz
allsel

/com, Loading
/com,*********

/com, **Pressure on ELBOW290 elements affects stiffness because of pressure term**
esel,s,ename,,290
sfe,all,1,pres,,350
allsel,all
save
finish

/com,
/com,=============
/com,   Modal Solve
/com,=============
/com,

/solution
antype,modal                            ! Perform Modal Analysis
modopt,lanb,10
mxpand,,,,yes                           ! Expand solution with Element Calculations ON
lumpm,on                                        ! Use Lumped Mass Approximation
solve
save
fini

/post1
/out,
/com, *******************************
/com,  Frequencies from Modal solve
/com, *******************************
set,list
finish

/out,scratch
/com,
/com,================
/com,   Spectrum Solve
/com,================
/com,

/com, **Spectrum in X, Y, and Z directions**
/com, **Spectra values in Y direction are 67% of the values for the X and Z directions**

/solution
antype,spectr                           ! Perform Spectrum Analysis
spopt,sprs                                      ! Single Point Excitation Response Spectrum
dmprat,0.02                                     ! Damping Ratio
grp,0.001                                       ! Group Modes based on Significance Level
svtyp,2                                 ! Seismic Acceleration Response Loading

sed,1                                           ! Excitation in X direction
freq
freq,3.1,4,5,5.81,7.09,8.77,9,10,10.99
freq,14.08,17.24,20,25,30,34.97,40,45,50
freq,588.93
sv,0.02,400,871,871,700,1188,1188,1090,733,440
sv,0.02,775,775,668,533,444,380,349,324,306
sv,0.02,145
solve

sed,,1                                  ! Excitation in Y direction
freq
freq,3.1,4,5,5.81,7.09,8.77,9,10,10.99
freq,14.08,17.24,20,25,30,34.97,40,45,50
freq,588.93
sv,0.02,267,581,581,467,792,792,727,489,293
sv,0.02,517,517,445,355,296,253,232,216,204
sv,0.02,97
solve

sed,,,1                                 ! Excitation in Z direction
freq
freq,3.1,4,5,5.81,7.09,8.77,9,10,10.99
freq,14.08,17.24,20,25,30,34.97,40,45,50
freq,588.93
sv,0.02,400,871,871,700,1188,1188,1090,733,440
sv,0.02,775,775,668,533,444,380,349,324,306
sv,0.02,145
solve
fini

/post1
/input,,mcom
*GET,AdisX,NODE,96,U,X
*GET,AdisY,NODE,78,U,Y
*GET,AdisZ,NODE,40,U,Z
*GET,ArotX,NODE,84,ROT,X
*GET,ArotY,NODE,76,ROT,Y
*GET,ArotZ,NODE,10,ROT,Z

/out,
/com, *========================================================
/com, * Maximum nodal displacements and rotations comparsion
/com, *========================================================
*stat,AdisX
*stat,AdisY
*stat,AdisZ
*stat,ArotX
*stat,ArotY
*stat,ArotZ


/com, *========================================================
/com, * Element Forces and Moments Comparison
/com, *==========================================================
/out,scratch

/com,==========
/com,    Node I
/com,==========

/com, Element #28 (Pipe289 elements)
/com,************************


esel,s,elem,,28
etable,pxi_28,smisc,1
etable,vyi_28,smisc,6
etable,vzi_28,smisc,5
etable,txi_28,smisc,4
etable,myi_28,smisc,2
etable,mzi_28,smisc,3
esel,all

/com, Element #50 (Elbow 290 elements)
/com,*************************

esel,s,elem,,50

etable,pxi_50,smisc,1
etable,vyi_50,smisc,6
etable,vzi_50,smisc,5
etable,txi_50,smisc,4
etable,myi_50,smisc,2
etable,mzi_50,smisc,3
esel,all


/com,==========
/com,  Node J
/com,==========

/com, Element #28 (Pipe289 elements)  
/com,************************

esel,s,elem,,28

etable,pxj_28,smisc,14
etable,vyj_28,smisc,19
etable,vzj_28,smisc,18
etable,txj_28,smisc,17
etable,myj_28,smisc,15
etable,mzj_28,smisc,16
esel,all


/com, Element #50 (Elbow290 elements)
/com,************************
esel,s,elem,,50

etable,pxj_50,smisc,36
etable,vyj_50,smisc,41
etable,vzj_50,smisc,40
etable,txj_50,smisc,39
etable,myj_50,smisc,37
etable,mzj_50,smisc,38
esel,all

allsel,all
/out,

/com, **********************************************
/com,  Element forces and moments at element 28, node i
/com, ************************************************

pretab,pxi_28,vyi_28,vzi_28,txi_28,myi_28,mzi_28

/com, **********************************************
/com,  Element forces and moments at element 28, node j
/com, ************************************************

pretab,pxj_28,vyj_28,vzj_28,txj_28,myj_28,mzj_28

/com, **********************************************
/com,  Element forces and moments at element 50, node i
/com, ************************************************

pretab,pxi_50,vyi_50,vzi_50,txi_50,myi_50,mzi_50

/com, **********************************************
/com,  Element forces and moments at element 50, node j
/com, ************************************************
pretab,pxj_50,vyj_50,vzj_50,txj_50,myj_50,mzj_50



/com,----------------------------------------------------------------------------

/com, *****************************
/com,  Reaction forces
/com, ******************************

prrsol

finish
/exit,nosave


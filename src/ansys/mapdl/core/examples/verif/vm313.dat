/COM,ANSYS MEDIA REL. 2023R1 (11/04/2022) REF. VERIF. MANUAL: REL. 2023R1
/VERIFY,VM313
/TITLE,VM313, PULL-IN VOLTAGE OF ELECTROSTATICALLY ACTUATED BEAM
/COM,
/COM, REF: CHOWDHURY, S., AHMADI, M., & MILLER, W.C. (2006)
/COM, PULL-IN VOLTAGE STUDY OF ELECTROSTATICALLY ACTUATED FIXED-FIXED
/COM, BEAMS USING A VLSI ON-CHIP INTERCONNECT CAPACITANCE MODEL
/COM, JOURNAL OF MICROELECTROMECHANICAL SYSTEMS, (15) 3, 639 - 651.
/COM,

/COM, PARAMETER FOR CREATING THE CDB FILES TO STORE GEOMETRY AND MESH DATA
_GEOMGEN = 0   ! SET TO 1 TO WRITE CDB FILES IF ANY INPUT FILE MODIFICATIONS
/OUT,SCRATCH_vm313

! MODEL PARAMETERS
BL=300     ! BEAM LENGTH, UM
BW=10      ! BEAM WIDTH, UM
BH=3       ! BEAM THICKNESS, UM
EG=2.7     ! ELECTRODE GAP, UM
FW=12      ! FARFIELD IN CROSS DIRECTION, UM
FH=6       ! FARFIELD ABOVE BEAM, UM

! NUMBER OF DIVISIONS ALONG EACH DIRECTION
NBL=100
NBW=4
NBH=1
NEG=1
NFW=4
NFH=2

EX=169E3    ! ELASTIC MODULUS OF THE BEAM, MPA
PRXY=0.06   ! POISSON'S RATIO

EPS0=8.854E-6  ! FREE SPACE PERMITTIVITY, PF/UM

V0=180      ! APPLIED VOLTAGE, VOLT

SIG0=80     ! INITIAL STRESS, MPA

! TO CREATE WHITE BACKGROUND
/RGB,INDEX,100,100,100, 0
/RGB,INDEX, 80, 80, 80,13
/RGB,INDEX, 60, 60, 60,14
/RGB,INDEX, 0, 0, 0,15

/PREP7
*IF,_GEOMGEN,EQ,1,THEN

ET,1,185,,3         ! SIMPLIFIED ENHANCED STRAIN
ET,2,226,1001       ! ELECTROSTATIC-STRUCTURAL ANALYSIS
KEYOPT,2,4,1        ! ELASTIC AIR

MP,EX,1,EX
MP,PRXY,1,PRXY

EMUNIT,EPZRO,EPS0
MP,PERX,2,1         ! RELATIVE PERMITTIVITY OF AIR
MP,EX,2,1E-6
MP,PRXY,2,0

BLOCK,-BL/2,BL/2,-BW/2,BW/2,,EG
BLOCK,-BL/2,BL/2,-BW/2-FW,-BW/2,,EG
BLOCK,-BL/2,BL/2,BW/2,BW/2+FW,,EG

BLOCK,-BL/2,BL/2,-BW/2,BW/2,EG,EG+BH
BLOCK,-BL/2,BL/2,-BW/2-FW,-BW/2,EG,EG+BH
BLOCK,-BL/2,BL/2,BW/2,BW/2+FW,EG,EG+BH

BLOCK,-BL/2,BL/2,-BW/2,BW/2,EG+BH,EG+BH+FH
BLOCK,-BL/2,BL/2,-BW/2-FW,-BW/2,EG+BH,EG+BH+FH
BLOCK,-BL/2,BL/2,BW/2,BW/2+FW,EG+BH,EG+BH+FH

VGLUE,ALL

! NOTE: THE FOLLOWING LINE SELECTIONS ARE BASED ON LINE LENGTHS.
! IF TWO LENGTHS ARE EQUAL (E.G. BW=FW), THEN THEIR SELECTION
! WILL BE MIXED.

LSEL,S,LENGTH,,BL
LESIZE,ALL,,,NBL

LSEL,S,LENGTH,,BW
LESIZE,ALL,,,NBW

LSEL,S,LENGTH,,BH
LESIZE,ALL,,,NBH

LSEL,S,LENGTH,,EG
LESIZE,ALL,,,NEG

LSEL,S,LENGTH,,FW
LESIZE,ALL,,,NFW

LSEL,S,LENGTH,,FH
LESIZE,ALL,,,NFH
LSEL,ALL

MSHMID,2
TYPE,2
MAT,2
VMESH,ALL

VSEL,S,LOC,X,0
VSEL,R,LOC,Y,0
VSEL,R,LOC,Z,EG+BH/2
ESLV,S
EMODIF,ALL,TYPE,1
EMODIF,ALL,MAT,1
ALLSE

CDWRITE,db,vm313,cdb
*ELSE
 CDREAD,db,vm313,cdb
*ENDIF

/SHOW,PNG
/VUP,1,Z
/VIEW,1,-1,-1,1
/PNUM,TYPE,1
/NUMB,1
EPLOT
/SHOW,CLOSE

NSEL,S,EXT
D,ALL,UX,0,,,,UY,UZ
NSEL,S,LOC,Z,0
D,ALL,VOLT,0
ALLSE

NSEL,S,LOC,Z,EG,EG+BH
NSEL,R,LOC,Y,-BW/2,BW/2
CP,NEXT,VOLT,ALL
NLOAD=NDNEXT(0)
ALLSEL

NC=NODE(0,0,EG)

ESEL,S,TYPE,,1
NSLE,S
INISTATE,SET,DTYP,STRE
INISTATE,DEFINE,,,,,SIG0
ALLSE
FINI

!--------------------------------------------------------------------------------

/SOLU
ANTYPE,STAT
OUTRES,ALL,ALL
NLGEOM,ON
NEQIT,50
TIME,1
NSUBST,50

D,NLOAD,VOLT,V0

TIME,1
DELTIM,2E-2,1E-6,2E-2
AUTOTS,ON
CUTC,ULIMIT,EG*1E-2
NCNV,2
SOLVE
FINI

!---------------------------------------------------------------------------------

/POST1
SET,LAST
SET,PREVIOUS
UPI=-UX(NLOAD)
VPI=VOLT(NLOAD)
FINI

/POST26
NSOL,3,NC,VOLT,,VOLT
NSOL,4,NC,U,Z,'UZ VS VOLT'
PRVAR,3,4
/SHOW,PNG
XVAR,3
/XRANGE,0,V0
/YRANGE,0,-EG
/AXLAB,X,VOLTAGE
/AXLAB,Y,DISPLACEMENT
PLVAR,4
/SHOW,CLOSE
FINI

! ANALYTICAL SOLUTION
*IF,BW/BH,GT,5,THEN
    EX=EX/(1-PRXY**2)
    SIG0=SIG0*(1-PRXY)
*ENDIF
W1=BW/EG
W2=BH/EG
BET=0
*IF,W1,GE,1,AND,W2,GE,.1,THEN
    BET=1
*ELSEIF,W1,LT,1,AND,W2,LT,.1,THEN
    BET=1.06*(EG/BW)**.25
*ENDIF
*IF,BET,NE,0,THEN
ALP=1.09*BET
CR=9.35
CB=32.36
CS=24.35
A1=CB*EX*BH**3/BL**4*EG/3
A2=CS*EX*BH/BL**4*(EG/3)**3
A3=CR*SIG0*BH/BL**2*EG/3
B1=5/(6*EG**2)
B2=.19/(EG**1.25*BW**.75)
B3=.4*BH**.5/(EG**1.5*BW)
VPI_TAR=SQRT((A1+A2+A3)/(ALP*EPS0*(B1+B2+B3)))
*ELSE
/COM, THE ANALYTICAL SOLUTION IS NOT APPLICABLE TO THIS RANGE OF BW, BH, AND EG
/EOF
*ENDIF

parsav
/OUT,vm313,vrt
/COM,
/COM, ---------------------------- VM313 RESULTS COMPARISON ----------------------
/COM,
/COM,                     |    TARGET    |  MECHANICAL APDL  |    RATIO
/COM,                     |              |                   |
*VWRITE,VPI_TAR,VPI,VPI/VPI_TAR
(2X,'PULL-IN VOLTAGE',5X,'|',2X,F10.5,2X,'|',4X,F10.5,5X,'|',2X,F9.6)
/OUT
*LIST,vm313,vrt

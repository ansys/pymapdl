/COM,ANSYS MEDIA REL. 2023R1 (11/04/2022) REF. VERIF. MANUAL: REL. 2023R1
/VERIFY,VM171
JPGPRF,500,100,1             ! MACRO TO SET PREFS FOR JPEG PLOTS
/SHOW,JPEG
/TITLE, VM171, PERMANENT MAGNET CIRCUIT WITH AN ELASTIC KEEPER
/COM,   MAGNETO-SOLID MECHANICS, MOON, PG. 275

/COM,   USING PLANE13 - 2-D COUPLED-FIELD SOLID
/PREP7
ET,1,PLANE13,4                  ! 2-D COUPLED-FIELD SOLID (UX,UY,AZ DOF'S)
ET,2,COMBIN14,,,2
R,1,1.6534E5                    ! SPRING CONSTANT
MP,EX,1,1E2                     
MP,EX,2,10E10
MP,EX,3,10E10
MP,EX,4,10E10
MP,NUXY,1,0.0
*REP,4,,1
MP,KXX,1,1                      ! APPLY DUMMY KXX TO PREVENT WARNING MSGS.
*REP,4,,1
EMUNIT,MKS                      ! MKS UNITS
MP,MURX,1,1                     ! AIR RELATIVE PERMEABILITY
MP,MURX,2,1E5                   ! IRON RELATIVE PERMEABILITY
MP,MURX,3,5.30504               ! PERMANENT MAGNET RELATIVE PERMEABILITY
MP,MGXX,3,149990.0              ! COERCIVE FORCE (X-DIRECTION)
MP,MURX,4,1E5                   ! KEEPER RELATIVE PERMEABILITY
N,1
N,6,5E-2
FILL
NGEN,6,6,1,6,1E-2,,1E-2
N,37,0,6E-2
N,38,5E-2,6E-2
MAT,3
E,2,3,9,8
EGEN,3,1,-1
MAT,2
E,1,2,8,7
EGEN,3,6,-1
E,5,6,12,11
EGEN,3,6,-1
MAT,4
E,25,26,32,31
EGEN,5,1,-1
MAT,1
E,19,20,26,25
E,23,24,30,29
TYPE,2                          ! SPRINGS
E,37,31
E,38,36
D,37,ALL,0,,38                  ! CONSTRAIN SPRING
ESEL,S,MAT,,2,3
NSLE
D,ALL,UX,0,,,,UY                ! CONSTRAIN PERMANENT MAGNET STRUCTURE
ESEL,ALL
NSEL,S,LOC,X,0
NSEL,A,LOC,X,5E-2
NSEL,A,LOC,Y,0
NSEL,A,LOC,Y,5E-2
D,ALL,AZ,0                      ! SET EXTERIOR FLUX-PARALLEL BOUNDARY
NSEL,ALL
CP,1,AZ,8,9,10,11,14            ! COUPLE INTERNAL NODES
CP,1,AZ,17,20,23,26
CP,1,AZ,27,28,29
NSLE                            ! SELECT ONLY NODES USED IN MODEL
D,ALL,UX,0                      ! CONSTRAIN ALL HORIZONTAL DISPLACEMENTS
D,ALL,TEMP,0                    ! CONSTRAIN UNUSED TEMP DOF
ESEL,S,MAT,,1                   ! SELECT KEEPER ELEMENTS
NSEL,S,LOC,Y,4E-2               ! SELECT NODES AT BOTTOM SURFACE
SF,ALL,MXWF                     ! APPLY MAXWELL PRESSURE SURFACE
NSEL,ALL
ESEL,ALL
FINISH
/SOLU    
NLGEOM,ON                       ! ACTIVATE LARGE DEFLECTION
CNVTOL,F                        ! SET FORCE CONVERGENCE (USE DEFAULTS)
CNVTOL,CSG                      ! SET CSG CONVERGENCE (.001% OF DEFAULT)
SOLVE
FINISH
/POST1
NSEL,S,LOC,Y,4e-2
PRNSOL,U,COMP                   ! PRINT NODAL DISPLACEMENTS
*GET,Y1,NODE,25,U,Y
*SET,Y,ABS(Y1)
ESEL,S,MAT,,3
NSLE
PRNSOL,B,COMP                   ! PRINT NODAL FLUX DENSITY IN PERMANENT MAGNET
*GET,B,NODE,2,B,X
NSEL,ALL
ESEL,S,MAT,,2,4                 ! SELECT ONLY ELEMENTS OF IRONAND KEEPER
/EDGE,1,1
/DSCALE,1,1                     ! TRUE SCALING OPTION
PLDISP,1                        ! DISPLAY DEFLECTED AND UNDEFLECTED SHAPE
*DIM,LABEL,CHAR,2,2
*DIM,VALUE,,2,3
LABEL(1,1) = 'DEFL ','MFLUX '
LABEL(1,2) = 'm','T'
*VFILL,VALUE(1,1),DATA,1.5E-3,.2496
*VFILL,VALUE(1,2),DATA,Y,B
*VFILL,VALUE(1,3),DATA,ABS(Y/1.5E-3),ABS(B/.2495)
SAVE,TABLE1
FINI
/CLEAR,NOSTART

/COM,   USING PLANE223 - 2-D 8-NODE COUPLED-FIELD SOLID
/PREP7
ET,1,PLANE223,10001             ! elastic air
keyop,1,4,1
ET,2,COMBIN14,,,2
ET,3,PLANE223,10001             ! ferromagnetic core, PM
keyop,3,4,0
ET,4,PLANE223,10001             ! keeper
keyop,4,4,0
R,1,1.6534E5                    ! SPRING CONSTANT
MP,EX,1,1E2                     
MP,EX,2,10E10
MP,EX,3,10E10
MP,EX,4,10E10
MP,NUXY,1,0.0
*REP,4,,1
MP,KXX,1,1                      ! APPLY DUMMY KXX TO PREVENT WARNING MSGS.
*REP,4,,1
EMUNIT,MKS                      ! MKS UNITS
MP,MURX,1,1                     ! AIR RELATIVE PERMEABILITY
MP,MURX,2,1E5                   ! IRON RELATIVE PERMEABILITY
MP,MURX,3,5.30504               ! PERMANENT MAGNET RELATIVE PERMEABILITY
MP,MGXX,3,149990.0              ! COERCIVE FORCE (X-DIRECTION)
MP,MURX,4,1E5                   ! KEEPER RELATIVE PERMEABILITY
N,1
N,6,5E-2
FILL
NGEN,6,6,1,6,1E-2,,1E-2
N,37,0,6E-2
N,38,5E-2,6E-2
type,3
MAT,3
E,2,3,9,8
EGEN,3,1,-1
MAT,2
E,1,2,8,7
EGEN,3,6,-1
E,5,6,12,11
EGEN,3,6,-1
type,4
MAT,4
E,25,26,32,31
EGEN,5,1,-1
type,1
MAT,1
E,19,20,26,25
E,23,24,30,29
TYPE,2                          ! SPRINGS
E,37,31
E,38,36
D,37,ALL,0,,38                  ! CONSTRAIN SPRING
ESEL,S,MAT,,2,3
NSLE
D,ALL,UX,0,,,,UY                ! CONSTRAIN PERMANENT MAGNET STRUCTURE
ESEL,ALL
NSEL,S,LOC,X,0
NSEL,A,LOC,X,5E-2
NSEL,A,LOC,Y,0
NSEL,A,LOC,Y,5E-2
D,ALL,AZ,0                      ! SET EXTERIOR FLUX-PARALLEL BOUNDARY
NSEL,ALL
CP,1,AZ,8,9,10,11,14            ! COUPLE INTERNAL NODES
CP,1,AZ,17,20,23,26
CP,1,AZ,27,28,29
NSLE                            ! SELECT ONLY NODES USED IN MODEL
D,ALL,UX,0                      ! CONSTRAIN ALL HORIZONTAL DISPLACEMENTS
FINISH
/SOLU    
NLGEOM,ON                       ! ACTIVATE LARGE DEFLECTION
CNVTOL,F                        ! SET FORCE CONVERGENCE (USE DEFAULTS)
CNVTOL,CSG                      ! SET CSG CONVERGENCE (.001% OF DEFAULT)
SOLVE
FINISH
/POST1
NSEL,S,LOC,Y,4e-2
PRNSOL,U,COMP                   ! PRINT NODAL DISPLACEMENTS
*GET,Y1,NODE,25,U,Y
*SET,Y,ABS(Y1)
ESEL,S,MAT,,3
NSLE
PRNSOL,B,COMP                   ! PRINT NODAL FLUX DENSITY IN PERMANENT MAGNET
*GET,B,NODE,2,B,X
NSEL,ALL
ESEL,S,MAT,,2,4                 ! SELECT ONLY ELEMENTS OF IRONAND KEEPER
/EDGE,1,1
/DSCALE,1,1                     ! TRUE SCALING OPTION
PLDISP,1                        ! DISPLAY DEFLECTED AND UNDEFLECTED SHAPE
*DIM,LABEL,CHAR,2,2
*DIM,VALUE,,2,3
LABEL(1,1) = 'DEFL ','MFLUX '
LABEL(1,2) = 'm','T'
*VFILL,VALUE(1,1),DATA,1.5E-3,.2496
*VFILL,VALUE(1,2),DATA,Y,B
*VFILL,VALUE(1,3),DATA,ABS(Y/1.5E-3),ABS(B/.2495)
SAVE,TABLE2
FINI

/NOPR
/COM
/OUT,vm171,vrt
/COM,------------------- VM171 RESULTS COMPARISON -------------
/COM,
/COM,                 |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
/COM,PLANE13:
RESUME,TABLE1
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'  ',E11.5,'  ',E14.5,' ',1F16.4)
/COM,
/COM,PLANE223:
RESUME,TABLE2
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'  ',E11.5,'  ',E14.5,' ',1F16.4)
/COM,
/COM,----------------------------------------------------------
/OUT
FINISH
*LIST,vm171,vrt

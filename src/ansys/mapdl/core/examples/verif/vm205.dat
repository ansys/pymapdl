/COM,ANSYS MEDIA REL. 2023R1 (11/04/2022) REF. VERIF. MANUAL: REL. 2023R1
/VERIFY,VM205
/TITLE,VM205,EARTH PRESSURE PROBLEM USING MOHR COULOMB MODEL
/COM,
/COM,  REFERENCE: DEEP EXCAVATION, THEORY AND PRACTICE,CHANG-YU OU,2006,PG:94
/COM,
JPGPRF,500,100,1
/SHOW,JPEG
/OUT,SCRATCH
/PREP7
BX=10				! WIDTH IN METERS
HY=10				! HEIGHT IN METERS
ESIZE=0.25			! ELEMENT SIZE

MP,EX,1,26.67E6		! YOUNG'S MODULUS
MP,NUXY,1,0.33		! POISSON'S RATIO
MP,DENS,1,1800		! DENSITY


/COM, NONLINEAR PARAMETERS FOR INTACT ROCK

PHIG=30			! FRICTION ANGLE
PHIR=30			! RESIDUAL FRICTION ANGLE
CG=0			! COHESION
CR=1.0*CG		! RESIDUAL COHESION
PSIG=30			! DILATANCY ANGLE

TB,MC,1,,,BASE
TBDATA,1,PHIG,CG,PSIG,PHIR,CR

BLC4,0,0,BX,HY

    
ET,1,PLANE182  		! 4 node plane element
KEYOPT,1,1,3    	! simplified enhanced strain formulation
KEYOPT,1,3,2    	! plane strain
  
ESIZE,ESIZE	   	    ! set element size
TYPE,1          	! set element type
MAT,1             	! set element material
MSHAPE,0,2D       	! quadrilateral mesh option
MSHKEY,1          	! mapped meshing
AMESH,ALL         	! mesh area
NSEL,S,LOC,Y,0
D,ALL,UY,0
ALLSEL,ALL
NSEL,S,LOC,X,0
NSEL,A,LOC,X,BX
D,ALL,UX,0
ALLSEL,ALL
FINISH

/SOLUTION
ANTYPE,STATIC      	! select static analysis
NROPT,FULL		  	! FULL Newton Raphson option
ACEL,0,9.81,0,     	! gravity load
ERESX,NO     	   	! Copy the integration point results      
DELTIM,1,0.01,1    	! set time stepping (load increments)
NEQIT,50           	! set maximum number of equilibrium iterations
CNVTOL,F,,0.001    	! set force convergence criterion
CNVTOL,U           	! set displacement convergence criterion
TIME,1             	! set time at end of load step
OUTRES,ALL,LAST    	! save results for last load increment
OUTRES,SVAR,LAST   	! save state variables for last load increment
SOLVE              	! solve first load step
FINISH


/SOLUTION
ANTYPE,STATIC,REST 		! restart analysis
UK=-0.03
NSEL,S,LOC,X,0
NLIST
ND=0
*GET,NNODES,NODE,0,COUNT
*DO,I,1,NNODES
ND=NDNEXT(ND)
U1=((UK/HY)*NY(ND))         
D,ND,UX,U1                  ! Specify UX=(UK/HY)* Ycoordinate of node
*ENDDO
ALLSEL,ALL
DELTIM,0.1,0.01,1         ! define time stepping (load increments)
NEQIT,50                  ! define maximum number of equilibrium iterations
CNVTOL,F,,0.001           ! define force convergence criterion
CNVTOL,U                  ! define displacement convergence criterion
TIME,2                    ! set time at end of load step
OUTRES,ALL,LAST           ! save results for last load increment
OUTRES,SVAR,LAST          ! save state variables for last load increment
SOLVE                     ! solve 4th load step
FINISH

/POST1
PATH,VERTICAL,2,30,20,
PPATH,1,0,0,0,0,0,
PPATH,2,0,0,HY,0,0,
PDEF,SY,S,Y
PDEF,SX,S,X
PDEF,SZ,S,Z
PCALC,DIV,KO,SX,SY,1,
PAGET,PATHDATA_,TABLES


PI=ACOS(-1)
TARGET_RESULT = TAN((30*PI)/180)**2     !From pg.94,Rankine's coefficient


*DIM,VALUE,,10,4

*VFILL,VALUE(1,1),DATA,PATHDATA_(3,2)
*VFILL,VALUE(2,1),DATA,PATHDATA_(5,2)
*VFILL,VALUE(3,1),DATA,PATHDATA_(7,2)
*VFILL,VALUE(4,1),DATA,PATHDATA_(9,2)
*VFILL,VALUE(5,1),DATA,PATHDATA_(11,2)
*VFILL,VALUE(6,1),DATA,PATHDATA_(13,2)
*VFILL,VALUE(7,1),DATA,PATHDATA_(15,2)
*VFILL,VALUE(8,1),DATA,PATHDATA_(17,2)
*VFILL,VALUE(9,1),DATA,PATHDATA_(19,2)
*VFILL,VALUE(10,1),DATA,PATHDATA_(21,2)

*VFILL,VALUE(1,2),DATA,PATHDATA_(3,8)
*VFILL,VALUE(2,2),DATA,PATHDATA_(5,8)
*VFILL,VALUE(3,2),DATA,PATHDATA_(7,8)
*VFILL,VALUE(4,2),DATA,PATHDATA_(9,8)
*VFILL,VALUE(5,2),DATA,PATHDATA_(11,8)
*VFILL,VALUE(6,2),DATA,PATHDATA_(13,8)
*VFILL,VALUE(7,2),DATA,PATHDATA_(15,8)
*VFILL,VALUE(8,2),DATA,PATHDATA_(17,8)
*VFILL,VALUE(9,2),DATA,PATHDATA_(19,8)
*VFILL,VALUE(10,2),DATA,PATHDATA_(21,8)

*VFILL,VALUE(1,3),DATA,TARGET_RESULT
*VFILL,VALUE(2,3),DATA,TARGET_RESULT
*VFILL,VALUE(3,3),DATA,TARGET_RESULT
*VFILL,VALUE(4,3),DATA,TARGET_RESULT
*VFILL,VALUE(5,3),DATA,TARGET_RESULT
*VFILL,VALUE(6,3),DATA,TARGET_RESULT
*VFILL,VALUE(7,3),DATA,TARGET_RESULT
*VFILL,VALUE(8,3),DATA,TARGET_RESULT
*VFILL,VALUE(9,3),DATA,TARGET_RESULT
*VFILL,VALUE(10,3),DATA,TARGET_RESULT

*VFILL,VALUE(1,4),DATA,PATHDATA_(3,8)/TARGET_RESULT
*VFILL,VALUE(2,4),DATA,PATHDATA_(5,8)/TARGET_RESULT
*VFILL,VALUE(3,4),DATA,PATHDATA_(7,8)/TARGET_RESULT
*VFILL,VALUE(4,4),DATA,PATHDATA_(9,8)/TARGET_RESULT
*VFILL,VALUE(5,4),DATA,PATHDATA_(11,8)/TARGET_RESULT
*VFILL,VALUE(6,4),DATA,PATHDATA_(13,8)/TARGET_RESULT
*VFILL,VALUE(7,4),DATA,PATHDATA_(15,8)/TARGET_RESULT
*VFILL,VALUE(8,4),DATA,PATHDATA_(17,8)/TARGET_RESULT
*VFILL,VALUE(9,4),DATA,PATHDATA_(19,8)/TARGET_RESULT
*VFILL,VALUE(10,4),DATA,PATHDATA_(21,8)/TARGET_RESULT

/COM,
/OUT,vm205,vrt
/COM,
/COM, -----------------------VM205 RESULTS COMPARISON------------
/COM,
/COM,
/COM,   | DEPTH | Mechanical APDL | TARGET | RATIO
/COM,
/COM, 
*VWRITE,VALUE(1,1),VALUE(1,2),VALUE(1,3),VALUE(1,4)
(F10.2,' ',F12.2,' ',F12.2,' ',F10.2)
/COM,
/COM,
/COM,--------------------------------------------------------------------
/OUT,
*list,vm205,vrt
FINISH
/out,scratch
/EXIT,NOSAVE


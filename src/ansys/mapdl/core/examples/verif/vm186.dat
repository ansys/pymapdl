/COM,ANSYS MEDIA REL. 150 (11/8/2013) REF. VERIF. MANUAL: REL. 150
/VERIFY,VM186
/PREP7  
JPGPRF,500,100,1             ! MACRO TO SET PREFS FOR JPEG PLOTS
/SHOW,JPEG
/TITLE, VM186, TRANSIENT ANALYSIS OF A SLOT EMBEDDED CONDUCTOR
C***            KONRAD, IEEE TRANS., MAGNETICS, VOL. MAG-18, NO. 1, JAN. 1982
/NOPR
ANTYPE,TRANS                  ! NONLINEAR TRANSIENT DYNAMIC ANALYSIS
ET,1,PLANE13                  ! PLANE13, AZ DOF, (FOR AIR)
ET,2,PLANE13,6                ! PLANE13, AZ VOLT DOF, (FOR CONDUCTOR)
EMUNIT,MUZRO,1                ! SET MUZERO=1
MP,MURX,1,1                   ! RELATIVE PERMEABILITY
MP,MURX,2,1                   ! RELATIVE PERMEABILITY (CONDUCTOR)
MP,RSVX,2,1                   ! RESISTIVITY (CONDUCTOR)
N,1
N,8,,7
FILL
NGEN,2,8,1,8,1,1
MAT,2
TYPE,2
E,1,2,10,9
EGEN,4,1,-1
MAT,1
TYPE,1
E,5,6,14,13
EGEN,3,1,-1
CP,1,AZ,1,9                   ! COUPLE AZ TO ENSURE 1-D SOLUTION
*REPEAT,5,1,,1,1
ESEL,,MAT,,2
NSLE
CP,6,VOLT,ALL                 ! COUPLE VOLT IN CONDUCTOR
ESEL,ALL
NSEL,S,LOC,Y,7
D,ALL,AZ,0                    ! FLUX-PARALLEL B.C.
NSEL,ALL
FINISH
/SOLU    
EQSLV,JCG,1E-9                ! USE THE JACOBI CONJUGATE GRADIENT SOLVER
T=1E-8                        ! INITIALIZE TIME PARAMETER
C=0                           ! INITIALIZE COUNTER PARAMETER
N=80                          ! NUMBER OF TIME INCREMENTS PER TURN
PI=2*ASIN(1)                  ! VALUE OF PI
CON=2*PI/N                    ! SET TIME INCREMENT
NEQIT,1                       ! 1 ITERATION PER TIME STEP
*CREATE,LOAD                  ! CREATE MACRO TO SET UP LOAD STEPS
TIME,T
I=4*SIN(T)                    ! CALCULATE CURRENT
F,1,AMPS,I                    ! APPLY CURRENT TO A NODE IN CONDUCTOR
T=T+CON                       ! INCREMENT TIME
C=C+1                         ! INCREMENT COUNTER
OUTRES,ALL,1
*IF,C,EQ,((N*.75)+1),THEN     ! SET FOR PRINTOUT AT DESIRED TIME POINTS
 OUTPR,,1
*ELSEIF,C,EQ,(N+1),THEN
 OUTPR,,1
*ELSE
 OUTPR,,0
*ENDIF
SOLVE
*END
*DO,I,1,81                    ! REPEAT MACRO EXECUTION
 *USE,LOAD                    ! EXECUTE MACRO
*ENDDO
FINISH
/POST26
NUMVAR,12                     ! INCREASE STORAGE ARRAY SIZE
ESOL,2,1,,NMISC,6,JE          ! STORE JE
*REPEAT,4,1,1
ESOL,6,1,,VOLUME              ! STORE VOLUME
*REPEAT,4,1,1
PROD,2,2,6                    ! CALCULATE IE=JE*VOLUME
*REPEAT,4,1,1,1
ADD,2,2,3,4,IE                ! SUMM IE OVER ALL CONDUCTOR ELEMENTS
ADD,10,2,5,,IE                ! IE TOTAL
ESOL,2,1,,SMISC,1,JS          ! STORE JS
*REPEAT,4,1,1
PROD,2,2,6                    ! CALCULATE IS=JS*VOLUME
*REPEAT,4,1,1,1
ADD,2,2,3,4,IS                ! SUM IS OVER ALL CONDUCTOR ELEMENTS
ADD,11,2,5,,IS                ! IS TOTAL
ESOL,2,1,,NMISC,7,JT          ! STORE JT
*REPEAT,4,1,1
PROD,2,2,6                    ! CALCULATE IT=JT*VOLUME
*REPEAT,4,1,1,1
ADD,2,2,3,4,IT                ! SUM IT OVER ALL CONDUCTOR ELEMENTS
ADD,12,2,5,,IT                ! IT TOTAL
/AXLAB,Y,CURRENT
/GROPT,AXNSC,2.0
PRVAR,10,11,12                ! PRINT EDDY, SOURCE, AND TOTAL CURRENT
PLVAR,10,11,12                ! DISPLAY EDDY, SOURCE, AND TOTAL CURRENT
FINISH
/POST1
SET,61,1,,,4.7124
*GET,A1,NODE,1,A,Z
*GET,A2,NODE,4,A,Z
*GET,A3,NODE,7,A,Z
SET,81,1,,,6.2832
*GET,A4,NODE,1,A,Z
*GET,A5,NODE,4,A,Z
*GET,A6,NODE,7,A,Z
*DIM,LABEL,CHAR,3,2
*DIM,VALUE,,3,3
LABEL(1,1) = 'NODE ','NODE ','NODE '
LABEL(1,2) = '1','4','7'
*VFILL,VALUE(1,1),DATA,-15.18,-14.68,-4
*VFILL,VALUE(1,2),DATA,A1,A2,A3
*VFILL,VALUE(1,3),DATA,ABS(A1/15.18),ABS(A2/14.68),ABS(A3/4)
SAVE,TABLE_1
*VFILL,VALUE(1,1),DATA,-3.26,-.92,0
*VFILL,VALUE(1,2),DATA,A4,A5,A6
*VFILL,VALUE(1,3),DATA,ABS(A4/3.26),ABS(A5/.92),
SAVE,TABLE_2
RESUME,TABLE_1
/COM
/OUT,vm186,vrt
/COM,------------------- VM186 RESULTS COMPARISON --------------
/COM,
/COM, VECTOR POTENTIAL |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
/COM,PLANE13
/COM
/COM,RESULTS AT T=(3*PI/2)
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.2,'  ',F14.2,'   ',1F15.3)
/NOPR
RESUME,TABLE_2
/COM,
/COM,RESULTS AT T=(2*PI)
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.2,'  ',F14.2,'   ',1F15.3)
/COM,-----------------------------------------------------------
/COM,
/OUT
FINISH

/CLEAR,NOSTART
/NOPR
/PREP7  
JPGPRF,500,100,1             ! MACRO TO SET PREFS FOR JPEG PLOTS
/SHOW,JPEG
/TITLE, VM186, TRANSIENT ANALYSIS OF A SLOT EMBEDDED CONDUCTOR
ANTYPE,TRANS                  ! NONLINEAR TRANSIENT DYNAMIC ANALYSIS
ET,1,PLANE233,0               ! PLANE233, AZ DOF, (FOR AIR)
ET,2,PLANE233,1,2             ! PLANE233, AZ VOLT DOF,EDDY CURRENTS (FOR CONDUCTOR)
EMUNIT,MUZRO,1                ! SET MUZERO=1
MP,MURX,1,1                   ! RELATIVE PERMEABILITY
MP,MURX,2,1                   ! RELATIVE PERMEABILITY (CONDUCTOR)
MP,RSVX,2,1                   ! RESISTIVITY (CONDUCTOR)
N,1
N,8,,7
FILL
NGEN,2,8,1,8,1,1
MAT,2
TYPE,2
E,1,2,10,9
EGEN,4,1,-1
MAT,1
TYPE,1
E,5,6,14,13
EGEN,3,1,-1
CP,1,AZ,1,9                   ! COUPLE AZ TO ENSURE 1-D SOLUTION
*REPEAT,5,1,,1,1
ESEL,,MAT,,2
NSLE
CP,6,VOLT,ALL                 ! COUPLE VOLT IN CONDUCTOR
ESEL,ALL
NSEL,S,LOC,Y,7
D,ALL,AZ,0                    ! FLUX-PARALLEL B.C.
NSEL,ALL
FINISH
/SOLU    
EQSLV,JCG,1E-9                ! USE THE JACOBI CONJUGATE GRADIENT SOLVER
T=1E-8                        ! INITIALIZE TIME PARAMETER
C=0                           ! INITIALIZE COUNTER PARAMETER
N=80                          ! NUMBER OF TIME INCREMENTS PER TURN
PI=2*ASIN(1)                  ! VALUE OF PI
CON=2*PI/N                    ! SET TIME INCREMENT
NEQIT,1                       ! 1 ITERATION PER TIME STEP
*CREATE,LOAD                  ! CREATE MACRO TO SET UP LOAD STEPS
TIME,T
I=4*SIN(T)                    ! CALCULATE CURRENT
F,1,AMPS,I                    ! APPLY CURRENT TO A NODE IN CONDUCTOR
T=T+CON                       ! INCREMENT TIME
C=C+1                         ! INCREMENT COUNTER
OUTRES,ALL,1
*IF,C,EQ,((N*.75)+1),THEN     ! SET FOR PRINTOUT AT DESIRED TIME POINTS
 OUTPR,,1
*ELSEIF,C,EQ,(N+1),THEN
 OUTPR,,1
*ELSE
 OUTPR,,0
*ENDIF
SOLVE
*END
*DO,I,1,81                    ! REPEAT MACRO EXECUTION
 *USE,LOAD                    ! EXECUTE MACRO
*ENDDO
FINISH
/POST26
NUMVAR,12                     ! INCREASE STORAGE ARRAY SIZE

ESOL,2,1,,NMISC,1,JT          ! STORE JT
*REPEAT,4,1,1
PROD,2,2,6                    ! CALCULATE IT=JT*VOLUME
*REPEAT,4,1,1,1
ADD,2,2,3,4,IT                ! SUM IT OVER ALL CONDUCTOR ELEMENTS
ADD,12,2,5,,IT                ! IT TOTAL
/AXLAB,Y,CURRENT
/GROPT,AXNSC,2.0
PRVAR,12                ! PRINT EDDY, SOURCE, AND TOTAL CURRENT
PLVAR,12                ! DISPLAY EDDY, SOURCE, AND TOTAL CURRENT
FINISH
/POST1
SET,61,1,,,4.7124
*GET,A1,NODE,1,A,Z
*GET,A2,NODE,4,A,Z
*GET,A3,NODE,7,A,Z
SET,81,1,,,6.2832
*GET,A4,NODE,1,A,Z
*GET,A5,NODE,4,A,Z
*GET,A6,NODE,7,A,Z
*DIM,LABEL,CHAR,3,2
*DIM,VALUE,,3,3
LABEL(1,1) = 'NODE ','NODE ','NODE '
LABEL(1,2) = '1','4','7'
*VFILL,VALUE(1,1),DATA,-15.18,-14.68,-4
*VFILL,VALUE(1,2),DATA,A1,A2,A3
*VFILL,VALUE(1,3),DATA,ABS(A1/15.18),ABS(A2/14.68),ABS(A3/4)
SAVE,TABLE_1
*VFILL,VALUE(1,1),DATA,-3.26,-.92,0
*VFILL,VALUE(1,2),DATA,A4,A5,A6
*VFILL,VALUE(1,3),DATA,ABS(A4/3.26),ABS(A5/.92),
SAVE,TABLE_2
RESUME,TABLE_1
/COM
/OUT,vm186,vrt,,APPEND
/COM,PLANE233
/COM
/COM,RESULTS AT T=(3*PI/2)
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.2,'  ',F15.2,'   ',1F15.3)
/NOPR
RESUME,TABLE_2
/COM,
/COM,RESULTS AT T=(2*PI)
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.2,'  ',F15.2,'   ',1F15.3)
/COM,-----------------------------------------------------------
/COM,
/OUT
FINISH
*LIST,vm186,vrt

/DELETE,TABLE_1
/DELETE,TABLE_2
/DELETE,LOAD
FINISH

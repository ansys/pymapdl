/COM,ANSYS MEDIA REL. 150 (11/8/2013) REF. VERIF. MANUAL: REL. 150
/VERIFY,VM203   
/SHOW,JPEG
JPGPRF,500,100,1             ! MACRO TO SET PREFS FOR JPEG PLOTS
/PREP7
/TITLE, VM203, DYNAMIC LOAD EFFECT ON SIMPLY-SUPPORTED THICK SQUARE PLATE
/COM REFERENCE: NAFEMS FORCED VIBRATION BENCHMARKS, TEST 21R
C***          USING SHELL281 ELEMENTS
ET,1,SHELL281	           ! DEFINE ELEMENT TYPE
SECTYPE,1,SHELL
SECDATA,1,1,0,5		     ! THICKNESS
MP,EX,1,200E9                ! DEFINE MATERIAL PROPERTIES
MP,NUXY,1,0.3
MP,ALPX,1,0.1E-5
MP,DENS,1,8000
N,1,0,0,0                    ! DEFINE MODEL
N,9,0,10,0
FILL
NGEN,5,40,1,9,1,2.5
N,21,1.25,0,0
N,29,1.25,10,0
FILL,21,29,3
NGEN,4,40,21,29,2,2.5
EN,1,1,41,43,3,21,42,23,2
EGEN,4,2,1
EGEN,4,40,1,4
FINISH
SAVE,MODEL
*CREATE,SOLVIT,MAC
/out,scratch
/SOLU
ANTYPE,MODAL                 ! DEFINE ANALYSIS TYPE AS MODAL VIBRATION
MODOPT,LANPCG,16
MXPAND,16,,,YES
SFE,ALL,,PRES,,-1E6          ! PRESS LOAD OF 1000,000 N/M**2
D,ALL,UX,0,,,,UY,ROTZ        ! APPLY CONSTRAINTS
D,1,UZ,0,0,9,1,ROTX
D,161,UZ,0,0,169,1,ROTX
D,1,UZ,0,0,161,20,ROTY
D,9,UZ,0,0,169,20,ROTY
SOLVE
*GET,F,MODE,1,FREQ
/out
FINISH
/SOLU
/TITLE, VM203, RANDOM VIBRATION , RESPONSE TO UNIFORM PSD FORCE
ANTYPE,SPECTR                 ! DEFINE ANALYSIS TYPE
SPOPT,PSD,2,ON                ! USE FIRST 2 MODES, CALC ELEM. STRESSES
PSDUNIT,1,PRES                ! DEFINE TYPE OF PSD AS A PRESSURE SPECTRUM 
DMPRAT,0.02
PSDFRQ,1,1,1.0,80.0
PSDVAL,1,1.0,1.0              ! IN N**2/HZ
SFEDELE,ALL,,PRES,,
LVSCALE,1                     ! USE AND SCALE THE LOAD VECTOR GENERATED AT MODAL ANALYSIS
PFACT,1,NODE
PSDRES,DISP,REL
PSDCOM
SOLVE
FINISH
/POST1
SET,3,1                       ! ONE SIGMA DISPLACEMENT SOLUTION RESULTS
/VIEW,1,2,3,4
PLNSOL,U,Z
PRNSOL,U,Z
L2=NODE(2,8,0)  
*GET,SIGEL2,NODE,L2,S,EQV   
NSEL,,NODE,,L2
PRNSOL,S,COMP
NSEL,ALL
FINISH
/SOLUTION
ANTYPE,HARMIC                ! REDEFINE ANALYSIS TYPE AND SOLVE AGAIN 
HROPT,MSUP                   ! USING MODE SUPERPOSITION HARMONIC ANALYSIS
HROUT,OFF,ON                 ! PRINT AMPLITUDE & PHASE, CLUSTER FREQUENCIES
KBC,1
HARFRQ,1,80
DMPRAT,0.02
NSUBSTEP,10
SOLVE
FINISH
/POST26
FILE,,rfrq
PRCPLX,1
NSOL,2,85,U,Z
PSDDAT,6,1,1.0,80,1.0
PSDTYP,2
PSDCAL,7,2
PSDPRT
PRVAR,2,7
*GET,P,VARI,7,EXTREM,VMAX
*STATUS,PARM
/AXLAB,Y,PSD  (M^2/HZ)
PLVAR,7
*DIM,LABEL,CHAR,2,2
*DIM,VALUE,,2,3
LABEL(1,1) = 'f ','PSD '
LABEL(1,2) = 'Hz','SQmmS/Hz'
*VFILL,VALUE(1,1),DATA,45.9,3.4018E-3
*VFILL,VALUE(1,2),DATA,F,P
*VFILL,VALUE(1,3),DATA,ABS(F/45.9),ABS(P/(3.4018E-3))
FINISH
*END
SOLVIT
SAVE,TABLE_1
/NOPR
RESUME,TABLE_1
/COM
/OUT,vm203,vrt
/COM,------------------- VM203 RESULTS COMPARISON --------------
/COM,
/COM,                 |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
/COM, SHELL281
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F11.6,'  ',F15.6,'   ',1F16.3)
/COM,
/COM,
/COM,-----------------------------------------------------------
/COM,
/OUT
FINISH
*LIST,vm203,vrt
/DELETE,MODEL
/DELETE,SOLVIT,MAC
/DELETE,TABLE_1

/COM,ANSYS MEDIA REL. 2023R1 (11/04/2022) REF. VERIF. MANUAL: REL. 2023R1
/VERIFY,VM299
/TITLE,VM299,SOUND PRESSURE LEVEL IN A FLAT ROOM
/COM,
/COM, REFERENCE: A.BILLON,J.PICAUT,"INTRODUCING ATMOSPHERIC ATTENUATION
/COM,            WITHIN A DIFFUSION MODEL FOR ROOM-ACOUSTIC PREDICTIONS"
/COM,            MARCH 2008.
/COM,

/PREP7
/NOPR
! ROOM SIZE
LX=30
LY=30
LZ=3
VOL=LX*LY*LZ
SURF=2*(LX*LY+LY*LZ+LX*LZ)
MFP=4*VOL/SURF

! MATERIAL PROPERTIES
C0 = 343
RHO = 1.21

ROOMD=MFP*C0/3
ATTN=0.01
ROOMDP=ROOMD/(1.+ATTN*MFP)

ALPHA=0.1
WS=1.E-2

! DEFINE MATERIALS
MP,DENS,1,RHO
MP,SONC,1,C0
TB,AFDM,1,,,ROOM
TBDATA,1,ROOMDP,ATTN

! GENERATE GEOMETRY
H=0.5

*DIM,A,ARRAY,3
*DIM,B,ARRAY,3
*DIM,C,ARRAY,3
A(1)=0
A(2)=2.
A(3)=LX
B(1)=0
B(2)=2.
B(3)=LY
C(1)=0
C(2)=1.
C(3)=LZ

*DO,I,1,2
  *DO,J,1,2
    *DO,K,1,2
      BLOCK,A(I),A(I+1),B(J),B(J+1),C(K),C(K+1)
    *ENDDO
  *ENDDO
*ENDDO
VGLUE,ALL

ET,1,220,3,4

TYPE,1
MAT,1
ESIZE,H
VMESH,9,15,1
TYPE,1
MAT,1
ESIZE,H
MSHAPE,0,3D
VMESH,1

NSEL,S,LOC,X,0
NSEL,A,LOC,X,LX
NSEL,A,LOC,Y,0
NSEL,A,LOC,Y,LY
NSEL,A,LOC,Z,0
NSEL,A,LOC,Z,LZ
SF,ALL,ATTN,ALPHA
ALLS

NSEL,S,LOC,X,A(2)
NSEL,R,LOC,Y,B(2)
NSEL,R,LOC,Z,C(2)
BF,ALL,MASS,WS
ALLS
FINISH

/SOLU
/OUT,SCRATCH
SOLVE
FINISH

/POST1
SET,LAST

PATH,X_SPL,2,,15
PPATH,1,NODE,0,15,1
PPATH,2,NODE,30,15,1
PDEF,UX,U,X,NOAV
PDEF,SPLX,SPL,,NOAV
/OUT
PRPATH,UX,SPLX
/OUT,SCRATCH
/GROPT,DIVX,15
/YRANGE,71,82
/GROPT,DIVY,11
/SHOW,PNG
PLPATH,UX,SPLX
/SHOW,CLOSE

N1=NODE(5,15,1)
N2=NODE(10,15,1)
N3=NODE(15,15,1)
N4=NODE(20,15,1)
N5=NODE(25,15,1)

*GET,EN_1,NODE,N1,ENKE
*GET,EN_2,NODE,N2,ENKE
*GET,EN_3,NODE,N3,ENKE
*GET,EN_4,NODE,N4,ENKE
*GET,EN_5,NODE,N5,ENKE

PREF=2E-5
PI=ACOS(-1)

SPL_1=10*LOG10((RHO*EN_1*C0**2)/PREF**2)
SPL_2=10*LOG10((RHO*EN_2*C0**2)/PREF**2)
SPL_3=10*LOG10((RHO*EN_3*C0**2)/PREF**2)
SPL_4=10*LOG10((RHO*EN_4*C0**2)/PREF**2)
SPL_5=10*LOG10((RHO*EN_5*C0**2)/PREF**2)

*DIM,LABEL,CHAR,5
*DIM,VALUE,ARRAY,5,3
*DIM,VALUE_REF,ARRAY,5,3
*DIM,VALUE_RATIO,ARRAY,5,3

LABEL(1,1)='X = 5 m','X = 10 m','X = 15 m','X = 20 m','X = 25 m'
*VFILL,VALUE(1,1),DATA,%SPL_1%,%SPL_2%,%SPL_3%,%SPL_4%,%SPL_5%
*VFILL,VALUE_REF(1,2),DATA,80.0,79.0,77.5,76.0,74.5
*VFILL,VALUE_RATIO(1,3),DATA,ABS(SPL_1/80.0),ABS(SPL_2/79.0),ABS(SPL_3/77.5),ABS(SPL_4/76.0),ABS(SPL_5/74.5)

/OUT,vm299,vrt
/COM,
/COM,--------------- RESULT COMPARISON -----------------
/COM,
/COM,      |  TARGET  |  MECHANICAL APDL  | RATIO
/COM,
*VWRITE,LABEL(1,1),VALUE_REF(1,2),VALUE(1,1),VALUE_RATIO(1,3)
(1X,A8,'   ',F7.3,'  ',F7.3,'   ',F7.3)
/COM,
/OUT,
*LIST,vm299,vrt
FINISH

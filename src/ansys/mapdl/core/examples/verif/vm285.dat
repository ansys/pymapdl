/COM,ANSYS MEDIA REL. 2023R1 (11/04/2022) REF. VERIF. MANUAL: REL. 2023R1
/VERIFY,VM285
/TITLE,VM285, C* INTEGRAL CALCULATION FOR A SINGLE-EDGED CRACKED PLATE
/COM
/COM   REFERENCE: MELVIN F.KANNINEN, CARL H.POPELAR, ADVANCED
/COM              FREACTURE MECHANICS,OXFORD UNIVERSITY PRESS,PG.550 
JPGPRF,500,100,1

/COM, PARAMETER FOR CREATING THE CDB FILES TO STORE GEOMETRY AND MESH DATA
/COM,
_GEOMGEN = 0   ! SET TO 1 TO WRITE CDB FILES IF ANY INPUT FILE MODIFICATIONS
PARSAV
/OUT,vm285_scratch

/PREP7
*IF,_GEOMGEN,EQ,1,THEN
/COM, PARAMETRIC INPUT FOR CREEP CONSTANT !(SECONDARY CREEP)
C1=5.E-12
C2=3
C3=0
C4=0
/COM, TIME PARAMETER
HOUR=1000
/COM, ELASTIC CONSTANT
MP,EX,1,200E3               ! YOUNG'S MODULUS
MP,NUXY,1,0.3               
TB,CREEP,1,,,1              ! CREEP MATERIAL MODEL
TBDATA,1,C1,C2,C3,C4

ET,1,PLANE182               ! PLANE 182 ELEMENT
KEYOPT,1,3,2		        ! PLANE STRAIN 

W = 20			            ! WIDTH OF THE PLATE
A = w*1/8	                ! CRACK LENGTH
L = 100			            ! LENGTH OF THE PLATE

K,1,0,0,0
K,2,A,0,0                   !CRACK TIP
K,3,W,0,0
K,4,W,5,0
K,5,W,L,0
K,6,0,L,0
K,7,0,5,0

A,1,2,3,4,7
A,7,4,5,6
AGLUE,ALL

KSCON,2,A/20,0,12,,10       ! CRACK TIP ELEMENTS
ESIZE,0.3

AMESH,1

ESIZE,3
AMESH,2
ALLSEL,ALL

CDWRITE,DB,vm285,cdb
 *ELSE
  CDREAD,DB,vm285,cdb
*ENDIF

/OUT,
NSEL,S,LOC,X,A,W            ! APPLY BOUNDARY CONDITIONS
NSEL,R,LOC,Y,0
D,ALL,UY,0
NSEL,ALL

NSEL,S,LOC,X,w,w
NSEL,R,LOC,Y,0
D,ALL,UX,0
NSEL,ALL
FINISH

/SOLUTION
ANTYPE,STATIC
NSEL,S,LOC,X,A,A
NSEL,R,LOC,Y,0
CM,CRACK1,NODE              !DEFINE THE CRACK TIP NODE COMPONENT
ALLSEL,ALL
CINT,NEW,1
CINT,TYPE,CSTAR             !CALCULATE C* INTEGRAL
CINT,CTNC,CRACK1            !CRACK ID
CINT,NCON,10                !NUMBER OF CONTOURS
CINT,SYMM,ON                !SYMMETRY ON
CINT,NORM,0,2
ALLSEL,ALL

NSEL,S,LOC,Y,L
SF,ALL,PRES,-150            ! APPLY TENSILE LOAD
NSEL,ALL
/OUT,vm285_scratch,,,APPEND
RATE,OFF
DELT,1.E-8,1.E-9, 1.E-8     ! TIME STEPS
TIME,1E-8                   ! END TIME
OUTRES,ALL,ALL
SOLVE

RATE, ON, ON
DELT,100,1,100              ! TIME STEPS
AUTOS,ON
TIME,HOUR                   !HOLD FOR 1000HOURS
OUTRES,ALL,ALL
ALLSEL,ALL
SOLVE
FINISH

/POST1
SET,LAST
PRCINT,1                   !GET THE C* INTEGRAL VALUES
*GET,J1,CINT,1,CTIP,2,,1,,CSTA
*GET,J2,CINT,1,CTIP,2,,2,,CSTA
*GET,J3,CINT,1,CTIP,2,,3,,CSTA
*GET,J4,CINT,1,CTIP,2,,4,,CSTA
*GET,J5,CINT,1,CTIP,2,,5,,CSTA
*GET,J6,CINT,1,CTIP,2,,6,,CSTA
*GET,J7,CINT,1,CTIP,2,,7,,CSTA
*GET,J8,CINT,1,CTIP,2,,8,,CSTA
*GET,J9,CINT,1,CTIP,2,,9,,CSTA
*GET,J10,CINT,1,CTIP,2,,10,,CSTA
JCSTAR = (ABS(J2)+ABS(J3)+ABS(J4)+ABS(J5)+ABS(J6)+ABS(J7)+ABS(J8)+ABS(J9))/8
*STAT,JCSTAR
JC_REFERENCE=0.0319
JC_RATIO = ABS(JCSTAR/JC_REFERENCE)
/COM
/OUT,vm285,vrt
/COM,------------------- VM285 RESULTS COMPARISON --------------
/COM,
/COM,           |  TARGET  |   Mechanical APDL  |  RATIO
/COM,
/COM, ****************************************
/COM,  USING PLANE 182 ELEMENT (2-D ANALYSIS)
/COM, *****************************************
/COM,
*VWRITE,'J* INTEGRAL',JC_REFERENCE,JCSTAR,JC_RATIO
(1X,A8,'   ',F12.3,'  ',F12.3,'   ',F12.2)
/NOPR
/COM,
/COM,-----------------------------------------------------------
/OUT,
FINISH
*LIST,vm285,vrt
/OUT,SCRATCH
/DEL,SCRATCH



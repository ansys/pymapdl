! Copyright (C) 2024 ANSYS, Inc. and/or its affiliates.
! SPDX-License-Identifier: MIT
! 
!
! Permission is hereby granted, free of charge, to any person obtaining a copy
! of this software and associated documentation files (the "Software"), to deal
! in the Software without restriction, including without limitation the rights
! to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
! copies of the Software, and to permit persons to whom the Software is
! furnished to do so, subject to the following conditions:
!
! The above copyright notice and this permission notice shall be included in all
! copies or substantial portions of the Software.
!
! THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
! IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
! FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
! AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
! LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
! OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
! SOFTWARE.

/COM,ANSYS MEDIA REL. 2023R2 (05/12/2023) REF. VERIF. MANUAL: REL. 2023R2
/VERIFY,VM20
/TITLE, VM20, CYLINDRICAL MEMBRANE UNDER PRESSURE
C*** STR. OF MATLS., TIMOSHENKO, PART 2, 3RD ED., PAGE 121, ART. 25
C*** USING SHELL181 - FINITE STRAIN MEMBRANE
/PREP7
ANTYPE,STATIC
ET,1,SHELL181
KEYOPT,1,1,1            ! MEMBRANE STIFFNESS ONLY
KEYOPT,1,3,2            ! FULL INTEGRATION
MP,EX,1,30E6
MP,NUXY,1,0.3
SECT,1,SHELL
SECD,1,1                ! THICKNESS = 1
CSYS,1                  ! CYLINDRICAL C.S.
N,1,60                  ! DEFINE NODES
N,2,60,,10
NGEN,2,2,1,2,1,,10
NROTAT,ALL              ! ROTATE NODAL C.S. TO CYLINDRICAL C.S.
E,1,2,4,3               ! DEFINE ELEMENT
CP,1,UX,1,2,3,4         ! COUPLE RADIAL DISPLACEMENTS
CP,2,UZ,2,4             ! COUPLE UZ DISPLACEMENTS
D,1,UZ,,,3,2
D,ALL,UY
SFE,1,4,PRES,,-15000    ! AXIAL TRACTION 
SFE,1,1,PRES,,-500      ! INTERNAL PRESSURE 
FINISH
/SOLU    
OUTPR,NSOL,1
OUTPR,RSOL,1
/OUT,SCRATCH
SOLVE
FINISH
/POST1
/OUT,
*GET,STRS_HOP,NODE,1,S,2
*GET,STRS_AX,NODE,1,S,1
*DIM,LABEL,CHAR,2,2
*DIM,VALUE,,2,3
LABEL(1,1) = 'AXIAL ST','HOOP STR'
LABEL(1,2) = 'RSS psi ','SS  psi '
*VFILL,VALUE(1,1),DATA,15000,29749
*VFILL,VALUE(1,2),DATA,STRS_HOP,STRS_AX
*VFILL,VALUE(1,3),DATA,ABS(STRS_HOP/15000),ABS(STRS_AX/29749)
SAVE,TABLE_1
/COM
/OUT,vm20,vrt
/COM,------------------- VM20 RESULTS COMPARISON ---------------------
/COM,
/COM,                 |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
/COM,
/COM, SHELL181
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.0,'  ',F14.0,'   ',1F15.3)
/COM,-----------------------------------------------------------------
/OUT
FINISH
*LIST,vm20,vrt
/DELETE,TABLE_1

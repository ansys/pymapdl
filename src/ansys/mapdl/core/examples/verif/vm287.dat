/COM,ANSYS MEDIA REL. 2023R1 (11/04/2022) REF. VERIF. MANUAL: REL. 2023R1
/VERIFY,vm287
/TITLE,vm287,INITIAL CRACK GROWTH ANGLE IN PURE SHEAR PROBLEM BY XFEM
/COM, REFERENCE: "F.ERDOGAN, G.C.SIH, ON THE CRACK EXTENSIN IN PLATES UNDER
/COM              PLANE LOADING AND TRANSVERSE SHEAR
/COM              JOURNAL OF BASIC ENGINEERING,1963(12):519-527
/COM
/COM *************************************************************
/COM
/COM PROBLEM: PURE SHEAR TEST WITH AN EDGE CRACK
/COM          NO CZM ON INITIAL CRACKS
/COM ELEMENTS: PLANE182, PLANE STRAIN
/COM MATERIAL: LINEAR ELASTIC, NLGEOM=OFF
/COM           BILINEAR COHESIVE BEHAVIOR ON INITIAL CRACK FACES
/COM           RIGID LINEAR BEHAVIOR ON NEWLY FORMED CRACK SURFACES
/COM 
/COM FRACTURE BEHAVIOR: MAXIMUM PRINCIPAL STRESS (PSMAX)
/COM
/COM RESULTS: THE ANGLE OF INITIAL CRACK PROPAGATION W.R.T. ORIGINAL
/COM          CRACK IS 67 DEGREES. THIS AGREES QUITE REASONABLY WITH
/COM          THE THEORETICAL RESULT OF 70.5 DEGREES (BASED ON
/COM	      ASYMPTOTIC SOLUTIONS FOR CRACKS IN HOMOGENEOUS MATERIALS)
/COM
/COM *************************************************************
/COM
JPGPRF,500,100,1

/PREP7

ET,1,PLANE182                       ! PLANE182 ELEMENTS
KEYOPT,1, 3, 2                      ! PLANE STRAIN OPTION

!CONTINUUM MATERIAL BEHAVIOR
MP, EX,   1, 100.0
MP, NUXY,  1, 0.0

!CRACK GROWTH CRITERION
TB, CGCR, 2, , , PSMAX
TBDATA, 1, 0.5

! DEFINE KEYPOINTS
K, 1,  0.0, 0.0
K, 2, 10.0, 0.0
K, 3, 10.0, 10.0
K, 4,  0.0, 10.0

! DEFINE AREA WITH KP
A, 1,2,3,4

! MESH THE AREA
ELEMENTSIZE=10.0/21.0
TYPE, 1
MAT, 1
ESIZE, ELEMENTSIZE
AMESH,1

! ELEMENT COMPONENT REQUIRED FOR XFENRICH COMMAND
NSEL,S,LOC,X, 0.0, 10.0
NSEL,R, LOC, Y, 0.0, 7.0
ESLN
CM, TESTCMP, ELEM
ALLSEL


!DEFINE ENRICHMENT IDENTIFICATION
!XFENRICH, ID#, EL COMPONENT, MAT ID FOR INIT_CRK_CZM
!XFDATA,    1,   LSM,EL#,ND#, PHI, PSI(CRACK TIP INDICATOR-NOT USED)

! FOLLOWING DATA EXTRACTED FROM PREPROCESSOR IN ANSYS GUI
XFENRICH,ENRICH1,TESTCMP,1
FLST,3,2,8  
FITEM,3,0.0,5.0,0  
FITEM,3,5.0,5.0,0,0  
XFDATA,ENRICH1,P51X
ALLSEL
/OUT
/COM *************************************************************
/COM
/COM                    INITIAL CRACK DATA
/COM
/COM *************************************************************

XFLIST

! CRK TIP ELEMENT
ESEL,S,ELEM,,221
CM, CRKTIPELEM, ELEM
ALLSEL,ALL

!FIXED B.C. - RHS BOTTOM CORNER
NSEL, S, LOC, X, 10.0
NSEL, R, LOC, Y,  0.0
D, ALL, UX, 0.0
D, ALL, UY, 0.0
ALLSEL

!FIXED B.C. - RHS TOP CORNER
NSEL, S, LOC, X, 10.0
NSEL, R, LOC, Y, 10.0
D, ALL, UX, 0.0
ALLSEL

!FORCE B.C. - LHS TOP PART
NSEL, S, LOC, X, 0.0
NSEL, R, LOC, Y, 5.0, 10.0
NSEL,U, NODE, ,74
F, ALL, FX, 0.42

!FORCE B.C. - LHS BOT PART
NSEL, S, LOC, X, 0.0
NSEL, R, LOC, Y, 0.0, 5.0
NSEL, U, NODE, ,75
F, ALL, FX, -0.42
ALLSEL
FINISH

! SOLUTION MODULE
! NOTE: NROPT IS ACTIVATED BY DEFAULT EVEN FOR LINEAR ANALYSIS
!       IN XFEM
/SOLUTION
ANTYPE,0
TIME, 1.0                           ! END TIME
DELTIM, 0.02, 1.0E-05, 0.02         ! TIME INCREMENTS
OUTRES,ALL, ALL

!CINT CALCULATIONS
CINT, NEW, 1                        ! NEW CRACK ID
CINT, CXFE, CRKTIPELEM              ! CRACK TIP ELEMENT NUMBER
CINT, TYPE, PSMAX                   ! CALCULATE CIRCUMFERENTIAL STRESS 
CINT, RADIUS, 0.6                   ! RADIUS AT WHICH THE VALUE IS EVALUATED
CINT, NCON, 6                       ! NUMBER OF CONTOURS
CINT, NORM, 0, 2                    ! DEFINE CRACK PLANE NORMAL
CINT, RSWEEP, 181, -90, 90          ! MINIMUM AND MAXIMUM SWEEP ANGLE

!CGROW CALCULATIONS
CGROW, NEW, 1                       ! NEW CRACK GROWTH SIMULATION FATA
CGROW, CID, 1                       ! CRACK CALCULATION ID
CGROW, METHOD, XFEM                 ! XFEM METHOD
CGROW, FCOPTION, MTAB, 2            ! CRACK GROWTH CRITERIAN USED WITH MATERIAL DATA
/OUT,SCRATCH
SOLVE
/OUT
/COM *************************************************************
/COM
/COM                    CRACK DATA AFTER PROPAGATION
/COM
/COM *************************************************************
XFLIST
/OUT,SCRATCH
FINISH

/POST1
SET,LAST
*GET,DD,XFEM,PHI,ELEM,222,NODE,294  ! LSM PHI VALUE FOR THIS NODE NUMBER OF THIS ELEMENT
/COM, THE INITIAL CRACK GROWTH ANGLE = ARCCOS(DD/0.5*ELEMENT SIZE)
/OUT,
/COM, THE INITIAL CRACK GROWTH ANGLE OF PURE SHEAR PROBLEM IN MECHANICAL APDL BY XFEM
CRACK_GROWTH_ANGLE=ACOS(ABS(DD)/(0.5*ELEMENTSIZE))*180/3.1416
/COM, THE ANALYTICAL SOLUTION FOR PURE SHEAR PROBLEM =70.5 DEGREE
REFERENCE_RESULT=70.5
RATIO=CRACK_GROWTH_ANGLE/REFERENCE_RESULT
/COM
/OUT,vm287,vrt
/COM,----- VM287 RESULTS COMPARISON -------------------------
/COM,
/COM,| Mechanical APDL  |  TARGET   |  RATIO
/COM,
*VWRITE,CRACK_GROWTH_ANGLE,REFERENCE_RESULT,RATIO
(8X,F8.3,'    ',F8.3,'     ',F5.3)
/COM,-----------------------------------------------------------------------
/OUT
*LIST,vm287,vrt
FINISH



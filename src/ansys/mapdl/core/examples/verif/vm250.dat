/COM,ANSYS MEDIA REL. 150 (11/8/2013) REF. VERIF. MANUAL: REL. 150
/VERIFY,VM250
JPGPRF,500,100,1                 !* MACRO TO SET PREFS FOR JPEG PLOTS
/SHOW,JPEG
/TITLE,VM250, GASKET MATERIAL UNDER UNIAXIAL COMPRESSION LOAD
/COM,   REF: ANY NONLINEAR MATERIAL VERIFICATION TEXT
/COM,   USING 3D 8-NODE INTER195 GASKET ELEMENTS
/PREP7
MP, EX, 1,15.2E6*6890
MP,NUXY, 1, 0.21
MP,DENS,1,7203
TB,GASKET,2,,13,COMP             !* COMPRESSION CURVE
TBPT,,0.508000E-04, 0.161226E+07
TBPT,,0.101600E-03, 0.520884E+07
TBPT,,0.152400E-03, 0.113134E+08
TBPT,,0.203200E-03, 0.200499E+08
TBPT,,0.254000E-03, 0.259960E+08
TBPT,,0.304800E-03, 0.290345E+08
TBPT,,0.355600E-03, 0.357453E+08
TBPT,,0.406400E-03, 0.440064E+08
TBPT,,0.457200E-03, 0.563189E+08
TBPT,,0.508000E-03, 0.748254E+08
TBPT,,0.558800E-03, 0.967287E+08
TBPT,,0.609600E-03, 0.129001E+09
TBPT,,0.683260E-03, 0.157147E+09
TB,GASKET,2,,5,LUNL              !* COMPRESSION CURVE
TBPT,,0.152400E-03, 2.430000E+11
TBPT,,0.304800E-03, 3.565000E+11
TBPT,,0.406400E-03, 5.923000E+11
TBPT,,0.558800E-03, 1.088000E+12
TBPT,,0.683260E-03, 1.490000E+12
*DIM,XA,TABLE,13,1
*DIM,YA,TABLE,13,1
XA(1,1) = 0.508000E-04
XA(2,1) = 0.101600E-03
XA(3,1) = 0.152400E-03
XA(4,1) = 0.203200E-03
XA(5,1) = 0.254000E-03
XA(6,1) = 0.304800E-03
XA(7,1) = 0.355600E-03
XA(8,1) = 0.406400E-03
XA(9,1) = 0.457200E-03
XA(10,1)= 0.508000E-03
XA(11,1)= 0.558800E-03
XA(12,1)= 0.609600E-03
XA(13,1)= 0.683260E-03
YA(1,1) = 0.161226E+07
YA(2,1) = 0.520884E+07
YA(3,1) = 0.113134E+08
YA(4,1) = 0.200499E+08
YA(5,1) = 0.259960E+08
YA(6,1) = 0.290345E+08
YA(7,1) = 0.357453E+08
YA(8,1) = 0.440064E+08
YA(9,1) = 0.563189E+08
YA(10,1)= 0.748254E+08
YA(11,1)= 0.967287E+08
YA(12,1)= 0.129001E+09
YA(13,1)= 0.157147E+09
*DIM,XB,TABLE,2,1
*DIM,YB,TABLE,2,1
XB(1,1) = 1.06E-04
YB(1,1) = 0.0
XB(2,1) = 0.152400E-03
YB(2,1) = 11313400
*DIM,XC,TABLE,2,1
*DIM,YC,TABLE,2,1
XC(1,1) = 2.23E-04
YC(1,1) = 0
XC(2,1) = 0.304800E-03
YC(2,1) = 29034500
*DIM,XD,TABLE,2,1
*DIM,YD,TABLE,2,1
XD(1,1) = 3.32E-04
YD(1,1) = 0.0
XD(2,1) = 0.406400E-03
YD(2,1) = 44006400
*DIM,XE,TABLE,2,1
*DIM,YE,TABLE,2,1
XE(1,1) = 4.70E-04
YE(1,1) = 0.0
XE(2,1) = 0.558800E-03
YE(2,1) = 96728700
*DIM,XF,TABLE,2,1
*DIM,YF,TABLE,2,1
XF(1,1) = 5.78E-04
YF(1,1) = 0.0
XF(2,1) = 0.683260E-03
YF(2,1) = 157147000
SAVE
/PREP7
ET,1,185                         !* 3D 8-NODE STRUCTURAL SOLID ELEMENT
ET,2,195,,0                      !* 3D 8-NODE GASKET ELEMENT
BLOCK,0,1,0,1,0,1
BLOCK,0,1,0,1,1.02,2.02
V,5,6,7,8,10,12,11,9
ESIZE,,4
TYPE,1
MAT, 1
VMESH,1
TYPE,2
MAT,2
IMESH,AREA,2,7, ,0,0,0.02,0.001  !* GENERATE GASKET ELEMENTS
TYPE,1
MAT, 1
VMESH,2
NSEL,S,LOC,X,0
D,ALL,UX
NSEL,S,LOC,Y,0
D,ALL,UY
NSEL,S,LOC,Z,0
D,ALL,UZ
NSEL,ALL
FINISH
/out,scratch
/SOLU
NLGEOM,ON
NSUBST,50,50,50
OUTRES,ALL,-10
NSEL,S,LOC,Z,2.02
SF,ALL,PRES,44006400         !* 1ST LOAD STEP -- LOAD THE MODEL
NSEL,ALL
SOLVE
NSEL,S,LOC,Z,2.02
SF,ALL,PRES,0                !* 2ND LOAD STEP -- UNLOAD THE MODEL
NSEL,ALL
SOLVE
NSEL,S,LOC,Z,2.02            !* 3RD LOAD STEP -- RELOAD THE MODEL
SF,ALL,PRES,157147000
NSEL,ALL
SOLVE
NSEL,S,LOC,Z,2.02
SF,ALL,PRES,0                !* 4TH LOAD STEP -- UNLOAD THE MODEL
NSEL,ALL
SOLVE
FINISH
/out
/POST26
/COLOR,CURVE,YGRE
/YRANGE,0,20E7
/XRANGE,0,8E-4
/AXLAB,X,CLOSURE
/AXLAB,Y,PRES
*VPLOT,XA(1,1),YA(1,1)
/NOERASE
*VPLOT,XB(1,1),YB(1,1)
*VPLOT,XC(1,1),YC(1,1)
*VPLOT,XD(1,1),YD(1,1)
*VPLOT,XE(1,1),YE(1,1)
*VPLOT,XF(1,1),YF(1,1)
ESOL,2,70,145,GKS,X
ESOL,3,70,145,GKD,X
PROD,4,2, , ,PRES, , ,-1
PROD,5,3, , ,CLOSURE, , ,-1
/COLOR,CURVE,MRED
/GMARKER,1,4,10
XVAR,5
PLVAR,4
PRVAR,5,4
FINISH
/POST1
SET,1,LAST
*GET,GK_PRES1,NODE,145,GKS,X
*GET,GK_CLOS1,NODE,145,GKD,X
SET,3,LAST
*GET,GK_PRES2,NODE,145,GKS,X
*GET,GK_CLOS2,NODE,145,GKD,X
GK_PRES1 = ABS(GK_PRES1)
GK_CLOS1 = ABS(GK_CLOS1)
GK_PRES2 = ABS(GK_PRES2)
GK_CLOS2 = ABS(GK_CLOS2)
R1 = GK_PRES1/44006400
R2 = GK_CLOS1/0.406400E-03
R3 = GK_PRES2/157147000
R4 = GK_CLOS2/0.683260E-03
*DIM,LABEL,CHAR,2,2
*DIM,VALUE,,2,3
*DIM,VALUE2,,2,3
LABEL(1,1) = 'GK','GK'
LABEL(1,2) = '-PRES','-CLOS'
*VFILL,VALUE(1,1),DATA,44006400,0.406400E-03
*VFILL,VALUE(1,2),DATA,GK_PRES1,GK_CLOS1
*VFILL,VALUE(1,3),DATA,R1,R2
*VFILL,VALUE2(1,1),DATA,157147000,0.683260E-03
*VFILL,VALUE2(1,2),DATA,GK_PRES2,GK_CLOS2
*VFILL,VALUE2(1,3),DATA,R3,R4
FINISH
SAVE,TABLE_1
/CLEAR, NOSTART                 !* CLEAR DATABASE FOR SECOND SOLUTION
/TITLE,VM250, GASKET MATERIAL UNDER UNIAXIAL COMPRESSION LOAD
/COM   USING 3D 16-NODE INTER194 GASKET ELEMENT
RESUME
/PREP7
ET,1,186                         !* 3D 20-NODE STRUCTURAL SOLID ELEMENT
ET,2,194,,0,,0                   !* 3D 16-NODE GASKET ELEMENT
BLOCK,0,1,0,1,0,1
BLOCK,0,1,0,1,1.02,2.02
V,5,6,7,8,10,12,11,9
ESIZE,,4
TYPE,1
MAT, 1
VMESH,1
TYPE,2
MAT,2
IMESH,AREA,2,7, ,0,0,0.02,0.001  !* GENERATE GASKET ELEMENTS
TYPE,1
MAT, 1
VMESH,2
NSEL,S,LOC,X,0
D,ALL,UX
NSEL,S,LOC,Y,0
D,ALL,UY
NSEL,S,LOC,Z,0
D,ALL,UZ
NSEL,ALL
FINISH
/out,scratch
/SOLU
NLGEOM,ON
NSUBST,50,50,50
OUTRES,ALL,-10
NSEL,S,LOC,Z,2.02
SF,ALL,PRES,44006400         !* 1ST LOAD STEP -- LOAD THE MODEL
NSEL,ALL
SOLVE
NSEL,S,LOC,Z,2.02
SF,ALL,PRES,0                !* 2ND LOAD STEP -- UNLOAD THE MODEL
NSEL,ALL
SOLVE
NSEL,S,LOC,Z,2.02            !* 3RD LOAD STEP -- RELOAD THE MODEL
SF,ALL,PRES,157147000
NSEL,ALL
SOLVE
NSEL,S,LOC,Z,2.02
SF,ALL,PRES,0                !* 4TH LOAD STEP -- UNLOAD THE MODEL
NSEL,ALL
SOLVE
FINISH
/out
/POST26
/COLOR,CURVE,YGRE
/YRANGE,0,20E7
/XRANGE,0,8E-4
/AXLAB,X,CLOSURE
/AXLAB,Y,PRES
*VPLOT,XA(1,1),YA(1,1)
/NOERASE
*VPLOT,XB(1,1),YB(1,1)
*VPLOT,XC(1,1),YC(1,1)
*VPLOT,XD(1,1),YD(1,1)
*VPLOT,XE(1,1),YE(1,1)
*VPLOT,XF(1,1),YF(1,1)
ESOL,2,70,472,GKS,X
ESOL,3,70,472,GKD,X
PROD,4,2, , ,PRES, , ,-1
PROD,5,3, , ,CLOSURE, , ,-1
/COLOR,CURVE,MRED
/GMARKER,1,4,10
XVAR,5
PLVAR,4
PRVAR,5,4
FINISH
/POST1
SET,1,LAST
*GET,GK_PRES1,NODE,472,GKS,X
*GET,GK_CLOS1,NODE,472,GKD,X
SET,3,LAST
*GET,GK_PRES2,NODE,472,GKS,X
*GET,GK_CLOS2,NODE,472,GKD,X
GK_PRES1 = ABS(GK_PRES1)
GK_CLOS1 = ABS(GK_CLOS1)
GK_PRES2 = ABS(GK_PRES2)
GK_CLOS2 = ABS(GK_CLOS2)
R1 = GK_PRES1/44006400
R2 = GK_CLOS1/0.406400E-03
R3 = GK_PRES2/157147000
R4 = GK_CLOS2/0.683260E-03
*DIM,LABEL,CHAR,2,2
*DIM,VALUE,,2,3
*DIM,VALUE2,,2,3
LABEL(1,1) = 'GK','GK'
LABEL(1,2) = '-PRES','-CLOS'
*VFILL,VALUE(1,1),DATA,44006400,0.406400E-03
*VFILL,VALUE(1,2),DATA,GK_PRES1,GK_CLOS1
*VFILL,VALUE(1,3),DATA,R1,R2
*VFILL,VALUE2(1,1),DATA,157147000,0.683260E-03
*VFILL,VALUE2(1,2),DATA,GK_PRES2,GK_CLOS2
*VFILL,VALUE2(1,3),DATA,R3,R4
SAVE,TABLE_2
RESUME,TABLE_1
/COM,
/OUT,vm250,vrt
/COM,------------------- VM250 RESULTS COMPARISON --------------
/COM,
/COM,               |  TARGET   |   Mechanical APDL  |  RATIO
/COM,
/COM,RESULTS USING INTER195 ELEMENTS:
/COM,
/COM,GASKET PRESSURE AND CLOSURE AT END OF 1ST LOADING:
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A2,A8,'   ',E12.6,'  ',1E15.6,'   ',1F15.3)
/COM,
/COM,GASKET PRESSURE AND CLOSURE AT THE END OF 2ND LOADING:
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE2(1,1),VALUE2(1,2),VALUE2(1,3)
(1X,A2,A8,'   ',E12.6,'  ',1E15.6,'   ',1F15.3)
/NOPR
RESUME,TABLE_2
/GOPR
/COM,
/COM,RESULTS USING INTER194 ELEMENTS:
/COM,
/COM,GASKET PRESSURE AND CLOSURE AT END OF 1ST LOADING:
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A2,A8,'   ',E12.6,'  ',1E15.6,'   ',1F15.3)
/COM,
/COM,GASKET PRESSURE AND CLOSURE AT THE END OF 2ND LOADING:
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE2(1,1),VALUE2(1,2),VALUE2(1,3)
(1X,A2,A8,'   ',E12.6,'  ',1E15.6,'   ',1F15.3)
/COM,-----------------------------------------------------------
/OUT
FINISH
*LIST,vm250,vrt

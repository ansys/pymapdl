/COM,ANSYS MEDIA REL. 2023R1 (11/04/2022) REF. VERIF. MANUAL: REL. 2023R1
/VERIFY,VM260
/OUT,SCRATCH
JPGPRF,500,100,1
/SHOW,JPEG
/COM, VERFICATION MANUAL FOR TWO DIMENSIONAL CONSOLIDATION PROBLEM
/TITLE,VM260,TWO DIMENSIONAL CONSOLIDATION SETTLEMENT UNDER A RECTANGULAR LOAD
C*** USING CPT212
/COM, REFERENCE: " A.T-F,SCHIFFMAN,ET AL.,AN ANALYSIS OF CONSOLIDATION THEORIES
/COM,			 JOURNAL OF SOLID MECHANICS AND FOUNDATION DIVISION,1969,PG:285-312
/COM,
/PREP7
W=1						! RECTANGULAR LOAD HALF-WIDTH
E=1000						! YOUNG'S MODULUS
A=4						! REFINEMENT COEFFICIENT
R=100						! LOADING
ET,1,CPT212					! 2D 4 NODE COUPLED PORE PRESSURE ELEMENT
KEYOPT,1,12,1 			         
KEYOPT,1,3,2					! PLANE STRAIN CONDISTIONS
RECT,0,6*W,0,-9*W				! AREA 6*W BY 9*W
LESIZE,3,,,12*A/W				! 12*A ELEMENTS ON HORIZONTAL EDGES
LESIZE,4,,,18*A/W				! 18*A ELEMENTS ON VERTICAL EDGES
AMAP,1,1,2,3,4

MP,EX,1,E
MP,NUXY,1,0
BULK = 0
FPX = 0.267E-8					! SOLID PERMEABILITY
ONE = 1.0
TB,PM,1,,,PERM
TBDATA,1,FPX,FPX,FPX				! PERMEABILITY COEFFICIENTS
TB,PM,1,,,BIOT				
TBDATA,1,ONE					! BIOT COEFFICIENT

NSEL,S,LOC,Y,-9*W
D,ALL,UX,0
D,ALL,UY,0					! BOTTOM SURFACE FIXED
ALLSEL,ALL
NSEL,S,LOC,X,0
D,ALL,UX,0					! SYMMETRY BOUNDARY CONDITION
ALLSEL,ALL
NSEL,S,LOC,X,6*W
D,ALL,UX,0					! ROLLER BOUNDARY CONDITION
ALLSEL,ALL
NSEL,S,LOC,Y,0
D,ALL,PRES,0					! PERMEABLE TOP SURFACE
ALLSEL,ALL

CVC = E*FPX					! COEFFICIENT OF CONSOLIDATION
TV = 0.1					! TIME FACTOR STEP
TT = TV*W*W/CVC					! CRITICAL TIME
FINI

/SOLU
NROPT,UNSYMM					! NEWTON RAPHSON WITH UNSYMMETRIC MATRICES
OUTRES,ALL,ALL
TIME,TT
NSUBS,10,10,10
KBC,1						! STEP LOAD
NSEL,S,LOC,Y,0
NSEL,R,LOC,X,0,W
SF,ALL,PRES,R
ALLSEL,ALL
SOLVE

TIME,10*TT
NSUBS,90,90,90
SOLVE
FINI

/POST1
*DIM,LABEL1,CHAR,11,3
*DIM,LABEL2,CHAR,15,3
*DIM,VALUE1,,11,3
*DIM,VALUE2,,15,3

LABEL1(1,1) = 'CPT212  ','','','','','',''
LABEL1(8,1) = '','',''
LABEL1(1,2) = 'P/R at Y=','P/R at Y=','P/R at Y=','P/R at Y=','P/R at Y=','P/R at Y=','P/R at Y='  
LABEL1(8,2) = 'P/R at Y=','P/R at Y=','P/R at Y=','P/R at Y='
LABEL1(1,3) = '0.0','0.5','1.0','1.5','2.0','2.5','3.0'
LABEL1(8,3) = '3.5','4.0','4.5','5.0',

LABEL2(1,1) = 'CPT212  ','','','','','',''
LABEL2(8,1) = '','','','','','',''
LABEL2(15,1) = ''
LABEL2(1,2) = 'P/R at Y=','P/R at Y=','P/R at Y=','P/R at Y=','P/R at Y=','P/R at Y=','P/R at Y='  
LABEL2(8,2) = 'P/R at Y=','P/R at Y=','P/R at Y=','P/R at Y=','P/R at Y=','P/R at Y=','P/R at Y='
LABEL2(15,2) = 'P/R at Y='
LABEL2(1,3) = '0.01','0.02','0.03','0.05','0.09','0.1','0.2'
LABEL2(8,3) = '0.3','0.4','0.5','0.6','0.7','0.8','0.9',
LABEL2(15,3) = '1.0'

/COM, PORE PRESSURE VERSUS DEPTH
VALUE1(1,1)=0
VALUE1(2,1)=0.635
VALUE1(3,1)=0.57
VALUE1(4,1)=0.44
VALUE1(5,1)=0.35
VALUE1(6,1)=0.295
VALUE1(7,1)=0.26
VALUE1(8,1)=0.236
VALUE1(9,1)=0.22
VALUE1(10,1)=0.208
VALUE1(11,1)=0.2
SET,1,10
VALUE1(1,2)=0
VALUE1(1,3)=1
*DO,I,2,11
N1=NODE(0,-(I-1)*0.5,0)
*GET,P,NODE,N1,PRES
VALUE1(I,2)=P/R
VALUE1(I,3)=ABS(VALUE1(I,1)/VALUE1(I,2))
*ENDDO

N1 = NODE(0,-0.5,0)
/COM, PORE PRESSURE VERSUS TIME
VALUE2(1,1)=0.77
VALUE2(2,1)=0.8
VALUE2(3,1)=0.79
VALUE2(4,1)=0.73
VALUE2(5,1)=0.615
VALUE2(6,1)=0.59
VALUE2(7,1)=0.42
VALUE2(8,1)=0.33
VALUE2(9,1)=0.27
VALUE2(10,1)=0.23
VALUE2(11,1)=0.20
VALUE2(12,1)=0.175
VALUE2(13,1)=0.16
VALUE2(14,1)=0.145
VALUE2(15,1)=0.135
SET,1,1
VALUE2(1,2)=PRES(N1)/R
SET,1,2
VALUE2(2,2)=PRES(N1)/R
SET,1,3
VALUE2(3,2)=PRES(N1)/R
SET,1,5
VALUE2(4,2)=PRES(N1)/R
SET,1,9
VALUE2(5,2)=PRES(N1)/R
SET,1,10
VALUE2(6,2)=PRES(N1)/R
*DO,I,1,9
SET,2,10*I
VALUE2(I+6,2)= PRES(N1)/R
*ENDDO
*DO,I,1,15
VALUE2(I,3)= VALUE2(I,1)/VALUE2(I,2)
*ENDDO
SAVE,INF1
FINISH
/CLEAR,NOSTART
/OUT,SCRATCH




/TITLE,VM260,TWO DIMENSIONAL CONSOLIDATION SETTLEMENT UNDER A RECTANGULAR LOAD
C*** USING CPT213
/PREP7
W=1							! RECTANGULAR LOAD HALF-WIDTH
E=1000							! YOUNG'S MODULUS
A=4							! REFINEMENT COEFFICIENT
R=100							! LOADING
ET,1,CPT213						! 2D 8 NODE COUPLED PORE PRESSURE ELEMENT
KEYOPT,1,12,1 			         
KEYOPT,1,3,2						! PLANE STRAIN CONDISTIONS
RECT,0,6*W,0,-9*W					! AREA 6*W BY 9*W
LESIZE,3,,,6*A/W					! 6*A ELEMENTS ON HORIZONTAL EDGES
LESIZE,4,,,9*A/W					! 9*A ELEMENTS ON VERTICAL EDGES
AMAP,1,1,2,3,4

MP,EX,1,E
MP,NUXY,1,0
BULK = 0
FPX = 0.267E-8						! SOLID PERMEABILITY
ONE = 1.0
TB,PM,1,,,PERM
TBDATA,1,FPX,FPX,FPX					! PERMEABILITY COEFFICIENTS
TB,PM,1,,,BIOT				
TBDATA,1,ONE						! BIOT COEFFICIENT

NSEL,S,LOC,Y,-9*W
D,ALL,UX,0
D,ALL,UY,0						! BOTTOM SURFACE FIXED
ALLSEL,ALL					     
NSEL,S,LOC,X,0
D,ALL,UX,0						! SYMMETRY BOUNDARY CONDITION
ALLSEL,ALL
NSEL,S,LOC,X,6*W
D,ALL,UX,0						! ROLLER BOUNDARY CONDITION
ALLSEL,ALL
NSEL,S,LOC,Y,0
D,ALL,PRES,0						! PERMEABLE TOP SURFACE
ALLSEL,ALL

CVC = E*FPX						! COEFFICIENT OF CONSOLIDATION
TV = 0.1						! TIME FACTOR STEP
TT = TV*W*W/CVC						! CRITICAL TIME
FINI

/SOLU
NROPT,UNSYMM						! NEWTON RAPHSON WITH UNSYMMETRIC MATRICES
OUTRES,ALL,ALL
TIME,TT
NSUBS,10,10,10
KBC,1							! STEP LOAD
NSEL,S,LOC,Y,0
NSEL,R,LOC,X,0,W
SF,ALL,PRES,R
ALLSEL,ALL
SOLVE

TIME,10*TT
NSUBS,90,90,90
SOLVE
FINI

/POST1
*DIM,LABEL1,CHAR,11,3
*DIM,LABEL2,CHAR,15,3
*DIM,VALUE1,,11,3
*DIM,VALUE2,,15,3

LABEL1(1,1) = 'CPT213  ','','','','','',''
LABEL1(8,1) = '','',''
LABEL1(1,2) = 'P/R at Y=','P/R at Y=','P/R at Y=','P/R at Y=','P/R at Y=','P/R at Y=','P/R at Y='  
LABEL1(8,2) = 'P/R at Y=','P/R at Y=','P/R at Y=','P/R at Y='
LABEL1(1,3) = '0.0','0.5','1.0','1.5','2.0','2.5','3.0'
LABEL1(8,3) = '3.5','4.0','4.5','5.0',

LABEL2(1,1) = 'CPT213  ','','','','','',''
LABEL2(8,1) = '','','','','','',''
LABEL2(15,1) = ''
LABEL2(1,2) = 'P/R at Y=','P/R at Y=','P/R at Y=','P/R at Y=','P/R at Y=','P/R at Y=','P/R at Y='  
LABEL2(8,2) = 'P/R at Y=','P/R at Y=','P/R at Y=','P/R at Y=','P/R at Y=','P/R at Y=','P/R at Y='
LABEL2(15,2) = 'P/R at Y='
LABEL2(1,3) = '0.01','0.02','0.03','0.05','0.09','0.1','0.2'
LABEL2(8,3) = '0.3','0.4','0.5','0.6','0.7','0.8','0.9',
LABEL2(15,3) = '1.0'

/COM, PORE PRESSURE VERSUS DEPTH
VALUE1(1,1)=0
VALUE1(2,1)=0.635
VALUE1(3,1)=0.57
VALUE1(4,1)=0.44
VALUE1(5,1)=0.35
VALUE1(6,1)=0.295
VALUE1(7,1)=0.26
VALUE1(8,1)=0.236
VALUE1(9,1)=0.22
VALUE1(10,1)=0.208
VALUE1(11,1)=0.2
SET,1,10
VALUE1(1,2)=0
VALUE1(1,3)=1
*DO,I,2,11
N1=NODE(0,-(I-1)*0.5,0)
*GET,P,NODE,N1,PRES
VALUE1(I,2)=P/R
VALUE1(I,3)=ABS(VALUE1(I,1)/VALUE1(I,2))
*ENDDO

N1 = NODE(0,-0.5,0)
/COM, PORE PRESSURE VERSUS TIME
VALUE2(1,1)=0.77
VALUE2(2,1)=0.8
VALUE2(3,1)=0.79
VALUE2(4,1)=0.73
VALUE2(5,1)=0.615
VALUE2(6,1)=0.59
VALUE2(7,1)=0.42
VALUE2(8,1)=0.33
VALUE2(9,1)=0.27
VALUE2(10,1)=0.23
VALUE2(11,1)=0.20
VALUE2(12,1)=0.175
VALUE2(13,1)=0.16
VALUE2(14,1)=0.145
VALUE2(15,1)=0.135
SET,1,1
VALUE2(1,2)=PRES(N1)/R
SET,1,2
VALUE2(2,2)=PRES(N1)/R
SET,1,3
VALUE2(3,2)=PRES(N1)/R
SET,1,5
VALUE2(4,2)=PRES(N1)/R
SET,1,9
VALUE2(5,2)=PRES(N1)/R
SET,1,10
VALUE2(6,2)=PRES(N1)/R
*DO,I,1,9
SET,2,10*I
VALUE2(I+6,2)= PRES(N1)/R
*ENDDO
*DO,I,1,15
VALUE2(I,3)= VALUE2(I,1)/VALUE2(I,2)
*ENDDO
SAVE,INF2

RESUME,INF1
/COM
/OUT,vm260,vrt
/COM,----- VM260 RESULTS COMPARISON - PORE PRESSURE VERSUS DEPTH ---------
/COM,
/COM,Y=DEPTH
/COM,P=PORE PRESSURE
/COM,R=EXTERNAL LOADING
/COM,
/COM,               	               |   TARGET   |   Mechanical APDL   |   RATIO
/COM,		
*VWRITE,LABEL1(1,1),LABEL1(1,2),LABEL1(1,3),VALUE1(1,1),VALUE1(1,2),VALUE1(1,3)
(1X,A8,A11,A11,'  ',F10.4,'  ',F14.4,'   ',F15.3)
/COM,
/NOPR
RESUME,INF2
*VWRITE,LABEL1(1,1),LABEL1(1,2),LABEL1(1,3),VALUE1(1,1),VALUE1(1,2),VALUE1(1,3)
(1X,A8,A11,A11,'  ',F10.4,'  ',F14.4,'   ',F15.3)
/COM,-----------------------------------------------------------------------
RESUME,INF1
/COM,
/COM,Y=DEPTH
/COM,P=PORE PRESSURE
/COM,R=EXTERNAL LOADING
/COM,
/COM,----- VM260 RESULTS COMPARISON - PORE PRESSURE VERSUS TIME  ----------
/COM,
/COM,                 	             |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
*VWRITE,LABEL2(1,1),LABEL2(1,2),LABEL2(1,3),VALUE2(1,1),VALUE2(1,2),VALUE2(1,3)
(1X,A8,A11,A11,'  ',F10.4,'  ',F14.4,'   ',F15.3)
/COM,
/NOPR
RESUME,INF2
*VWRITE,LABEL2(1,1),LABEL2(1,2),LABEL2(1,3),VALUE2(1,1),VALUE2(1,2),VALUE2(1,3)
(1X,A8,A11,A11,'  ',F10.4,'  ',F14.4,'   ',F15.3)
/COM,-----------------------------------------------------------------------
/OUT
*LIST,vm260,vrt
FINISH
/DELETE,INF1
/DELETE,INF2


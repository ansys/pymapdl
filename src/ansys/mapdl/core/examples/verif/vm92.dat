! Copyright (C) 2024 ANSYS, Inc. and/or its affiliates.
! SPDX-License-Identifier: MIT
! 
!
! Permission is hereby granted, free of charge, to any person obtaining a copy
! of this software and associated documentation files (the "Software"), to deal
! in the Software without restriction, including without limitation the rights
! to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
! copies of the Software, and to permit persons to whom the Software is
! furnished to do so, subject to the following conditions:
!
! The above copyright notice and this permission notice shall be included in all
! copies or substantial portions of the Software.
!
! THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
! IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
! FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
! AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
! LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
! OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
! SOFTWARE.

/COM,ANSYS MEDIA REL. 2023R2 (05/12/2023) REF. VERIF. MANUAL: REL. 2023R2
/VERIFY,VM92
/PREP7
/TITLE, VM92, INSULATED WALL TEMPERATURE
C***        PRINCIPLES OF HEAT TRANSFER, KREITH, 2ND. PRINTING, PAGE 32, EX. 2-5
ANTYPE,STATIC                ! THERMAL ANALYSIS
ET,1,LINK34
ET,2,LINK33
R,1,1                        ! AREA = 1
MP,KXX,1,.8
MP,HF,1,12
MP,KXX,2,.1
MP,HF,2,2
N,1
N,2
N,3,.75
N,4,(14/12)                  ! 14 INCHES TO FEET
N,5,(14/12)
E,1,2
TYPE,2
E,2,3
MAT,2
E,3,4
TYPE,1
E,4,5
D,1,TEMP,3000
D,5,TEMP,80
FINISH
/SOLU    
SOLVE
FINISH
/POST1
PRNSOL,TEMP                  ! PRINT NODAL TEMPERATURES
PRNLD,HEAT                   ! PRINT HEAT FLOW RATES
*GET,TI,NODE,2,TEMP
*GET,TO,NODE,4,TEMP
FINISH
/POST26
ESOL,2,4,5,HEAT,,HEAT
STORE
*GET,HEAT,VARI,2,EXTREM,VMAX
*DIM,LABEL,CHAR,3,2
*DIM,VALUE,,3,3
LABEL(1,1) = 'Q, BTU/h','TI,     ','TO,     '
LABEL(1,2) = 'r       ','F       ','F       '
*VFILL,VALUE(1,1),DATA,513,2957,336
*VFILL,VALUE(1,2),DATA,HEAT,TI,TO
*VFILL,VALUE(1,3),DATA,ABS(HEAT/513) ,ABS(TI/2957),ABS(TO/336)
/COM
/OUT,vm92,vrt
/COM,------------------- VM92 RESULTS COMPARISON --------------
/COM,
/COM,                 |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.0,'  ',F14.0,'   ',1F15.3)
/COM,----------------------------------------------------------
/OUT
FINISH
*LIST,vm92,vrt

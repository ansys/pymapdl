/COM,ANSYS MEDIA REL. 150 (11/8/2013) REF. VERIF. MANUAL: REL. 150
/VERIFY,VM32
/PREP7
/TITLE, VM32, THERMAL STRESSES IN A LONG CYLINDER
!           STR. OF MATLS., TIMOSHENKO, PART 2, 3RD ED., PAGE 234, PROB. 1
!           THERMAL SOLUTION ***
ET,1,PLANE55,,,1       ! AXISYMMETRIC KEYOPT(S) OPTION
ET,2,PLANE55,,,1
MP,KXX,1,3
N,1,.1875
N,8,.625
FILL,,,,,,,,2          ! BIAS MESH DENSITY TOWARD CENTERLINE 
NGEN,2,10,1,8,1,,.1
E,11,1,2,12
TYPE,2
E,12,2,3,13
EGEN,5,1,2
TYPE,1
E,8,18,17,7
FINISH
/SOLU    
ANTYPE,STATIC          
D,1,TEMP,-1,,11,10     ! APPLY TEMPERATURES TO INNER AND OUTER SURFACES
D,8,TEMP,,,18,10
OUTPR,BASIC,ALL
/OUT,SCRATCH
SOLVE
FINISH
/POST1
/OUT,
LFT_NODE = NODE (0.1875,0,0) 
IN_NODE  = NODE (0.2788,0,0)
RT_NODE  = NODE (0.625,0,0)
*GET,LFT_TEMP,NODE,LFT_NODE,TEMP
*GET,IN_TEMP,NODE,IN_NODE,TEMP
*GET,RT_TEMP,NODE,RT_NODE,TEMP
*DIM,VALUE_C1,,3,3
*VFILL,VALUE_C1(1,1),DATA,-1,-.67037,0
*VFILL,VALUE_C1(1,2),DATA,LFT_TEMP,IN_TEMP,RT_TEMP
*VFILL,VALUE_C1(1,3),DATA,ABS(LFT_TEMP/1 ),ABS(IN_TEMP/.67037 ),0
*DIM,LABEL_1,CHAR,3,2
LABEL_1(1,1) = 'T (C) X=','T (C) X=','T (C) X='
LABEL_1(1,2) = '.1875 in','.2788 in','0.625 in'
SAVE,TABLE_1

FINISH
/PREP7
!           STRESS SOLUTION, STATIC ANALYSIS   ***
ETCHG,TTS              ! CHANGE ELEMENT TYPE PLANE55 TO PLANE42
KEYOPT,1,1,2
KEYOPT,1,3,1
KEYOPT,2,1,2
KEYOPT,2,3,1
MP,EX,1,30E6           ! DEFINE STRUCTURAL PROPERTIES
MP,ALPX,,1.435E-5
MP,NUXY,1,.3
CPNGEN,7,UY,11,18      ! COUPLE APPROPRIATE NODAL DISPLACEMENTS
CP,8,UX,1,11      
CPSGEN,8,1,8
FINISH
/SOLU    
ANTYPE,STATIC          
D,1,UY,,,8
LDREAD,TEMP,,,,,,rth   ! READ IN BODY FORCE TEMPERATURES
/OUT,SCRATCH
SOLVE
FINISH
/POST1
LFT_NODE = NODE (0.1875,0,0)
RT_NODE  = NODE (0.625,0,0)
/OUT,
*GET,LFT_AXST,NODE,LFT_NODE,S,Y
*GET,LFT_TST,NODE,LFT_NODE ,S,Z
*GET,RT_AXST,NODE,RT_NODE,S,Y
*GET,RT_TST,NODE,RT_NODE ,S,Z
*DIM,VALUE_C2,,4,3
*VFILL,VALUE_C2(1,1),DATA,420.42,420.42,-194.58,-194.58
*VFILL,VALUE_C2(1,2),DATA,LFT_AXST,LFT_TST,RT_AXST,RT_TST
*VFILL,VALUE_C2(1,3),DATA,ABS(LFT_AXST/420.42),ABS(LFT_TST/420.42),ABS(RT_AXST/194.58)
*VFILL,VALUE_C2(4,3),DATA,ABS(RT_TST/194.58)
*DIM,LABEL_2,CHAR,4,2
LABEL_2(1,1) = 'A_STS ps','T_STS ps','A_STS ps','T_STS ps'
LABEL_2(1,2) = 'i X=.187','i X=.187','i X=.625','i X=.625'
SAVE,TABLE_2
RESUME,TABLE_1
/COM
/OUT,vm32,vrt
/COM,------------------- VM32 RESULTS COMPARISON ---------------
/COM,
/COM,                 |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
/COM,THERMAL ANALYSIS:
/COM,
*VWRITE,LABEL_1(1,1),LABEL_1(1,2),VALUE_C1(1,1),VALUE_C1(1,2),VALUE_C1(1,3)
(1X,A8,A8,'   ',F10.5,'  ',F14.5,'   ',1F15.3)
/NOPR,
RESUME,TABLE_2
/GOPR
/COM,STATIC ANALYSIS:
/COM,
*VWRITE,LABEL_2(1,1),LABEL_2(1,2),VALUE_C2(1,1),VALUE_C2(1,2),VALUE_C2(1,3)
(1X,A8,A8,'   ',F10.2,'  ',F14.2,'   ',1F15.3)
/COM,
/COM,-----------------------------------------------------------
/OUT
FINISH
*LIST,vm32,vrt


/COM,ANSYS MEDIA REL. 2023R1 (11/04/2022) REF. VERIF. MANUAL: REL. 2023R1
/VERIFY,VM280
/SHOW,JPEG
JPGPRF,500,100,1                        !* MACRO TO SET PREFS FOR JPEG PLOTS
/TITLE,VM280, CONDUCTION IN A DOUBLY INSULATED PIPE CARRYING STEAM
C*** USING SOLID278
/COM,  REF: KOTHANDARAMAN,C.P.,
/COM, "FUNDAMENTALS OF HEAT AND MASS TRANSFER"-3RD EDITION, 
/COM,  CH-2 STEADY STATE CONDUCTION,P35,PROBLEM2.2
/PREP7
ET,1,SOLID278				!*3-D 8-NODE THERMAL SOLID	                       
MP,KXX,1,49				    !*CONDUCTIVITY OF THE PIPE MATERIAL	       
MP,KXX,2,0.15				!*CONDUCTIVITY OF FIRST LAYER OF INSULATION
MP,KXX,3,0.48				!*CONDUCTIVITY OF SECOND LAYER OF INSULATION      
CSYS,1	
CYL4,,,0.06,0,0.0675,90,0.25  		!*DEFINE THE GEOMETRY OF THE SYSTEM IN METERS
CYL4,,,0.0675,0,0.1175,90,0.25
CYL4,,,0.1175,0,0.1675,90,0.25
VPTN,ALL				!*PARTITIONING OF THE ENTITIES
ESIZE,0.005				!*MESH VOLUMES	       
MAT,1
TYPE,1
VMESH,1
ALLSEL,ALL
MAT,2
TYPE,1
VMESH,4
ALLSEL,ALL
MAT,3
TYPE,1
VMESH,5
ALLSEL,ALL
FINI
/SOLU
ANTYPE,STATIC
NSEL,S,LOC,X,0.06					       
ESLN,
SFE,ALL,4,CONV,1,85
SFE,ALL,4,CONV,2,230
ALLSEL,ALL
NSEL,S,LOC,X,0.1675			     
ESLN,
SFE,ALL,6,CONV,1,18
SFE,ALL,6,CONV,2,35
ALLSEL,ALL				
SOLVE					
FINI
/POST1
SET,LAST
PLNSOL,TEMP
*GET,TEMP_0.06,NODE,NODE(0.06,0,0),TEMP,,
*GET,TEMP_0.0675,NODE,NODE(0.0675,0,0),TEMP
*GET,TEMP_0.1175,NODE,NODE(0.1175,0,0),TEMP
*GET,TEMP_0.1675,NODE,NODE(0.1675,0,0),TEMP

*DIM,LABEL,CHAR,4,3
*DIM,VALUE,,4,3
LABEL(1,1) = 'TEMP @ x','TEMP @ x', 'TEMP @ x', 'TEMP @ x'
LABEL(1,2) = '=0.06','=0.0675 ','=0.1175','=0.1675'
LABEL(1,3) = 'T (C)','T (C)','T (C)', 'T (C)'
*VFILL,VALUE(1,1),DATA,222.3,222.2,77.04,48.03
*VFILL,VALUE(1,2),DATA,temp_0.06,temp_0.0675,temp_0.1175,temp_0.1675
*VFILL,VALUE(1,3),DATA,ABS(temp_0.06/222.3),ABS(temp_0.0675/222.2),ABS(temp_0.1175/77.04),ABS(temp_0.1675/48.03)
SAVE,TABLE1
FINI
/CLEAR,NOSTART


C*** USING SOLID279 - 3-D 20-NODE THERMAL SOLID 
/PREP7
ET,1,SOLID279				!*3-D 20-NODE THERMAL SOLID                       
MP,KXX,1,49				    !*CONDUCTIVITY OF THE PIPE MATERIAL	       
MP,KXX,2,0.15				!*CONDUCTIVITY OF FIRST LAYER OF INSULATION
MP,KXX,3,0.48				!*CONDUCTIVITY OF SECOND LAYER OF INSULATION      
CSYS,1	
CYL4,,,0.06,0,0.0675,90,0.25  		!*DEFINE THE GEOMETRY OF THE SYSTEM IN METERS
CYL4,,,0.0675,0,0.1175,90,0.25
CYL4,,,0.1175,0,0.1675,90,0.25
VPTN,ALL				!*PARTITIONING OF THE ENTITIES
ESIZE,0.005				!*MESH VOLUMES	
MAT,1
TYPE,1
VMESH,1
ALLSEL,ALL
MAT,2
TYPE,1
VMESH,4
ALLSEL,ALL
MAT,3
TYPE,1
VMESH,5
ALLSEL,ALL
FINI
/SOLU
NSEL,S,LOC,X,0.06		       
ESLN,
SFE,ALL,4,CONV,1,85
SFE,ALL,4,CONV,2,230
ALLSEL,ALL
NSEL,S,LOC,X,0.1675		       
ESLN,
SFE,ALL,6,CONV,1,18
SFE,ALL,6,CONV,2,35
ALLSEL,ALL
ANTYPE,STATIC
SOLVE
FINI
/POST1
PLNSOL,TEMP
*GET,TEMP_0.06,NODE,NODE(0.06,0,0),TEMP,,
*GET,TEMP_0.0675,NODE,NODE(0.0675,0,0),TEMP
*GET,TEMP_0.1175,NODE,NODE(0.1175,0,0),TEMP
*GET,TEMP_0.1675,NODE,NODE(0.1675,0,0),TEMP


*DIM,LABEL,CHAR,4,3
*DIM,VALUE,,4,3
LABEL(1,1) = 'TEMP @ x','TEMP @ x', 'TEMP @ x', 'TEMP @ x'
LABEL(1,2) = '=0.06','=0.0675 ','=0.1175','=0.1675'
LABEL(1,3) = 'T (C)','T (C)','T (C)', 'T (C)'
*VFILL,VALUE(1,1),DATA,222.3,222.2,77.04,48.03
*VFILL,VALUE(1,2),DATA,temp_0.06,temp_0.0675,temp_0.1175,temp_0.1675
*VFILL,VALUE(1,3),DATA,ABS(temp_0.06/222.3),ABS(temp_0.0675/222.2),ABS(temp_0.1175/77.04),ABS(temp_0.1675/48.03)
SAVE,TABLE2
FINI
/CLEAR,NOSTART


C*** USING SOLID291 - 3-D 10-NODE THERMAL SOLID 
/PREP7
CSYS,1
ET,1,SOLID291				!*3-D 10-NODE THERMAL SOLID                       
MP,KXX,1,49				    !*CONDUCTIVITY OF THE PIPE MATERIAL	       
MP,KXX,2,0.15				!*CONDUCTIVITY OF FIRST LAYER OF INSULATION
MP,KXX,3,0.48				!*CONDUCTIVITY OF SECOND LAYER OF INSULATION

CYL4,,,0.06,0,0.0675,90,0.25  		!*DEFINE THE GEOMETRY OF THE SYSTEM IN METERS
CYL4,,,0.0675,0,0.1175,90,0.25
CYL4,,,0.1175,0,0.1675,90,0.25
VPTN,ALL				!*PARTITIONING OF THE ENTITIES
ESIZE,0.01				!*MESH VOLUMES
/OUT,vm280_SCRATCH	
MAT,1
TYPE,1
VMESH,1
ALLSEL,ALL
MAT,2
TYPE,1
VMESH,4
ALLSEL,ALL
MAT,3
TYPE,1
VMESH,5
ALLSEL,ALL
FINI
/SOLU
NSEL,S,LOC,X,0.06	
SF,ALL,CONV,85,230	       
ALLSEL,ALL
NSEL,S,LOC,X,0.1675	
SF,ALL,CONV,18,35	       
ALLSEL,ALL
ANTYPE,STATIC
SOLVE
FINI
/POST1
PLNSOL,TEMP
*GET,TEMP_0.06,NODE,NODE(0.06,0,0),TEMP,,
*GET,TEMP_0.0675,NODE,NODE(0.0675,0,0),TEMP
*GET,TEMP_0.1175,NODE,NODE(0.1175,0,0),TEMP
*GET,TEMP_0.1675,NODE,NODE(0.1675,0,0),TEMP


*DIM,LABEL,CHAR,4,3
*DIM,VALUE,,4,3
LABEL(1,1) = 'TEMP @ x','TEMP @ x', 'TEMP @ x', 'TEMP @ x'
LABEL(1,2) = '=0.06','=0.0675 ','=0.1175','=0.1675'
LABEL(1,3) = 'T (C)','T (C)','T (C)', 'T (C)'
*VFILL,VALUE(1,1),DATA,222.3,222.2,77.04,48.03
*VFILL,VALUE(1,2),DATA,temp_0.06,temp_0.0675,temp_0.1175,temp_0.1675
*VFILL,VALUE(1,3),DATA,ABS(temp_0.06/222.3),ABS(temp_0.0675/222.2),ABS(temp_0.1175/77.04),ABS(temp_0.1675/48.03)
SAVE,TABLE3

/NOPR
/COM
/OUT,vm280,vrt
/COM,------------------- VM280 RESULTS COMPARISON --------------
/COM,
/COM,                       |   TARGET   |   MECHANICAL APDL   |   RATIO
/COM,
RESUME,TABLE1
/COM, TEMPERATURES AT THE INTERFACE FOR SOLID278
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),LABEL(1,3),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,A5,'   ',F10.3,'  ',1F14.3,'   ',1F15.3)
/COM,
RESUME,TABLE2
/COM, TEMPERATURES AT THE INTERFACE FOR SOLID279
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),LABEL(1,3),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,A5,'   ',F10.3,'  ',1F14.3,'   ',1F15.3)
/COM,
RESUME,TABLE3
/COM, TEMPERATURES AT THE INTERFACE FOR SOLID291
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),LABEL(1,3),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,A5,'   ',F10.3,'  ',1F14.3,'   ',1F15.3)
/COM,
/COM,------------------------------------------------------------------
/OUT,
FINI
*LIST,vm280,vrt

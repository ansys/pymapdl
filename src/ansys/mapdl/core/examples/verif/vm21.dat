/COM,ANSYS MEDIA REL. 2023R1 (11/04/2022) REF. VERIF. MANUAL: REL. 2023R1
/VERIFY,VM21
/PREP7
MP,PRXY,,0.3
/TITLE, VM21, TIE ROD WITH LATERAL LOADING, NO STREES STIFFENING 
C***   STR. OF MATLS., TIMOSHENKO, PART 2, 3RD ED., PAGE 42, ART. 6
ANTYPE,STATIC
ET,1,BEAM188
KEYOPT,1,3,3
SECT,1,BEAM,RECT
SECD,2.5,2.5
MP,EX,1,30E6
N,1                              ! DEFINE NODES
N,5,100
FILL
E,1,2                            ! DEFINE ELEMENTS
EGEN,4,1,1
D,ALL,UY,,,,,ROTX,ROTZ           ! BOUNDARY CONDITIONS AND LOADINGS
D,1,UZ
NSEL,S,,,5
DSYM,SYMM,X                      ! DEFINE SYMMETRY BOUNDARY
NSEL,ALL         
F,1,FX,-21972.6                  ! APPLY LOADS
SFBEAM,ALL,1,PRES,1.79253        
FINISH
/SOLU
/OUT,SCRATCH                            
SOLVE                          
FINISH
/POST1 
/OUT,
NSEL,S,,,1,5,4
PRNSOL,U,Z
PRNSOL,ROT,Y
NSEL,ALL
PRRSOL
RGHT_END = NODE (200,0,0)
LFT_END  = NODE (0,0,0)
*GET,UZ_MX_C2,NODE,RGHT_END,U,Z
*GET,SLOPE_C2,NODE,LFT_END,ROT,Y
FINISH
/POST26
RFORCE,2,RGHT_END,M,Y
STORE
*GET,M_MX_C2,VARI,2,EXTREM,VMAX 
FINISH
/PREP7
/TITLE, VM21, TIE ROD WITH LATERAL LOADING, STRESS STIFFENING PRESENT
NSUBST,5                         
AUTOTS,ON                        ! AUTO TIME STEPPING ACTIVATED
FINISH
/SOLU    
NLGEOM,ON
CNVTOL,F,,.0001,,1               ! SMALLER CONVERGENCE TOLERANCE
/OUT,SCRATCH
SOLVE                           
FINISH 
/POST1
/OUT,
NSEL,S,,,1,5,4
PRNSOL,U,Z
PRNSOL,ROT,Y
PRRSOL
*GET,UZ_MX_C1,NODE,RGHT_END,U,Z
*GET,SLOPE_C1,NODE,LFT_END,ROT,Y
FINISH
/POST26
RFORCE,2,RGHT_END,M,Y
STORE
*GET,M_MX_C1,VARI,2,EXTREM,VMAX 
*DIM,LABEL,CHAR,3,2
*DIM,VALUE_C1,,3,3
*DIM,VALUE_C2,,3,3
LABEL(1,1) = 'UZ MAX  ','SLOPE   ','MOMENT M'
LABEL(1,2) = '(in)    ','(rad)   ','AX in-lb'
*VFILL,VALUE_C1(1,1),DATA,-.19945,.0032352,-4580.1
*VFILL,VALUE_C1(1,2),DATA,UZ_MX_C1,SLOPE_C1,M_MX_C1
*VFILL,VALUE_C1(1,3),DATA,ABS(UZ_MX_C1/.19945),ABS(SLOPE_C1/.0032352), ABS(M_MX_C1/4580.1)
*VFILL,VALUE_C2(1,1),DATA,-.38241,.0061185,-8962.7
*VFILL,VALUE_C2(1,2),DATA,UZ_MX_C2,SLOPE_C2,M_MX_C2
*VFILL,VALUE_C2(1,3),DATA,ABS(UZ_MX_C1/.19945),ABS(SLOPE_C1/.0032352), ABS(M_MX_C1/4580.1)
/COM
/OUT,vm21,vrt
/COM,------------------- VM21 RESULTS COMPARISON ---------------------
/COM,
/COM,                        |     TARGET     |    Mechanical APDL   |   RATIO
/COM,
/COM,RESULTS FOR F<>0 (STIFFENED):
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE_C1(1,1),VALUE_C1(1,2),VALUE_C1(1,3)
(1X,A8,A8,'   ',F17.7,'  ',F19.7,'   ',1F15.3)
/COM,
/COM,RESULTS FOR F=0 (UNSTIFFENED):
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE_C2(1,1),VALUE_C2(1,2),VALUE_C2(1,3)
(1X,A8,A8,'   ',F17.7,'  ',F19.7,'   ',1F15.3)
/COM,-----------------------------------------------------------------
/OUT
FINISH
*LIST,vm21,vrt





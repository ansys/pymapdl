/COM,ANSYS MEDIA REL. 150 (11/8/2013) REF. VERIF. MANUAL: REL. 150
/VERIFY,VM36
/PREP7
/TITLE, VM36, LIMIT MOMENT ANALYSIS
!           MECHANICS OF SOLIDS, CRANDALL AND DAHL, 1959, PAGE 389, EX. 8.9
ANTYPE,STATIC
ET,1,BEAM188,,,3
ET,2,COMBIN40,,,5,             ! ROTY D.O.F. SPRING
SECT,1,BEAM,RECT
SECD,3.93597,3.93597
R,2,1E12,,,,27777.8           ! DEFINE REAL CONSTANTS FOR COMBIN ELEMENTS
R,3,1,,,,1E6
MP,EX,1,30E6                  ! DEFINE MATERIAL PROPERTIES
MP,NUXY,1,.3
N,1                           ! BEGIN NODES DEFINITION
N,2,100
N,3,100
N,4,150
N,5,150
E,1,2                         ! DEFINE BEAM ELEMENTS
E,3,4
TYPE,2                        ! DEFINE BREAKAWAY HINGE ELEMENTS
REAL,2  
E,2,3  
E,4,5
REAL,3  
E,2,3  
E,4,5                         ! EXTRA ELEMENTS FOR SOLUTION STABILITY
OUTPR,ALL,ALL                 
CNVTOL,M,27778,.001
CP,1,UX,2,3                   ! COUPLE TRANSLATIONS ACROSS PLASTIC HINGE
CPLGEN,1,UZ                   ! GENERATE 2ND SET IN DIRECTION UZ W/ SAME NODES
CPSGEN,2,2,1,2,1              ! GENERATE TWO ADDITIONAL SETS W/ DIFFERENT NODES
DSYM,SYMM,Y                   ! CONSTRAIN MODEL SYMMETRICALLY IN Y DIRECTION
D,1,UZ                        ! CONSTRAIN SIMPLY SUPPORTED END AGAINST DISP.
D,4,UZ,,,,,UX                 ! CONSTRAIN RIGID END AGAINST TWO DIRECTIONAL DISP
D,5,ROTY                      ! CONSTRAIN RIGID END AGAINST ROTATIONAL MOVEMENT
F,2,FZ,-1000                  ! APPLY ELASTIC FORCE AT HINGE B
FINISH
/SOLU    
SOLCONTROL,0
/OUT,SCRATCH
SOLVE                         ! WRITE LOAD STEP
FINISH

/POST26
/OUT,
RFORCE,2,1,F,Z
RFORCE,3,5,M,Y
STORE
*GET,RA,VARI,2,EXTREM,VMAX 
*GET,MC,VARI,3,EXTREM,VMAX 
*GET,UB,NODE,2,U,Z
*DIM,LABEL1,CHAR,3,2
*DIM,VALUE1,,3,3
LABEL1(1,1) = 'DEFLECTI','REACTION','MOMENT_C'
LABEL1(1,2) = 'ON (in) ','_A (lb) ',' (ib-lb)'
*VFILL,VALUE1(1,1),DATA,-.02829,148.15,27778
*VFILL,VALUE1(1,2),DATA,UB,RA,MC
*VFILL,VALUE1(1,3),DATA,ABS(UB/.02829) ,ABS(RA/148.15 ),ABS(MC/27778)
SAVE,TABLE_1
FINISH

/SOLUTION

NSUBST,3                      ! USE CONVERGENCE CRITERIA,3 SUBSTEPS MAX
OUTPR,ALL,LAST                ! PRINT LAST ITERATION.
F,2,FZ,-1388.8                ! APPLY VALUE SLIGHTLY SMALLER THAN PL TO HINGE B
/OUT,SCRATCH
SOLVE
FINI

/POST1
/OUT,
ESEL,S,ELEM,,3,4
ETABLE,ELEM_STAT,NMISC,1	    !RETRIEVE THE STATUS(COMBIN40)
ESEL,ALL
PRETAB,

/GRAPHICS,POWER
/ESHAPE,1
/VIEW,1,1,1,1
/SHOW

PLETAB,ELEM_STAT                    !PLOT ELEMENT TABLE(COMBIN40 STATUS)
*GET,PLETAB_MAX1,PLNSOL,0,MAX
*GET,PLETAB_MIN1,PLNSOL,0,MIN

*DIM,LABEL2,CHAR,2,2
*DIM,VALUE2,,2,3
LABEL2(1,1) = 'HINGE','HINGE'
LABEL2(1,2) = '@B','@C'
*VFILL,VALUE2(1,1),DATA,1.0,2.0
*VFILL,VALUE2(1,2),DATA,PLETAB_MIN1,PLETAB_MAX1
*VFILL,VALUE2(1,3),DATA,ABS(PLETAB_MIN1/1.0) ,ABS(PLETAB_MAX1/2)
SAVE,TABLE_2

FINI

/SOLU

F,2,FZ,-1390                  ! APPLY VALUE SLIGHTLY LARGER THAN PL TO HINGE B
!                               LARGE DISPLACEMENT VALUES INDICATE COLLAPSE
!                               AND PLASTIC DEFORMATION
/OUT,SCRATCH
SOLVE

FINISH

/POST1
/OUT,
ESEL,S,ELEM,,3,4
ETABLE,ELEM_STAT,NMISC,1		!RETRIEVE THE STATUS(COMBIN40)
ESEL,ALL
PRETAB,

PLETAB,ELEM_STAT			!PLOT ELEMENT TABLE(COMBIN40 STATUS)

*GET,PLETAB_MAX2,PLNSOL,0,MAX
*GET,PLETAB_MIN2,PLNSOL,0,MIN

*DIM,LABEL3,CHAR,2,2
*DIM,VALUE3,,2,3
LABEL3(1,1) = 'HINGE','HINGE'
LABEL3(1,2) = '@B','@C'
*VFILL,VALUE3(1,1),DATA,-2.0,2.0
*VFILL,VALUE3(1,2),DATA,PLETAB_MIN2,PLETAB_MAX2
*VFILL,VALUE3(1,3),DATA,ABS(ABS(PLETAB_MIN2)/2.0) ,ABS(PLETAB_MAX2/2.0)
SAVE,TABLE_3
FINI

RESUME,TABLE_1
/COM
/OUT,vm36,vrt
/COM,------------------- VM36 RESULTS COMPARISON ---------------
/COM,
/COM,                 |   TARGET   |   ANSYS   |   RATIO
/COM,
/COM,RESULTS FOR P=1000 LBS (ELASTIC):
/COM,
*VWRITE,LABEL1(1,1),LABEL1(1,2),VALUE1(1,1),VALUE1(1,2),VALUE1(1,3)
(1X,A8,A8,'   ',F11.5,'  ',F11.5,'   ',1F5.3)
/COM,
/COM,
/NOPR
/COM,RESULTS FOR P=1388.8 LBS:
/COM
RESUME,TABLE_2
*VWRITE,LABEL2(1,1),LABEL2(1,2),VALUE2(1,1),VALUE2(1,2),VALUE2(1,3)
(1X,A8,A8,'   ',F11.5,'  ',F11.5,'   ',1F5.3)
/NOPR
/COM,
/COM,RESULTS FOR P=1390 LBS :
/COM
RESUME,TABLE_3
*VWRITE,LABEL3(1,1),LABEL3(1,2),VALUE3(1,1),VALUE3(1,2),VALUE3(1,3)
(1X,A8,A8,'   ',F11.5,'  ',F11.5,'   ',1F5.3)
/COM,
/COM,-----------------------------------------------------------
/COM,

/OUT
FINISH
*LIST,vm36,vrt

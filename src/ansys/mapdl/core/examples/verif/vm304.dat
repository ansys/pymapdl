/COM,ANSYS MEDIA REL. 2023R1 (11/04/2022) REF. VERIF. MANUAL: REL. 2023R1
/VERIFY,VM304
/TITLE,VM304,SLOSHING MODES IN A HOLLOW CYLINDRICAL CAVITY
/COM,
/COM, REFERENCE: ROBERT D. BLEVINS, "FORMULAS FOR NATURAL FREQUENCY AND MODE SHAPE",
/COM,            KRIEGER PUBLISHING COMPANY; MALABAR, FLORIDA, USA, 1993.
/COM,
/COM, PARAMETER FOR CREATING THE CDB FILES TO STORE GEOMETRY AND MESH DATA
_GEOMGEN = 0   ! SET TO 1 TO WRITE CDB FILES IF ANY INPUT FILE MODIFICATIONS
/OUT,SCRATCH_vm304

! PREPROCESSING
/PREP7
*IF,_GEOMGEN,EQ,1,THEN

! MATERIAL PARAMETERS
C0    = 1500       ! SPEED OF SOUND
RHO_F = 1000       ! FLUID DENSITY
E     = 1.44E11    ! YOUNG'S MODULUS
NU    = 0.35       ! POISSON'S RATIO
RHO_S = 7700       ! SOLID DENSITY

! GEOMETRY PARAMETERS
RI = 0.1           ! INNER RADIUS
RO = 0.2           ! OUTER RADIUS
H  = 0.1           ! HEIGHT

! MESH PARAMETERS
NR     = 5
NTHETA = 30
NZ     = 5

! CREATE GEOMETRY
LOCAL,11,,,,,,,90           ! LOCAL COORDINATE
WPCSYS,,11
CYL4,0,0,RI,,RO,,H
WPRO,,90.
VSBW,ALL
WPRO,,,90.
VSBW,ALL
LOCAL,12,1,,,,,,90
CSYS,12
LSEL,ALL
LSEL,U,LOC,X,RI
LSEL,U,LOC,X,RO
LESIZE,ALL,,,NR
LSEL,S,LOC,X,RI
LSEL,U,LOC,Z,H
LSEL,U,LOC,Z,0.0
LESIZE,ALL,,,NZ
LSEL,S,LOC,X,RO
LSEL,U,LOC,Z,H
LSEL,U,LOC,Z,0.0
LESIZE,ALL,,,NZ
LSEL,S,LOC,X,RI
LSEL,R,LOC,Z,H
LESIZE,ALL,,,NTHETA
LSEL,S,LOC,X,RO
LSEL,R,LOC,Z,H
LESIZE,ALL,,,NTHETA
LSEL,S,LOC,X,RI
LSEL,R,LOC,Z,0.0
LESIZE,ALL,,,NTHETA
LSEL,S,LOC,X,RO
LSEL,R,LOC,Z,0.0
LESIZE,ALL,,,NTHETA
CSYS,11
ALLS
MSHKEY,1

! DEFINE ELEMENTS AND MATERIALS
ET,1,220,,0        ! COUPLED ACOUSTIC ELEMENT FLUID220
MP,SONC,1,C0
MP,DENS,1,RHO_F
ET,2,186,          ! STRUCTURAL ELEMENT SOLID186
MP,EX,2,E
MP,DENS,2,RHO_S
MP,NUXY,2,NU

! CREATE MESH
TYPE,1
MAT,1
VSWEEP,ALL
ALLS
TYPE,2
MAT,2
VSYMM,Z,ALL
ALLS
NUMMGR,ALL
VSEL,S,LOC,Z,0,-H
ESLV,S,
EMODIF,ALL,,2
ALLS

! FLAG FREE SURFACE
NSEL,S,LOC,Z,H
SF,ALL,FREE

! DEFINE BOUNDARY CONDITIONS AT BOTTOM
NSEL,S,LOC,Z,-H
D,ALL,UZ,0
D,ALL,UX,0
D,ALL,UY,0

! FLAG FSI INTERFACE
NSEL,S,LOC,Z,0
SF,ALL,FSI

ALLS
CSYS,0

! GRAVITY ACCELERATION
ACEL,9.81,0,0
ALLS

CDWRITE,db,vm304,cdb
*ELSE
 CDREAD,db,vm304,cdb
*ENDIF

FINISH

! PERFORM SOLUTION
/SOLU
ANTYPE,MODAL
MODOPT,UNSYM,19,0.05
MXPAND,ALL
SOLVE
FINI

! POST-PROCESSING
/POST1

! ANALYTICAL RESULTS
*DIM,ANALNUMCIRC,CHAR,10
*DIM,ANALNUMRAD,CHAR,10
*DIM,ANALFREQ,ARRAY,10
ANALNUMCIRC(1) = '0','0','0','0','0','1','1','1','0','1'
ANALNUMRAD(1)  = '1','2','3','4','5','0','1','2','6','3'
ANALFREQ(1)    = 0.9963,1.7042,2.1755,2.5214,2.8064,2.8184,2.8560,2.9623,3.0449,3.1210

! NUMERICAL RESULTS
*DIM,NUMFREQ,ARRAY,10
*DIM,NUMFREQSUBS,ARRAY,10   ! NUMERICAL FREQUENCIES CORRESPONDING TO SUBSTEPS
NUMFREQSUBS(1) = 1,3,5,7,9,11,12,14,16,18
*DO,I,1,10
    SET,1,NUMFREQSUBS(I)
    *GET,NUMFREQ(I),ACTIVE,,SET,FREQ
*ENDDO

! RATIO
*DIM,RATIO,ARRAY,10
*DO,I,1,10
    RATIO(I) = ABS(NUMFREQ(I)/ANALFREQ(I))
*ENDDO

! PRINT FIGURE
/SHOW,PNG,REV
/VUP,,X
/VIEW,,1,1,1
EPLOT  ! FIGURE 1
/EDGE,,1
SET,1,3
PLNSOL,PRES ! FIGURE 2
SET,1,14
PLNSOL,PRES ! FIGURE 3

/OUT,vm304,vrt
/COM,-------------------- VM304 RESULTS COMPARISON ---------------------
/COM,
/COM,-------------------------------------------------------------------
/COM,  MODAL FREQUENCIES
/COM,-------------------------------------------------------------------
/COM,
/COM,(I,J) |  TARGET      |  MECHANICAL APDL  | RATIO
/COM,
*VWRITE,ANALNUMCIRC(1),ANALNUMRAD(1),ANALFREQ(1),NUMFREQ(1),RATIO(1)
(' (',A1,',',A1,')',' |  ',F6.4,'      |  ',F6.4,'           |  ',F5.3)
/COM,
/COM,-------------------------------------------------------------------
/OUT,
*LIST,vm304,vrt
FINISH

/EXIT,NOSAVE

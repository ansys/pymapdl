/COM,ANSYS MEDIA REL. 2023R1 (11/04/2022) REF. VERIF. MANUAL: REL. 2023R1
/VERIFY,VM34
/TITLE, VM34, BENDING OF A TAPERED PLATE (BEAM)
C***     INTROD. TO STRESS ANALYSIS, HARRIS, 1ST PRINTING, PAGE 114, PROB. 61
C***     PLATE ELEMENTS (SHELL63)
/PREP7
ANTYPE,STATIC               ! STATIC ANALYSIS
ET,1,SHELL63,2
R,1,.5                      ! THICKNESS = 0.5
MP,EX,1,30E6
MP,NUXY,1,0                 ! POISSON'S RATIO IS ZERO
N,1
N,8,20,-1.5
FILL
N,11
N,18,20,1.5
FILL
E,1,2,12
E,2,3,12
E,13,12,3
E,3,4,14
E,14,13,3
EGEN,3,2,2,5
CP,1,UZ,2,12                ! COUPLE APPROPRIATE DEGREES OF FREEDOM
CP,2,ROTY,2,12
CPSGEN,6,1,1,2              ! GENERATE 6 SETS OF EQUATIONS
OUTPR,ALL,ALL
D,8,ALL,,,18,10
D, ALL,ROTX,0               ! REMOVE "TORSIONAL" DEGREES OF FREEDOM
F,1,FZ,-10
FINISH
/SOLU    
/OUT,SCRATCH
SOLVE
FINISH
/POST1
/OUT,
ETABLE,STRS,S,1             ! STORE S1(TOP) FOR SHELL63
ESORT,STRS                  ! SORT ELEMENTS BASED ON S1(TOP)
*GET,SMAX,SORT,,MAX         ! GET MAXIMUM S1 AS SMAX
PRNSOL,DOF                  ! PRINT NODAL DISPLACEMENTS
LFT_NODE = NODE (0,0,0)
*GET,DEFL,NODE,LFT_NODE,U,Z
*DIM,LABEL,CHAR,2,2
*DIM,VALUE_C1,,2,3
LABEL(1,1) = 'DEFLECTI','MX_PRIN_'
LABEL(1,2) = 'ON (in) ','STRS psi'
*VFILL,VALUE_C1(1,1),DATA,-.042667,1600
*VFILL,VALUE_C1(1,2),DATA,DEFL,SMAX
*VFILL,VALUE_C1(1,3),DATA,ABS(DEFL/.042667 ) ,ABS( SMAX/1600 )
FINISH
SAVE,TABLE_1
/CLEAR, NOSTART 
/PREP7
/TITLE, VM34, BENDING OF A TAPERED PLATE (BEAM188)
C***          TAPERED BEAM ELEMENTS (BEAM188)
ANTYPE,STATIC               ! STATIC ANALYSIS
ET,1,BEAM188
MP,EX,1,30E6                
MP,NUXY,1,0                 ! POISSON'S RATIO IS ZERO
SECTYPE,1,BEAM,RECT         ! RECTANGULAR BEAM CROSS-SECTION
SECDATA,1E-6,0.5           ! CROSS-SECTION AT LEFT END OF TAPERED BEAM
SECTYPE,2,BEAM,RECT         ! RECTANGULAR BEAM CROSS-SECTION
SECDATA,3.0,0.5             ! CROSS-SECTION AT RIGHT END OF TAPERED BEAM
SECTYPE,3,TAPER             ! TAPERED BEAM
SECDATA,1, 0.0,0.0          ! STARTING LOCATION OF TAPERED BEAM
SECDATA,2, 20.0, 0.0        ! ENDING LOCATION OF TAPERED BEAM
N,1
N,8,20
FILL
N,10,,,1                 
NGEN,8,1,10              
SECNUM,3
E,1,2,10
EGEN,7,1,1,,,,,1  
D,8,ALL
D,1,UY,,,7,,ROTX,ROTZ
F,1,FZ,-10
FINISH
/SOLU    
/OUT,SCRATCH
SOLVE
FINISH  
/POST1
/OUT,
*GET,SMAX,SECR,ALL,S,X,MAX  ! HIGHEST COMPONENT TOTAL STRESS OF ALL ELEMENTS 
PRNSOL,DOF                  ! PRINT NODAL DISPLACEMENTS
LFT_NODE = NODE (0,0,0)
*GET,DEFL,NODE,LFT_NODE,U,Z
*DIM,LABEL,CHAR,2,2
*DIM,VALUE_C2,,2,3
LABEL(1,1) = 'DEFLECTI','MX_PRIN_'
LABEL(1,2) = 'ON (in) ','STRS psi'
*VFILL,VALUE_C2(1,1),DATA,-.042667,1600
*VFILL,VALUE_C2(1,2),DATA,DEFL,SMAX
*VFILL,VALUE_C2(1,3),DATA,ABS(DEFL/.042667 ) ,ABS( SMAX/1600 )
FINISH
SAVE,TABLE_2
/CLEAR, NOSTART 
/TITLE, VM34, BENDING OF A TAPERED PLATE (BEAM)
C***     SHELL ELEMENTS (SHELL181)
/PREP7
ANTYPE,STATIC               ! STATIC ANALYSIS
ET,1,SHELL181
SECTYPE,1,SHELL
SECDATA,0.5,1,0,3           ! THICKNESS = 0.5                     
MP,EX,1,30E6
MP,NUXY,1,0                 ! POISSON'S RATIO IS ZERO
N,1
N,8,20,-1.5
FILL
N,11
N,18,20,1.5
FILL
E,1,2,12
E,2,3,12
E,13,12,3
E,3,4,14
E,14,13,3
EGEN,3,2,2,5
CP,1,UZ,2,12                ! COUPLE APPROPRIATE DEGREES OF FREEDOM
CP,2,ROTY,2,12
CPSGEN,6,1,1,2              ! GENERATE 6 SETS OF EQUATIONS
OUTPR,ALL,ALL
D,8,ALL,,,18,10
D, ALL,ROTX,0               ! REMOVE "TORSIONAL" DEGREES OF FREEDOM
F,1,FZ,-10
FINISH
/SOLU    
/OUT,SCRATCH
SOLVE
FINISH
/POST1
/OUT,
ETABLE,STRS,S,1             ! STORE S1(TOP) FOR SHELL181
ESORT,STRS                  ! SORT ELEMENTS BASED ON S1(TOP)
*GET,SMAX,SORT,,MAX         ! GET MAXIMUM S1 AS SMAX
PRNSOL,DOF                  ! PRINT NODAL DISPLACEMENTS
LFT_NODE = NODE (0,0,0)
*GET,DEFL,NODE,LFT_NODE,U,Z
*DIM,LABEL,CHAR,2,2
*DIM,VALUE_C3,,2,3
LABEL(1,1) = 'DEFLECTI','MX_PRIN_'
LABEL(1,2) = 'ON (in) ','STRS psi'
*VFILL,VALUE_C3(1,1),DATA,-.042667,1600
*VFILL,VALUE_C3(1,2),DATA,DEFL,SMAX
*VFILL,VALUE_C3(1,3),DATA,ABS(DEFL/.042667 ) ,ABS( SMAX/1600 )
FINISH
SAVE,TABLE_3
/CLEAR, NOSTART 
/TITLE, VM34, BENDING OF A TAPERED PLATE (BEAM)
C***     SHELL ELEMENTS (SHELL281)
/PREP7
ANTYPE,STATIC               ! STATIC ANALYSIS
ET,1,SHELL281, , ,
SECTYPE,1,SHELL
SECDATA,0.5,1,0,3           ! THICKNESS = 0.5
MP,EX,1,30E6
MP,NUXY,1,0                 ! POISSON'S RATIO IS ZERO
K, ,0
K, ,20,-1.5
K, ,20,1.5
A,1,2,3
LSEL,S,LINE,,1,3,2
LESIZE,ALL, , ,7
LSEL,INVE
LESIZE,ALL, , ,1
LSEL,ALL
AMESH,1
CP,1,UZ,3,30
CP,2,ROTY,3,30
CP,3,UZ,4,31,29
CP,4,ROTY,4,31,29
CP,5,UZ,6,36,27
CP,6,ROTY,6,36,27
CP,7,UZ,8,32,25
CP,8,ROTY,8,32,25
CP,9,UZ,10,33,23
CP,10,ROTY,10,33,23
CP,11,UZ,12,34,21
CP,12,ROTY,12,34,21
CP,13,UZ,14,35,19
CP,14,ROTY,14,35,19
OUTPR,NSOL,ALL
OUTPR,RSOL,ALL
NSEL,S,LOC,X,20
D,ALL,ALL
NSEL,ALL
D,ALL,ROTX,0
F,1,FZ,-10
FINISH
/SOLU    
/OUT,SCRATCH
SOLVE
FINISH
/POST1
/OUT,
ETABLE,STRS,S,1             ! STORE S1(TOP) 
ESORT,STRS                  ! SORT ELEMENTS BASED ON S1(TOP)
*GET,SMAX,SORT,,MAX         ! GET MAXIMUM S1 AS SMAX
PRNSOL,DOF                  ! PRINT NODAL DISPLACEMENTS
LFT_NODE = NODE (0,0,0)
*GET,DEFL,NODE,LFT_NODE,U,Z
*DIM,LABEL,CHAR,2,2
*DIM,VALUE_C4,,2,3
LABEL(1,1) = 'DEFLECTI','MX_PRIN_'
LABEL(1,2) = 'ON (in) ','STRS psi'
*VFILL,VALUE_C4(1,1),DATA,-.042667,1600
*VFILL,VALUE_C4(1,2),DATA,DEFL,SMAX
*VFILL,VALUE_C4(1,3),DATA,ABS(DEFL/.042667 ) ,ABS( SMAX/1600 )
FINISH
SAVE,TABLE_4
/NOPR
RESUME,TABLE_1
/COM
/OUT,vm34,vrt
/COM,------------------- VM34 RESULTS COMPARISON ---------------
/COM,
/COM,                 |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
/COM,RESULTS USING SHELL63:
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE_C1(1,1),VALUE_C1(1,2),VALUE_C1(1,3)
(1X,A8,A8,'   ',F12.6,'  ',F16.6,'   ',1F15.3)
/NOPR
RESUME,TABLE_2
/GOPR
/COM,
/COM,RESULTS USING TAPERED BEAM188:
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE_C2(1,1),VALUE_C2(1,2),VALUE_C2(1,3)
(1X,A8,A8,'   ',F12.6,'  ',F16.6,'   ',1F15.3)
/NOPR
RESUME,TABLE_3
/GOPR
/COM,
/COM,RESULTS USING SHELL181:
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE_C3(1,1),VALUE_C3(1,2),VALUE_C3(1,3)
(1X,A8,A8,'   ',F12.6,'  ',F16.6,'   ',1F15.3)
/NOPR
RESUME,TABLE_4
/GOPR
/COM,
/COM,RESULTS USING SHELL281:
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE_C4(1,1),VALUE_C4(1,2),VALUE_C4(1,3)
(1X,A8,A8,'   ',F12.6,'  ',F16.6,'   ',1F15.3)
/COM,-----------------------------------------------------------
/OUT
FINISH
*LIST,vm34,vrt
/DELETE,TABLE_1
/DELETE,TABLE_2
/DELETE,TABLE_3
/DELETE,TABLE_4

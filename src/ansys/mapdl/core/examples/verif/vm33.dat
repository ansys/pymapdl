/COM,ANSYS MEDIA REL. 2023R1 (11/04/2022) REF. VERIF. MANUAL: REL. 2023R1
/VERIFY,VM33
JPGPRF,500,100,1             ! MACRO TO SET PREFS FOR JPEG PLOTS
/SHOW,JPEG
/PREP7
/TITLE, VM33, TRANSIENT THERMAL STRESS IN A CYLINDER
/COM,   REF: ROARK AND YOUNG "FORMULAS FOR STRESS AND STRAIN",5TH
/COM,   EDITION, MCGRAW-HILL, PG. 585
/COM,
ET,1,SOLID5                    ! SOLID5 UX,UY,UZ,TEMP,VOLT,MAG DOF SET
MP,KXX,1,625E-6                ! DEFINE THERMAL CONDUCTIVITY
MP,EX,1,30E6                   ! MODULUS OF ELASTICITY
MP,NUXY,1,.3                   ! POISSON'S RATIO
MP,ALPX,1,8.4E-6               ! COEFFICIENT OF THERMAL EXPANSION
MP,DENS,1,.284                 ! DENSITY (LB/IN**3)
MP,C,1,.10                     ! SPECIFIC HEAT
CSYS,1
H=.20                          ! MODEL HEIGHT
TH=2.5                         ! MODEL HALF-ANGLE
A=1                            ! INNER RADIUS
B=3                            ! OUTER RADIUS
N,1,A,TH                       ! DEFINE KEYPOINTS
N,16,B,TH
FILL,1,16,14,2,1,,,5 
NGEN,2,16,1,16,1,,,H 
NGEN,2,32,1,32,1,,-(TH*2)
E,1,2,18,17,33,34,50,49
EGEN,15,1,-1
SAVE                           ! SAVE DB FOR LATER USE
NSEL,S,LOC,Y,TH
NSEL,A,LOC,Y,-TH
DSYM,SYMM,Y,1                  ! DEFINE STRUCTURAL B.C.
NSEL,S,LOC,Z
DSYM,SYMM,Z,1
NSEL,S,LOC,Z,H                 ! SELECT NODES ON TOP SURFACE
CP,1,UZ,ALL                    ! COUPLE ALL NODES IN UZ
NSEL,S,LOC,X,B                 ! SELECT NODES AT OUTER RADIUS
D,ALL,TEMP,500                 ! DEFINE FINAL SURFACE TEMPERATURE
NSEL,ALL
FINISH
/out,scratch
/SOLU
ANTYPE,TRANS                   ! TRANSIENT ANALYSIS
TIMINT,OFF,STRUC               ! SUPPRESS STRUCTURAL DYNAMICS
CNVTOL,HEAT                    ! CONVERGENCE BASED ON HEAT FLOWS
CNVTOL,F                       !  AND FORCES ONLY
AUTOTS,ON                      ! AUTOMATIC TIME STEPPING
OUTRES,,ALL                    ! RESULTS FOR ALL TIME POINTS
KBC,0                          ! RAMP LOAD OVER LOAD STEP
TREF,70                        ! SET REFERENCE TEMPERATURE
TUNIF,70                       ! SET INITIAL UNIFORM TEMPERATURE
DELTIM,1,,60                   ! MINIMUM TIME STEP OF 1 SEC
TIME,430                       ! TIME AT END OF LOAD STEP
SOLVE
FINISH
/out
/POST1
*GET,IN_STRS,NODE,1,S,Y
*GET,OUT_STRS,NODE,16,S,Y
FINISH
/POST26
NSOL,2,1,TEMP                  ! STORE TEMP AT INNER RADIUS
NSOL,3,16,TEMP                 ! STORE TEMP AT OUTER RADIUS
ESOL,4,1,1,S,Y,SYB             ! STORE SY AT INNER RADIUS
ESOL,5,15,16,S,Y,SYC            ! STORE SY AT OUTER RADIUS
ADD,6,3,2,,DELT,,,1,-1         ! CALCULATE DELTA TEMP. (OUTER-INNER)
PRVAR,2,3,4,5,6                ! PRINT VARIABLES VS. TIME
/GRID,1
/AXLAB,Y,DELT
PLVAR,6                        ! DISPLAY DELTA TEMP. VS TIME
/AXLAB,Y,SY
PLVAR,4,5                      ! DISPLAY SY VS. TIME

*DIM,LABEL,CHAR,2,2
*DIM,VALUE,,2,3
LABEL(1,1) = 'STRS R=B','STRS R=A'
LABEL(1,2) = '(PSI)   ','(PSI)   '
*VFILL,VALUE(1,1),DATA,-13396,10342
*VFILL,VALUE(1,2),DATA,OUT_STRS,IN_STRS
*VFILL,VALUE(1,3),DATA,ABS(OUT_STRS/13396),ABS(IN_STRS/10342)
/COM
/OUT,vm33,vrt
/COM,------------------- VM33 RESULTS COMPARISON ---------------
/COM,
/COM,                 |   TARGET   |   Mechanical APDL   |   RATIO
/COM,SOLID5
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.0,'  ',F14.0,'   ',1F15.3)
/OUT
FINISH
!
/CLEAR,NOSTART
/UNITS,BIN
/PREP7
RESUME
ET,1,SOLID226,11,0             ! SOLID226 UX,UY,UZ,TEMP
EMID,ADD
MP,KXX,1,625E-6*9.34E3         ! DEFINE THERMAL CONDUCTIVITY, LBF/(S-F)
MP,EX,1,30E6                   ! MODULUS OF ELASTICITY, PSI
MP,NUXY,1,.3                   ! POISSON'S RATIO
MP,ALPX,1,8.4E-6               ! COEFFICIENT OF THERMAL EXPANSION, 1/F
MP,DENS,1,.284                 ! DENSITY (LB/IN**3)
MP,C,1,0.10*9.34E3             ! SPECIFIC HEAT, LBF-IN/(F-LB)
NSEL,S,LOC,Y,TH
NSEL,A,LOC,Y,-TH
DSYM,SYMM,Y,1                  ! DEFINE STRUCTURAL B.C.
NSEL,S,LOC,Z
DSYM,SYMM,Z,1
NSEL,S,LOC,Z,H                 ! SELECT NODES ON TOP SURFACE
CP,1,UZ,ALL                    ! COUPLE ALL NODES IN UZ
NSEL,S,LOC,X,B                 ! SELECT NODES AT OUTER RADIUS
D,ALL,TEMP,500                 ! DEFINE FINAL SURFACE TEMPERATURE
NSEL,ALL
FINISH
/out,scratch
/SOLU
ANTYPE,TRANS                   ! TRANSIENT ANALYSIS
TIMINT,OFF,STRUC               ! SUPPRESS STRUCTURAL DYNAMICS
CNVTOL,HEAT                    ! CONVERGENCE BASED ON HEAT FLOWS
CNVTOL,F                       !  AND FORCES ONLY
AUTOTS,ON                      ! AUTOMATIC TIME STEPPING
OUTRES,,ALL                    ! RESULTS FOR ALL TIME POINTS
KBC,0                          ! RAMP LOAD OVER LOAD STEP
TREF,70                        ! SET REFERENCE TEMPERATURE
TUNIF,70                       ! SET INITIAL UNIFORM TEMPERATURE
TOFFST,460
DELTIM,1,,60                   ! MINIMUM TIME STEP OF 1 SEC
TIME,430                       ! TIME AT END OF LOAD STEP
SOLVE
FINISH
/out
/POST1
*GET,IN_STRS,NODE,1,S,Y
*GET,OUT_STRS,NODE,16,S,Y
FINISH
/POST26
NSOL,2,1,TEMP                  ! STORE TEMP AT INNER RADIUS
NSOL,3,16,TEMP                  ! STORE TEMP AT OUTER RADIUS
ESOL,4,1,1,S,Y,SYB             ! STORE SY AT INNER RADIUS
ESOL,5,15,16,S,Y,SYC            ! STORE SY AT OUTER RADIUS
ADD,6,3,2,,DELT,,,1,-1         ! CALCULATE DELTA TEMP. (OUTER-INNER)
PRVAR,2,3,4,5,6                ! PRINT VARIABLES VS. TIME
/GRID,1
/AXLAB,Y,DELT
PLVAR,6                        ! DISPLAY DELTA TEMP. VS TIME
/AXLAB,Y,SY
PLVAR,4,5                      ! DISPLAY SY VS. TIME
*DIM,LABEL,CHAR,2,2
*DIM,VALUE,,2,3
LABEL(1,1) = 'STRS R=B','STRS R=A'
LABEL(1,2) = '(PSI)   ','(PSI)   '
*VFILL,VALUE(1,1),DATA,-13396,10342
*VFILL,VALUE(1,2),DATA,OUT_STRS,IN_STRS
*VFILL,VALUE(1,3),DATA,ABS(OUT_STRS/13396),ABS(IN_STRS/10342)
/COM
/OUT,vm33,vrt,,append
/COM,SOLID226
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.0,'  ',F14.0,'   ',1F15.3)
/COM,-----------------------------------------------------------
/OUT
FINISH
*LIST,vm33,vrt


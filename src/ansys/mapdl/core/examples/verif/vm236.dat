/COM,ANSYS MEDIA REL. 2023R1 (11/04/2022) REF. VERIF. MANUAL: REL. 2023R1
/VERIFY,VM236
/TITLE,VM236,HYSTERESIS LOOP VERIFICATION OF A CLAMPED BEAM
JPGPRF,500,100,1             ! MACRO TO SET PREFS FOR JPEG PLOTS
/SHOW,JPEG
/COM, ------------------------------------------------------
/COM,          2D BEAM UNDER ELECTROSTATIC LOAD
/COM,          --------------------------------
/COM,   COMPARE WITH 3D MODEL FROM THE PAPER:
/COM,   J.R.GILBERT, G.K.ANANTHASURESH, S.D.SENTURIA, (MIT)
/COM,   "3D MODELLING OF CONTACT PROBLEMS AND HYSTERESIS IN
/COM,    COUPLED ELECTRO-MECHANICS", MEMS'96, PP. 127-132.
/COM,
/COM,                       3d MODEL:
/COM,   BEAM IS CLAMPED AT EITHER END, SUSPENDED 0.7UM OVER
/COM,   A GROUND PLANE WITH CONTACT STOP AT 0.1UM ABOVE THE
/COM,   GROUND PLANE.   BEAM DIMENSIONS AND MATERIAL PROPERTIES:
/COM,   LENGTH BL=80UM, WIDTH WB=10UM, HEIGHT BH=.5UM, E=169GPA, MU=0.25
/COM,   INITIAL GAP: GAP=0.7UM , FINISHING GAP GFI=0.1UM
/COM,   MAXIMUM DISPLACEMENT IS 0.6UM (GAP-GFI)
/COM,
/COM,	VALUE OF THE PULL-IN VOLTAGE: 18v
/COM,	BOTH PULL-IN AND RELEASE BEHAVIORS ARE MODELED (HYSTERESIS LOOP).

!-------------- Control parameters ----------------------
*DIM,UU,ARRAY,5			! RESULTS ARRAY

VLTG1 = 11.0			! BIAS VOLTAGE 1
VLTG2 = 14.5			! BIAS VOLTAGE 2
VLTG  = 18.0   			! PULL-IN VOLTAGE

ESIZE=0.5     			! ELEMENT MESH SIZE

!---------------- Geometry parameters ---------------

BL=40				! BEAM LENGTH
BH=.5				! BEAM HEIGHT
GAP=.7				! MAXIMUM GAP
GAP0=.6				! AIR GAP
EPS0=8.854E-6

!---------------------- Model ---------------

/PREP7

EMUNIT,EPZRO,EPS0

ET,1,182,3,,2
ET,2,223,1001,1,,1   ! force coupling 
ET,3,178,,1

MP,EX,1,169E3
MP,NUXY,1,0.25
MP,PERX,2,1
MP,EX,2,1E-5
MP,NUXY,2,0.0
MP,MU,3,0

R,1,C0,EPS0
R,2,-1690

RECT,,BL,GAP,GAP+BH
RECT,,BL,,GAP+BH
AOVLAP,ALL
NUMMRG,KP

ASEL,S,LOC,Y,GAP+BH/2
AATT,1,,1
ASEL,INVERT
AATT,2,1,2
CM,AREA1,AREA

ALLS
ESIZE,ESIZE

ASEL,S,MAT,,1
MSHAPE,0,2D
MSHKEY,2
AMESH,ALL

ASEL,S,MAT,,2         ! "ELASTIC AIR" 
MSHA,0,2D
MSHMID,2              ! MESH WITHOUT MIDSIDE NODES
LSEL,S,,,9,10
LESIZE,ALL,,,1        ! ONE ELEMENT THROUGH LAYER
AMESH,ALL

TYPE,3				! GAP ELEMENT MESH
MAT,3
REAL,2
*GET,NOMAX,NODE,0,NUM,MAX
KN=BL/ESIZE
K8=NOMAX+1
XL=0
*DO,I8,1,KN+1
 N52=K8
 N53=N52+1
 N,N52,XL,GAP
 N,N53,XL,GAP-GAP0
 E,N53,N52
 K8=K8+2
 XL=XL+ESIZE
*ENDDO
NUMMRG,NODE
ALLS

!--------------- BOUNDARY CONDITIONS -----------------

ESEL,S,TYPE,,2
NSLE,S
NSEL,R,LOC,Y,GAP
CM,BNODE,NODE

NSLE,S
NSEL,R,LOC,Y,0
D,ALL,VOLT,0			! GROUND 

ALLSEL,ALL
NSEL,S,LOC,X,0     		!  FIX LEFT END
NSEL,A,LOC,Y,0     		!  FIX BOTTOM
D,ALL,UX,0
D,ALL,UY,0

ALLSEL,ALL
NSEL,S,LOC,X,BL    		!  SYMMETRY LINE
D,ALL,UX,0

ESEL,S,TYPE,,3
NSLE,S
NSEL,R,LOC,Y,GAP-GAP0   	! FIX GAP ELEMENTS
D,ALL,ALL

ALLSEL,ALL
D,ALL,UZ			! CONSTRAIN CONTACT178 ELEMENTS IN Z
FINISH
SAVE

!/PNUM,TYPE,1
/AUTO,1
EPLOT

!------------------ Loading (below pull-in) ---------------------

/SOLU

EQSLV,SPARSE
CNVTOL,F,1,1.0E-4
AUTOTS,ON
NSUBST,1
OUTRES,ALL,ALL
NEQIT,50
NLGEOM,ON

CMSEL,S,BNODE			! BIAS 1
D,ALL,VOLT,VLTG1
ALLSEL,ALL

/OUT,SCRATCH
SOLVE

CMSEL,S,BNODE			! BIAS 2
D,ALL,VOLT,VLTG2
ALLSEL,ALL

SOLVE
FINISH

!---------------------------- POSTPROCESSING ------------------
/POST26
NSEL,S,,,2 		
NSOL,2,2,U,Y,UY  		! Displacement at the tip
NSOL,3,2,VOLT,,VOLT		! Voltage at the tip
/OUT,
PRVAR,VOLT,UY
*GET,UU(1),VARI,2,RTIME,1
*GET,UU(2),VARI,2,RTIME,2
ALLSEL,ALL
FINISH

!------------------- Pull-in  --------------------------------------
!--- 2-Step Solution: 	- moving beam to close-to-pull-in position
!---			- applying pull-in voltage and releasing BC
!-------------------------------------------------------------------

!-------- Step 1 (displacement) ------------

/SOLU
ANTYPE
ICDELE
IC,ALL,ALL,0.0			
CMSEL,S,BNODE			
DDELE,ALL,VOLT
ALLSEL,ALL


NSEL,S,LOC,X,BL			! DISPLACEMENT bc
NSEL,R,LOC,Y,GAP
D,ALL,UY,-0.65
ALLSEL,ALL

NSUBST,1
/OUT,SCRATCH
SOLVE
FINISH
SAVE

/POST1
SET,LIST
/OUT,
ALLSEL,ALL
*GET,NNODE,NODE,,NUM,MAX
*DIM,ICUX,,NNODE
*DIM,ICUY,,NNODE

SET,LAST
*DO,I,1,NNODE
ICUX(I)=UX(I)
ICUY(I)=UY(I)
*ENDDO
FINISH

!-------- Step 2 (voltage) ------------
/SOLU

ALLSEL,ALL
DDELE,ALL			! DELETE OLD bc

ESEL,S,TYPE,,2
NSLE,S
NSEL,R,LOC,Y,0
D,ALL,VOLT,0

ALLSEL,ALL
NSEL,S,LOC,X,0     		!  FIX ONE END
NSEL,A,LOC,Y,0     		!  FIX BOTTOM
D,ALL,UX,0
D,ALL,UY,0

ALLSEL,ALL
NSEL,S,LOC,X,BL    		!  SYMMETRY LINE
D,ALL,UX,0
NSEL,ALL

ESEL,S,TYPE,,3
NSLE,S
NSEL,R,LOC,Y,GAP-GAP0   	! FIX GAP ELEMENTS
D,ALL,ALL

ALLSEL,ALL

CMSEL,S,BNODE			! APPLY PULL-IN VOLTAGE
IC,ALL,VOLT,VLTG	
D,ALL,VOLT,VLTG
ALLSEL,ALL

*DO,I,1,NNODE			! NEW INITIAL CONDIITONS
 ICUQX=ICUX(I)
 ICUQY=ICUY(I)
 IC,I,UX,ICUQX
 IC,I,UY,ICUQY
*ENDDO

OUTRES,ALL,ALL
AUTOTS,ON
NSUBS,1
D,ALL,UZ
/OUT,SCRATCH
SOLVE

!------------------- UNLOADING (from 18V to 11V) -------------

CMSEL,S,BNODE			
D,ALL,VOLT,VLTG2		! APPLY 14.5v
ALLSEL,ALL
SOLVE 

CMSEL,S,BNODE			
D,ALL,VOLT,VLTG1		! APPLY 11.0v
ALLSEL,ALL
SOLVE 

FINI

!---------------------------- Postprocessing ------------------
/POST26
ALLS
NSEL,S,,,2 		
NSOL,2,2,U,Y,uy  		! DISPLACEMENT AT THE TIP
NSOL,3,2,VOLT,,volt		! VOLTAGE AT THE TIP
/OUT,
PRVAR,2,3
*GET,UU(3),VARI,2,RTIME,1
*GET,UU(4),VARI,2,RTIME,2
*GET,UU(5),VARI,2,RTIME,3

*DIM,LABEL,CHAR,5
*DIM,VALUE,,5,3
LABEL(1) = '@ 11V','@ 14.5V','@ 18V','@ 14.5V','@ 11V'
*VFILL,VALUE(1,1),DATA,-0.0722,-0.1451,-0.6004,-0.6002,-0.0723
*VFILL,VALUE(1,2),DATA,UU(1),UU(2),UU(3),UU(4),UU(5)
V1 = UU(1)/(-0.0722)
V2 = UU(2)/(-0.1451)
V3 = UU(3)/(-0.6004)
V4 = UU(4)/(-0.6002)
V5 = UU(5)/(-0.0723)
*VFILL,VALUE(1,3),DATA,ABS(V1),ABS(V2),ABS(V3),ABS(V4),ABS(V5)
/OUT,vm236,vrt
/COM
/COM,------------------- VM236 RESULTS COMPARISON ---------------------
/COM,
/COM,           |   TARGET   |    Mechanical APDL    |   RATIO
/COM,
/COM, UY ...
/COM
*VWRITE,LABEL(1),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,'   ',F12.4,'  ',F16.4,'   ',1F15.3)
/COM,----------------------------------------------------------------
/OUT
FINISH
*LIST,vm236,vrt

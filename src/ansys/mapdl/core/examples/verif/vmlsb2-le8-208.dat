! Copyright (C) 2024 ANSYS, Inc. and/or its affiliates.
! SPDX-License-Identifier: MIT
! 
!
! Permission is hereby granted, free of charge, to any person obtaining a copy
! of this software and associated documentation files (the "Software"), to deal
! in the Software without restriction, including without limitation the rights
! to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
! copies of the Software, and to permit persons to whom the Software is
! furnished to do so, subject to the following conditions:
!
! The above copyright notice and this permission notice shall be included in all
! copies or substantial portions of the Software.
!
! THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
! IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
! FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
! AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
! LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
! OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
! SOFTWARE.

/COM,ANSYS MEDIA REL. 2023R2 (05/12/2023) REF. VERIF. MANUAL: REL. 2023R2
/VERIFY,vmlsb2-le8-208
/TITLE,vmlsb2-le8-208,AXISYMMETRIC SHELL WITH PRESSURE LOADING
/COM, REFERENCE: NAFEMS BENCHMARKS,REPORT:LSB2,1990-06-15/2
/COM, ORIGINIAL TEST NUMBER: LE8
/PREP7
ET,1,SHELL208				! SHELL 208 ELEMENT
KEYOPT,1,3,2				! INCLUDE EXTRA INTERNAL NODES
SECTYPE,1,SHELL
SECDATA,0.01,1,0,3

MP,EX,1,210E9
MP,NUXY,1,0.3

K,1,0,0.5,0
K,2,0.25,0.5,0
K,3,0.25,0,0
K,4,0.1875,0.5,0
K,5,0.1875,0.5625,0
K,6,0.1508,0.5505,0
K,7,0,0.5+0.25,0

LARC,1,6,7,0.25
LARC,6,5,4,0.0625
LARC,5,2,4,0.0625
L,2,3

KDELE,7
KDELE,4

LESIZE,1,,,16
LESIZE,2,,,8
LESIZE,3,,,10
LESIZE,4,,,8

LMESH,ALL
ALLSEL,ALL

NSEL,S,LOC,X,0.25
NSEL,R,LOC,Y,0
D,ALL,UY,0
NSEL,ALL

NSEL,S,LOC,X,0
NSEL,R,LOC,Y,0.5
D,ALL,UX,0
D,ALL,ROTZ,0
ALLSEL,ALL

SFE,ALL,2,PRES,0,1E6			! UNIFORM INTERNAL PRESSURE
ALLSEL,ALL
FINI

/SOLU
ANTYPE,STATIC
NSUBS,10,10,10
OUTRES,ALL,ALL
TIME,1
SOLVE
FINI

/POST1
SET,LAST
*GET,SZ_HOOP,NODE,2,S,Z			! HOOP STRESS FROM Mechanical APDL IN PA
*DIM,LABEL,CHAR,1
*DIM,VALUE,,1,3
LABEL(1,1) = 'HOOP_STRESS'
*VFILL,VALUE(1,1),DATA,94.55
*VFILL,VALUE(1,2),DATA,(SZ_HOOP/1000000)
*VFILL,VALUE(1,3),DATA,((SZ_HOOP/1000000)/(94.55))
/OUT,vmlsb2-le8-208,vrt
/COM,------------------- vmlsb2-le8-208 RESULTS COMPARISON --------------
/COM,
/COM,           |  TARGET  |   Mechanical APDL  |  RATIO
/COM,
/COM, 
*VWRITE,LABEL(1,1),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,'   ',F10.4,'  ',F14.4,'   ',F15.3)
/COM,------------------------------------------------------------------------
/OUT,
FINISH
*LIST,vmlsb2-le8-208,vrt

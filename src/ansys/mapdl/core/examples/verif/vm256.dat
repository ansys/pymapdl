/COM,ANSYS MEDIA REL. 2023R1 (11/04/2022) REF. VERIF. MANUAL: REL. 2023R1
/VERIFY,VM256
/COM,   VERIFICATION MANUAL FOR FRACTURE MECHANICS, REL 11.0
/TITLE,VM256, FRACTURE MECHANICS PARAMETERS - CRACK IN A 2D PLATE
/COM    REFERENCE: BROWN AND SRAWLEY, ASTM SPECIAL TECHNICAL PUBLICATION NO. 410.
/COM    ****** CRACK IN 2-DIMENSIONS USING 2-D PLANE183 ELEMENT  ******

/COM, PARAMETER FOR CREATING THE CDB FILES TO STORE GEOMETRY AND MESH DATA
/COM,
_GEOMGEN = 0   ! SET TO 1 TO WRITE CDB FILES IF ANY INPUT FILE MODIFICATIONS
PARSAV
/OUT,vm256_scratch

/PREP7
*IF,_GEOMGEN,EQ,1,THEN
ET,1,PLANE183,,,2             ! PLANE183 (PLANE STRAIN)
MP,EX,1,30E6
MP,NUXY,1,0.3
K,1                           ! DEFINE KEYPOINTS AND LINE SEGMENTS
K,2,19
K,3,19,20
K,4,-1,20
K,5,-1
L,1,2
L,2,3
LESIZE,2,,,16
L,3,4
LESIZE,3,,,16
L,4,5,
LESIZE,4,,,24,.2
L,5,1
KSCON,1,.15,0,8               ! DEFINE CRACK TIP ELEMENT SIZE, NO SINGULAR ELEMENTS
AL,1,2,3,4,5
DL,1,1,SYMM                   ! APPLY SOLID MODEL BOUNDARY CONDITIONS
DL,4,1,SYMM
SFL,3,PRES,-.5641895
AMESH,1
OUTPR,ALL
ALLSEL,ALL
CDWRITE,DB,vm256-1,cdb
 *ELSE
  CDREAD,DB,vm256-1,cdb
*ENDIF
/OUT,
FINISH
/SOLU
ANTYPE,STATIC
OUTRES,ALL,ALL
NSEL,S,LOC,X,0,19
NSEL,R,LOC,Y,0
D,ALL,UY,0
ALLSEL,ALL
NSEL,S,LOC,X,-1
NSEL,R,LOC,Y,0
ALLSEL,ALL
NSEL,S,LOC,X,0
NSEL,R,LOC,Y,0
CM,CRACKTIP,NODE             ! DEFINE CRACK TIP NODE COMPONENT
ALLSEL,ALL
CINT,NEW,1                   ! DEFINE CRACK ID
CINT,TYPE,SIFS               ! DEFINE CRACK TYPE SIFS
CINT,CTNC,CRACKTIP           ! DEFINE CRACK TIP NODE COMPONENT
CINT,SYMM,ON                 ! SYMMETRY ON
CINT,NCON,6                  ! NUMBER OF CONTOURS
CINT,NORM,0,2                ! DEFINE CRACK PLANE NORMAL
CINT,NEW,2                   ! DEFINE CRACK ID
CINT,TYPE,JINT               ! DEFINE CRACK TYPE JINT
CINT,CTNC,CRACKTIP           ! DEFINE CRACK TIP NODE COMPONENT
CINT,SYMM,ON                 ! SYMMETRY ON
CINT,NCON,6                  ! NUMBER OF CONTOURS
CINT,NORM,0,2                ! DEFINE CRACK PLANE NORMAL
CINT,NEW,3                   ! DEFINE CRACK ID
CINT,TYPE,MFOR               ! DEFINE CRACK TYPE MFOR
CINT,CTNC,CRACKTIP           ! DEFINE CRACK TIP NODE COMPONENT
CINT,SYMM,ON                 ! SYMMETRY ON
CINT,NCON,6                  ! NUMBER OF CONTOURS
CINT,NORM,0,2                ! DEFINE CRACK PLANE NORMAL
CINT,NEW,4                   ! DEFINE CRACK ID
CINT,TYPE,TSTRESS            ! DEFINE CRACK TYPE - TSTRESS
CINT,CTNC,CRACKTIP           ! DEFINE CRACK TIP NODE COMPONENT
CINT,SYMM,ON                 ! SYMMETRY ON
CINT,NCON,6                  ! NUMBER OF CONTOURS
CINT,NORM,0,2                ! DEFINE CRACK PLANE NORMAL
CINT,LIST
ALLSEL,ALL
/OUT,vm256_scratch,,,append
SOLVE
FINI
/POST1
/OUT,
PRCINT,1,1,K1                  ! PRINT K VALUES 
PRCINT,2,1,JINT                ! PRINT J VALUES
PRCINT,3,1,MFTX                ! PRINT MATERIAL FORCE
PRCINT,4,1,TSTRESS             ! PRINT TSTRESS
*GET,K,CINT,1,CTIP,1,,5,,K1    ! GET K VALUE FOR CRACK TIP NODE 
*STATUS,K
*GET,J,CINT,2,CTIP,1,,5,,JINT        ! GET JINT VALUE FOR CRACK TIP NODE 
*STATUS,J
*GET,MF,CINT,3,CTIP,1,,5,,MFTX       ! GET MFOR VALUE FOR CRACK TIP NODE IN X DIRECTION
*STATUS,MF
*GET,TS,CINT,4,CTIP,1,,5,,TSTRESS    ! GET TSTRESS VALUE FOR CRACK TIP NODE 
*STATUS,TS
/OUT,vm256_scratch,,,append
*DIM,LABEL,CHAR,4,
*DIM,VALUE,,4,3
LABEL(1,1) = 'KI ',
*VFILL,VALUE(1,1),DATA,1.0          ! STRESS INTENSITY VALUE OBTAINED FROM REFERENCE
*VFILL,VALUE(1,2),DATA,K
*VFILL,VALUE(1,3),DATA,ABS(K/1.0)
LABEL(2,1) = 'JINT ',
*VFILL,VALUE(2,1),DATA,0.303e-7     ! J INTEGRAL VALUE OBTAINED FROM REFERENCE
*VFILL,VALUE(2,2),DATA,J
*VFILL,VALUE(2,3),DATA,ABS(J/0.303e-7)
LABEL(3,1) = 'MFOR ',
*VFILL,VALUE(3,1),DATA,0.303e-7     ! MATERIAL FORCE VALUE OBTAINED FROM REFERENCE
*VFILL,VALUE(3,2),DATA,MF
*VFILL,VALUE(3,3),DATA,ABS(MF/0.303e-7)
LABEL(4,1) = 'Tstress ',
*VFILL,VALUE(4,1),DATA,-0.564       ! TSTRESS VALUE OBTAINED FROM REFERENCE
*VFILL,VALUE(4,2),DATA,TS
*VFILL,VALUE(4,3),DATA,ABS(TS/0.564)
SAVE,TABLE_1
FINISH
/CLEAR, NOSTART                     ! CLEAR DATABASE FOR 2ND SOLUTION
PARRES,CHANGE

/OUT,
/COM ************* CRACK IN 3D PLATE USING SOLID 185 ELEMENT ******************* C
/OUT,vm256_scratch,,,append

/PREP7
*IF,_GEOMGEN,EQ,1,THEN
SMRT,OFF
SHPP,OFF
/TITLE, VM256, FRACTURE MECHANICS STRESS INTENSITY  - CRACK IN A FINITE WIDTH PLATE
/COM,     ****** CRACK IN 3-DIMENSIONS USING SOLID185
/COM,  
MP,EX,1,30E6
MP,NUXY,1,0.3
THICKNESS=2     
K,1                           ! DEFINE KEYPOINTS AND LINE SEGMENTS
K,2,19
K,3,19,20
K,4,-1,20
K,5,-1
L,1,2
L,2,3
LESIZE,2,,,16
L,3,4
LESIZE,3,,,16
L,4,5,
LESIZE,4,,,24,.2
L,5,1
AL,1,2,3,4,5
ET,1,182
ET,2,185
ESIZE,,20 
KSCON,1,1/20,0,8                    
AMESH,ALL                      ! CREATE 2D BASE MODEL

TYPE,2
ESIZE,,2
VEXT,ALL,,,0,0,THICKNESS       ! CREATE 3D MODEL
ALLSEL

ESEL,S,ENAME,,182
ACLEAR,1
ALLSEL

CDWRITE,DB,vm256-2,cdb
 *ELSE
  CDREAD,DB,vm256-2,cdb
*ENDIF

/OUT,
NSEL,S,LOC,X,0
NSEL,R,LOC,Y,0
CM,CRACKTIP,NODE             ! DEFINE CRACK TIP NODE COMPONENT
ALLSEL,ALL

NSEL,S,LOC,Y,20.0
SF,ALL,PRES,-.5641895        ! SURFACE PRESSURE
ALLSEL

NSEL,S,LOC,Z,0.0
D,ALL,UZ,0.0
ALLSEL
      
NSEL,S,LOC,Z,2
D,ALL,UZ,0.0
ALLSEL

NSEL,S,LOC,X,-1
D,ALL,UX,0
ALLSEL

NSEL,S,LOC,X,0,19
NSEL,R,LOC,Y,0
D,ALL,UY,0
ALLSEL

FINI


/SOLU

ANTYPE,STATIC
OUTRES,ALL,ALL
CINT,NEW,1                      ! DEFINE CRACK ID
CINT,TYPE,SIFS                  ! DEFINE CRACK TYPE SIFS
CINT,CTNC,CRACKTIP,NODE(-1,0,0) ! DEFINE CRACK TIP NODE COMPONENT
CINT,SYMM,ON                    ! SYMMETRY ON
CINT,NCON,6                     ! NUMBER OF CONTOURS
CINT,NORM,0,2                   ! DEFINE CRACK PLANE NORMAL
CINT,NEW,2                      ! DEFINE CRACK ID
CINT,TYPE,JINT                  ! DEFINE CRACK TYPE JINT
CINT,CTNC,CRACKTIP,NODE(-1,0,0) ! DEFINE CRACK TIP NODE COMPONENT
CINT,SYMM,ON                    ! SYMMETRY ON
CINT,NCON,6                     ! NUMBER OF CONTOURS
CINT,NORM,0,2                   ! DEFINE CRACK PLANE NORMAL
CINT,NEW,3                      ! DEFINE CRACK ID
CINT,TYPE,MFOR                  ! DEFINE CRACK TYPE MFOR
CINT,CTNC,CRACKTIP,NODE(-1,0,0) ! DEFINE CRACK TIP NODE COMPONENT
CINT,SYMM,ON                    ! SYMMETRY ON
CINT,NCON,6                     ! NUMBER OF CONTOURS
CINT,NORM,0,2                   ! DEFINE CRACK PLANE NORMAL
CINT,NEW,4                      ! DEFINE CRACK ID
CINT,TYPE,TSTRESS               ! DEFINE CRACK TYPE TSTRESS
CINT,CTNC,CRACKTIP,NODE(-1,0,0) ! DEFINE CRACK TIP NODE COMPONENT
CINT,SYMM,ON                    ! SYMMETRY ON
CINT,NCON,6                     ! NUMBER OF CONTOURS
CINT,NORM,0,2                   ! DEFINE CRACK PLANE NORMAL
CINT,LIST

ALLSEL,ALL

/OUT,vm256_scratch,,,append
SOLVE
FINI
/POST1
/OUT,
PRCINT,1,NODE(0,0,0),K1                  ! PRINT K VALUES 
PRCINT,2,NODE(0,0,0),JINT                ! PRINT J VALUES
PRCINT,3,NODE(0,0,0),MFTX                ! PRINT MATERIAL FORCE
PRCINT,4,NODE(0,0,0),TSTRESS             ! PRINT TSTRESS
*GET,K,CINT,1,CTIP,NODE(0,0,0),,4,,K1    ! GET K VALUE FOR CRACK TIP NODE 
*STATUS,K
*GET,J,CINT,2,CTIP,NODE(0,0,0),,4,,JINT     ! GET JINT VALUE FOR CRACK TIP NODE 
*STATUS,J
*GET,MF,CINT,3,CTIP,NODE(0,0,0),,4,,MFTX    ! GET MFOR VALUE FOR CRACK TIP NODE IN X DIRECTION
*STATUS,MF
*GET,TS,CINT,4,CTIP,NODE(0,0,0),,4,,TSTRESS ! GET TSTRESS VALUE FOR CRACK TIP NODE 
*STATUS,TS
/OUT,vm256_scratch,,,append
*DIM,LABEL,CHAR,4,
*DIM,VALUE,,4,3
LABEL(1,1) = 'KI ',
*VFILL,VALUE(1,1),DATA,1.0        ! STRESS INTENSITY VALUE OBTAINED FROM REFERENCE
*VFILL,VALUE(1,2),DATA,K
*VFILL,VALUE(1,3),DATA,ABS(K/1.0)
LABEL(2,1) = 'JINT ',
*VFILL,VALUE(2,1),DATA,0.303e-7   ! J INTEGRAL VALUE OBTAINED FROM REFERENCE
*VFILL,VALUE(2,2),DATA,J
*VFILL,VALUE(2,3),DATA,ABS(J/0.303e-7)
LABEL(3,1) = 'MFOR ',
*VFILL,VALUE(3,1),DATA,0.303e-7   ! MATERIAL FORCE VALUE OBTAINED FROM REFERENCE
*VFILL,VALUE(3,2),DATA,MF
*VFILL,VALUE(3,3),DATA,ABS(MF/0.303e-7)
LABEL(4,1) = 'Tstress ',
*VFILL,VALUE(4,1),DATA,-0.564     ! TSTRESS VALUE OBTAINED FROM REFERENCE
*VFILL,VALUE(4,2),DATA,TS
*VFILL,VALUE(4,3),DATA,ABS(TS/0.564)
SAVE,TABLE_2
FINISH
/CLEAR, NOSTART                   ! CLEAR DATABASE FOR 3RD SOLUTION
PARRES,CHANGE

/OUT,
/COM ************* CRACK IN 3D PLATE USING SOLID 186 ELEMENT ******************* C
/OUT,vm256_scratch,,,append

/PREP7
*IF,_GEOMGEN,EQ,1,THEN
SMRT,OFF
SHPP,OFF
/TITLE,VM256, FRACTURE MECHANICS STRESS INTENSITY  - CRACK IN A FINITE WIDTH PLATE
/COM,     ****** CRACK IN 3-DIMENSIONS USING SOLID186
/COM,
MP,EX,1,30E6
MP,NUXY,1,0.3
THICKNESS=2     
K,1                           ! DEFINE KEYPOINTS AND LINE SEGMENTS
K,2,19
K,3,19,20
K,4,-1,20
K,5,-1
L,1,2
L,2,3
LESIZE,2,,,16
L,3,4
LESIZE,3,,,16
L,4,5,
LESIZE,4,,,24,.2
L,5,1
AL,1,2,3,4,5

ET,1,183
ET,2,186
ESIZE,,20 
KSCON,1,1/20,0,8                    
AMESH,ALL                      ! CREATE 2D BASE MODEL

TYPE,2
ESIZE,,2
VEXT,ALL,,,0,0,THICKNESS       ! CREATE 3D MODEL
ALLSEL

ESEL,S,ENAME,,183
ACLEAR,1
ALLSEL
 
CDWRITE,DB,vm256-3,cdb
 *ELSE
  CDREAD,DB,vm256-3,cdb
*ENDIF

/OUT,
NSEL,S,LOC,X,0
NSEL,R,LOC,Y,0
CM,CRACKTIP,NODE               ! DEFINE CRACK TIP NODE COMPONENT
ALLSEL,ALL

NSEL,S,LOC,Y,20.0
SF,ALL,PRES,-.5641895          ! SURFACE PRESSURE
ALLSEL

NSEL,S,LOC,Z,0.0
D,ALL,UZ,0.0
ALLSEL
      
NSEL,S,LOC,Z,2
D,ALL,UZ,0.0
ALLSEL

NSEL,S,LOC,X,-1
D,ALL,UX,0
ALLSEL

NSEL,S,LOC,X,0,19
NSEL,R,LOC,Y,0
D,ALL,UY,0
ALLSEL

FINI


/SOLU

ANTYPE,STATIC
OUTRES,ALL,ALL
CINT,NEW,1                      ! DEFINE CRACK ID
CINT,TYPE,SIFS                  ! DEFINE CRACK TYPE SIFS
CINT,CTNC,CRACKTIP,NODE(-1,0,0) ! DEFINE CRACK TIP NODE COMPONENT
CINT,SYMM,ON                    ! SYMMETRY ON
CINT,NCON,6                     ! NUMBER OF CONTOURS
CINT,NORM,0,2                   ! DEFINE CRACK PLANE NORMAL
CINT,NEW,2                      ! DEFINE CRACK ID
CINT,TYPE,JINT                  ! DEFINE CRACK TYPE JINT
CINT,CTNC,CRACKTIP,NODE(-1,0,0) ! DEFINE CRACK TIP NODE COMPONENT
CINT,SYMM,ON                    ! SYMMETRY ON
CINT,NCON,6                     ! NUMBER OF CONTOURS
CINT,NORM,0,2                   ! DEFINE CRACK PLANE NORMAL
CINT,NEW,3                      ! DEFINE CRACK ID
CINT,TYPE,MFOR                  ! DEFINE CRACK TYPE MFOR
CINT,CTNC,CRACKTIP,NODE(-1,0,0) ! DEFINE CRACK TIP NODE COMPONENT
CINT,SYMM,ON                    ! SYMMETRY ON
CINT,NCON,6                     ! NUMBER OF CONTOURS
CINT,NORM,0,2                   ! DEFINE CRACK PLANE NORMAL
CINT,NEW,4                      ! DEFINE CRACK ID
CINT,TYPE,TSTRESS               ! DEFINE CRACK TYPE TSTRESS
CINT,CTNC,CRACKTIP,NODE(-1,0,0) ! DEFINE CRACK TIP NODE COMPONENT
CINT,SYMM,ON                    ! SYMMETRY ON
CINT,NCON,6                     ! NUMBER OF CONTOURS
CINT,NORM,0,2                   ! DEFINE CRACK PLANE NORMAL
CINT,LIST

ALLSEL,ALL

/OUT,vm256_scratch,,,append
SOLVE
FINI
/POST1
/OUT,
PRCINT,1,NODE(0,0,0),K1                  ! PRINT K VALUES 
PRCINT,2,NODE(0,0,0),JINT                ! PRINT J VALUES
PRCINT,3,NODE(0,0,0),MFTX                ! PRINT MATERIAL FORCE
PRCINT,4,NODE(0,0,0),TSTRESS             ! PRINT TSTRESS
*GET,K,CINT,1,CTIP,NODE(0,0,0),,5,,K1    ! GET K VALUE FOR CRACK TIP NODE 
*STATUS,K
*GET,J,CINT,2,CTIP,NODE(0,0,0),,5,,JINT     ! GET JINT VALUE FOR CRACK TIP NODE 
*STATUS,J
*GET,MF,CINT,3,CTIP,NODE(0,0,0),,5,,MFTX    ! GET MFOR VALUE FOR CRACK TIP NODE IN X DIRECTION
*STATUS,MF
*GET,TS,CINT,4,CTIP,NODE(0,0,0),,5,,TSTRESS ! GET TSTRESS VALUE FOR CRACK TIP NODE 
*STATUS,TS
/OUT,vm256_scratch,,,append
*DIM,LABEL,CHAR,4,
*DIM,VALUE,,4,3
LABEL(1,1) = 'KI ',
*VFILL,VALUE(1,1),DATA,1.0          ! STRESS INTENSITY VALUE OBTAINED FROM REFERENCE
*VFILL,VALUE(1,2),DATA,K
*VFILL,VALUE(1,3),DATA,ABS(K/1.0)
LABEL(2,1) = 'JINT ',
*VFILL,VALUE(2,1),DATA,0.303e-7     ! J INTEGRAL VALUE OBTAINED FROM REFERENCE
*VFILL,VALUE(2,2),DATA,J
*VFILL,VALUE(2,3),DATA,ABS(J/0.303e-7)
LABEL(3,1) = 'MFOR ',
*VFILL,VALUE(3,1),DATA,0.303e-7     ! MATERIAL FORCE VALUE OBTAINED FROM REFERENCE
*VFILL,VALUE(3,2),DATA,MF
*VFILL,VALUE(3,3),DATA,ABS(MF/0.303e-7)
LABEL(4,1) = 'Tstress ',
*VFILL,VALUE(4,1),DATA,-0.564       ! TSTRESS VALUE OBTAINED FROM REFERENCE
*VFILL,VALUE(4,2),DATA,TS
*VFILL,VALUE(4,3),DATA,ABS(TS/0.564)
SAVE,TABLE_3
FINISH
RESUME,TABLE_1

/COM
/OUT,vm256,vrt
/COM,------------------- VM256 RESULTS COMPARISON --------------
/COM,
/COM,           |  TARGET  |   Mechanical APDL  |  RATIO
/COM,
/COM, ****************************************
/COM,  USING PLANE 183 ELEMENT (2-D ANALYSIS)
/COM, *****************************************
/COM,
*VWRITE,LABEL(1,1),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,'   ',E12.5,'  ',E12.5,'   ',F15.3)
/NOPR
RESUME,TABLE_2
/GOPR
/COM,
/COM, *****************************************
/COM,  USING SOLID 185 ELEMENT (3-D ANALYSIS)
/COM, *****************************************
/COM,
*VWRITE,LABEL(1,1),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,'   ',E12.5,'  ',E12.5,'   ',F15.3)
/NOPR
RESUME,TABLE_3
/GOPR
/COM,
/COM, ********************************************************
/COM,  USING SOLID 186 ELEMENT - SURFACE CRACK (3-D ANALYSIS)
/COM, ********************************************************
/COM,
*VWRITE,LABEL(1,1),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,'   ',E12.5,'  ',E12.5,'   ',F15.3)
/NOPR
/COM,
/COM,-----------------------------------------------------------
/OUT,
FINISH
*LIST,vm256,vrt
/DELETE,TABLE_1
/DELETE,TABLE_2
/DELETE,TABLE_3

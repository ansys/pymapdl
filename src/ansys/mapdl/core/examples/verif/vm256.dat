/COM,ANSYS MEDIA REL. 150 (11/8/2013) REF. VERIF. MANUAL: REL. 150
/VERIFY,VM256
/COM,   VERIFICATION MANUAL FOR FRACTURE MECHANCIS, REL 11.0
/TITLE,VM256 FRACTURE MECHANICS STRESS INTENSITY  - CRACK IN A 2D PLATE
/COM    REFERENCE: BROWN AND SRAWLEY, ASTM SPECIAL TECHNICAL PUBLICATION NO. 410.
/COM    ****** CRACK IN 2-DIMENSIONS USING 2-D PLANE183 ELEMENT  ******
/PREP7
ET,1,PLANE183,,,2              ! PLANE183 (PLANE STRAIN)
MP,EX,1,30E6
MP,NUXY,1,0.3
K,1                           ! DEFINE KEYPOINTS AND LINE SEGMENTS
K,2,4
K,3,4,5
K,4,-1,5
K,5,-1
L,1,2
L,2,3
LESIZE,2,,,4
L,3,4
LESIZE,3,,,4
L,4,5,
LESIZE,4,,,6,.2
L,5,1
ESIZE,,5
KSCON,1,.15,0,8               ! DEFINE CRACK TIP ELEMENT SIZE, NO SINGULAR ELEMENTS
AL,1,2,3,4,5
DL,1,1,SYMM                   ! APPLY SOLID MODEL BOUNDARY CONDITIONS
DL,4,1,SYMM
SFL,3,PRES,-.5641895
AMESH,1
OUTPR,ALL
FINISH
/SOLU
ANTYPE,STATIC
OUTRES,ALL,ALL
NSEL,S,LOC,X,0,10
NSEL,R,LOC,Y,0
D,ALL,UY,0
ALLSEL,ALL
NSEL,S,LOC,X,0
NSEL,R,LOC,Y,0
D,ALL,UX,0
ALLSEL,ALL
NSEL,S,LOC,X,0
NSEL,R,LOC,Y,0
CM,CRACKTIP,NODE             ! DEFINE CRACK TIP NODE COMPONENT
ALLSEL,ALL
CINT,NEW,1                   ! DEFINE CRACK ID
CINT,TYPE,SIFS               ! DEFINE CRACK TYPE
CINT,CTNC,CRACKTIP           ! DEFINE CRACK TIP NODE COMPONENT
CINT,SYMM,ON                 ! SYMMETRY ON
CINT,NCON,6                  ! NUMBER OF COUNTOURS
CINT,NORM,0,2                ! DEFINE CRACK PLANE NORMAL
CINT,LIST
ALLSEL,ALL
/OUT,SCRATCH
SOLVE
FINI
/POST1
/OUT,
PRCINT,1,1,K1                  ! PRINT K VALUES 
*GET,K,CINT,1,CTIP,1,,5,,K1    ! GET K VALUE FOR CRACK TIP NODE 
*STATUS,K
/OUT,SCRATCH
*DIM,LABEL,CHAR,1,
*DIM,VALUE,,1,3
LABEL(1,1) = 'KI ',
*VFILL,VALUE(1,1),DATA,1.0249     ! STRESS INTENSITY VALUE OBTAINED FROM REFERENCE
*VFILL,VALUE(1,2),DATA,K
*VFILL,VALUE(1,3),DATA,ABS(K/1.0249)
SAVE,TABLE_1
FINISH
/CLEAR, NOSTART                ! CLEAR DATABASE FOR 2ND SOLUTION
/OUT,


/COM ************* CRACK IN 3D PLATE USING SOLID 185 ELEMENT ******************* C

/PREP7
SMRT,OFF
/TITLE, VM256, FRACTURE MECHANICS STRESS INTENSITY  - CRACK IN A FINITE WIDTH PLATE
/COM,     ****** CRACK IN 3-DIMENSIONS USING SOLID185
/COM,
ET,1,SOLID185          ! SOLID 185 ELEMENT
ET,2,SOLID185          ! ELEMENTS AROUND THE CRACK TIP
MP,EX,1,3E7
MP,NUXY,1,0.3
CSYS,1                 ! CYLINDRICAL COORDINATE SYSTEM
N,1
NGEN,9,20,1
N,11,0.8
N,171,0.8,180
FILL,11,171,7,31,20
CSYS,0                ! CARTESIAN COORDINATE SYSTEM
FILL,1,11,9,2,1,9,20,3
N,15,4
N,75,4,5
FILL,15,75,2,35,20
N,155,-1,5
FILL,75,155,3,95,20
N,172,-1
FILL,155,172,5,177,-1,,,.15
FILL,11,15,3,,,7,20,3
NGEN,2,200,1,177,,,,.25
/OUT,SCRATCH
E,2,22,1,1,202,222,201,201
EGEN,8,20,-1
E,2,3,23,22,202,203,223,222
EGEN,8,20,-1
EGEN,9,1,-8
EGEN,5,1,73,78
E,171,151,173,172,371,351,373,372
E,151,131,174,173,351,331,374,373
E,131,132,175,174,331,332,375,374
EGEN,3,1,-1
E,134,135,155,177,334,335,355,377
TYPE,2
EMODIF,1                    ! MODIFY ELEMENTS 1 TO 8 FROM TYPE,1 TO TYPE,2
*REPEAT,8,1
NUMMRG,NODE                 ! MERGE COINCIDENT NODES
ALLSEL,ALL
/OUT,
NSEL,S,LOC,X,0
NSEL,R,LOC,Y,0
NLIST
CM,CRACKTIP,NODE            ! CRACK TIP NODE COMPONENT
ALLSEL,ALL
NSEL,S,LOC,X,-1
DSYM,SYMM,X                 ! SYMMETRY BOUNDARY CONDITIONS
NSEL,S,LOC,X,0,4
NSEL,R,LOC,Y,0
DSYM,SYMM,Y                 ! SYMMETRY BOUNDARY CONDITIONS
ALLSEL,ALL
D,ALL,UZ,0
ALLSEL,ALL
NSEL,S,LOC,Y,5
SF,ALL,PRES,-0.5641895      ! SURFACE PRESSURE
ALLSEL,ALL
FINI
/SOLU
AUTOTS,ON
NSUBST,10
OUTRES,ALL,ALL
CINT,NEW,1                    ! CRACK ID
CINT,TYPE,SIFS               ! DEFINE CRACK TYPE
CINT,CTNC,CRACKTIP,NODE(-1,0,0)    !DEFINE CRACK TIP COMPONENT
CINT,NCON,6                   ! NO OF CONTOURS
CINT,SYMM,ON                  ! SYMMETRY ON
CINT,NORM,0,2                 ! CRACK PLANE NORMAL
CINT,LIST
ALLSEL,ALL
SAVE
/NERR,0,,,,
/OUT,SCRATCH
SOLVE
FINI
/POST1
/OUT,
PRCINT,1,1,K1                  ! PRINT K VALUES 
*GET,K,CINT,1,CTIP,NODE(0,0,0),,5,,K1    ! GET K VALUE FOR CRACK TIP NODE 
*STATUS,K
*DIM,LABEL,CHAR,1,
*DIM,VALUE,,1,3
LABEL(1,1) = 'KI ',
*VFILL,VALUE(1,1),DATA,1.0249     ! STRESS INTENSITY VALUE OBTAINED FROM REFERENCE
*VFILL,VALUE(1,2),DATA,K
*VFILL,VALUE(1,3),DATA,ABS(K/1.0249)
SAVE,TABLE_2
FINISH
/CLEAR, NOSTART                ! CLEAR DATABASE FOR 3RD SOLUTION
/OUT,


/COM ************* CRACK IN 3D PLATE USING SOLID 186 ELEMENT ******************* C


/PREP7
SMRT,OFF
/TITLE,VM256 FRACTURE MECHANICS STRESS INTENSITY  - CRACK IN A FINITE WIDTH PLATE
/COM,     ****** CRACK IN 3-DIMENSIONS USING SOLID186
/COM,
ET,1,PLANE183
ET,2,186                      ! SOLID ELEMENT 186
MP,EX,1,30E6
MP,NUXY,1,0.3
K,1                           ! DEFINE KEYPOINTS AND LINE SEGMENTS
K,2,4
K,3,4,5
K,4,-1,5
K,5,-1
L,1,2
L,2,3
LESIZE,2,,,4
L,3,4
LESIZE,3,,,4
L,4,5,
LESIZE,4,,,6,.2
L,5,1
ESIZE,,5
KSCON,1,.15,0,8               ! DEFINE CRACK TIP ELEMENT SIZE, NO SINGULAR ELEMENTS
AL,1,2,3,4,5
AMESH,1

TYPE,2
ESIZE,,2
VEXT,1,,,0,0,0.25
ALLSEL
ESEL,S,ENAME,,183
ACLEAR,1
ALLSEL

/OUT,
NSEL,S,LOC,X,0
NSEL,R,LOC,Y,0
NLIST
CM,CRACKTIP,NODE            ! CRACK TIP NODE COMPONENT
ALLSEL,ALL
NSEL,S,LOC,X,-1
DSYM,SYMM,X                 ! SYMMETRY BOUNDARY CONDITIONS
NSEL,S,LOC,X,0,4
NSEL,R,LOC,Y,0
DSYM,SYMM,Y                 ! SYMMETRY BOUNDARY CONDITIONS
ALLSEL,ALL
D,ALL,UZ,0
ALLSEL,ALL
NSEL,S,LOC,Y,5
SF,ALL,PRES,-0.5641895      ! SURFACE PRESSURE
ALLSEL,ALL
FINI
/SOLU
AUTOTS,ON
NSUBST,10
OUTRES,ALL,ALL
CINT,NEW,1                    ! CRACK ID
CINT,TYPE,SIFS               ! DEFINE CRACK TYPE
CINT,CTNC,CRACKTIP,NODE(-1,0,0)     !DEFINE CRACK TIP COMPONENT
CINT,NCON,6                   ! NO OF CONTOURS
CINT,SYMM,ON                  ! SYMMETRY ON
CINT,NORM,0,2                 ! CRACK PLANE NORMAL
CINT,LIST
ALLSEL,ALL
SAVE
/NERR,0,,,,
/OUT,SCRATCH
SOLVE
FINI
/POST1
/OUT,
PRCINT,1,1,K1                  ! PRINT K VALUES 
*GET,K,CINT,1,CTIP,NODE(0,0,0),,5,,K1    ! GET K VALUE FOR CRACK TIP NODE 
*STATUS,K
*DIM,LABEL,CHAR,1,
*DIM,VALUE,,1,3
LABEL(1,1) = 'KI ',
*VFILL,VALUE(1,1),DATA,1.0249     ! STRESS INTENSITY VALUE OBTAINED FROM REFERENCE
*VFILL,VALUE(1,2),DATA,K
*VFILL,VALUE(1,3),DATA,ABS(K/1.0249)
SAVE,TABLE_3
FINISH
RESUME,TABLE_1

/COM
/OUT,vm256,vrt
/COM,------------------- VM256 RESULTS COMPARISON --------------
/COM,
/COM,           |  TARGET  |   Mechanical APDL  |  RATIO
/COM,
/COM, ****************************************
/COM,  USING PLANE 183 ELEMENT (2-D ANALYSIS)
/COM, *****************************************
/COM,
*VWRITE,LABEL(1,1),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,'   ',F10.4,'  ',F14.4,'   ',F15.3)
/NOPR
RESUME,TABLE_2
/GOPR
/COM,
/COM, *****************************************
/COM,  USING SOLID 185 ELEMENT (3-D ANALYSIS)
/COM, *****************************************
/COM,
*VWRITE,LABEL(1,1),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,'   ',F10.4,'  ',F14.4,'   ',F15.3)
/NOPR
RESUME,TABLE_3
/GOPR
/COM,
/COM, ********************************************************
/COM,  USING SOLID 186 ELEMENT - SURFACE CRACK (3-D ANALYSIS)
/COM, ********************************************************
/COM,
*VWRITE,LABEL(1,1),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,'   ',F10.4,'  ',F14.4,'   ',F15.3)
/NOPR
/COM,
/COM,-----------------------------------------------------------
/OUT
FINISH
*LIST,vm256,vrt


































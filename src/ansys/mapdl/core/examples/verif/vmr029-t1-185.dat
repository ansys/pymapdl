! Copyright (C) 2024 ANSYS, Inc. and/or its affiliates.
! SPDX-License-Identifier: MIT
! 
!
! Permission is hereby granted, free of charge, to any person obtaining a copy
! of this software and associated documentation files (the "Software"), to deal
! in the Software without restriction, including without limitation the rights
! to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
! copies of the Software, and to permit persons to whom the Software is
! furnished to do so, subject to the following conditions:
!
! The above copyright notice and this permission notice shall be included in all
! copies or substantial portions of the Software.
!
! THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
! IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
! FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
! AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
! LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
! OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
! SOFTWARE.

/COM,ANSYS MEDIA REL. 2023R2 (05/12/2023) REF. VERIF. MANUAL: REL. 2023R2
/VERIFY,VMR029-T1-185
/TITLE,VMR029-T1-185,ELASTIC LARGE DEFLECTION RESPONSE OF A Z-SHAPED CANTILEVER UNDER END LOAD
/COM,REFERENCE NAFEMS 3D BEAMS AND SHELLS MANUAL TEST 3DNLG-1
/SHOW,JPEG
JPGPRF,500,100,1

/PREP7
MP,EX,1,2.0E5
MP,NUXY,1,0.3
ET,1,185
KEYOPT,1,2,2
K,1,
K,2,60.401315561,
K,3,120,30,
K,4,180,30,
K,5,180,31.7,
K,6,119.5986844,31.7,
K,7,60,1.7,
K,8,,1.7
L,1,2
L,2,3
L,3,4
L,4,5
L,5,6
L,6,7
L,7,8
L,8,1
L,2,7
L,3,6
AL,1,9,7,8
AL,2,10,6,9
AL,3,4,5,10
VOFFST,1,-20,
VOFFST,2,-20,
VOFFST,3,-20,
NUMMRG,ALL
LESIZE,9,,,1
LESIZE,1,,,9
LESIZE,17,,,3
LESIZE,2,,,9
LESIZE,3,,,9
LESIZE,10,,,1
LESIZE,25,,,3
MSHKEY,1
TYPE,1 $ MAT,1
VMESH,1,3
NUMMRG,NODE
NUMMRG,ELEM
NUMMRG,KP
ALLSEL,ALL
NSEL,S,LOC,X,0.0
D,ALL,ALL
ALLSEL,ALL
NSEL,S,LOC,X,180.0
NSEL,R,LOC,Y,31.7
F,ALL,FY,1000
ALLS,
FINISH

/SOLU
NLGEOM,ON
TIME,1
NSUBST,20,10000,10
OUTRES,ALL,ALL
/OUT,SCRATCH
SOLVE
FINISH

/POST26
/SHOW,,jpeg
/OUT
*DIM,XA,TABLE,11,1
*DIM,YA,TABLE,11,1
XA(  1,1)= 0
YA(  1,1)= 0
XA(  2,1)= 15
YA(  2,1)= 16
XA(  3,1)= 30
YA(  3,1)= 33
XA(  4,1)= 45
YA(  4,1)= 47.25
XA(  5,1)= 60
YA(  5,1)= 63
XA(  6,1)= 80.43
YA(  6,1)= 104.5
XA(  7,1)= 90
YA(  7,1)= 150
XA(  8,1)= 105
YA(  8,1)= 233.3
XA(  9,1)= 120
YA(  9,1)= 450
XA( 10,1)= 133.1
YA( 10,1)= 1263
XA( 11,1)= 143.4
YA( 11,1)= 4000
/XRANGE,0,150
/YRANGE,0,4000
/AXLAB,X,TIP DISPLACEMENT
/AXLAB,Y,LOAD
/COLOR,CURVE,YGRE
*VPLOT,XA(1,1),YA(1,1)
/NOERASE
NSOL,2,189,U,Y
ADD,3,1,,,APLOAD,,,4000
/COLOR,CURVE,MRED
XVAR,2
PLVAR,3
PRVAR,2,3
FINISH

/POST1
NSEL,S,NODE,,62,80,2
NSEL,A,NODE,,136,152,2
NSEL,A,NODE,,208,224,2
PRNSOL,U,COMP
PRNSOL,S,COMP
NSEL,ALL
SET,LIST
SET,LAST
*GET,UY3,NODE,189,U,Y
HF3=UY3/143.4
*DIM,VALUE,,1,3
*DIM,LABEL,CHAR,3
LABEL(1) = 'vmr029-','t1-185'
*VFILL,VALUE(1,1),DATA,4000.0
*VFILL,VALUE(1,2),DATA,UY3
*VFILL,VALUE(1,3),DATA,HF3
/OUT,vmr029-t1-185,vrt
/COM
/COM,-------------- VMR029-T1 RESULTS COMPARISON--------------
/COM,
/COM,|  LOAD  |      Mechanical APDL      |   RATIO   |      TEST      |
/COM,
/COM,SOLID185
*VWRITE,VALUE(1,1), VALUE(1,2), VALUE(1,3), LABEL(1),LABEL(2)
(1X,F8.2,'     ',F12.2,'      ',F12.2,'      ',A7,A8)
/COM,
/COM,----------------------------------------------------------
/OUT
FINISH
*LIST,vmr029-t1-185,vrt

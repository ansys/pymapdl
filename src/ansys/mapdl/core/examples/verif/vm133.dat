/COM,ANSYS MEDIA REL. 2023R1 (11/04/2022) REF. VERIF. MANUAL: REL. 2023R1
/VERIFY,VM133
/PREP7
MP,PRXY,,0.3
/TITLE, VM133, MOTION OF A ROD DUE TO IRRADIATION INDUCED CREEP
C***    REFERENCE - ANY BASIC CALCULUS BOOK
ANTYPE,STATIC
ET,1,BEAM188
SECT,1,BEAM,RECT
SECD,0.5,0.5
MP,EX,1,300
TB,CREEP,1,,,3
TBDATA,1,0.5e-2,1,0,0,1
N,1
N,2,1
E,1,2
D,1,ALL                ! FIX ONE END
F,2,FX,0.25            ! FORCE INDUCING CONSTANT STRESS
FINISH
/SOLU
RATE,OFF    
BFE,1,TEMP,1,1000,1000,1000,1000 ! APPLY CONSTANT TEMPERATURE
TIME,1E-8              ! NEAR ZERO TIME FOR FIRST LOAD STEP
OUTPR,BASIC,1          ! PRINT BASIC ELEMENT SOLUTION 
OUTRES,EPCR,1          ! STORE CREEP STRAIN RESULTS FOR EVERY SUBSTEP
CNVTOL,F,,,,1E-6       ! NEAR ZERO VALUE FOR MINREF FIELD
CNVTOL,M,-1            ! CONVERGENCE CRITERION BASED UPON MOMENTS IS 
                       !  REMOVED AS IT IS NOT NEEDED FOR THIS TEST
/OUT,SCRATCH
SOLVE                  ! LOAD STEP 1
NSUBST,50,500,50
TIME,5
OUTPR,BASIC,5
RATE,ON
SOLVE                  ! LOAD STEP 2
FINISH
/POST26
ESOL,2,1,,LEPCR,1,EPCR ! STORE CREEP STRAIN
/OUT,
PRVAR,2                ! PRINT STRAIN VARIATION WITH TIME
*GET,T1,VARI,2,RTIME,0
*GET,T2,VARI,2,RTIME,.5
*GET,T3,VARI,2,RTIME,1
*GET,T4,VARI,2,RTIME,5
*status,parm
*DIM,LABEL,CHAR,4,2
*DIM,VALUE,,4,3
LABEL(1,1) = 'CRP STR @','CRP STR @','CRP STR @','CRP STR @'
LABEL(1,2) = '0 hr','.5 hr','1 hr','5 hr'
*VFILL,VALUE(1,1),DATA,0,.00197,.00316,.00497
*VFILL,VALUE(1,2),DATA,T1,T2,T3,T4
*VFILL,VALUE(1,3),DATA,000,ABS(T2/.00197),ABS(T3/.00316),ABS(T4/.00497)
/COM
/OUT,vm133,vrt
/COM,------------------- VM133 RESULTS COMPARISON -------------
/COM,
/COM,                 |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.5,'  ',F12.5,'   ',1F15.3)
/COM,----------------------------------------------------------
/OUT
FINISH
*LIST,vm133,vrt






/COM,ANSYS MEDIA REL. 2023R1 (11/04/2022) REF. VERIF. MANUAL: REL. 2023R1
/VERIFY,VM5
JPGPRF,500,100,1             ! MACRO TO SET PREFS FOR JPEG PLOTS
/SHOW,JPEG
/PREP7
/TITLE, VM5, LATERALLY LOADED TAPERED SUPPORT STRUCTURE (QUAD. ELEMENTS)
C***     MECHANICS OF SOLIDS, CRANDALL AND DAHL, 1959, PAGE 342, PROB. 7.18
C***          USING PLANE42 ELEMENTS
ANTYPE,STATIC
ET,1,PLANE182,2,,3      
R,1,2
MP,EX,1,30E6
MP,NUXY,1,0.0            ! POISSON'S RATIO SET TO 0.0 TO AGREE WITH BEAM THEORY
N,1,25
N,7,75 
FILL
N,8,25,-3 
N,14,75,-9
FILL     
E,2,1,8,9 
EGEN,6,1,1   
NSEL,S,LOC,X,75   
D,ALL,ALL                ! CONSTRAIN NODES AT FIXED END
NSEL,ALL
F,1,FY,-4000
FINISH
/SOLU    
OUTPR,,1
/OUT,SCRATCH
SOLVE
FINISH
/POST1
/OUT,
END_NODE = NODE (75,0,0)
*GET,STS_E_182,NODE,END_NODE,S,X       ! STRESS AT FIXED END (END NODE )
PLDISP,2
MID_NODE = NODE (50,0,0)
*GET,STS_M_182,NODE,MID_NODE,S,EQV 
FINISH
/PREP7
/TITLE, VM5, LATERALLY LOADED TAPERED SUPPORT STRUCTURE (QUAD. ELEMENTS)
C***          MECHANICS OF SOLIDS, CRANDALL AND DAHL, 1959, PAGE 342, PROB. 7.18
C***          USING PLANE183 ELEMENTS
C***                      ! CHANGE ELEMENT TYPE TO HIGHER ORDER PLANE82
ET,1,PLANE183,,,3
EMID                      ! ADD MIDSIDE NODES TO PLANE183 ELEMENTS
NSEL,R,LOC,X,75  
NSEL,R,LOC,Y,-4.5         ! SELECT MIDSIDE NODE AT FIXED END
D,ALL,ALL                 ! CONSTRAIN  MIDSIDE NODE AT FIXED END
NSEL,ALL
FINISH
/SOLU    
/OUT,SCRATCH
SOLVE
FINISH
/POST1
/OUT,
END_NODE = NODE (75,0,0) 
*GET,STS_E_183,NODE,END_NODE,S,X       ! STRESS AT FIXED END (END NODE )
MID_NODE = NODE (50,0,0)   
*GET,STS_M_183,NODE,MID_NODE,S,EQV  

*DIM,LABEL,CHAR,2,2
*DIM,VALUEI,,2,3
*DIM,VALUEII,,2,3
LABEL(1,1) = 'MID_STRS','END-STRS'
LABEL(1,2) = ' (psi)  ',' (psi)  '
*VFILL,VALUEI(1,1),DATA,8333,7407
*VFILL,VALUEI(1,2),DATA,STS_M_182,STS_E_182
*VFILL,VALUEI(1,3),DATA,(STS_M_182/8333),(STS_E_182/7407)
*VFILL,VALUEII(1,1),DATA,8333,7407
*VFILL,VALUEII(1,2),DATA,STS_M_183,STS_E_183
*VFILL,VALUEII(1,3),DATA,(STS_M_183/8333),(STS_E_183/7407)
/COM,STS_M_42 = STRESS AT MID-LENGTH USING ELEMENT 42
/COM,STS_E_42 = STRESS AT FIXED END USING ELEMENT 42
/COM,STS_M_183 = STRESS AT MID-LENGTH USING ELEMENT 183
/COM,STS_E_183 = STRESS AT FIXED END USING ELEMENT 183
/COM,
/OUT,vm5,vrt
/COM,------------------- VM5 RESULTS COMPARISON ---------------------
/COM,
/COM,RESULTS FOR PLANE182:
/COM,
/COM,                 |   TARGET   |   Mechanical APDL   |   RATIO
*VWRITE,LABEL(1,1),LABEL(1,2),VALUEI(1,1),VALUEI(1,2),VALUEI(1,3)
(1X,A8,A8,'   ',F10.3,'  ',F14.3,'   ',1F15.3)
/COM,
/COM,RESULTS FOR PLANE183:
/COM,
/COM,                 |   TARGET   |   Mechanical APDL   |   RATIO
*VWRITE,LABEL(1,1),LABEL(1,2),VALUEII(1,1),VALUEII(1,2),VALUEII(1,3)
(1X,A8,A8,'   ',F10.3,'  ',F14.3,'   ',1F15.3)
/COM,----------------------------------------------------------------
/OUT

FINISH
*LIST,vm5,vrt

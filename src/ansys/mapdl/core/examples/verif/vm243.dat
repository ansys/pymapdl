/COM,ANSYS MEDIA REL. 150 (11/8/2013) REF. VERIF. MANUAL: REL. 150
/VERIFY,VM243
JPGPRF,500,100,1             ! MACRO TO SET PREFS FOR JPEG PLOTS
/SHOW,JPEG
/TITLE,VM243,CANTILEVER BEAM WITH TRIANGULAR LOADING DEFINED BY FUNCTION
/COM,  REFERENCE: F.P BEER AND E.J. JOHNSTON,JR, MECHANICS OF MATERIALS,
/COM, 		MCGRAW-HILL, NEW YORK, NY, 1981, PP. 356,366,397,613
MINLOAD=0					! MINIMUM LOAD
MAXLOAD=1  					! MAXIMUM LOAD
L=10						! LENGTH
THICK=1					! THICKNESS
E1=30e6					! YOUNGS MODULUS

/PREP7
ET,1,PLANE183
KEYOPT,1,3,3
KEYOPT,1,6,0
KEYOPT,1,10,0
R,1,THICK,
MPTEMP,,,,,,,,
MPTEMP,1,0
MPDATA,EX,1,,E1
MPDATA,PRXY,1,,.27
MPDATA,DENS,1,,1
RECTNG,0,L,0,THICK      
RECTNG,0,L,10,10+THICK
AMESH,ALL
*DEL,_FNCNAME
*DEL,_FNCMTID
*SET,_FNCNAME,'PRES1'

*DIM,%_FNCNAME%,TABLE,6,5,1
*SET,%_FNCNAME%(0,0,1), 0.0, -999
*SET,%_FNCNAME%(2,0,1), 0.0
*SET,%_FNCNAME%(3,0,1), 0.0
*SET,%_FNCNAME%(4,0,1), 0.0
*SET,%_FNCNAME%(5,0,1), 0.0
*SET,%_FNCNAME%(6,0,1), 0.0
*SET,%_FNCNAME%(0,1,1), 1.0, -1, 0, (MAXLOAD-MINLOAD)/L, 0, 0, 2
*SET,%_FNCNAME%(0,2,1), 0.0, -2, 0, 1, -1, 3, 2
*SET,%_FNCNAME%(0,3,1),   0, -1, 0, MINLOAD, 0, 0, -2
*SET,%_FNCNAME%(0,4,1), 0.0, -3, 0, 1, -2, 1, -1
*SET,%_FNCNAME%(0,5,1), 0.0, 99, 0, 1, -3, 0, 0

SFL,3,PRES, %PRES1%        		! LOADING BY FUNCTION
SFL,7,PRES,MAXLOAD,MINLOAD 		! LOADING BY TWO END POINT VALUES
DL,4,,ALL,0
DL,8,,ALL,0
FINISH

/SOLU
/STATUS,SOLU
ANTYPE,STATIC
SOLVE
FINISH

/POST1
PLDISP,0
/EFACET,1
AVPRIN,0,
LSEL,S,LINE,,1
NSLL,S,1
PRNSOL,U,SUM
LSEL,S,LINE,,5
NSLL,S,1
PRNSOL,U,SUM				
ALLSEL

I1=(THICK*THICK*THICK*THICK)/12  	! MOMENT OF INERTIA FOR SQUARE BEAM
TARGET_DISP=(11/120)*(L*L*L*L*MAXLOAD)/(E1*I1)  
*GET,TBDISP,NODE,2,U,SUM   		! DISPLACEMENT AT CORNER FOR TABULAR LOADED BEAM
*GET,LDDISP,NODE,127,U,SUM 		! DISPLACEMENT AT CORNER FOR LINEAR LOADED BEAM
*DIM,LABEL,CHAR,2,2
*DIM,VALUE,,2,3
LABEL(1,1) = 'DISP, TA'
LABEL(1,2) = 'BLE (M)'
LABEL(2,1) = 'DISP, S1'
LABEL(2,2) = 'OPE (M)'

*VFILL,VALUE(1,1),DATA,TARGET_DISP
*VFILL,VALUE(1,2),DATA,TBDISP
*VFILL,VALUE(1,3),DATA,ABS(TBDISP/TARGET_DISP)
*VFILL,VALUE(2,1),DATA,TARGET_DISP
*VFILL,VALUE(2,2),DATA,LDDISP
*VFILL,VALUE(2,3),DATA,ABS(LDDISP/TARGET_DISP)
/COM
/OUT,vm243,vrt
/COM,------------------- VM243 RESULTS COMPARISON -------------
/COM,
/COM,                 |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',E10.3,'  ',E14.3,'   ',1F15.2)
/COM,----------------------------------------------------------
/OUT
FINISH
*LIST,vm243,vrt

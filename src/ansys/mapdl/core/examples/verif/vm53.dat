/COM,ANSYS MEDIA REL. 2023R1 (11/04/2022) REF. VERIF. MANUAL: REL. 2023R1
/VERIFY,VM53
/PREP7
/TITLE, VM53, VIBRATION OF A STRING UNDER TENSION
C***           VIBRATION THEORY AND APPLICATIONS, THOMSON, 2ND. PRINTING,
C***           PAGE 264, ART. 8.2,  
ANTYPE,STATIC              ! STATIC ANALYSIS
ET,1,LINK180
KEYOPT,1,3,1		   	   ! TENSION ONLY OPTION
SECTYPE,1,LINK   
SECDATA,306796E-8	         ! DEFINE AREA
MP,EX,1,30E6
MP,DENS,1,73E-5
N,1                        ! DEFINE NODES
N,14,100
FILL
E,1,2                      ! DEFINE ELEMENTS
EGEN,13,1,1
INIS,SET,CSYS,-2           ! ARBITRARY INITIAL STRAIN
INIS,SET,DTYP,EPEL
INIS,DEFINE,,,,,543248E-8
OUTPR,BASIC,1
D,ALL,ALL                  ! FIX ALL MOTIONS FOR STATIC STRESSES
FINISH
/out,scratch
/SOLU    
RESCONTROL,LINEAR,ALL,1     ! NEEDED FOR PERTURBED ANALYSIS
SOLVE
FINISH
/out
/POST1
ETABLE,STRS,LS,1
*GET,STRSS,ELEM,13,ETAB,STRS
FINISH
/POST26
RFORCE,2,1,F,X
STORE
*GET,FORCE,VARI,2,EXTREM,VMAX
PARSAV,ALL
/out,scratch
/SOLU    
ANTYPE,STATIC,RESTART,,,PERTURB   ! RESTART FROM LAST LOAD STEP AND LAST SUB STEP
PERTURB,MODAL,,,PARKEEP           ! PERFORM PERTURBED MODAL SOLVE
SOLVE,ELFORM                      ! REFORM ELEMENT MATRICES

PARRES,CHANGE
MODOPT,LANB,3              ! EXTRACT 3 MODES USING LANB EXTRACTION METHOD
MXPAND,3                   ! EXPAND FIRST THREE MODES
DDELE,2,UX,13              ! RELEASE INTERIOR DOFS
DDELE,2,UY,13
SOLVE
/out
*GET,FREQ1,MODE,1,FREQ
*GET,FREQ2,MODE,2,FREQ
*GET,FREQ3,MODE,3,FREQ
*DIM,LABEL,CHAR,5,2
*DIM,VALUE,,5,3
LABEL(1,1) = '      F,','      SI','      f1','      f2','      f3'
LABEL(1,2) = ' lb     ','GMA,psi ',', Hz    ',', Hz    ',', Hz    '
*VFILL,VALUE(1,1),DATA,500,162974,74.708,149.42,224.12
*VFILL,VALUE(1,2),DATA,ABS(FORCE),STRSS,FREQ1,FREQ2,FREQ3
*VFILL,VALUE(1,3),DATA,ABS(FORCE/500),ABS(STRSS/162974),ABS(FREQ1/74.708)
*VFILL,VALUE(4,3),DATA,ABS(FREQ2/149.42),ABS(FREQ3/224.12)
/COM
/OUT,vm53,vrt
/COM,------------------- VM53 RESULTS COMPARISON ---------------
/COM,
/COM,                 |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.3,'  ',F14.3,'   ',1F15.3)
/COM,-----------------------------------------------------------
/OUT
FINISH
*LIST,vm53,vrt

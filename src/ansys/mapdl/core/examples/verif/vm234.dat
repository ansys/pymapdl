! Copyright (C) 2024 ANSYS, Inc. and/or its affiliates.
! SPDX-License-Identifier: MIT
! 
!
! Permission is hereby granted, free of charge, to any person obtaining a copy
! of this software and associated documentation files (the "Software"), to deal
! in the Software without restriction, including without limitation the rights
! to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
! copies of the Software, and to permit persons to whom the Software is
! furnished to do so, subject to the following conditions:
!
! The above copyright notice and this permission notice shall be included in all
! copies or substantial portions of the Software.
!
! THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
! IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
! FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
! AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
! LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
! OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
! SOFTWARE.

/COM,ANSYS MEDIA REL. 2023R2 (05/12/2023) REF. VERIF. MANUAL: REL. 2023R2
/VERIFY,vm234
JPGPRF,500,100,1             ! MACRO TO SET PREFS FOR JPEG PLOTS
/SHOW,JPEG
/TITLE, VM234, CYCLIC LOADING ON RUBBER BLOCK
/COM, REFERENCE: HOLZAPFEL, GERHARD A. " ON LARGE STRAIN VISCOELASTICITY:
/COM,		     CONTINUUM, FORMULATION AND FINITE ELEMENT APPLICATIONS TO
/COM,		     ELASTOMERIC STRUCTURES", INTERNATIONAL JOURNAL FOR NUMERICAL
/COM,		     METHODS, VOL. 39, PG: 3903-3926,1996.
/COM
/OUT,SCRATCH
/PREP7
MP,EX,1,422500.0              		 
TB,HYPER,1, ,3,OGDEN,					! HYPER ODGEN MATERIAL MODEL
TBDATA,1,6.3E+05*3.0,1.3,1200*3.0,5,-10000*3.0,-2
TB,PRONY,1, ,2,SHEAR
TBDATA,,1.0/3.0,0.40,1.0/3.0,0.20,
N,1, 0.0, 0.0, 0.0            
N,2, 0.1, 0.0, 0.0
N,3, 0.1, 0.1, 0.0
N,4, 0.0, 0.1, 0.0
N,5, 0.0, 0.0, 0.1
N,6, 0.1, 0.0, 0.1
N,7, 0.1, 0.1, 0.1
N,8, 0.0, 0.1, 0.1
ET,1,SOLID185,,,,                  			! SOLID 185 ELEMENT
KEYOPT,1,6,1
E,1,2,3,4,5,6,7,8
FINISH

/SOLUTION
/OUT,SCRATCH
NSEL,S,LOC,X                 				
D,ALL,UX
NSEL,S,LOC,Y
D,ALL,UY
NSEL,S,LOC,Z
D,ALL,UZ
NSEL,ALL
*DIM,AMPL,ARRAY,4             			! AMPLITUDE VECTOR DEFINITION
AMPL(1)=0.01
AMPL(2)=0.02
AMPL(3)=0.03
AMPL(4)=0.04
*DIM,SOLTIME,ARRAY,161       				! TIME VECTOR DEFINITION
SOLTIME(1)=0.0
*DO,I,2,161,1
  SOLTIME(I)=SOLTIME(I-1)+0.1
*ENDDO
*DIM,BC_X,ARRAY,161          				! DISPLACEMENT VECTOR DEFINITION
J=1
*DO,I,1,161,1
  BC_X(I)=AMPL(J)*SIN(SOLTIME(I)/2.0*3.141592654)
  *IF,SOLTIME(I),EQ,(4.0*J),THEN
    J=J+1
  *ENDIF
*ENDDO
NLGEOM,ON                     			! SOLUTION CONTROLS
CNVT,U,1,1.0e-8
CNVT,F,1,1.0e-6
OUTRES,ALL,ALL
TIME,1E-07
DELTIM,1E-07,1E-08,5E-08
/OUT,SCRATCH
SOLVE
*DO,I,2,161,1
  D,2,UX,BC_X(I)
  D,3,UX,BC_X(I)
  D,6,UX,BC_X(I)
  D,7,UX,BC_X(I)
  TIME,SOLTIME(I)
  NSUB,5,10,5
  SOLVE
*ENDDO
TIME,20.0
SOLVE
FINISH
/POST26
/OUT,
ESOL,2,1,7,S,X
ESOL,3,1,7,EPEL,X
NSOL,4,7,U,X
PRVAR,2,3,4
PLVAR,2
PLVAR,3
PLVAR,4
*GET,SIG16,VARI,2,RTIME,16
*GET,SIG20,VARI,2,RTIME,20
*DIM,LABEL,CHAR,2
*DIM,VALUE,,2,3
LABEL(1) = 'SIG16,PA','SIG20,PA'
*VFILL,VALUE(1,1),DATA,601300.0,0.0
*VFILL,VALUE(1,2),DATA,ABS(SIG16),ABS(SIG20)
*VFILL,VALUE(1,3),DATA,ABS(SIG16/601300),0.0
/OUT,vm234,vrt
/COM
/COM,------------------- VM234 RESULTS COMPARISON -------------------
/COM,
/COM,         |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
*VWRITE,LABEL(1),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,'   ',F10.1,'  ',F14.1,'   ',1F15.3)
/COM,----------------------------------------------------------------
/OUT
FINISH
*LIST,vm234,vrt

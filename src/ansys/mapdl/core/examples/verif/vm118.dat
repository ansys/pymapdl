/COM,ANSYS MEDIA REL. 2023R1 (11/04/2022) REF. VERIF. MANUAL: REL. 2023R1
/VERIFY,VM118
/PREP7
/TITLE, VM118, CENTERLINE TEMP. OF A HEAT GENERATING WIRE
C***      HEAT, MASS AND MOMENTUM TRANS., ROHSENOW AND CHOI, 2ND. PR., PAGE 106,
C***      EX. 6.5, USING PLANE55 ELEMENTS (PLANE ELEMENTS)
ANTYPE,STATIC                 ! THERMAL ANALYSIS
ET,1,PLANE55
MP,KXX,1,13
CSYS,1                        ! CYLINDRICAL COORDINATE SYSTEM
N,1,1E-10,-5                  ! USE NON-ZERO RADIUS SINCE NODE IS NOT AT THETA=0
N,6,(.375/12),-5
FILL
NGEN,2,10,1,6,1,,10
E,1,2,12,12
E,2,3,13,12
EGEN,4,1,2
CP,1,TEMP,2,12                 ! COUPLING TO ENSURE CIRCUMFERENTIAL SYMMETRY
CPSGEN,5,1,1
NSEL,S,LOC,X,(0.375/12)
SF,ALL,CONV,5,70
NSEL,ALL
BFE,ALL,HGEN,,111311.7         ! ELEMENT HEAT GENERATION
OUTPR,BASIC,1
FINISH
/SOLU    
SOLVE
FINISH
/POST1
*GET,TEMP,NODE,6,TEMP           ! GET TEMPERATURE AT SURFACE NODE
PI=2*ASIN(1)
AREA=36*2*(0.375/12)*SIN(PI/36) ! COMPUTE AREA OF OUTER BOUNDARY
HRATE=AREA*5.0*(TEMP-70)        ! TOTAL HEAT DISSIPATION RATE
PRNSOL,TEMP                     ! PRINT NODAL TEMPERATURES
*status,parm                         ! SHOW STATUS
*GET,TCL,NODE,1,TEMP
*DIM,LABEL,CHAR,3,2
*DIM,VALUE,,3,3
LABEL(1,1) = 'T CL,   ','T S,    ','q,   BTU'
LABEL(1,2) = 'DEGREE F','DEGREE F','/hr     '
*VFILL,VALUE(1,1),DATA,419.9,417.9,341.5
*VFILL,VALUE(1,2),DATA,TCL,TEMP,HRATE
*VFILL,VALUE(1,3),DATA,ABS(TCL/419.9) ,ABS( TEMP/417.9 ),ABS(HRATE/341.5)
SAVE,TABLE1
FINISH

/PREP7
C***      USING SOLID70 ELEMENTS (SOLID ELEMENTS)
EDELE,ALL                       ! DELETE PLANE55 ELEMENTS
ET,1,SOLID70                    ! CHANGE ELEMENT TYPE
NGEN,2,20,1,16,1,,,-1           ! GENERATE 2ND PLANE OF NODES
NUMCMP,ELEM
E,1,2,12,12,21,22,32,32
E,2,3,13,12,22,23,33,32
EGEN,4,1,2
CPDELE,1,6,1                    ! REMOVE PREVIOUS COUPLING SPECIFICATIONS
CP,1,TEMP,1,21                  ! COUPLING TO ENSURE AXIAL SYMMETRY
CP,2,TEMP,2,12,22,32            ! COUPLING TO ENSURE CIRCUMFERENTIAL SYMMETRY
CPSGEN,5,1,2
CSYS,1
NSEL,S,LOC,X,(0.375/12)
SF,ALL,CONV,5,70
NSEL,ALL
BFE,ALL,HGEN,1,111311.7         ! ELEMENT HEAT GENERATION
FINISH
/SOLU    
SOLVE
FINISH
/POST1
*GET,TEMP,NODE,6,TEMP            ! GET TEMPERATURE AT SURFACE NODE
PI=2*ASIN(1)
AREA=36*2*(0.375/12)*SIN(PI/36)  ! LENGTH ALONG 10 DEG ON OUTER FACE
HRATE=AREA*5.0*(TEMP-70)         ! TOTAL HEAT DISSIPATION RATE
PRNSOL,TEMP                      ! PRINT NODAL TEMPERATURES
*status,parm                          ! SHOW PARAMETER STATUS
*GET,TCL,NODE,1,TEMP
LABEL(1,1) = 'T CL,   ','T S,    ','q,   BTU'
LABEL(1,2) = 'DEGREE F','DEGREE F','/hr     '
*VFILL,VALUE(1,1),DATA,419.9,417.9,341.5
*VFILL,VALUE(1,2),DATA,TCL,TEMP,HRATE
*VFILL,VALUE(1,3),DATA,ABS(TCL/419.9) ,ABS( TEMP/417.9 ), ABS(HRATE/341.5)
SAVE,TABLE2
RESUME,TABLE1
/COM
/OUT,vm118,vrt
/COM,------------------- VM118 RESULTS COMPARISON -------------
/COM,
/COM,                 |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
/COM,STIF55 RESULTS:
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.1,'  ',F12.1,'   ',1F15.3)
/NOPR
RESUME,TABLE2
/GOPR
/COM,
/COM,STIF70 RESULTS:
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.1,'  ',F12.1,'   ',1F15.3)
/COM,----------------------------------------------------------
/OUT
FINISH
*LIST,vm118,vrt


/COM,ANSYS MEDIA REL. 2023R1 (11/04/2022) REF. VERIF. MANUAL: REL. 2023R1
/VERIFY,VM297
/TITLE,VM297,ACOUSTIC PROPOGATION WITH MEAN FLOW IN MUFFLER
/COM,
/COM, REFERENCE: FINITE ELEMENT AND BOUNDARY ELEMENT MODELLING FOR THE ACOUSTIC WAVE TRANSMISSION
/COM,            IN MEAN FLOW MEDIUM
/COM,            T. TSUJI, T. TSUCHIYA, AND Y. KAGAWA
/COM,            JOURNAL OF SOUND AND VIBRATION (2002) 255(5), 849-866
/COM,

/PREP7

L1 = 0.12		! TOTAL LENGTH OF MUFFLER IN METERS
R1 = 0.01		! RADIUS OF DUCT IN METERS
L2 = 0.06		! LENGTH OF CHAMBER IN METERS
R2 = 0.02		! RADIUS OF CHAMBER IN METERS
Z2 = (L1-L2)/2		! LENGTH OF DUCT IN METERS

F1 = 0			! BEGINING FREQUENCY
F2 = 5000		! ENDING FREQUENCY

RHO0=1.2       		! DENSITY IN KG/M^3           
C0=340         		! SPEED OF SOUND IN M/S
MACH=0.3       		! MACH NUMBER
V0=MACH*C0     		! MEAN FLOW VELOCITY IN M/S

CYLIND,R1,0,0,L1,0,90
CYLIND,R2,R1,Z2,Z2+L2,0,90
CYLIND,R1,0,0,L1,90,180
CYLIND,R2,R1,Z2,Z2+L2,90,180
CYLIND,R1,0,0,L1,180,270
CYLIND,R2,R1,Z2,Z2+L2,180,270
CYLIND,R1,0,0,L1,270,360
CYLIND,R2,R1,Z2,Z2+L2,270,360
VGLUE,ALL 

ET,1,FLUID220,,1,,0  	 ! ACOUSTIC ELEMENT without FSI or PML

MP,DENS,1,RHO0
MP,SONC,1,C0

TYPE,1
MAT,1
ESIZE,0.002
/OUT,vm297_SCRATCH
VSWEEP,ALL
/OUT,

NSEL,S,LOC,Z,0
SF,ALL,SHLD,1.0,0    	 ! APPLY SURFACE VELOCITY LOAD AT INLET
NSEL,S,LOC,Z,L1      
SF,ALL,INF           	 ! APPLY INFINITE ROBIN RADIATION BOUNDARY AT TERMINAL

ALLSEL,ALL
BF,ALL,VMEN,0.,0.,V0 	 ! APPLY UNIFORM MEAN FLOW FOR ACOUSTIC ELEMENT IN Z DIRECTION
FINISH

/SOLUTION
ANTYPE,HARM		  ! HARMONIC ANALYSIS
HARFRQ,F1,F2	    	  ! FREQUENCY RANGE
HROPT,FULL
KBC,1
NSUB,100	     	  ! NUMBER OF SUBSTEPS
DDOPT,FREQ		  ! FREQUENCY BASED DECOMPOISTION		
/OUT,vm297_SCRATCH,,,APPEND
SOLVE
FINISH

/POST1

SET,1,40,,3
*GET,P1,NODE,NODE(0,0,0.12),PRES
NSEL,S,LOC,X,0
NSEL,R,LOC,Y,0
*GET,_NNOD,NODE,0,COUNT
*DIM,NNODE,ARRAY,_NNOD
*DIM,ZCORD,ARRAY,_NNOD
*DIM,P_AMP,ARRAY,_NNOD
*DIM,P_PHA,ARRAY,_NNOD

*DO,_I,1,_NNOD
  NODE=NDNEXT(NODE)
  NNODE(_I)=NODE
  *GET,ZL,NODE,NODE,LOC,Z
  ZCORD(_I)=ZL
 
  SET,1,40,,3		 	! AMPLITUDE SOLUTION AT 2000HZ
  *GET,_PA,NODE,NODE,PRES
  P_AMP(_I)=_PA
  
  SET,1,40,,4			! PHASE ANGLE SOLUTION AT 2000HZ
  *GET,_PP,NODE,NODE,PRES
  P_PHA(_I)=_PP
*ENDDO

/OUT
/COM,
/COM, NODE   ZCORD   PRESSURE AMPLITUDE   PRESSURE PHASE
/COM,
*VWRITE,NNODE(1),ZCORD(1),P_AMP(1),P_PHA(1)
(2X,F7.0,1X,E12.4,1X,E14.7,1X,E14.7)
ALLSEL,ALL
/OUT,SCRATCH
FINISH

/POST26
PRCPLX,1			! PRINT AMPLTIUDE AND PHASE ANGLE
NSEL,S,LOC,Z,L1			! HARMONIC RESPONSE AT THE TERMINATION END
NOD = NDNEXT(0)
NSOL,2,NOD,PRES,,PRES 
PRVAR,2
*GET,RFREQ,VARI,2,EXTREM,TMAX
PRVAR,2
/SHOW,PNG,REV
PLVAR,2
/SHOW,CLOSE
FINISH

*DIM,LABEL,CHAR,1,2
*DIM,VALUE,,2,3

LABEL(1,1)='Amp@.12m'
LABEL(1,2)='PEAK_FRQ'

*VFILL,VALUE(1,1),DATA,1010	! INTERPOLATED FROM FIGURE.11,PG:860 IN REFERENCE FOR DISTANCE=0.12M,M=0.3
*VFILL,VALUE(1,2),DATA,P1
*VFILL,VALUE(1,3),DATA,ABS(P1/1010)
*VFILL,VALUE(2,1),DATA,3000	! INTERPOLATED FROM FIGURE.12,PG:860 IN REFERENCE,M=0.3
*VFILL,VALUE(2,2),DATA,RFREQ
*VFILL,VALUE(2,3),DATA,ABS(RFREQ/3000)

/OUT,vm297,vrt
/COM,
/COM, --------------- RESULT COMPARISON -------------------------------
/COM,
/COM,    			  |  TARGET  |  MECHANICAL APDL  | RATIO     
/COM,
/COM, -------------------------------------------------------
/COM,  PRESSURE AMPLITUDE ALONG THE CENTER LINE FOR Z=0.12m
/COM, --------------------------------------------------------
*VWRITE,LABEL(1,1),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A12,'     ',F10.3,'  ',F14.3,'   ',F15.3)
/COM
/COM, -------------------------------------------------------------
/COM, PEAK RESONANCE FREQUENCY FOR A FREQUENCY RANGE OF 0-5000HZ
/COM,--------------------------------------------------------------
/COM,
*VWRITE,LABEL(1,2),VALUE(2,1),VALUE(2,2),VALUE(2,3)
(1X,A12,'     ',F10.3,'  ',F14.3,'   ',F15.3)
/COM,
/COM,-------------------------------------------------------------------
/OUT,
*LIST,vm297,vrt
FINISH
/EXIT,NOSAVE

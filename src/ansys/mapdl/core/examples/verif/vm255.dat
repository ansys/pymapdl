/COM,ANSYS MEDIA REL. 2023R1 (11/04/2022) REF. VERIF. MANUAL: REL. 2023R1
/VERIFY,VM255
/SHOW,JPEG
JPGPRF,500,100,1           !* MACRO TO SET PREFS FOR JPEG PLOTS
/TITLE,VM255,DELAMINATION OF DOUBLE CANTILEVER BEAM USING DEBONDING
/COM,   REF: ALFANO, G. AND CRISFIELD, M. A.,
/COM,   "FINITE ELEMENT INTERFACE MODELS FOR THE DELAMINATION ANALYSIS
/COM,   OF LAMINATED COMPOSITES: MECHANICAL AND COMPUTATIONAL ISSUES"
/COM,   INT. J. NUMER. METH. ENGNG 2001, 50:1701-1736.
/PREP7
ET,1,PLANE182              !* 2D 4-NODE STRUCTURAL SOLID ELEMENT
KEYOPT,1,1,2               !* ENHANCE STRAIN FORMULATION
KEYOPT,1,3,2               !* PLANE STRAIN
ET,2,PLANE182
KEYOPT,2,1,2
KEYOPT,2,3,2
ET,3,TARGE169              !* 2D TARGET ELEMENT
ET,4,CONTA172              !* 2D 3-NODE CONTACT ELEMENT
KEYOPT,4,10,1              !* USE CONSTANT CONTACT STIFFNESS
KEYOPT,4,12,5              !* BONDED ALWAYS CONTACT
MP,EX,1,1.353E5            !* E11 = 135.3 GPA
MP,EY,1,9.0E3              !* E22 =   9.0 GPA
MP,EZ,1,9.0E3              !* E33 =   9.0 GPA
MP,GXY,1,5.2E3             !* G12 =   5.2 GPA
MP,PRXY,1,0.24
MP,PRXZ,1,0.24
MP,PRYZ,1,0.46
KOPEN  = 1.E6
TB,CZM,2,1,1,CBDE
TBDATA,1,1.7,0.28,,,1.E-8
RECTNG,0,100,0,1.5         !* DEFINE AREAS
RECTNG,0,100,0,-1.5
LSEL,S,LINE,,2,8,2         !* DEFINE LINE DIVISION
LESIZE,ALL,0.75
LSEL,INVE
LESIZE,ALL, , ,200
ALLSEL,ALL
TYPE,1                     !* MESH AREA 2
MAT,1
LOCAL,11,0,0,0,0
ESYS,11
AMESH,2
CSYS,0
TYPE,2                     !* MESH AREA 1
ESYS,11
AMESH,1
CSYS,0
NSEL,S,LOC,X,30,100
TYPE,3
MAT,2
REAL,3
ESEL,S,TYPE,,2
NSLE,S
NSEL,R,LOC,Y
ESURF                      !* TARGET ELEMENTS
TYPE,4
REAL,3
RMODIF,3,3,-KOPEN
RMODIF,3,12,-KOPEN
ESEL,S,TYPE,,1
NSLE,S
NSEL,R,LOC,Y
NSEL,R,LOC,X,30,100
ESURF                      !* CONTACT ELEMENTS
ALLSEL,ALL
NSEL,S,LOC,X,100           !* APPLY CONSTRAINTS
D,ALL,ALL
NSEL,ALL
FINISH
/SOLU
ESEL,S,TYPE,,2
NSLE,S
NSEL,R,LOC,X
NSEL,R,LOC,Y,1.5           !* APPLY DISPLACEMENT LOADING ON TOP
D,ALL,UY,10
NSEL,ALL
ESEL,ALL
ESEL,S,TYPE,,1
NSLE,S
NSEL,R,LOC,X
NSEL,R,LOC,Y,-1.5          !* APPLY DISPLACEMENT LOADING ON BOTTOM
D,ALL,UY,-10
NSEL,ALL
ESEL,ALL
NLGEOM,ON
TIME,1
NSUBST,100,100,100
OUTRES,ALL,ALL
/OUT,SCRATCH
SOLVE                      !* PERFORM SOLUTION
FINISH
/POST26
NSEL,S,LOC,Y,1.5
NSEL,R,LOC,X,0
/OUT,
*GET,NTOP,NODE,0,NUM,MAX
NSEL,ALL
NSOL,2,NTOP,U,Y,UY
RFORCE,3,NTOP,F,Y,FY
PROD,4,3, , ,RF, , ,20
/TITLE,VM255, DCB: REACTION AT TOP NODE VERSES PRESCRIBED DISPLACEMENT
/AXLAB,X,DISP U (mm)
/AXLAB,Y,REACTION FORCE R (N)
/YRANGE,0,60
XVAR,2
PLVAR,4
PRVAR,UY,RF
*GET,TMAX,VARI,4,EXTREM,TMAX   !* TIME CORRESPONDING TO MAX RFORCE
FINISH
/POST1
SET, , , , ,TMAX               !* RETRIEVE RESULTS AT TMAX
NSEL,S,NODE, ,NTOP             !* SELECT NODE NTOP
*GET,RF_NTOP,NODE,NTOP,RF,FY   !* FY RFORCE AT NODE NTOP
*GET,UY_NTOP,NODE,NTOP,U,Y     !* DISP AT NODE NTOP CORRESPONDING TO RFORCE
RF_MAX = RF_NTOP*20            !* PLANE STRAIN OPTION AND WIDTH = 20 mm
SET,LAST                       !* RETRIEVE RESULTS AT LAST SUBSTEP
*GET,RF_END,NODE,NTOP,RF,FY    !* FY RFORCE AT NODE NTOP AT LAST SUBSTEP
*GET,UY_END,NODE,NTOP,U,Y      !* DISP AT NODE NTOP CORRESPONDING TO RFORCE
RF_END = RF_END*20             !* PLANE STRAIN OPTION AND WIDTH = 20 mm
*DIM,LABEL,CHAR,2,2
*DIM,VALUE,,2,3
*DIM,VALUE2,,2,3
LABEL(1,1) = 'RFORCE','DISP '
LABEL(1,2) = 'FY (N)','UY (mm)'
*VFILL,VALUE(1,1),DATA,50.0,1.5
*VFILL,VALUE(1,2),DATA,RF_MAX,UY_NTOP
*VFILL,VALUE(1,3),DATA,ABS(RF_MAX/50.0),ABS(UY_NTOP/1.5)
*VFILL,VALUE2(1,1),DATA,24.0,10.0
*VFILL,VALUE2(1,2),DATA,RF_END,UY_END
*VFILL,VALUE2(1,3),DATA,ABS(RF_END/24.0),ABS(UY_END/10.0)
/COM
/OUT,vm255,vrt
/COM,------------------- VM255 RESULTS COMPARISON --------------
/COM,
/COM,                 |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
/COM,MAX RFORCE AND CORRESPONDING DISP USING DEBONDING:
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.3,'  ',1F14.3,'   ',1F15.3)
/COM,
/COM,RFORCE CORRESPONDING TO DISP U = 10.0 USING DEBONDING:
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE2(1,1),VALUE2(1,2),VALUE2(1,3)
(1X,A8,A8,'   ',F10.3,'  ',1F14.3,'   ',1F15.3)
/COM,-----------------------------------------------------------
/OUT
FINISH
*LIST,vm255,vrt

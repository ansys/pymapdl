/COM,ANSYS MEDIA REL. 150 (11/8/2013) REF. VERIF. MANUAL: REL. 150
/VERIFY,VM274
/TITLE,VM274,STABILIZING SQUEAL DAMPING
C***
/PREP7
			        !MATERIAL MODEL 1
MP,EX,1,2.1E11		!YOUNG'S MODULUS (N/m^2)
MP,NUXY,1,0.3		!POISSON'S RATIO
MP,DENS,1,8000		!DENSITY (kg/m^3)

			        !MATERIAL MODEL 2 
MP,MU,2,0.5		    !COEFFICIENT OF FRICTION


ET,1,SOLID185		!3D 8-NODE STRUCTURAL SOLID
ET,2,CONTA173		!3D 4-NODE SURFACE-TO-SURFACE CONTACT
KEYOPT,2,2,0		!AUGUMENTED LAGRANGET ALGORITHM
KEYOPT,2,10,2		!UPDATE CONTACT STIFFNESS EACH ITERATION
ET,3,TARGE170		!3-D TARGET SEGMENT
KEYOPT,3,2,1		!BOUNDARY CONDITIONS SET BY USER
ET,4,MASS21		    !STRUCTURAL MASS
KEYOPT,4,2,1		!COORDINATE SYSTEM ALIGNED TO NODE         
ET,5,COMBIN14		!SPRING-DAMPER


BLOCK,0,.6,0,.01,0,.2	!CREATE BASE PLATE
TYPE,1			        !SOLID ELEMENTS
ESIZE,0.01		        !ELEMENT SIZE
VMESH,1			        !MESH THE BLOCK
CM,GROUND,ELEM		    !CREATE NAMED COMPONENT


NSEL,S,LOC,Y,.01	!SELECT NODES ON TOP FACE OF BLOCK
TYPE,2			    !CONTACT ELEMENTS
MAT,2			    !MATERIAL MODEL 2
REAL,2			    !REAL CONSTANT SET 2
ESURF			    !CREATE CONTACT ON TOP SURFACE


K,1001,.25,.01,.05	!CREATE 4 KEYPOINTS ON TOP SURFACE
K,1002,.35,.01,.05	!WHERE RIGID BLOCK IS SIMULATED
K,1003,.35,.01,.15
K,1004,.25,.01,.15


A,1001,1002,1003,1004	!CREATE AN AREA FROM ABOVE KEYPOINTS
TYPE,3			        !TARGET ELEMENTS
REAL,2			        !REAL CONSTANT SET 2
MAT,2			        !MATERIAL MODEL 2
RMOD,2,34,1		        !STABILIZING SQUEAL DAMPING FACTOR 
AMESH,7			        !CREATE TARGET ON AREA


N,10000,.3,.06,.1	    !CREATE NODE
TSHAP,PILOT		        !SET TARGET TYPE TO PILOT NODE
E,10000			        !CREATE PILOT TARGET ELEMENT

TYPE,4			!STRUCTURAL MASS
REAL,4			!REAL CONSTANT SET 4
R,4,8,8,8,      !MASS = 8Kg
E,10000			!PLACE MASS ON PILOT NODE


NSEL,S,LOC,Y	!PICK ALL NODES AT BOTTOM OF PLATE
D,ALL,ALL		!FIXED SUPPORT
NSEL,ALL		!PICK ALL NODES

N ,10004,.05,.06,.1	!CREATE OPPOSITE END NODE FOR SPRING ELEMENT
TYPE,5			    !SPRING ELEMENT
REAL,5			    !REAL CONSTANT SET 5
R,5,315			    !SPRING STIFFNESS (N/M)
E,10004,10000		!CREATE SPRING ELEMENT
FINISH

/SOLU			        
ANTYPE,STATIC		!STATIC ANALYSIS
OUTRES,ALL,ALL		!WRITE ALL QUANTITIES TO RESULTS FILE
NLGEOM,ON		    !NONLINEAR GEOMETRY
NROPT,UNSYM 		!UNSYMMETRIC MATRICES
RESCONTROL,DEFINE,LAST	!WRITE A .Xnnn FILE FOR LAST LOAD STEP
F,10000,FY,-1		    !APPLY DOWNWARD VERTICAL FORCE TO NODE ABOUT PILOT
D,10004,ALL		        !CONSTRAIN END NODE OF SPRING ELEMENT
D,10000,UZ		        !CONSTRAIN PILOT NODE IN UZ
D,10000,ROTX		    !CONSTRAIN PILOT NODE IN ROTX
D,10000,ROTY		    !CONSTRAIN PILOT NODE IN ROTY
D,10000,ROTZ		    !CONSTRAIN PILOT NODE IN ROTZ
/OUT,SCRATCH
SOLVE			        !SOLVE FIRST LOAD STEP

			            !USE AN ANGULAR VELOCITY TO SIMULATE SLIDE

CMROT,GROUND,,.1,,-99.75,.01,0.5 !SET AXIS OF ROTATION
SOLVE			!SOLVE SECOND LOAD STEP 
FINI			!EXIT SOLUTION PROCESSOR

/SOLU			                !RE-ENTER SOLUTION	
ANTYPE,STATIC,RESTART,,,PERTURB	!DEFINE A PERTURB ANALYSIS TYPE
PERTURB,MODAL,,CURRENT,PARKEEP	!PERTURB MODAL, KEEPING SUPPORTS
SOLVE,ELFORM		            !SOLVE FOR ELEMENT MATRIX  


MODOPT,DAMP,2,		!2 MODES, DAMP SOLVER
MXPAND,,,,YES    	!EXPAND MODES
SOLVE			    !COMPLETE MODAL SOLUTION
FINISH


/POST1			!ENTER POST-PROCESSOR
FILE,,rstp		!READ PERTURB RESULTS FILE
/OUT,
*GET,DRATIO,MODE,1,DAMP

*STAT,DRATIO

*DIM,LABEL,CHAR,1
*DIM,VALUE,,1,3
LABEL(1,1)='DAMPING RATIO'
*VFILL,VALUE(1,1),DATA,0.000498012
*VFILL,VALUE(1,2),DATA,DRATIO
*VFILL,VALUE(1,3),DATA,ABS(DRATIO/(0.000498012))
*STAT,VALUE
/COM
/OUT,vm274,vrt
/COM,------------------- VM274 RESULTS COMPARISON ---------------------
/COM,
/COM,                 |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
*VWRITE,LABEL(1,1),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,8X,'   ',F10.8,'  ',F14.8,'  ',1F15.3)
/COM,----------------------------------------------------------------
/OUT
FINISH
*LIST,vm274,vrt

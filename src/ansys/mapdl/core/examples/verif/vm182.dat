/COM,ANSYS MEDIA REL. 2023R1 (11/04/2022) REF. VERIF. MANUAL: REL. 2023R1
/VERIFY,VM182
/PREP7   
JPGPRF,500,100,1             ! MACRO TO SET PREFS FOR JPEG PLOTS
/SHOW,JPEG
/TITLE, VM182, TRANSIENT RESPONSE OF A SPRING-MASS SYSTEM
C***               R. K. VIERCK, "VIBRATION ANALYSIS", 2ND EDITION, SEC.5-8
ANTYPE,MODAL          ! MODE-FREQUENCY ANALYSIS
MODOPT,LANB,2,,,      ! PRINT TWO REDUCED MODE SHAPES
ET,1,COMBIN40,,,2     ! UY DOF
R,1,6,,2              ! K1=6 N/M     M1=2 KG
R,2,16,,2             ! K2=16 N/M    M2=2 KG
N,1
N,2,0,1
N,3,0,2
REAL,1
E,1,2
REAL,2
E,2,3
D,3,ALL
OUTPR,,ALL
FINISH
/SOLU
/OUT,vm182_SCRATCH    
SOLVE
/OUT
FINISH
/SOLU    
ANTYPE,TRANS          ! TRANSIENT DYNAMIC ANALYSIS
TRNOPT,MSUP,2         ! MODE SUPERPOSITION, BOTH MODES
DELTIM,0.01           ! INTEGRATION TIME STEP = .01
OUTPR,,NONE
OUTRES,,1
KBC,1                 ! STEP BOUNDARY CONDITIONS
F,1,FY,0              ! FORCE = 0 AT TIME = 0 (INIT. CONDITIONS)
/OUT,vm182_SCRATCH,,,APPEND    
SOLVE
/OUT
TIME,1.8
F,1,FY,50             ! FORCE = 50N FROM TIME = 0 TO 1.8 SEC
/OUT,vm182_SCRATCH,,,APPEND    
SOLVE
/OUT
TIME,2.4
F,1,FY,0              ! FORCE = 0 FROM TIME = 1.8 TO 2.4 SEC
/OUT,vm182_SCRATCH,,,APPEND    
SOLVE
/OUT
FINISH
/POST26
FILE,,rdsp            ! REDUCED DISPLACEMENTS FILE
NSOL,2,1,U,Y,UY1
NSOL,3,2,U,Y,UY2
/GRID,1
/AXLAB,Y,DISP
PLVAR,2,3             ! DISPLAY DISPLACEMENT RESPONSE VS. TIME
PRVAR,2,3
*GET,Y1,VARI,2,RTIME,1.3
*GET,Y2,VARI,3,RTIME,1.3    
*GET,Y3,VARI,2,RTIME,2.4
*GET,Y4,VARI,3,RTIME,2.4
*DIM,LABEL,CHAR,2,2
*DIM,VALUE,,2,3
LABEL(1,1) = 'DISP 1 ','DISP 2 '
LABEL(1,2) = 'm','m'
*VFILL,VALUE(1,1),DATA,14.48,3.99
*VFILL,VALUE(1,2),DATA,Y1,Y2
*VFILL,VALUE(1,3),DATA,ABS(Y1/14.48),ABS(Y2/3.99)
SAVE,TABLE_1
*VFILL,VALUE(1,1),DATA,18.32,6.14
*VFILL,VALUE(1,2),DATA,Y3,Y4
*VFILL,VALUE(1,3),DATA,ABS(Y3/18.32),ABS(Y4/6.14),
SAVE,TABLE_2
RESUME,TABLE_1
/COM
/OUT,vm182,vrt
/COM,------------------- VM182 RESULTS COMPARISON --------------
/COM,
/COM,                  |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
/COM,RESULTS AT T=1.3 S
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.2,'  ',F14.2,'   ',1F15.3)
/NOPR
RESUME,TABLE_2
/GOPR
/COM,
/COM,RESULTS AT T=2.4 S
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.2,'  ',F14.2,'   ',1F15.3)
/COM,-----------------------------------------------------------
/OUT
FINISH
/DELETE,TABLE_1
/DELETE,TABLE_2
FINISH
*LIST,vm182,vrt

/COM,ANSYS MEDIA REL. 2023R1 (11/04/2022) REF. VERIF. MANUAL: REL. 2023R1
/VERIFY,VM56
/PREP7
SMRT,OFF
/TITLE, VM56, HYPERELASTIC THICK CYLINDER UNDER INTERNAL PRESSURE
/COM           REF: ODEN, J.T., "FINITE ELEMENTS OF NONLINEAR CONTINUA"
/COM                MCGRAW-HILL, 1972, PP 325-331
ANTYPE,STATIC
NLGEOM,ON                 ! LARGE DEFLECTION
ET,1,PLANE183, , ,1       ! 2-D AXISYM 8-NODE STRUCTURAL SOLID 
ET,2,PLANE183, , ,1       ! 2-D AXISYM 8-NODE STRUCTURAL SOLID 
NU1  = 0.495
DD1  = 2*(1-2*NU1)/(40+10)
DD2  = 2*(1-2*NU1)/(120+30)
TB,HYPER,1,2,2,MOONEY
TBTEMP,20                 ! MOONEY COEFFICIENTS AT TEMP = 20
TBDATA,1,40,10,DD1
TBTEMP,40
TBDATA,1,120,30,DD2
K,1,7                     ! DEFINE KEYPOINTS
K,3,7,2.5
K,2,18.625
K,4,18.625,2.5
A,1,2,4,3                 ! DEFINE AREA
ESIZE,2.5
AMESH,1                   ! CREATE NODES AND ELEMENTS
TYPE,2
EMODIF,1                  ! PRINT ONLY INNERMOST ELEMENT RESULTS
BFUNIF,TEMP,30            ! UNIFORM TEMPERATURES
D,ALL,UY,0                ! FIX ALL NODES AXIALLY
FINISH
/SOLU    
/TITLE, PRESSURE = 90 PSI
NEQIT,20                  ! MAXIMUM 20 EQUILIBRIUM ITERATIONS
NSEL,S,LOC,X,7.0,7.0
SF,ALL,PRES,90            ! APPLY INTERNAL PRESSURE OF 90 PSI
NSEL,ALL
/OUT,SCRATCH
SOLVE
/TITLE, PRESSURE = 150 PSI
NSEL,S,LOC,X,7.0
SF,ALL,PRES,150           ! APPLY INTERNAL PRESSURE OF 150 PSI
NSEL,ALL
SOLVE
FINISH
/POST1                    ! POSTPROCESS
/OUT,
SET,2
ETABLE,SX1,S,X  
AVPRIN,0,0, 
ELM=0
NSEL,S,LOC,X,6.5,8.5
ESLN
ELM=ELNEXT(ELM)
*GET,SIGX,ELEM,ELM,ETABLE,SX1
ELM=0
ESEL,ALL
NSEL,ALL
*GET,DEF,NODE,1,U,X
*DIM,LABEL,CHAR,2,2
*DIM,VALUE,,2,3
LABEL(1,1) = 'UR(INNER'
LABEL(1,2) = ' RAD),in'
LABEL(2,1) = 'SIGX: EL'
LABEL(2,2) = ' 1 CENT  '
VALUE(1,1) = 7.180
VALUE(1,2) = DEF
VALUE(1,3) = ABS(DEF/7.180)
VALUE(2,1) = -122.0
VALUE(2,2) = SIGX
VALUE(2,3) = ABS(SIGX/(-122.0))
SAVE,TABLE_1
FINISH
/CLEAR,NOSTART ! CLEAR THE PREVIOUS DATABASE
/PREP7
SMRT,OFF
/TITLE, VM56: HYPERELASTIC THICK CYLINDER UNDER INTERNAL PRESSURE
ANTYPE,STATIC
NLGEOM,ON
ET,1,SOLID185              ! 3-D 8-NODE STRUCTURAL SOLID
KEYOPT,1,2,1		       ! REDUCED  INTEGRATION
ET,2,SOLID185              ! 3-D 8-NODE STRUCTURAL SOLID
KEYOPT,2,2,1		       ! REDUCED INTEGRATION
NU1 = 0.495
DD  = 2*(1-2*NU1)/(80+20)
TB,HYPER,1,1,2,MOONEY
TBDATA,1,80,20,DD
CSYS,1
K,1,7,3.16                ! DEFINE KEYPOINTS
K,2,7,3.16,.775
K,3,7,-3.16,.775
K,4,7,-3.16
KGEN,2,ALL,,,11.625
V,1,2,3,4,5,6,7,8         ! DEFINE VOLUME
LSEL,S,LINE,,5
LSEL,A,LINE,,7
LSEL,A,LINE,,11
LSEL,A,LINE,,9
LESIZE,ALL,,,5
LSEL,ALL
ESIZE,,1
VMESH,ALL                 ! CREATE NODES AND ELEMENTS
TYPE,2
EMODIF,1
NROTAT,ALL                ! ROTATE ALL NODES INTO CYLINDRICAL COORDINATES
D,ALL,UZ,0.0              ! CONSTRAIN ALL NODES AXIALLY
D,ALL,UY,0.0              ! CONSTRAIN ALL NODES TANGENTIALLY
FINISH
/SOLU    
/TITLE, PRESSURE = 90 PSI
NEQIT,30                  ! MAXIMUM 30 EQUILIBRIUM ITERATIONS
NSEL,S,LOC,X,7
SF,ALL,PRES,90            ! INTERNAL PRESSURE OF 90 PSI
NSEL,ALL
/OUT,SCRATCH
SOLVE
/TITLE, PRESSURE = 150 PSI
NSEL,S,LOC,X,7
SF,ALL,PRES,150           ! INTERNAL PRESSURE OF 150 PSI
NSEL,ALL
SOLVE
FINISH
/POST1                    ! POSTPROCESS
/OUT,
SET,2
ETABLE,SX1,S,X  
AVPRIN,0,0, 
ELM=0
NSEL,S,LOC,X,6.5,8.5
ESLN
ELM=ELNEXT(ELM)
*GET,SIGX,ELEM,ELM,ETABLE,SX1
ELM=0
ESEL,ALL
NSEL,ALL
*GET,DEF,NODE,1,U,X
*DIM,LABEL,CHAR,2,2
*DIM,VALUE,,2,3
LABEL(1,1) = 'UR(INNER'
LABEL(1,2) = ' RAD),in'
LABEL(2,1) = 'SIGX: EL'
LABEL(2,2) = ' 1 CENT  '
VALUE(1,1) = 7.180
VALUE(1,2) = DEF
VALUE(1,3) = ABS(DEF/7.180)
VALUE(2,1) = -122.0
VALUE(2,2) = SIGX
VALUE(2,3) = ABS(SIGX/(-122.0))
SAVE,TABLE_2
FINISH
/CLEAR,NOSTART ! CLEAR THE PREVIOUS DATABASE
/PREP7
SMRT,OFF
/TITLE, VM56: HYPERELASTIC THICK CYLINDER UNDER INTERNAL PRESSURE
ANTYPE,STATIC
NLGEOM,ON
ET,1,SOLID186             ! 3-D 20-NODE STRUCTURAL SOLID
KEYOPT,1,2,0		      ! REDUCED INTEGRATION
ET,2,SOLID186             ! 3-D 20-NODE STRUCTURAL SOLID
KEYOPT,2,2,0		      ! REDUCED INTEGRATION
NU1 = 0.495
DD  = 2*(1-2*NU1)/(80+20)
TB,HYPER,1,1,2,MOONEY
TBDATA,1,80,20,DD
CSYS,1
K,1,7,3.16                ! DEFINE KEYPOINTS
K,2,7,3.16,.775
K,3,7,-3.16,.775
K,4,7,-3.16
KGEN,2,ALL,,,11.625
V,1,2,3,4,5,6,7,8         ! DEFINE VOLUME
LSEL,S,LINE,,5
LSEL,A,LINE,,7
LSEL,A,LINE,,11
LSEL,A,LINE,,9
LESIZE,ALL,,,5
LSEL,ALL
ESIZE,,1
VMESH,ALL                 ! CREATE NODES AND ELEMENTS
TYPE,2
EMODIF,1
NROTAT,ALL                ! ROTATE ALL NODES INTO CYLINDRICAL COORDINATES
D,ALL,UZ,0.0              ! CONSTRAIN ALL NODES AXIALLY
D,ALL,UY,0.0              ! CONSTRAIN ALL NODES TANGENTIALLY
FINISH
/SOLU   
/TITLE, PRESSURE = 90 PSI
NEQIT,30                  ! MAXIMUM 30 EQUILIBRIUM ITERATIONS
NSEL,S,LOC,X,7
SF,ALL,PRES,90            ! INTERNAL PRESSURE OF 90 PSI
NSEL,ALL
/OUT,SCRATCH
SOLVE
/TITLE, PRESSURE = 150 PSI
NSEL,S,LOC,X,7
SF,ALL,PRES,150           ! INTERNAL PRESSURE OF 150 PSI
NSEL,ALL
SOLVE
FINISH
/POST1                    ! POSTPROCESS
/OUT,
SET,2
*GET,DEF,NODE,1,U,X
*DIM,LABEL,CHAR,1,2
*DIM,VALUE,,1,3
LABEL(1,1) = 'UR(INNER'
LABEL(1,2) = ' RAD),in'
VALUE(1,1) = 7.180
VALUE(1,2) = DEF
VALUE(1,3) = ABS(DEF/7.180)
SAVE,TABLE_3
RESUME,TABLE_1
/COM
/OUT,vm56,vrt
/COM,------------------- VM56 RESULTS COMPARISON --------------
/COM,
/COM,                 |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
/COM,RESULTS USING PLANE183:
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.3,'  ',F14.3,'   ',1F15.3)
/NOPR
RESUME,TABLE_2
/GOPR
/COM,
/COM,RESULTS USING SOLID185:
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.3,'  ',F14.3,'   ',1F15.3)
/NOPR
RESUME,TABLE_3
/GOPR
/COM,
/COM,RESULTS USING SOLID186:
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.3,'  ',F14.3,'   ',1F15.3)
/COM,-----------------------------------------------------------
/COM,
/OUT
FINISH
*LIST,vm56,vrt

/DELETE,TABLE_1
/DELETE,TABLE_2
/DELETE,TABLE_3
FINISH

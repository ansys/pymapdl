/COM,ANSYS MEDIA REL. 2023R1 (11/04/2022) REF. VERIF. MANUAL: REL. 2023R1
/VERIFY,vm222
/TITLE,vm222, Warping Torsion Bar
JPGPRF,500,100,1             ! MACRO TO SET PREFS FOR JPEG PLOTS
/SHOW,JPEG
!
/PREP7
!
!BUILD MODEL USING BEAM188 ELEMENTS
!
K,,
K,,1000
K,,500,100
L,1,2
ET,1,BEAM188
KEYOPT,1,1,1
SECT,1,BEAM,I
SECD,40,40,80,2,2,2
SECPLOT,1
!
MP,EX, 1, 217396.3331684	!SET MATERIAL PROPS
MP,NUXY,1,  0.335579823862
NEL=50
LATT,1,,,,3			!SET DEFAULT ATTRIBUTES
LESIZE,1,,,NEL
LMESH,ALL			!MESH MODEL
DK,1,ALL			!PIN MODEL AT KEYPOINT 1
SAVE,PREPDATA,DB		!SAVE DB FOR FUTURE PARTS OF TEST
DK,2,ALL			!PIN MODEL AT KEYPOINT 2
!
MLOAD1=(1000/NEL)/2
MLOAD2=MLOAD1*2
/ESHAPE,1	!TURN ESHAPE ON TO SHOW 3-D IMAGE OF MODEL
FINISH
/SOLU
OUTRES,ALL,ALL
F,ALL,MX,MLOAD2
FDEL,1,ALL
FDEL,2,ALL
F,1,MX,MLOAD1
F,2,MX,MLOAD1
/OUT,SCRATCH
SOLVE
FINISH
/POST1
/OUT,
PRNSOL,DOF
PRRSOL
NSORT,ROT,X 
*GET,ROTX1,SORT, ,MAX		!GET VERIFIED RESULT FOR PART 1
PRES,SMISC,27
ETABLE,ROTX,ROT,X
/ESHAPE,1
PLDISP				!PLOT DISPLACED SHAPE
PLETAB,ROTX,NOAV
ETABLE,BIMOMENT,SMISC,27
PLETAB,BIMOMENT,NOAV
PARSAV				!SAVE PARAMETERS FOR NEXT PART OF TEST
FINISH
/CLEAR,NOSTART
PARRES,CHANGE			!RESTORE SAVED PARAMETERS
/PREP7
!
! REBUILD AND REMESH MODEL USING BEAM189 ELEMENTS
!
K,,
K,,1000
K,,500,100
L,1,2
ET,1,BEAM189
KEYOPT,1,1,1
SECT,1,BEAM,I
SECD,40,40,80,2,2,2

MP,EX, 1, 217396.3331684
MP,NUXY,1,  0.335579823862
NEL=50
LATT,1,,,,3
LESIZE,1,,,NEL
LMESH,ALL
!
! APPLY BOUNDRY CONDITIONS AND SET UP NEW PARAMETERS
!
DK,1,ALL
DK,2,ALL
MLOAD=(1000/NEL)/6.0
MLOAD1=MLOAD
MLOAD2=MLOAD*2
MLOAD4=MLOAD*4
FINISH
/SOLU
OUTRES,ALL,ALL
!
! SET UP LOOP TO APPLY LOADS
!
*DO,I,1,NEL,1
 J=NELEM(I,1)
 K=NELEM(I,2)
 L=NELEM(I,3)
 F,J,MX,MLOAD2
 F,K,MX,MLOAD2
 F,L,MX,MLOAD4
*ENDDO
F,1,MX,MLOAD1
F,2,MX,MLOAD1 
FLIST
/OUT,SCRATCH
SOLVE
FINISH
/POST1
/OUT,
PRNSOL,DOF
NSORT,ROT,X 
*GET,ROTX2,SORT, ,MAX		!GET VERIFIED RESULTS FOR PART 2
PRRSOL,MX
PRRSOL,BMOM
PRES,SMISC,4
PRES,SMISC,17
PRES,SMISC,27
T1=ROTX1
T2=ROTX2
!
!SET UP AND FILL VM RATIO TABLE
!
RAT_1 = ABS(T1/0.3292617E-3)
RAT_2 = ABS(T2/0.3292617E-3)
*DIM,LABEL,CHAR,2
*DIM,VALUE,,2,2
*DIM,RESULTS,,2,1
LABEL(1) = 'BEAM188','BEAM189'
*VFILL,VALUE(1,1),DATA,0.3292617E-3,0.3292617E-3
*VFILL,RESULTS(1,1),DATA,T1,T2
*VFILL,VALUE(1,2),DATA,RAT_1,RAT_2
/OUT,vm222,vrt
/COM
/COM,------------------- VM222  RESULTS COMPARISON ---------------------
/COM,   MX TWIST
/COM,   IN X-DIR |   TARGET    |    Mechanical APDL    |    RATIO
/COM,
*VWRITE,LABEL(1),VALUE(1,1),RESULTS(1,1),VALUE(1,2)
(1X,A10,'     ',E10.4,'      ',E14.4,'       ',1F15.3)
/COM,-------------------------------------------------------------------
/out
*LIST,vm222,vrt
FINISH

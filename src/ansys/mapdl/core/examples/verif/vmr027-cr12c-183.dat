! Copyright (C) 2024 ANSYS, Inc. and/or its affiliates.
! SPDX-License-Identifier: MIT
! 
!
! Permission is hereby granted, free of charge, to any person obtaining a copy
! of this software and associated documentation files (the "Software"), to deal
! in the Software without restriction, including without limitation the rights
! to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
! copies of the Software, and to permit persons to whom the Software is
! furnished to do so, subject to the following conditions:
!
! The above copyright notice and this permission notice shall be included in all
! copies or substantial portions of the Software.
!
! THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
! IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
! FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
! AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
! LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
! OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
! SOFTWARE.

/COM,ANSYS MEDIA REL. 2023R2 (05/12/2023) REF. VERIF. MANUAL: REL. 2023R2
/VERIFY,vmr027-cr12c-183
/TITLE,vmr027-cr12c-183,2D PLANE STRESS-STEPPED LOAD PRIMARY-SECONDARY CREEP
/COM, REFERENCE: TEST 12(C) FROM NAFEMS REPORT R0027.
/SHOW,JPEG
JPGPRF,500,100,1

/PREP7
C*** PARAMETRIC INPUT FOR CREEP CONSTANT !(PRIMARY AND SECONDARY CREEP)***
*SET,C1,0.5E-14
*SET,C2,5
*SET,C3,-0.5
*SET,C4,0
*SET,C5,1E-16
*SET,C6,5
*SET,C7,0
C*** TIME PARAMETER
*SET,HOUR,200
C*** ELASTIC CONSTANT
MP,EX,1,200E3
MP,NUXY,1,0.3
TUNIF,HOT
TOFF,1E-10
TB,CREEP,1,,,11
TBDATA,1,C1,C2,C3,C4,C5,C6
TBDATA,7,C7
SAVE
N,1,0,0,0
N,2,100,0
N,3,100,100
N,4,0,100
N,5,50,0
N,6,100,50
N,7,50,100
N,8,0,50
ET,1,183
KEYOPT,1,3,0
E,1,2,3,4,5,6,7,8
NSEL,S,LOC,X,0
D,ALL,UX,
NSEL,ALL
NSEL,S,LOC,Y,50
NSEL,R,LOC,X,0
D,ALL,UY,
NSEL,ALL

/SOLU
NSEL,S,LOC,X,100
SF,ALL,PRES,-100
NSEL,ALL
RATE, ON
TIME, 1.0E-8
/OUT,SCRATCH
SOLVE
/OUT
RATE, ON
TIME,10
/OUT,SCRATCH
SOLVE
/OUT
RATE, ON
TIME,100
/OUT,SCRATCH
SOLVE
/OUT
RATE, ON
TIME,1000
/OUT,SCRATCH
SOLVE
/OUT
RATE,ON
TIME,10000
/OUT,SCRATCH
SOLVE
/OUT

/SOLU
NSEL,S,LOC,X,100
SF,ALL,PRES,-110
NSEL,ALL
RATE,ON,ON
DELT,1000,999.999,1000.0001
AUTOS,OFF
OUTRES,ALL,ALL
TIME,11000
/OUT,SCRATCH
SOLVE
/OUT
RATE,ON,ON
DELT,1000
AUTOS,OFF
OUTRES,ALL,ALL
TIME,12000
/OUT,SCRATCH
SOLVE
/OUT
RATE,ON,ON
DELT,1000
AUTOS,OFF
OUTRES,ALL,ALL
TIME,15000
/OUT,SCRATCH
SOLVE
/OUT
RATE,ON,ON
DELT,1000
AUTOS,OFF
OUTRES,ALL,ALL
TIME,20000
/OUT,SCRATCH
SOLVE
/OUT

/POST26
ESOL,2,1,,EPCR,X
PRVAR,2
/AXLAB,Y,CREEP STRAIN
PLVAR,2

/POST1
SET,,,,,,,1
*GET,SH1,NODE,3,EPCR,X
R1=1.0
SET,,,,,,,2
*GET,SH2,NODE,3,EPCR,X
R2=SH2/0.000326
SET,,,,,,,3
*GET,SH3,NODE,3,EPCR,X
R3=SH3/0.00110
SET,,,,,,,4
*GET,SH4,NODE,3,EPCR,X
R4=SH4/0.00416
SET,,,,,,,5
*GET,SH5,NODE,3,EPCR,X
R5=SH5/0.02
SET,,,,,,,6
*GET,SH6,NODE,3,EPCR,X
R6=SH6/0.02240
SET,,,,,,,7
*GET,SH7,NODE,3,EPCR,X
R7=SH7/0.02476
SET,,,,,,,10
*GET,SH8,NODE,3,EPCR,X
R8=SH8/0.03167
SET,,,,,,,15
*GET,SH9,NODE,3,EPCR,X
R9=SH9/0.04278
*DIM,VALUE,,9,3
*DIM,LABEL,CHAR,10
LABEL(1) = '0','10', '100', '1000', '10000', '11000','12000','15000','20000'
*VFILL,VALUE(1,1),DATA,0.0,0.000326,0.00110,0.00416,0.02,0.02240,0.02476,0.03167,0.04278
*VFILL,VALUE(1,2),DATA,SH1,SH2,SH3,SH4,SH5,SH6,SH7,SH8,SH9
*VFILL,VALUE(1,3),DATA,R1,R2,R3,R4,R5,R6,R7,R8,R9
/COM
/COM
/COM,--------- vmr027-cr12c-183 RESULTS COMPARISON-----------
/COM,
/COM,|   TIME   | TARGET |   Mechanical APDL  |   RATIO
/COM,
*VWRITE,LABEL(1), VALUE(1,1), VALUE(1,2), VALUE(1,3)
(1X,A8,'   ',F8.6,'   ',F13.6,'   ',1F13.3)
FINISH

/POST26
*DIM,LABEL1,CHAR,2
*DIM,VALUE1,,2,3
LABEL1(1) = ' ECR11X ',' ECR11Y '
*VFILL,VALUE1(1,1),DATA,SH9
*VFILL,VALUE1(1,2),DATA,R9
*DIM,LABEL2,CHAR,3
LABEL2(1) = 'vmr027-','cr12c-18','3'

/OUT,vmr027-cr12c-183,vrt
/COM
/COM,------------------- vmr027-cr12c RESULTS COMPARISON ---------------------
/COM,
/COM,            |   Mechanical APDL   |   RATIO  |         INPUT         |
/COM,
/COM, PLANE183
*VWRITE,LABEL1(1),VALUE1(1,1),VALUE1(1,2),LABEL2(1),LABEL2(2),LABEL2(3)
(1X,A8,'      ',F13.3,'      ',F9.4,'     ',A7,A8,A8)
/COM,
/COM,----------------------------------------------------------------
/OUT

FINISH
*LIST,vmr027-cr12c-183,vrt

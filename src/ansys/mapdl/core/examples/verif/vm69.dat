! Copyright (C) 2024 ANSYS, Inc. and/or its affiliates.
! SPDX-License-Identifier: MIT
! 
!
! Permission is hereby granted, free of charge, to any person obtaining a copy
! of this software and associated documentation files (the "Software"), to deal
! in the Software without restriction, including without limitation the rights
! to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
! copies of the Software, and to permit persons to whom the Software is
! furnished to do so, subject to the following conditions:
!
! The above copyright notice and this permission notice shall be included in all
! copies or substantial portions of the Software.
!
! THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
! IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
! FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
! AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
! LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
! OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
! SOFTWARE.

/COM,ANSYS MEDIA REL. 2023R2 (05/12/2023) REF. VERIF. MANUAL: REL. 2023R2
/VERIFY,VM69
/PREP7
/TITLE, VM69, SEISMIC RESPONSE
C***VIBRATION THEORY AND APPLICATIONS, THOMSON, 2ND PRINTING, PAGE 78, EX 3.11-1
ET,1,COMBIN40,,,,,,2
R,1,9.8696,,1             ! SPRING CONSTANT = 9.8696, MASS = 1
N,1
N,2
E,1,2
!M,2,UX
FINISH
/SOLU    
ANTYPE,MODAL              ! MODE-FREQUENCY SEISMIC RESPONSE
MXPAND,1,,,YES            ! EXPAND MODES;   ELEM STRESS  
                          ! ONLY ONE MODE WILL BE USED IN SPECTRUM ANALYSIS
MODOPT,LANB,1             ! USE BLOCK LANCZOS EIGENVALUE EXTRACTION METHOD
D,1,UX
OUTPR,NSOL,ALL
OUTRES,ALL,ALL    
SOLVE
*GET,FREQ,MODE,1,FREQ
FINISH
/SOLU    
ANTYPE,SPECTR             ! SPECTRUM ANALYSIS
SPOPT,SPRS
SED,1                     ! GLOBAL X-AXIS AS SPECTRUM DIRECTION
SVTYP,3                   ! SEISMIC DISPLACEMENT SPECTRUM
FREQ,.4,.5,.6             ! FREQUENCY POINTS FOR SV V/S FREQ TABLE
SV,,1.01270849,1.02,1.02905569 ! SPECT. VALUES ASSOCIATED WITH FREQ. POINTS
OUTPR,NSOL,ALL
OUTRES,ALL,ALL    
SOLVE
*GET,MC1,MODE,1,MCOEF     ! OBTAIN MODE COEFF. FOR THIS SPECTRUM & MODE 1
FINISH
/POST1
SET,1,1,MC1               ! MULTIPY DATABASE FOR MODE1 BY MODE COEFFICIENT
PRNSOL,U,X
*GET,AMP,NODE,2,U,X
*DIM,LABEL,CHAR,2,2
*DIM,VALUE,,2,3
LABEL(1,1) = '     f, ','     AE,'
LABEL(1,2) = '      Hz','      in'
*VFILL,VALUE(1,1),DATA,.5,1.02
*VFILL,VALUE(1,2),DATA,FREQ,AMP 
*VFILL,VALUE(1,3),DATA,ABS(FREQ/.5),ABS(AMP/1.02)
/COM
/OUT,vm69,vrt
/COM,------------------- VM69 RESULTS COMPARISON --------------
/COM,
/COM,                 |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.4,'  ',F14.4,'   ',1F15.3)
/COM,----------------------------------------------------------
/OUT
FINISH
*LIST,vm69,vrt

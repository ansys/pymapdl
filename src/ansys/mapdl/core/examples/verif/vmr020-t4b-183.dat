! Copyright (C) 2024 ANSYS, Inc. and/or its affiliates.
! SPDX-License-Identifier: MIT
! 
!
! Permission is hereby granted, free of charge, to any person obtaining a copy
! of this software and associated documentation files (the "Software"), to deal
! in the Software without restriction, including without limitation the rights
! to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
! copies of the Software, and to permit persons to whom the Software is
! furnished to do so, subject to the following conditions:
!
! The above copyright notice and this permission notice shall be included in all
! copies or substantial portions of the Software.
!
! THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
! IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
! FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
! AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
! LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
! OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
! SOFTWARE.

/COM,ANSYS MEDIA REL. 2023R2 (05/12/2023) REF. VERIF. MANUAL: REL. 2023R2
/verify,vmr020-t4b-183
/title,vmr020-t4b-183,Crack at a hole in a plate (Uniaxial tension)
/com,
/com, Problem is taken from NAFEMS Publication
/com, "2D Test Cases in Linear Elastic Fracture Mechanics"
/com, Test case 4.2
/com,
/com, Reference: Rooke D P and Cartwright D J : Compendium
/com, of stress intensity factors, HMSO, London, England (1976)
/com,
/com, **********************************************************
/com, Stress Intensity Factor Calculation using Interaction
/com, Integral Approach
/prep7

e=207000	    !youngs modulus
nu=0.3		    !poissons ratio
sig=100		    !surface load
a=3		        !crack length
pi=3.141593

et,1,plane183,,,2   	!plane183 elements
			
mp,ex,1,e
mp,nuxy,1,nu

k,1,3,0
k,2,10,0
k,3,10,20
k,4,0,20
k,5,0,2.5
k,6,2.5,0
k,7,10,5
k,8,0,5


l,1,2
l,2,7
l,7,3
l,3,4
l,4,8
l,8,5
csys,2
l,5,6
csys,0
l,6,1
l,7,8

al,1,2,9,6,7,8
al,3,4,5,9

esize,1
amesh,2

kscon,1,0.15,1,4,0.75	!crack tip elements
esize,1
amesh,1
finish

/solu
autots,on
nsubst,10
outres,all,all
nsel,s,loc,x,3,10
nsel,r,loc,y,0
d,all,uy,0
nsel,all
nsel,s,loc,y,2.5,20
nsel,r,loc,x,0
d,all,ux,0
nsel,all
sfl,4,press,-sig

nsel,s,loc,x,3
nsel,r,loc,y,0
cm,crack1,node		!define crack tip node component

cint,new,1
cint,type,sifs      ! Calculate stress intensity factor
cint,name,crack1	!crack ID	
cint,ncon,4		    !number of contours
cint,symm,on		!symmetry on
cint,norm,0,2
cint,list
allsel,all
solve
finish

/out,scratch
/post1
prcint,1
*get,k1_1,cint,1,ctip,node(3,0,0),,1,dtype,k1
*get,k1_2,cint,1,ctip,node(3,0,0),,2,dtype,k1
*get,k1_3,cint,1,ctip,node(3,0,0),,3,dtype,k1

con1 = ((pi*a)**0.5)
k1=(k1_1+k1_2+k1_3)/3
k0=(sig*con1)
norm_sif=k1/k0
/out,
*stat,norm_sif

*DIM,LABEL,CHAR,1,5
*DIM,VALUE,,1,3
LABEL(1,1) = 'KI'
*VFILL,VALUE(1,1),DATA,1.050
*VFILL,VALUE(1,2),DATA,norm_sif
*VFILL,VALUE(1,3),DATA,ABS(norm_sif/1.050 ) 
SAVE,TABLE_1
FINI
/CLEAR,NOSTART

/com,******************************************************************************
/com,*** Using J-Integral Approach

/prep7

e=207000	        !youngs modulus
nu=0.3		        !poissons ratio
sig=100		        !surface load
a=3		            !crack length
pi=3.141593

et,1,plane183,,,2   !plane183 elements
			
mp,ex,1,e
mp,nuxy,1,nu

k,1,3,0
k,2,10,0
k,3,10,20
k,4,0,20
k,5,0,2.5
k,6,2.5,0
k,7,10,5
k,8,0,5


l,1,2
l,2,7
l,7,3
l,3,4
l,4,8
l,8,5
csys,2
l,5,6
csys,0
l,6,1
l,7,8

al,1,2,9,6,7,8
al,3,4,5,9

esize,1
amesh,2

kscon,1,0.15,1,4,0.75	!crack tip elements
esize,1
amesh,1
finish

/solu
autots,on
nsubst,10
outres,all,all
nsel,s,loc,x,3,10
nsel,r,loc,y,0
d,all,uy,0
nsel,all
nsel,s,loc,y,2.5,20
nsel,r,loc,x,0
d,all,ux,0
nsel,all
sfl,4,press,-sig

nsel,s,loc,x,3
nsel,r,loc,y,0
cm,crack1,node		!define crack tip node components

cint,new,1
cint,name,crack1	!crack ID
cint,ncon,4		    !number of contours
cint,symm,on		!symmetry on
cint,norm,0,2
cint,list
allsel,all
solve
finish

/out,scratch
/post1
prcint,1
*get,j1,cint,1,ctip,node(3,0,0),,1,,,
*get,j2,cint,1,ctip,node(3,0,0),,2,,,
*get,j3,cint,1,ctip,node(3,0,0),,3,,,
*get,j4,cint,1,ctip,node(3,0,0),,4,,,
j_avg=(abs(j1)+abs(j2)+abs(j3)+abs(j4))/4
*stat,j_avg
con1 = ((pi*a)**0.5)
con2 = E/(1-(nu*nu))
k1 = ((con2*j_avg)**0.5)
k0=(sig*con1)
norm_sif=k1/k0
/out,
*stat,norm_sif

*DIM,LABEL,CHAR,1,5
*DIM,VALUE,,1,3
LABEL(1,1) = 'KI'
*VFILL,VALUE(1,1),DATA,1.050
*VFILL,VALUE(1,2),DATA,norm_sif
*VFILL,VALUE(1,3),DATA,ABS(norm_sif/1.050 ) 
SAVE,TABLE_2


/NOPR
/COM
/OUT,vmr020-t4b-183,vrt
/COM,------------------- vmr020-t4b-183 RESULTS COMPARISON ---------------------
/COM,
/COM,             |   TARGET   |   MECHANICAL APDL   |   RATIO
/COM,
RESUME,TABLE_1
/COM,USING STRESS INTENSITY FACTOR CALCULATION 
*VWRITE,LABEL(1,1),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,'   ',F10.3,'  ',F12.3,'   ',1F16.3)
/COM,
/COM,
/COM,
RESUME,TABLE_2
/COM,USING J-INTEGRAL APPROACH
/COM,
*VWRITE,LABEL(1,1),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,'   ',F10.3,'  ',F12.3,'   ',1F16.3)
/COM,
/COM,
/COM,
/COM,
/COM,-----------------------------------------------------------------

/OUT
FINISH
*list,vmr020-t4b-183,vrt 


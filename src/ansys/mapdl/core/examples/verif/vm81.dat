/COM,ANSYS MEDIA REL. 2023R1 (11/04/2022) REF. VERIF. MANUAL: REL. 2023R1
/VERIFY,VM81
/PREP7
/TITLE, VM81, TRANSIENT RESPONSE OF A DROP CONTAINER (NONLINEAR)
C***        VIBRATION THEORY AND APPLICATIONS, THOMSON, 2ND. PRINTING, PAGE 110,
C***        EX. 4.6-1, USING NON-LINEAR TRANSIENT DYNAMIC ANALYSIS
ET,1,COMBIN40,,,2,,,2    ! SPRING, MASS, GAP COMBINATION ELEMENT
R,1,1973.92,,.5,1        ! SPRING STIFFNESS, MASS, AND GAP
N,1                      ! DEFINE NODES AND ELEMENT
N,2
E,2,1
FINISH
/SOLU    
ANTYPE,TRANS             ! NONLINEAR TRANSIENT DYNAMIC ANALYSIS
OUTRES,ALL,ALL
TIME,1E-6
D,2,UY
ACEL,,386                ! BOUNDARY CONDITIONS AND LOADING
KBC,1                    ! STEPPED BOUNDARY CONDITIONS
CNVTOL,U,-1
CNVTOL,F,1,0.001         ! FORCE CONVERGENCE CRITERIA
NSUBS,1,1,1
AUTOTS,OFF
TINTP,,,,0.5,0.5,0.2
SOLVE
NSUBST,110,110,1
TIME,.11
SOLVE
FINISH
/POST26                  ! TIME-HISTORY POSTPROCESSOR
NSOL,2,1,U,Y
NSOL,3,1,VEL,Y           ! VELOCITY
PRVAR,2,3
*GET,Y1,VARI,2,RTIME,.072
*GET,V1,VARI,3,RTIME,.072
*GET,Y2,VARI,2,RTIME,.1
*GET,Y3,VARI,2,RTIME,.101
*DIM,LABEL1,CHAR,3,1
*DIM,VALUE1,,3,3
LABEL1(1,1) = 'TIME sec','Y, in   ','V,in/sec'
*VFILL,VALUE1(1,1),DATA,.07198,-1.00,-27.79
*VFILL,VALUE1(1,2),DATA,.072,Y1,V1
*VFILL,VALUE1(1,3),DATA,ABS(.072/.07198),ABS(Y1/1),ABS(V1/27.79)
*DIM,LABEL2,CHAR,2,2
*DIM,VALUE2,,2,3
LABEL2(1,1) = 't=.1 sec','t=1.01 s'
LABEL2(1,2) = ' Y, in  ','ec Y, in'
*VFILL,VALUE2(1,1),DATA,-1.5505,-1.5502
*VFILL,VALUE2(1,2),DATA,Y2,Y3
*VFILL,VALUE2(1,3),DATA,ABS(Y2/1.5505),ABS(Y3/1.5502)
SAVE,TABLE1
FINISH
/CLEAR, NOSTART ! CLEAR THE DATABASE
/PREP7
/TITLE, VM81, TRANSIENT RESPONSE OF A DROP CONTAINER (QUASI-LINEAR)
ET,1,MASS21,,,4          ! TWO-DIMENSIONAL MASS ELEMENT
R,1,.5                   ! MASS
N,1                      ! DEFINE NODE AND ELEMENT
E,1
D,1,UX
GP,2,1,FY,1973.92,1      ! GAP CONDITION
FINISH

/SOLU
ANTYPE,MODAL
MODOPT,LANB,1           ! PERFORM MODAL SOLVE USING LANB EIGENSOLVER
MXPAND,1
ACEL,,386               ! CREATING LOAD VECTOR
SOLVE
FINISH

/SOLU    
ANTYPE,TRANS             ! TRANSIENT DYNAMIC ANALYSIS
TRNOPT,MSUP,1,,,,,YES    ! MODE SUPERPOSITION TRANSIENT ANALYSIS, VAOUT=YES
DELTIM,1E-3
ACEL,0,0,0
LVSCALE,0
KBC,1
OUTRES,ALL,ALL
SOLVE

TIME,.110
KBC,1                    ! STEPPED BOUNDARY CONDITIONS
LVSCALE,1.0              ! SCALING THE LOAD VECTOR 
SOLVE
FINISH

/POST26                 ! TIME-HISTORY POSTPROCESSOR
FILE,,rdsp              ! REDUCED DISPLACEMENTS FILE
NSOL,2,1,U,Y
NSOL,3,1,VEL,Y          ! CALCULATE VELOCITY
PRVAR,2,3
*GET,Y1,VARI,2,RTIME,.072
*GET,V1,VARI,3,RTIME,.072
*GET,Y2,VARI,2,RTIME,.1
*GET,Y3,VARI,2,RTIME,.101
*DIM,LABEL1,CHAR,3,1
*DIM,VALUE1,,3,3
LABEL1(1,1) = 'TIME sec','Y, in   ','V,in/sec'
*VFILL,VALUE1(1,1),DATA,.07198,-1.00,-27.79
*VFILL,VALUE1(1,2),DATA,.072,Y1,V1
*VFILL,VALUE1(1,3),DATA,ABS(.072/.07198),ABS(Y1/1),ABS(V1/27.79)
*DIM,LABEL2,CHAR,2,2
*DIM,VALUE2,,2,3
LABEL2(1,1) = 't=.1 sec','t=1.01 s'
LABEL2(1,2) = ' Y, in  ','ec Y, in'
*VFILL,VALUE2(1,1),DATA,-1.5505,-1.5502
*VFILL,VALUE2(1,2),DATA,Y2,Y3
*VFILL,VALUE2(1,3),DATA,ABS(Y2/1.5505),ABS(Y3/1.5502)
SAVE,TABLE2
RESUME,TABLE1
/COM
/OUT,vm81,vrt
/COM,------------------- VM81 RESULTS COMPARISON --------------
/COM,
/COM,FULL DYNAMIC     |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
/COM,AT IMAPACT
*VWRITE,LABEL1(1,1),VALUE1(1,1),VALUE1(1,2),VALUE1(1,3)
(1X,A8,'   ',F18.4,'  ',F14.4,'   ',1F15.3)
/COM,
/COM,AT ZERO VELOCITY
*VWRITE,LABEL2(1,1),LABEL2(1,2),VALUE2(1,1),VALUE2(1,2),VALUE2(1,3)
(1X,A8,A8,'   ',F10.4,'  ',F14.4,'   ',1F15.3)
/NOPR
RESUME,TABLE2
/GOPR
/COM,
/COM,MODE SUPERPOSITION|   TARGET   |   Mechanical APDL   |   RATIO
/COM,
/COM,AT IMAPACT
*VWRITE,LABEL1(1,1),VALUE1(1,1),VALUE1(1,2),VALUE1(1,3)
(1X,A8,'   ',F18.4,'  ',F14.4,'   ',1F15.3)
/COM,
/COM,AT ZERO VELOCITY
*VWRITE,LABEL2(1,1),LABEL2(1,2),VALUE2(1,1),VALUE2(1,2),VALUE2(1,3)
(1X,A8,A8,'   ',F10.4,'  ',F14.4,'   ',1F15.3)

/COM,----------------------------------------------------------
/OUT
FINISH
*LIST,vm81,vrt

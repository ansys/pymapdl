/COM,ANSYS MEDIA REL. 2023R1 (11/04/2022) REF. VERIF. MANUAL: REL. 2023R1
/VERIFY,VM278
/TITLE, VM278, BEAMS CONTACT TRACTION
C***  REFERENCE
C***
C***
/PREP7
LENGDE=100			! MODELING PARAMETERS
OD=0.5
WT=0.01
!*** SOLID MODEL ***
!-------------------
K,1,0,0,0
K,2,LENGDE,0,0
L,1,2				! GEOMETRY OF THE BEAM 
!*** MATERIAL PROPERTIES FOR STEEL IN NEWTONS AND METERS ***
!----------------------------------------------------------------
MP,EX,1,2E11			! YOUNG'S MODULUS
MP,PRXY,1,0.3			! POISSON'S RATIO
MP,DENS,1,7850			! DENSITY
MP,KXX,1,60.5			! CONDUCTIVITY PLAIN CARBON STEEL: 60.5 W/(m*k)
MP,C,1,434			! SPECIFIC HEAT PLAIN CARBON STEEL: 434 J/(kg*K)
MP,ALPX,1,12E-6			! COEFFICIENT OF THERMAL EXPANSION
!*** CONTACT MATERIAL PROPERTIES ***
!-------------------------------------
MP,MU,2,0.2			! COEFFICIENT OF FRICTION
!*** ELEMENT TYPES ***
!---------------------
ET,1,PIPE289,,,,2		! 3-D 3-NODE PIPE
ET,2,CONTA177			! 3-D LINE-TO-SURFACE CONTACT
KEYOPT,2,2,1			! PENALTY FUNCTION ALGORITHM
KEYOPT,2,3,1  			! TRACTION-BASED CONTACT TYPE
ET,3,170			! 3-D TARGET SEGMENT
!*** SECTION DATA ***
!--------------------
! EXAMPLE SECTION:
SECTYPE,1,PIPE,,PIPE1		! DEFINE PIPE SECTION
SECDATA,OD,WT,16		! APPLY MODELING PARAMETERS TO PIPE SECTION
! *** REAL CONSTANTS ***
! ----------------------
R,2				! INITIALIZE REAL CONSTANT SET 2
RMODIF, 2,3,-1000000		! ABSOLUTE VALUE OF PENALTY STIFFNESS
! *** MESHING ***
! ---------------
BIAS_FACT=5			! DEFINE MESHING PARAMETER
LESIZE,1,,,10,BIAS_FACT		! SET LINE MESHING CHARACTERISTICS
LMESH,ALL			! CREATE PIPE ELEMENTS
! CONTACT/TARGET
TYPE, 2				! CONTACT TYPE
REAL, 2	
MAT, 2
ESURF				! PLACE CONTACT ON PIPE ELEMENTS
! FIND THE HIGHEST NODE NUMBER DEFINED
*GET, NODE_MAXNUM_DEF, NODE, 0, NUM, MAXD	! GET THE HIGHEST NODE NUMBER
TARGET_NODE1=NODE_MAXNUM_DEF+1	! CHOOSE TARGET NODE NUMBERS
TARGET_NODE2=NODE_MAXNUM_DEF+2
TARGET_NODE3=NODE_MAXNUM_DEF+3
TARGET_NODE4=NODE_MAXNUM_DEF+4
N,TARGET_NODE1,-10,-10,0	! MAKE NODES FOR TARGET ELEMENT
N,TARGET_NODE2,110,-10,0
N,TARGET_NODE3,110,10,0
N,TARGET_NODE4,-10,10,0
TYPE, 3				! CREATE TARGET ELEMENT
E, TARGET_NODE1,TARGET_NODE2,TARGET_NODE3,TARGET_NODE4
ALLSEL,ALL
FINISH
/SOLU
! *** BOUNDARY CONDITIONS *** 
! ---------------------------
KSEL,S,KP,,1			! SELECT KEYPOINT AT LEFT SIDE OF BEAM
NSLK,S,				! SELECT IT'S CORRESPONDING NODE
D,ALL,UY,0			! FIX THE NODE IN THE VERTICAL AXIS
D,ALL,UX,0			! FIX THE NODE IN THE HORIZONTAL AXIS
D,ALL,ROTX,0                    ! FIX THE NODE ABOUT THE HORIZONTAL AXIS
!*
KSEL,S,KP,,2			! SELECT KEYPOINT AT RIGHT SIDE OF BEAM
NSLK,S,				! SELECT IT'S CORRESPONDING NODE
D,ALL,UY,0			! FIX THE NODE IN THE VERTICAL AXIS
!*
ALLSEL
ACEL,,,9.81			! APPLY GRAVITY TO THE SYSTEM
ALLSEL
NLHIST,PAIR,CAREA,CONT,CAREA,2	! PRINT THE CONTACT AREA TO .nlh FILE
/OUT,SCRATCH
SOLVE
FINISH
*LIST,vm278,nlh			! VERIFY THAT NLH FILE CONTAINS CORRECT AREA
/POST1
SET,LAST
/OUT,
ESEL,S,ENAME,,CONTA177
ETAB,PENE,CONT,PENE		! STORE CONTACT PENETRATION IN A TABLE
ETAB,PRES,CONT,PRES		! STORE CONTACT PRESSURE IN A TABLE
ETAB,AREA,NMIS,58 		! STORE CONTACT AREA IN A TABLE
*GET,C_PENE,ETAB,1,ELEM,11	! GET THE PENETRATION AT THE LEFT ELEMENT
*GET,C_PRES,ETAB,2,ELEM,20	! GET THE PRESSURE AT THE RIGHT ELEMENT
ASUM=0
*DO,I,11,20			! SUM THE CONTACT AREA OVER ALL CONTACT ELEMENTS
*GET,AREA%I%,ETAB,3,ELEM,I
ASUM=ASUM+AREA%I%
*ENDDO
*DIM,LABEL,CHAR,3,1
*DIM,VALUE,,3,3
LABEL(1,1) = 'PRESSURE ','PENETR ','AREA '
*VFILL,VALUE(1,1),DATA,4737,4.737E-3,25
*VFILL,VALUE(1,2),DATA,C_PRES,C_PENE,ASUM
*VFILL,VALUE(1,3),DATA,(C_PRES/4737),(C_PENE/4.737E-3),(ASUM/25)
FINISH
/COM
/OUT,vm278,vrt
/COM,----------------- VM278 RESULTS COMPARISON ---------------
/COM,
/COM,
/COM,CONTACT RESULT |  TARGET  |   Mechanical APDL   |   RATIO
/COM,
*VWRITE,LABEL(1),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A10,4X,E12.4,6X,E10.4,6X,1F9.3)
/COM,
/COM,
/COM,----------------------------------------------------------
/OUT
FINISH
*LIST,vm278,vrt


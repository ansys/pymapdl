! Copyright (C) 2024 ANSYS, Inc. and/or its affiliates.
! SPDX-License-Identifier: MIT
! 
!
! Permission is hereby granted, free of charge, to any person obtaining a copy
! of this software and associated documentation files (the "Software"), to deal
! in the Software without restriction, including without limitation the rights
! to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
! copies of the Software, and to permit persons to whom the Software is
! furnished to do so, subject to the following conditions:
!
! The above copyright notice and this permission notice shall be included in all
! copies or substantial portions of the Software.
!
! THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
! IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
! FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
! AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
! LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
! OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
! SOFTWARE.

/COM,ANSYS MEDIA REL. 2023R2 (05/12/2023) REF. VERIF. MANUAL: REL. 2023R2
/VERIFY,VM157
JPGPRF,500,100,1
/SHOW,JPEG
/TITLE,VM157, MODAL ANALYSIS OF GAS WITH SUDDEN TEMPERATURE CHANGE
/COM, REFERENCE: OBERG, C.L., RYAN, N.W., BAER, A.D.
/COM, A STUDY OF T-BURNER BEHAVIOR, AIAA VOL. 6 NO. 6
/COM, PP 1131-1137 (1968)

MAT_COLD_DENS = 1.1E-7
MAT_COLD_SONC = 1100*12

DIM_FRACTION  = 0.31
DIM_RADIUS    = 1.5
DIM_LENGTH    = 9
DIM_COLD_LNGT = DIM_LENGTH*DIM_FRACTION
DIM_HOT_LNGT  = DIM_LENGTH-DIM_COLD_LNGT

TMP_OFFSET    = 460
TMP_COLD      = 900-TMP_OFFSET
TMP_HOT       = 4500-TMP_OFFSET
REF_PRESSURE  = 14.7

MAT_HOT_DENS  = MAT_COLD_DENS*(TMP_COLD+TMP_OFFSET)/(TMP_HOT+TMP_OFFSET)
MAT_HOT_SONC  = MAT_COLD_SONC*SQRT((TMP_HOT+TMP_OFFSET)/(TMP_COLD+TMP_OFFSET))

DIM_ELEM_SIZE = MAT_HOT_SONC/3000/50

/PREP7
ET,1,FLUID30,,1                 ! FLUID30,NO FSI (SYMMETRIC ELEMENT MATRIX)
! *** CORRECT
R,1,REF_PRESSURE
MP,DENS,1,MAT_COLD_DENS         ! DENSITY OF COLD GAS
MP,SONC,1,MAT_COLD_SONC         ! VELOCITY OF COLD GAS
MP,REFT,1,TMP_COLD              ! REFERENCE TEMPERATURE
TOFFST,TMP_OFFSET

CYLIND,,DIM_RADIUS,,DIM_COLD_LNGT,,90
CYLIND,,DIM_RADIUS,DIM_COLD_LNGT,DIM_COLD_LNGT+DIM_HOT_LNGT,,90
VGLUE,ALL

MSHAPE,0
ESIZE,DIM_ELEM_SIZE
VSEL,S,LOC,Z,0,DIM_COLD_LNGT
VMESH,ALL
VSEL,INVE
VMESH,ALL
ALLSEL,ALL
FINISH

/SOLU
ANTYPE,MODAL
MODOPT,LANB,5,,,,ON                 ! LANCZOS EIGENSOLVER
MXPAND,5
VSEL,S,VOLUME,,1,,,1
BFE,ALL,TEMP,,TMP_COLD
BF,ALL,SPRE,REF_PRESSURE
ALLSEL,ALL
VSEL,S,VOLUME,,3,,,1
BFE,ALL,TEMP,,TMP_HOT
BF,ALL,SPRE,REF_PRESSURE
ALLSEL,ALL
/OUT,SCRATCH
SOLVE
FINISH

/POST1
/OUT,
SET,1,2
ASEL,S,LOC,Z,DIM_COLD_LNGT+DIM_HOT_LNGT
NSLA,S,1
*GET,OUTPUT_PRES,NODE,NDNEXT(0),PRES
ALLSEL,ALL

*DIM,LABEL,CHAR,1,1
*DIM,VALUE,,1,3

LABEL(1,1) = 'AMPLITUDE'
*VFILL,VALUE(1,1),DATA,0.45
*VFILL,VALUE(1,2),DATA,ABS(OUTPUT_PRES)
*VFILL,VALUE(1,3),DATA,ABS(0.45/OUTPUT_PRES) 

/OUTPUT,vm157,vrt
/COM,-----------------------VM157 RESULTS COMPARISON -----------------------
/COM,
/COM, NORMALIZED MODES (PRESSURE) CALCULATED
/COM, RATIO OF HOT END TO COLD END AMPLITUDES
/COM,
/COM,      |  TARGET  |  Mechanical APDL |  RATIO
/COM,
*VWRITE,LABEL(1,1),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A3,' ',F10.3,' ',F13.3,' ',F15.3)
/COM,
/COM,-------------------------------------------------------------------------
/OUTPUT
*LIST,vm157,vrt
FINISH
/EXIT,NOSAVE

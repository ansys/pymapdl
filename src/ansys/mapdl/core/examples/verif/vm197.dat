/COM,ANSYS MEDIA REL. 2023R1 (11/04/2022) REF. VERIF. MANUAL: REL. 2023R1
/VERIFY,VM197
/COM,   REF: "DYNAMICS OF ROTATING MACHINES" - MICHAEL I.FRISWELL,
/COM,   JOHN E. T. PENNY, SEAMUS D. GERVEY, ARTHUR W. LEES - 2010, P423-429
/COM,
/TITLE, VM197, ROTATING ELASTIC SYSTEM

/OUT,SCRATCH_vm197

/FILNAME,CASE1
JPGPRF,500,100,1

MASS = 0.2
KX = 20E+3
KY = 100E+3
KZ = 25E+3
R0 = 200E-3

DXYZ = R0/2

NBSTEP = 42
SPINMAX   = 5000
DSPIN  = SPINMAX/(NBSTEP-2)
*DIM,SPINS,,NBSTEP
*VFILL,SPINS,RAMP,0,DSPIN
SPINS(NBSTEP) = 8000

*DIM,SPINSTAT,,4
SPINSTAT(1)=0
SPINSTAT(2)=500
SPINSTAT(3)=1000
SPINSTAT(4)=8000

PI = ACOS(-1)
RATIO = PI/30

/PREP7

N,1,0,R0,0

N,2,,R0+DXYZ
N,3,,R0-DXYZ

N,4,-DXYZ,R0
N,5, DXYZ,R0

N,6,,R0,-DXYZ
N,7,,R0, DXYZ

ET,1,MASS21
KEYOPT,1,3,2    ! 3D WITHOUT INERTIA
R,1,MASS
TYPE,1
REAL,1
E,1

ET,2,COMBIN14
KEYOPT,2,2,1   ! UX
R,2,KX
TYPE,2
REAL,2
E,4,1
E,5,1

ET,3,COMBIN14
KEYOPT,3,2,2   ! UY
R,3,KY
TYPE,3
REAL,3
E,2,1
E,3,1

ET,4,COMBIN14
KEYOPT,4,2,3   ! UZ
R,4,KZ
TYPE,4
REAL,4
E,6,1
E,7,1

NSEL,U,NODE,,1
D,ALL,ALL
ALLSEL
FINISH

/SOLU               ! STATIC ANALYSIS OF THE SYSTEM
ANTYPE,STATIC
CORIOLIS,ON
D,1,ALL
DDELE,1,UY
*DO,ILOOP,1,4
   OMEGA,,,SPINSTAT(ILOOP)*RATIO
   SOLVE
*ENDDO
FINISH

/POST1
*DIM,UY1,,4
*DO,L,1,4
   SET,L,1
   *GET,UY1(L),NODE,1,U,Y
*ENDDO
FINISH

/SOLU               ! MODAL ANALYSIS OF THE SYSTEM
ANTYPE,MODAL
MODOPT,DAMP,6
MXPAND,6

DDELE,1,ALL

CORIOLIS,ON

*DO,ILOOP,1,NBSTEP
   OMEGA,,,SPINS(ILOOP)*RATIO
   SOLVE
*ENDDO

FINISH

/POST1
/SHOW,PNG,REV
/YRANGE,0,250
/XRANGE,0,5000
PLCAMP,OFF,,RPM,,,,YES,YES              ! CAMPBELL VALUES PRINTING
PRCAMP,OFF,,RPM,,,,YES,YES              ! PLOT CAMPBELL DIAGRAM
/SHOW,CLOSE

*DIM,FREQU,,4,2                         ! 1ST AND 2ND NATURAL FREQUENCIES FOR IN-PLANE MODES
*GET,FREQU(1,1),CAMP,1,FREQ,1           ! AT 0, 500, 1000 AND 8000 RPM
*GET,FREQU(2,1),CAMP,1,FREQ,5
*GET,FREQU(3,1),CAMP,1,FREQ,9
*GET,FREQU(4,1),CAMP,1,FREQ,42
*GET,FREQU(1,2),CAMP,5,FREQ,1
*GET,FREQU(2,2),CAMP,5,FREQ,5
*GET,FREQU(3,2),CAMP,5,FREQ,9
*GET,FREQU(4,2),CAMP,5,FREQ,42

PRCAMP,ON,0,RPM,,,1,YES,YES
                      
*get,STABV1,CAMP,1,VSTA					! STABILITY THRESHOLD
*get,STABV2,CAMP,2,VSTA					! STABILITY THRESHOLD

*IF,STABV1,NE,0,THEN
        A = STABV1
*ELSEIF,STABV2,NE,0,THEN
        A = STABV2
*ENDIF

*DIM,VALUE,,13,3
*DIM,LABEL,CHAR,1,5
LABEL(1,1) = '0'
LABEL(1,2) = '500'
LABEL(1,3) = '1000'
LABEL(1,4) = '8000'
LABEL(1,5) = 'SPEED'

/COM, EQUILIBRIUM DISPLACEMENTS AT DIFFERENT LOAD STEPS
*VFILL,VALUE(1,1),DATA,0
*VFILL,VALUE(1,2),DATA,UY1(1)
*VFILL,VALUE(1,3),DATA,1
*VFILL,VALUE(2,1),DATA,0.5498E-3
*VFILL,VALUE(2,2),DATA,UY1(2)
*VFILL,VALUE(2,3),DATA,ABS(0.5498E-3/UY1(2))
*VFILL,VALUE(3,1),DATA,2.218E-3
*VFILL,VALUE(3,2),DATA,UY1(3)
*VFILL,VALUE(3,3),DATA,ABS(2.218E-3/UY1(3))
*VFILL,VALUE(4,1),DATA,470.8E-3
*VFILL,VALUE(4,2),DATA,UY1(4)
*VFILL,VALUE(4,3),DATA,ABS(470.8E-3/UY1(4))

/COM, FREQUENCIES IN RRF FOR IP MODES
*VFILL,VALUE(5,1),DATA,71.18
*VFILL,VALUE(5,2),DATA,FREQU(1,1)
*VFILL,VALUE(5,3),DATA,ABS(71.18/FREQU(1,1))
*VFILL,VALUE(6,1),DATA,70.21
*VFILL,VALUE(6,2),DATA,FREQU(2,1)
*VFILL,VALUE(6,3),DATA,ABS(70.21/FREQU(2,1))
*VFILL,VALUE(7,1),DATA,67.4
*VFILL,VALUE(7,2),DATA,FREQU(3,1)
*VFILL,VALUE(7,3),DATA,ABS(67.4/FREQU(3,1))
*VFILL,VALUE(8,1),DATA,0
*VFILL,VALUE(8,2),DATA,FREQU(4,1)
*VFILL,VALUE(8,3),DATA,1
*VFILL,VALUE(9,1),DATA,159.15
*VFILL,VALUE(9,2),DATA,FREQU(1,2)
*VFILL,VALUE(9,3),DATA,ABS(159.15/FREQU(1,2))
*VFILL,VALUE(10,1),DATA,160.02
*VFILL,VALUE(10,2),DATA,FREQU(2,2)
*VFILL,VALUE(10,3),DATA,ABS(160.02/FREQU(2,2))
*VFILL,VALUE(11,1),DATA,162.51
*VFILL,VALUE(11,2),DATA,FREQU(3,2)
*VFILL,VALUE(11,3),DATA,ABS(162.51/FREQU(3,2))
*VFILL,VALUE(12,1),DATA,259.57
*VFILL,VALUE(12,2),DATA,FREQU(4,2)
*VFILL,VALUE(12,3),DATA,ABS(259.57/FREQU(4,2))

/COM, THE STABILITY THRESHOLD
*VFILL,VALUE(13,1),DATA,4271
*VFILL,VALUE(13,2),DATA,A
*VFILL,VALUE(13,3),DATA,ABS(4271/A)
SAVE,TABLE_1

/CLEAR,NOSTART

/FILNAME,CASE2

MASS = 0.2
KX = 120E+3
KY = 150E+3
KZ = 180E+3
R0 = 200E-3

DXY = 100E-3
DZ  = 50E-3

NBSTEP = 46
SPIN   = 9000
DSPIN  = SPIN/(NBSTEP-1)
*DIM,SPINS,,NBSTEP
*VFILL,SPINS,RAMP,0,DSPIN

PI = ACOS(-1)
RATIO = PI/30

/PREP7

N,1,0,R0,0

N,2,,R0+DXY
N,3,,R0-DXY

N,4,-DXY,R0
N,5, DXY,R0

N,6,,R0,-DZ
N,7,,R0, DZ

ET,1,MASS21
KEYOPT,1,3,2    ! 3D WITHOUT INERTIA
R,1,MASS
TYPE,1
REAL,1
E,1

ET,2,COMBIN14
R,2,KX
TYPE,2
REAL,2
E,4,1
E,5,1

ET,3,COMBIN14
R,3,KY
TYPE,3
REAL,3
E,2,1
E,3,1

ET,4,COMBIN14
R,4,KZ
TYPE,4
REAL,4
E,6,1
E,7,1

NSEL,U,NODE,,1
D,ALL,ALL
ALLSEL

FINISH

/SOLU
ANTYPE,STATIC

NLGEOM,ON
NSUB,2,20,1

*DO,ILOOP,6,NBSTEP,5
   /GOPR
   OMEGA,,,SPINS(ILOOP)*RATIO
   SOLVE
*ENDDO

FINISH

/POST1
*DIM,UY1,,9
I=1
*DO,L,6,NBSTEP,5
   SET,I,2
   *GET,UY1(I),NODE,1,U,Y
   I=I+1
*ENDDO
FINISH

*DO,ILOOP,1,NBSTEP

   /SOLU
   ANTYPE,STATIC

   NLGEOM,ON
   NSUB,2,20,1

   CORIOLIS,ON
   OMEGA,,,SPINS(ILOOP)*RATIO
   CAMPBELL,RSTP

   SOLVE
   FINISH

   /SOLU
   ANTYPE,STATIC, RESTART,,,PERTURB
   PERTURB,MODAL
   SOLVE,ELFORM

   MODOPT,DAMP,6
   MXPAND,6
   SOLVE
   FINI

*ENDDO

FINISH

/POST1
FILE,,rstp
/SHOW,JPEG,REV
/YRANGE,0,400
/GTHK,CURVE,-1          ! DRAW THE MARKERS ONLY /GMARKER - NO CURVE
/PSYMB,MARK,4           ! CONTROLS THE MARKER SIZE (1-10)
*DO,ILOOP,1,6
 /GMARKER,ILOOP,3       ! SPECIFY THE CURVE MARKING STYLE - DIAMONDS HERE
*ENDDO
PLCAMP,OFF,,RPM,,,,YES,YES  ! PLOT CAMPBELL DIAGRAM
/SHOW,CLOSE

PRCAMP,ON,0,RPM,,,1,YES,YES

*get,STABV1,CAMP,1,VSTA					! STABILITY THRESHOLD
*get,STABV2,CAMP,2,VSTA					! STABILITY THRESHOLD

*IF,STABV1,NE,0,THEN
        A = STABV1
*ELSEIF,STABV2,NE,0,THEN
        A = STABV2
*ENDIF

*DIM,LABEL,CHAR,1,10
LABEL(1,1)  = '1000'
LABEL(1,2)  = '2000'
LABEL(1,3)  = '3000'
LABEL(1,4)  = '4000'
LABEL(1,5)  = '5000'
LABEL(1,6)  = '6000'
LABEL(1,7)  = '7000'
LABEL(1,8)  = '8000'
LABEL(1,9)  = '9000'
LABEL(1,10)  = 'SPEED'

/COM, EQUILIBRIUM DISPLACEMENTS AT DIFFERENT LOAD STEPS
*DIM,VALUE,,10,3
*VFILL,VALUE(1,1),DATA,1.4720E-3
*VFILL,VALUE(1,2),DATA,UY1(1)
*VFILL,VALUE(1,3),DATA,ABS(1.4720E-3/UY1(1))
*VFILL,VALUE(2,1),DATA,5.9642E-3
*VFILL,VALUE(2,2),DATA,UY1(2)
*VFILL,VALUE(2,3),DATA,ABS(5.9642E-3/UY1(2))
*VFILL,VALUE(3,1),DATA,13.398E-3
*VFILL,VALUE(3,2),DATA,UY1(3)
*VFILL,VALUE(3,3),DATA,ABS(13.398E-3/UY1(3))
*VFILL,VALUE(4,1),DATA,23.073E-3
*VFILL,VALUE(4,2),DATA,UY1(4)
*VFILL,VALUE(4,3),DATA,ABS(23.073E-3/UY1(4))
*VFILL,VALUE(5,1),DATA,34.177E-3
*VFILL,VALUE(5,2),DATA,UY1(5)
*VFILL,VALUE(5,3),DATA,ABS(34.177E-3/UY1(5))
*VFILL,VALUE(6,1),DATA,46.481E-3
*VFILL,VALUE(6,2),DATA,UY1(6)
*VFILL,VALUE(6,3),DATA,ABS(46.481E-3/UY1(6))
*VFILL,VALUE(7,1),DATA,60.215E-3
*VFILL,VALUE(7,2),DATA,UY1(7)
*VFILL,VALUE(7,3),DATA,ABS(60.215E-3/UY1(7))
*VFILL,VALUE(8,1),DATA,75.829E-3
*VFILL,VALUE(8,2),DATA,UY1(8)
*VFILL,VALUE(8,3),DATA,ABS(75.829E-3/UY1(8))
*VFILL,VALUE(9,1),DATA,93.932E-3
*VFILL,VALUE(9,2),DATA,UY1(9)
*VFILL,VALUE(9,3),DATA,ABS(93.932E-3/UY1(9))

/COM, THE STABILITY THRESHOLD
*VFILL,VALUE(10,1),DATA,7250
*VFILL,VALUE(10,2),DATA,A
*VFILL,VALUE(10,3),DATA,ABS(7250/A)
SAVE,TABLE_2

FINISH

/CLEAR,NOSTART
/NOPR,
/OUT,vm197,vrt
/COM,------------ VM197 RESULTS COMPARISON --------------
/COM,
RESUME,TABLE_1
/COM, CASE 1
/COM,
/COM,        |   TARGET   |   MECHANICAL APDL   |   RATIO
/COM,
/COM, ---------------------------------------
/COM, EQUILIBRIUM DISPLACEMENTS
/COM, ---------------------------------------
*VWRITE,LABEL(1,1),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,'   ',F10.4,'  ',F14.4,'   ',F15.3)
*VWRITE,LABEL(1,2),VALUE(2,1),VALUE(2,2),VALUE(2,3)
(1X,A8,'   ',F10.4,'  ',F14.4,'   ',F15.3)
*VWRITE,LABEL(1,3),VALUE(3,1),VALUE(3,2),VALUE(3,3)
(1X,A8,'   ',F10.4,'  ',F14.4,'   ',F15.3)
*VWRITE,LABEL(1,4),VALUE(4,1),VALUE(4,2),VALUE(4,3)
(1X,A8,'   ',F10.4,'  ',F14.4,'   ',F15.3)
/COM,
/COM, ---------------------------------------
/COM, FIRST NATURAL FREQUENCIES (HZ)
/COM, ---------------------------------------
*VWRITE,LABEL(1,1),VALUE(5,1),VALUE(5,2),VALUE(5,3)
(1X,A8,'   ',F10.4,'  ',F14.4,'   ',F15.3)
*VWRITE,LABEL(1,2),VALUE(6,1),VALUE(6,2),VALUE(6,3)
(1X,A8,'   ',F10.4,'  ',F14.4,'   ',F15.3)
*VWRITE,LABEL(1,3),VALUE(7,1),VALUE(7,2),VALUE(7,3)
(1X,A8,'   ',F10.4,'  ',F14.4,'   ',F15.3)
*VWRITE,LABEL(1,4),VALUE(8,1),VALUE(8,2),VALUE(8,3)
(1X,A8,'   ',F10.4,'  ',F14.4,'   ',F15.3)
/COM
/COM, ---------------------------------------
/COM, SECOND NATURAL FREQUENCIES (HZ)
/COM, ---------------------------------------
*VWRITE,LABEL(1,1),VALUE(9,1),VALUE(9,2),VALUE(9,3)
(1X,A8,'   ',F10.4,'  ',F14.4,'   ',F15.3)
*VWRITE,LABEL(1,2),VALUE(10,1),VALUE(10,2),VALUE(10,3)
(1X,A8,'   ',F10.4,'  ',F14.4,'   ',F15.3)
*VWRITE,LABEL(1,3),VALUE(11,1),VALUE(11,2),VALUE(11,3)
(1X,A8,'   ',F10.4,'  ',F14.4,'   ',F15.3)
*VWRITE,LABEL(1,4),VALUE(12,1),VALUE(12,2),VALUE(12,3)
(1X,A8,'   ',F10.4,'  ',F14.4,'   ',F15.3)
/COM
/COM, ---------------------------------------
/COM, STABILITY THRESHOLD (RPM)
/COM, ---------------------------------------
*VWRITE,LABEL(1,5),VALUE(13,1),VALUE(13,2),VALUE(13,3)
(1X,A8,'   ',F10.4,'  ',F14.4,'   ',F15.3)
/COM, ---------------------------------------
/COM,
RESUME,TABLE_2
/COM, CASE 2
/COM,
/COM,        |   TARGET   |   MECHANICAL APDL   |   RATIO
/COM,
/COM, ---------------------------------------
/COM, EQUILIBRIUM DISPLACEMENTS
/COM, ---------------------------------------
*VWRITE,LABEL(1,1),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,'   ',F10.4,'  ',F14.4,'   ',F15.3)
*VWRITE,LABEL(1,2),VALUE(2,1),VALUE(2,2),VALUE(2,3)
(1X,A8,'   ',F10.4,'  ',F14.4,'   ',F15.3)
*VWRITE,LABEL(1,3),VALUE(3,1),VALUE(3,2),VALUE(3,3)
(1X,A8,'   ',F10.4,'  ',F14.4,'   ',F15.3)
*VWRITE,LABEL(1,4),VALUE(4,1),VALUE(4,2),VALUE(4,3)
(1X,A8,'   ',F10.4,'  ',F14.4,'   ',F15.3)
*VWRITE,LABEL(1,5),VALUE(5,1),VALUE(5,2),VALUE(5,3)
(1X,A8,'   ',F10.4,'  ',F14.4,'   ',F15.3)
*VWRITE,LABEL(1,6),VALUE(6,1),VALUE(6,2),VALUE(6,3)
(1X,A8,'   ',F10.4,'  ',F14.4,'   ',F15.3)
*VWRITE,LABEL(1,7),VALUE(7,1),VALUE(7,2),VALUE(7,3)
(1X,A8,'   ',F10.4,'  ',F14.4,'   ',F15.3)
*VWRITE,LABEL(1,8),VALUE(8,1),VALUE(8,2),VALUE(8,3)
(1X,A8,'   ',F10.4,'  ',F14.4,'   ',F15.3)
*VWRITE,LABEL(1,9),VALUE(9,1),VALUE(9,2),VALUE(9,3)
(1X,A8,'   ',F10.4,'  ',F14.4,'   ',F15.3)
/COM
/COM, ---------------------------------------
/COM, STABILITY THRESHOLD (RPM)
/COM, ---------------------------------------
*VWRITE,LABEL(1,10),VALUE(10,1),VALUE(10,2),VALUE(10,3)
(1X,A8,'   ',F10.4,'  ',F14.4,'   ',F15.3)
/COM, ---------------------------------------
/OUT
FINISH
*LIST,vm197,vrt
/EXIT,NOSAVE

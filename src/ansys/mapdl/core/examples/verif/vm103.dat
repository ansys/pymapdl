/COM,ANSYS MEDIA REL. 2023R1 (11/04/2022) REF. VERIF. MANUAL: REL. 2023R1
/VERIFY,VM103
/PREP7
/TITLE, VM103, THIN PLATE WITH CENTRAL HEAT SOURCE
C*** CONDUCTION HEAT TRANSFER, SCHNEIDER, 2ND. PRINTING, PAGE 173, ART. 8-1
C*** USING SHELL131 ELEMENTS
ANTYPE,STATIC              ! THERMAL ANALYSIS
ET,1,SHELL131,,,2
SECT,1,SHELL
SECD,.1
MP,KXX,1,5
CSYS,1                     ! CYLINDRICAL COORDINATE SYSTEM
N,1,.1,-5
N,8,.8,-5
FILL
NGEN,2,10,1,8,1,,10
N,21
N,9,(2/15),-5
N,10,(1/6),-5
NGEN,2,10,9,10,1,,10
E,21,1,11                  ! 7 TRIANGULAR ELEMENTS
E,1,9,11
E,19,11,9
E,9,10,20
E,20,19,9
E,10,2,20
E,12,20,2
E,12,2,3,13                ! 6 QUADRILATERAL ELEMENTS
EGEN,6,1,8
CP,1,TEMP,1,11             ! COUPLE NODAL TEMPS TANGENTIALLY
CPSGEN,10,1,1
OUTPR,BASIC,1
ESEL,S,ELEM,,2,13,1
SFE,ALL,1,CONV,,30
SFE,ALL,1,CONV,2,100
SFE,ALL,2,CONV,,20
SFE,ALL,2,CONV,2,0
ESEL,ALL
BFE,1,HGEN,,250E3          ! HEAT SOURCE
FINISH
/SOLU
SOLVE
FINISH
/POST1
*DIM,VALUE,,10,3
*VFILL,VALUE(1,1),DATA,226.3,103.2,73.8,65.8,62.8,60.8,60.2,60,173.1
*VFILL,VALUE(10,1),DATA,130.7
*DO,I,1,10,1
*GET,TN,NODE,I,TEMP
*VFILL,VALUE(I,2),DATA,TN
*VFILL,VALUE(I,3),DATA,ABS(VALUE(I,2)/VALUE(I,1))
*ENDDO
*DIM,LABEL,CHAR,10,2
LABEL(1,1) = 'T,F (AT ','T,F (AT ','T,F (AT ','T,F (AT ','T,F (AT ','T,F (AT ','T,F (AT '
LABEL(8,1) = 'T,F (AT ','T,F (AT ','T,F (AT '
LABEL(1,2) = 'NODE 1) ','NODE 2) ','NODE 3) ','NODE 4) ','NODE 5) ','NODE 6) ','NODE 7) '
LABEL(8,2) = 'NODE 8) ','NODE 9) ','NODE 10)'
SAVE,INF1
FINISH
/CLEAR,NOSTART




/TITLE, VM103, THIN PLATE WITH CENTRAL HEAT SOURCE
C*** USING SHELL132 ELEMENTS
/PREP7
ANTYPE,STATIC              ! THERMAL ANALYSIS
ET,1,SHELL132,,,2
SECT,1,SHELL
SECD,.1
MP,KXX,1,5
CSYS,1                     ! CYLINDRICAL COORDINATE SYSTEM
N,1,.05,-5
N,16,.8,-5
FILL
NGEN,3,30,1,16,1,,5
N,91
N,17,(7/60),-5
N,21,(11/60),-5
FILL
NGEN,3,30,17,21,1,,5
E,91,2,62,62,1,32,62,61	   ! 1 TRIANGULAR ELEMENT
E,2,18,78,62,17,48,77,32   ! 9 QUADRILATERAL ELEMENTS
E,18,20,80,78,3,50,63,48
E,20,4,64,80,21,34,81,50
E,4,6,66,64,5,36,65,34
EGEN,6,2,5
NSLE			   ! DELETE UNUSED NODES
NSEL,INVE
NDELE,ALL
NSEL,ALL
CP,1,TEMP,2,32,62          ! COUPLE NODAL TEMPS TANGENTIALLY
CPSGEN,21,1,1
OUTPR,BASIC,1
ESEL,S,ELEM,,2,9,1
SFE,ALL,1,CONV,,30
SFE,ALL,1,CONV,2,100
SFE,ALL,2,CONV,,20
SFE,ALL,2,CONV,2,0
ESEL,ALL
BFE,1,HGEN,,250E3          ! HEAT SOURCE
FINISH
/SOLU
SOLVE
FINISH
/POST1
*DIM,VALUE,,10,3
*VFILL,VALUE(1,1),DATA,226.3,103.2,73.8,65.8,62.8,60.8,60.2,60,173.1
*VFILL,VALUE(10,1),DATA,130.7
*DO,I,1,10,1
*GET,TN,NODE,(I*2),TEMP
*VFILL,VALUE(I,2),DATA,TN
*VFILL,VALUE(I,3),DATA,ABS(VALUE(I,2)/VALUE(I,1))
*ENDDO
*DIM,LABEL,CHAR,10,2
LABEL(1,1) = 'T,F (AT ','T,F (AT ','T,F (AT ','T,F (AT ','T,F (AT ','T,F (AT ','T,F (AT '
LABEL(8,1) = 'T,F (AT ','T,F (AT ','T,F (AT '
LABEL(1,2) = 'NODE 1) ','NODE 2) ','NODE 3) ','NODE 4) ','NODE 5) ','NODE 6) ','NODE 7) '
LABEL(8,2) = 'NODE 8) ','NODE 9) ','NODE 10)'
SAVE,INF2

RESUME,INF1
/COM
/OUT,vm103,vrt
/COM,------------------- VM103 RESULTS COMPARISON --------------
/COM,
/COM,                 |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
/COM,--SHELL131--
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.1,'  ',F10.1,'   ',1F5.2)
/COM,
/NOPR
RESUME,INF2
/COM,--SHELL132--
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.1,'  ',F10.1,'   ',1F5.2)
/COM,----------------------------------------------------------
/OUT
FINISH
*LIST,vm103,vrt


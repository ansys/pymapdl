/COM,ANSYS MEDIA REL. 2023R1 (11/04/2022) REF. VERIF. MANUAL: REL. 2023R1
/VERIFY,VM40
/PREP7                
MP,PRXY,,0.3
JPGPRF,500,100,1             ! MACRO TO SET PREFS FOR JPEG PLOTS
/SHOW,JPEG
/TITLE, VM40, LARGE DEFLECTION AND ROTATION OF A BEAM PINNED AT ONE END
C***           REFERENCE - ANY BASIC MATHEMATICS BOOK
PI=(4.0)*ATAN(1.0)    ! ANALYST FORGETS VALUE OF PI - LETS Mechanical APDL CALCULATE IT
ANTYPE,TRANS          ! NONLINEAR TRANSIENT DYNAMIC ANALYSES
NLGEOM,ON             ! LARGE DEFLECTIONS
ET,1,BEAM188
SECT,1,BEAM,ASEC
SECD,1,1,0.1,1,1,1    ! ARBITRARY GEOMETRIC PROPERTIES
MP,EX,1,30E6          ! DEFINE MATERIAL PROPERTIES
MP,DENS,1,1E-10       ! DEFINE DENSITY OF ALMOST ZERO
N,1                   ! BEGIN NODAL DEFINITION
N,2,10
E,1,2                 ! DEFINE ELEMENT
FINISH
/OUT,SCRATCH
/SOLU    
D,1,ROTZ,PI*2         ! ONE COMPLETE REVOLUTION
D,1,UX,,,,,UY         ! CONSTRAIN NODE 1 (PINNED END OF BEAM)
D,ALL,UZ
D,ALL,ROTX
D,ALL,ROTY
NSUBST,24,24,1
TIME,.15              ! TIME STEP OF 0.00625 SEC. (.15/24)
OUTRES,NSOL,1         ! SAVE NODAL DOF SOLUTION FOR EVERY SUBSTEP
OUTRES,ESOL,1         ! SAVE ELEMENT SOLUTION FOR EVERY SUBSTEP
CNVTOL,F,1,0.00001    ! CONVERGENCE CRITERION BASED UPON FORCES
CNVTOL,M,1,0.00001    ! CONVERGENCE CRITERION BASED UPON MOMENTS
AUTOTS,OFF
NEQIT,25
TINTP,,,,0.5,0.5,0.2
KBC,0
SOLVE
FINISH
/OUT
/POST26
NSOL,2,2,U,X,UX        ! DEFINE NODE 2 UX DISP AS VARIABLE 2
NSOL,3,2,U,Y,UY        ! DEFINE NODE 2 UY DISP AS VARIABLE 3
NSOL,4,1,ROT,Z,ROTZ    ! DEFINE NODE 1 ROTZ AS VARIABLE 4
ESOL,6,1,,SMISC,36,SDIR! GET AXIAL STRESS OF ELEMENT AT NODE 2
DERIV,5,4,,,INPUT_W    ! CALCULATE DERIVATIVE OF VAR. 4 WRT VARIABLE 1 (TIME)
PRVAR,2,3,4,5,6        ! PRINT VARIABLES 1 THRU 6
PLVAR,2,3              ! DISPLAY VARIABLES 2 AND 3 AS A FUNCTION OF TIME
STORE
*GET,MX_STRS,VARI,6,EXTREM,VMAX
*GET,DEFX_60,VARI,2,RSET,4
*GET,DEFY_90,VARI,3,RSET,6
*GET,DEFX_180,VARI,2,RSET,12
*GET,DEFY_210,VARI,3,RSET,14
*GET,DEFX_315,VARI,2,RSET,21
*GET,DEFY_360,VARI,3,RSET,24
*DIM,LABEL,CHAR,6,2
*DIM,VALUE,,6,3
*DIM,STRSS,,1,1
*VFILL,STRSS(1,1),DATA,MX_STRS
LABEL(1,1) = '60 DEG, ','90 DEG, ','180 DEG,','210 DEG,','315 DEG,','360 DEG,'
LABEL(1,2) = ' UX (in)',' UY (in)',' UX (in)',' UX (in)',' UX (in)',' UX (in)'
*VFILL,VALUE(1,1),DATA,-5,10,-20,-5,-2.93,0
*VFILL,VALUE(1,2),DATA,DEFX_60,DEFY_90,DEFX_180,DEFY_210,DEFX_315,DEFY_360
*VFILL,VALUE(1,3),DATA,ABS(DEFX_60/5),ABS(DEFY_90/10),ABS(DEFX_180/20),ABS(DEFY_210/5)
*VFILL,VALUE(5,3),DATA,ABS(DEFX_315/2.93),0
/COM
/OUT,vm40,vrt
/COM,------------------- VM40 RESULTS COMPARISON ---------------
/COM,
/COM,                 |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.2,'  ',F14.2,'   ',1F15.3)
/COM,
/COM,THE MAXIMUM AXIAL STRESS IS:
*VWRITE,STRSS(1,1)
(1X,F4.2)
/COM,-----------------------------------------------------------
/OUT
FINISH
*LIST,vm40,vrt


/COM,ANSYS MEDIA REL. 2023R1 (11/04/2022) REF. VERIF. MANUAL: REL. 2023R1
/VERIFY,VM289
/TITLE,VM289,SPHERICAL CAVITY IN AN INFINITE MEDIUM
/COM,
/COM, REFERENCE: 
/COM, SOIL DYNAMICS, ARNOLD VERRUJIT, DELFT UNIVERSITY
/COM, OF TECHNOLOGY, PP 138-139

/PREP7
P=750                       ! INTERNAL PRESSURE IN PA
PI=ACOS(-1)

R=1                         ! RADIUS IN M
H=4                                   
D=0.5*PI*R/5                ! ELEMENT SIZE
DPML=H+4*D                  ! PML ELEMENT THICKNESS

EXX=1e+07	                ! YOUNG'S MODULUS
NUXY=0.33	                ! POISSON'S RATIO
GX=EXX/(2*(1+NUXY))         ! RIGIDITY MODULUS

MP,EX,1,EXX                 ! DEFINE MATERIAL MODEL
MP,NUXY,1,NUXY
MP,GXY,1,GX

ET,1,SOLID186               ! 3D 20 NODE STRUCTURAL SOLID
ET,2,SOLID186
KEYO,2,15,1                 ! INCLUDE PML ABSORBING CONDITION

*DIM,A,ARRAY,3
A(1)=0
A(2)=H
A(3)=DPML
*DO,i,1,2
 *DO,j,1,2
  *DO,k,1,2
    BLOCK,A(i),A(i+1),A(j),A(j+1),A(k),A(k+1)
  *ENDDO
 *ENDDO
*ENDDO
VGLUE,ALL

SPHERE,0,R,0,90             ! CREATE A SPHERICAL VOLUME
VSEL,S,,,1,2
VSBV,1,2,,DELE,DELE         ! SUBTRACT VOLUME 2 FROM VOLUME 1
ALLSEL

ALLSEL,BELOW,VOLU
KL,14,.5                    ! GENERATING KEY POINT ON EXISTING LINE
KL,16,.5
KL,22,.5
K,,.5,.5,.5

A,3,18,20,7                 ! CREATING AREA THROUGH KEY POINTS
A,20,9,8,7
A,20,2,6,7
VSBA,3,ALL                  ! SUBTRACT AREAS FROM VOLUME 3
ALLSEL

VSEL,S,LOC,X,0,A(2)
VSEL,R,LOC,Y,0,A(2)
VSEL,R,LOC,Z,0,A(2)
CM,VOL1,VOLU
ALLSEL

CMSEL,S,VOL1
ESIZE,D
TYPE,1
MAT,1
VMESH,ALL                   ! SPHERICAL CAVITY     
ALLSEL

CMSEL,U,VOL1
ESIZE,D
TYPE,2
MAT,1
VMESH,ALL                   ! PML LAYER
ALLSEL

CSYS,2
NSEL,S,LOC,X,R
SF,ALL,PRES,P               ! INTERNAL PRESSURE
ALLSEL
CSYS,0

NSEL,S,LOC,X,A(3)
NSEL,A,LOC,Y,A(3)
NSEL,A,LOC,Z,A(3)
D,ALL,ALL
ALLSEL
NSEL,S,LOC,Z,0
DSYM,SYMM,Z                 ! SYMMETRIC BOUNDARY CONTION NORMAL TO Z 
ALLSEL
NSEL,S,LOC,Y,0
DSYM,SYMM,Y                 ! SYMMETRIC BOUNDARY CONDITION NORMAL TO Y
ALLSEL
NSEL,S,LOC,X,0
DSYM,SYMM,X                 ! SYMMETRIC BOUNDARY CONDITION NORMAL TO X
ALLSEL
FINISH

/SOLUTION
ANTY,STATIC
OUTRES,ALL,ALL
TIME,1.0
/OUT,SCRATCH
SOLVE
FINISH

/POST1
CSYS,2
SET,LAST
NSEL,S,LOC,Z,0
NSEL,R,LOC,Y,0
NSEL,R,LOC,X,R,H            ! POST PROCESSING DISPLACEMENT,STRESS FROM X=1M-4M        
NLIST
NSORT,LOC,X,1
NSORT,U,X,0
PRNS,U,X
*GET,UX1,NODE,NODE(1.0000,0,0),U,X
*GET,UX2,NODE,NODE(1.9474,0,0),U,X
*GET,UX3,NODE,NODE(3.0526,0,0),U,X
*GET,UX4,NODE,NODE(3.8421,0,0),U,X
PRNSOL,S,COMP
*GET,SX1,NODE,NODE(1.0000,0,0),S,X
*GET,SX2,NODE,NODE(1.9474,0,0),S,X
*GET,SX3,NODE,NODE(3.0526,0,0),S,X
*GET,SX4,NODE,NODE(3.8421,0,0),S,X
*GET,SY1,NODE,NODE(1.0000,0,0),S,Y
*GET,SY2,NODE,NODE(1.9474,0,0),S,Y
*GET,SY3,NODE,NODE(3.0526,0,0),S,Y
*GET,SY4,NODE,NODE(3.8421,0,0),S,Y

*DIM,LOC,,4
*DIM,RAD_D1,,4
*DIM,RAD_D2,,4
*DIM,RAD_S1,,4
*DIM,RAD_S2,,4
*DIM,TAN_S1,,4
*DIM,TAN_S2,,4
*DIM,RATIO1,,4
*DIM,RATIO2,,4
*DIM,RATIO3,,4

LOC(1,1)=1.0000,1.9474,3.0526,3.8421                    ! DISTANCE FROM INNER RADIUS OF SPHERICAL CAVLITY
RAD_D2(1,1)=UX1,UX2,UX3,UX4                             ! MAPDL RADIAL DISPLACEMENTS
RAD_S2(1,1)=SX1,SX2,SX3,SX4                             ! MAPDL RADIAL STRESSES
TAN_S2(1,1)=SY1,SY2,SY3,SY4                             ! MAPDL TANGENTIAL STRESS

*DO,I,1,4
RAD_D1(I,1)=(P*R**3)/(4*Gx*LOC(I,1)**2)                 ! ANALYTICAL RESULTS
RAD_S1(I,1)=-(P*R**3)/(LOC(I,1)**3)
TAN_S1(I,1)=(P*R**3)/(2*LOC(I,1)**3)

RATIO1(I,1)=RAD_D2(I,1)/RAD_D1(I,1)
RATIO2(I,1)=RAD_S2(I,1)/RAD_S1(I,1)
RATIO3(I,1)=TAN_S2(I,1)/TAN_S1(I,1)
*ENDDO
/OUT,vm289,vrt
/COM,
/COM, ------------VM289 RESULTS COMPARISON ------------
/COM,
/COM, LOCATION   |  TARGET  |   Mechanical APDL  |  RATIO
/COM,
/COM, RADIAL DISPLACEMENT
/COM,
*VWRITE,LOC(1,1),RAD_D1(1,1),RAD_D2(1,1),RATIO1(1,1)
(F7.4,1XG14.5,1XG14.5,1XF10.3)
/COM,
/COM, RADIAL STRESS
/COM,
*VWRITE,LOC(1,1),RAD_S1(1,1),RAD_S2(1,1),RATIO2(1,1)
(F7.4,1XF10.3,4XF10.3,5XF10.3)
/COM,
/COM, TANGENTIAL STRESS
/COM, 
*VWRITE,LOC(1,1),TAN_S1(1,1),TAN_S2(1,1),RATIO3(1,1)
(F7.4,1XF10.3,4XF10.3,5XF10.3)
/COM, ---------------------------------------------------
/OUT,
*LIST,vm289,vrt
FINISH
/EXIT,NOSAVE

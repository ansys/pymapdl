/COM,ANSYS MEDIA REL. 150 (11/8/2013) REF. VERIF. MANUAL: REL. 150
/VERIFY,VM251
JPGPRF,500,100,1                !* MACRO TO SET PREFS FOR JPEG PLOTS
/SHOW,JPEG
/TITLE,VM251, SHAPE MEMORY ALLOY UNDER UNIAXIAL TENSION LOAD
/COM,   REF: FERDINANDO AURICHIO, ROBERT L. TAYLOR, JACOB LUBLINER
/COM,   "SHAPE-MEMORY ALLOYS: MACROMODELLING AND NUMERICAL SIMULATIONS
/COM,   OF SUPERELASTIC BEHAVIOR"
/COM,   COMPUT. METHODS APPL. MECH. ENGNG. 146 (1997) 281-312
/COM,   USING 2D 4-NODE PLANE182 STRUCTURAL SOLID ELEMENTS
/PREP7
ET,1,182                        !* 2D 4-NODE STRUCTURAL SOLID ELEMENT
KEYOPT,1,3,1                    !* AXISYMMETRIC OPTION
MP, EX, 1, 60.0E3               !* MPA
MP,NUXY, 1, 0.3
TB,SMA,1
TBDATA,1,520,600,300,200,0.07,0 !* SHAPE MEMORY ALLOY
N,101, 0.00, 0.00
N,102,10.00, 0.00
N,103,10.00,10.00
N,104, 0.00,10.00
TYPE,1
MAT,1
E,101,102,103,104
NSEL,S,LOC,X
D,ALL,UX
NSEL,S,LOC,Y
D,ALL,UY
NSEL,ALL
FINISH
/SOLU
NLGEOM,ON
NSUBST,100,100,100
OUTRES,ALL,1
NSEL,S,LOC,Y,10
SF,ALL,PRES,-600        !* 1ST LOAD STEP -- LOAD THE MODEL
NSEL,ALL
/OUT,SCRATCH
SOLVE
NSEL,S,LOC,Y,10
SF,ALL,PRES,0          !* 2ND LOAD STEP -- UNLOAD THE MODEL
NSEL,ALL
SOLVE
FINISH
/POST26
ESOL,2,1,103,S,EQV           !* EQUIVALENT STRESS AT NODE 103
ESOL,3,1,103,EPEL,EQV        !* ELASTIC STRAIN AT NODE 103
ESOL,4,1,103,EPPL,EQV        !* PLASTIC STRAIN AT NODE 103
ADD,5,3,4                    !* TOTAL STRAIN AT NODE 103
PROD,6,5, , ,STRAIN, , ,100  !* PERCENT TOTAL STRAIN
XVAR,6
/AXLAB,X,Strain[%]
/AXLAB,Y,Stress [MPa]
/YRANGE,0,700                !* SET Y-RANGE
/XRANGE,0,8                  !* SET X-RANGE
/GROPT,DIVY,7
PLVAR,2                      !* PLOT TOTAL STRAIN VS EQV STRESS
/OUT,
PRVAR,2,5
FINISH
/POST1
SET, , , , ,0.87
*GET,SIG_SAS,NODE,103,S,EQV
*GET,EPTO_SAS,NODE,103,EPTO,EQV
SET, , , , ,1
*GET,SIG_FAS,NODE,103,S,EQV
*GET,EPTO_FAS,NODE,103,EPTO,EQV
SET, , , , ,1.5
*GET,SIG_SSA,NODE,103,S,EQV
*GET,EPTO_SSA,NODE,103,EPTO,EQV
SET, , , , ,1.67
*GET,SIG_FSA,NODE,103,S,EQV
*GET,EPTO_FSA,NODE,103,EPTO,EQV
R1 = SIG_SAS/520
R2 = EPTO_SAS/0.01
R3 = SIG_FAS/600
R4 = EPTO_FAS/0.08
R5 = SIG_SSA/300
R6 = EPTO_SSA/0.074
R7 = SIG_FSA/200
R8 = EPTO_FSA/0.32E-02
*DIM,LABEL,CHAR,8,2
*DIM,VALUE,,8,3
LABEL(1,1) = 'SIG','EPTO','SIG','EPTO','SIG','EPTO','SIG','EPTO'
LABEL(1,2) = '-SAS','-SAS','-FAS','-FAS','-SSA','-SSA','-FSA','-FSA'
*VFILL,VALUE(1,1),DATA,520,0.01,600,0.08,300,0.074,200,0.32E-02
*VFILL,VALUE(1,2),DATA,SIG_SAS,EPTO_SAS,SIG_FAS,EPTO_FAS,SIG_SSA,EPTO_SSA,SIG_FSA,EPTO_FSA
*VFILL,VALUE(1,3),DATA,R1,R2,R3,R4,R5,R6,R7,R8
SAVE,TABLE_1
FINISH
/CLEAR, NOSTART                 !* CLEAR DATABASE FOR SECOND SOLUTION
/TITLE,VM251, SHAPE MEMORY ALLOY UNDER UNIAXIAL TENSION LOAD - 2D AXISYMMETRIC
/COM    USING 2D 8-NODE PLANE183 STRUCTURAL SOLID ELEMENTS
/PREP7
ET,1,183                        !* 3D 8-NODE STRUCTURAL SOLID ELEMENT
KEYOPT,1,3,1                    !* AXISYMMETRIC OPTION
MP, EX, 1, 60.0E3               !* MPA
MP,NUXY, 1, 0.3
TB,SMA,1
TBDATA,1,520,600,300,200,0.07,0 !* SHAPE MEMORY ALLOY
N,101, 0.00, 0.00
N,102, 1.00, 0.00
N,103, 1.00, 1.00
N,104, 0.00, 1.00
N,105, 0.50, 0.00
N,106, 1.00, 0.50
N,107, 0.50, 1.00
N,108, 0.00, 0.50
TYPE,1
MAT,1
E,101,102,103,104,105,106,107,108
NSEL,S,LOC,X
D,ALL,UX
NSEL,S,LOC,Y
D,ALL,UY
NSEL,ALL
FINISH
/SOLU
NLGEOM,ON
NSUBST,200,200,200
OUTRES,ALL,1
NSEL,S,LOC,Y,1.0
SF,ALL,PRES,-600        !* 1ST LOAD STEP -- LOAD THE MODEL
NSEL,ALL
/OUT,SCRATCH
SOLVE
NSEL,S,LOC,Y,1.0
SF,ALL,PRES,0          !* 2ND LOAD STEP -- UNLOAD THE MODEL
NSEL,ALL
SOLVE
FINISH
/POST26
ESOL,2,1,103,S,EQV           !* EQUIVALENT STRESS AT NODE 103
ESOL,3,1,103,EPEL,EQV        !* ELASTIC STRAIN AT NODE 103
ESOL,4,1,103,EPPL,EQV        !* PLASTIC STRAIN AT NODE 103
ADD,5,3,4                    !* TOTAL STRAIN AT NODE 103
PROD,6,5, , ,STRAIN, , ,100  !* PERCENT TOTAL STRAIN
XVAR,6
/TITLE,UNIAXIAL TENSION STRESS-STRAIN RESPONSE FRO A Ni-Ti ALLOY
/AXLAB,X,Strain[%]
/AXLAB,Y,Stress [MPA]
/GROPT,DIVY,7
/YRANGE,0,700                !* SET Y-RANGE
/XRANGE,0,8                  !* SET X-RANGE
PLVAR,2                      !* PLOT TOTAL STRAIN VS EQV STRESS
/OUT,
PRVAR,2,5
FINISH
/POST1
SET, , , , ,0.87
*GET,SIG_SAS,NODE,103,S,EQV
*GET,EPTO_SAS,NODE,103,EPTO,EQV
SET, , , , ,1
*GET,SIG_FAS,NODE,103,S,EQV
*GET,EPTO_FAS,NODE,103,EPTO,EQV
SET, , , , ,1.5
*GET,SIG_SSA,NODE,103,S,EQV
*GET,EPTO_SSA,NODE,103,EPTO,EQV
SET, , , , ,1.67
*GET,SIG_FSA,NODE,103,S,EQV
*GET,EPTO_FSA,NODE,103,EPTO,EQV
R1 = SIG_SAS/520
R2 = EPTO_SAS/0.01
R3 = SIG_FAS/600
R4 = EPTO_FAS/0.08
R5 = SIG_SSA/300
R6 = EPTO_SSA/0.074
R7 = SIG_FSA/200
R8 = EPTO_FSA/0.32E-02
*DIM,LABEL,CHAR,8,2
*DIM,VALUE,,8,3
LABEL(1,1) = 'SIG','EPTO','SIG','EPTO','SIG','EPTO','SIG','EPTO'
LABEL(1,2) = '-SAS','-SAS','-FAS','-FAS','-SSA','-SSA','-FSA','-FSA'
*VFILL,VALUE(1,1),DATA,520,0.01,600,0.08,300,0.074,200,0.32E-02
*VFILL,VALUE(1,2),DATA,SIG_SAS,EPTO_SAS,SIG_FAS,EPTO_FAS,SIG_SSA,EPTO_SSA,SIG_FSA,EPTO_FSA
*VFILL,VALUE(1,3),DATA,R1,R2,R3,R4,R5,R6,R7,R8
SAVE,TABLE_2
FINISH
/CLEAR, NOSTART                 !* CLEAR DATABASE FOR SECOND SOLUTION
/TITLE,VM251, SHAPE MEMORY ALLOY UNDER UNIAXIAL TENSION LOAD
/COM    USING 3D 8-NODE SOLID185 STRUCTURAL SOLID ELEMENTS
/PREP7
ET,1,185                        !* 3D 8-NODE STRUCTURAL SOLID ELEMENT
MP, EX, 1, 60.0E3               !* MPA
MP,NUXY, 1, 0.3
TB,SMA,1
TBDATA,1,520,600,300,200,0.07,0 !* SHAPE MEMORY ALLOY
N,101, 0.00, 0.00
N,102, 10.00, 0.00
N,103, 10.00, 10.00
N,104, 0.00, 10.00
N,105, 0.00, 0.00,10.00
N,106, 10.00, 0.00,10.00
N,107, 10.00, 10.00,10.00
N,108, 0.00, 10.00,10.00
TYPE,1
MAT,1
E,101,102,103,104,105,106,107,108
TYPE,1
NSEL,S,LOC,X
D,ALL,UX
NSEL,S,LOC,Y
D,ALL,UY
NSEL,S,LOC,Z
D,ALL,UZ
NSEL,ALL
FINISH
/SOLU
NLGEOM,ON
NSUBST,100,100,100
OUTRES,ALL,1
NSEL,S,LOC,Y,10.0
SF,ALL,PRES,-600        !* 1ST LOAD STEP -- LOAD THE MODEL
NSEL,ALL
/OUT,SCRATCH
SOLVE
NSEL,S,LOC,Y,10.0
SF,ALL,PRES,0          !* 2ND LOAD STEP -- UNLOAD THE MODEL
NSEL,ALL
SOLVE
FINISH
/POST26
ESOL,2,1,103,S,EQV           !* EQUIVALENT STRESS AT NODE 103
ESOL,3,1,103,EPEL,EQV        !* ELASTIC STRAIN AT NODE 103
ESOL,4,1,103,EPPL,EQV        !* PLASTIC STRAIN AT NODE 103
ADD,5,3,4                    !* TOTAL STRAIN AT NODE 103
PROD,6,5, , ,STRAIN, , ,100  !* PERCENT TOTAL STRAIN
XVAR,6
/axlab,x,Strain[%]
/axlab,y,Stress [MPa]
/YRANGE,0,700                !* SET Y-RANGE
/XRANGE,0,8                  !* SET X-RANGE
/GROPT,DIVY,7
PLVAR,2                      !* PLOT TOTAL STRAIN VS EQV STRESS
/OUT,
PRVAR,2,5
FINISH
/POST1
SET, , , , ,0.87
*get,SIG_SAS,node,103,s,eqv
*get,EPTO_SAS,node,103,epto,eqv
SET, , , , ,1
*get,SIG_FAS,node,103,s,eqv
*get,EPTO_FAS,node,103,epto,eqv
SET, , , , ,1.5
*get,SIG_SSA,node,103,s,eqv
*get,EPTO_SSA,node,103,epto,eqv
SET, , , , ,1.67
*get,SIG_FSA,node,103,s,eqv
*get,EPTO_FSA,node,103,epto,eqv
R1 = SIG_SAS/520
R2 = EPTO_SAS/0.01
R3 = SIG_FAS/600
R4 = EPTO_FAS/0.08
R5 = SIG_SSA/300
R6 = EPTO_SSA/0.074
R7 = SIG_FSA/200
R8 = EPTO_FSA/0.32E-02
*DIM,LABEL,CHAR,8,2
*DIM,VALUE,,8,3
LABEL(1,1) = 'Sig','EPTO','Sig','EPTO','Sig','EPTO','Sig','EPTO'
LABEL(1,2) = '-SAS','-SAS','-FAS','-FAS','-SSA','-SSA','-FSA','-FSA'
*VFILL,VALUE(1,1),DATA,520,0.01,600,0.08,300,0.074,200,0.32E-02
*VFILL,VALUE(1,2),DATA,SIG_SAS,EPTO_SAS,SIG_FAS,EPTO_FAS,SIG_SSA,EPTO_SSA,SIG_FSA,EPTO_FSA
*VFILL,VALUE(1,3),DATA,R1,R2,R3,R4,R5,R6,R7,R8
SAVE,TABLE_3
RESUME,TABLE_1
/COM
/OUT,vm251,vrt
/COM,------------------- VM251 RESULTS COMPARISON --------------
/COM,
/COM,               |  TARGET   |   Mechanical APDL  |  RATIO
/COM,
/COM,RESULTS USING PLANE182 ELEMENT
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A4,A8,'   ',F10.3,'  ',1F14.3,'   ',1F15.3)
/NOPR
RESUME,TABLE_2
/GOPR
/COM,
/COM,RESULTS USING PLANE183 ELEMENT
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A4,A8,'   ',F10.3,'  ',1F14.3,'   ',1F15.3)
/NOPR
RESUME,TABLE_3
/GOPR
/COM,
/COM,RESULTS USING SOLID185 ELEMENT
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A4,A8,'   ',F10.3,'  ',1F14.3,'   ',1F15.3)
/COM,-----------------------------------------------------------
/OUT
FINISH
*LIST,vm251,vrt

/COM,ANSYS MEDIA REL. 150 (11/8/2013) REF. VERIF. MANUAL: REL. 150
/VERIFY,VM108
/PREP7
/TITLE, VM108, TEMPERATURE GRADIENT ACROSS A SOLID CYLINDER
C***    HILDEBRAND, ADVANCED CALCULUS, PAGE 447, EQUATIONS 38-44
ANTYPE,STATIC
ET,1,PLANE75            ! AXISYMMETRIC HARMONIC THERMAL SOLID
MP,KXX,1,1
N,1
N,5,20
FILL
NGEN,2,5,1,5,1,,5       ! AXIAL LENGTH IS ARBITRARY
E,1,2,7,6               ! FOUR ELEMENTS ALONG RADIUS AT THETA=0
EGEN,4,1,1
OUTPR,BASIC,1
MODE,1,1                ! ANTISYMMETRIC MODE (ISYM=1)
D,5,TEMP,80,,10,5       ! DEFINE PEAK TEMPERATURE
FINISH
/SOLU    
SOLVE
FINISH
/POST1
*GET,TN1,NODE,1,TEMP
*GET,TN2,NODE,2,TEMP
*GET,TN3,NODE,3,TEMP
*GET,TN4,NODE,4,TEMP
*DIM,VALUE,,4,3
*DIM,LABEL,CHAR,4,2
*VFILL,VALUE(1,1),DATA,0,20,40,60 
*VFILL,VALUE(1,2),DATA,TN1,TN2,TN3,TN4
*VFILL,VALUE(1,3),DATA,0,ABS(TN2/20),ABS(TN3/40),ABS(TN4/60)
LABEL(1,1) = 'NODE 1 T','NODE 2 T','NODE 3 T','NODE 4 T'
LABEL(1,2) = ', F     ',', F     ',', F     ',', F     '
/COM
/OUT,vm108,vrt
/COM,------------------- VM108 RESULTS COMPARISON --------------
/COM,
/COM,                 |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.1,'  ',F12.1,'   ',1F15.2)
/COM,----------------------------------------------------------
/OUT
FINISH
*LIST,vm108,vrt


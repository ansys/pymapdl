/COM,ANSYS MEDIA REL. 150 (11/8/2013) REF. VERIF. MANUAL: REL. 150
/VERIFY,VM123
/PREP7
/TITLE, VM123, LAMINAR FLOW IN A PIPING SYSTEM
C***    FLOW OF FLUIDS, CRANE TECH. PAPER NO. 410, PAGE 4-5, EX. 4-9
ET,1,FLUID116,2     ! THERMAL-FLOW PIPE
R,1,.4206           ! PIPE DIAMETER
rmore
rmore,,53           ! LOSS LENGTH
MP,DENS,1,1.7546    ! MASS DENSITY OF OIL
MP,MU,1,.05         ! INITIAL FRICTION FACTOR
MP,VISC,1,.010032   ! OIL VISCOSITY
N,1
N,2,300
E,1,2
D,2,PRES,0          ! OUTLET REFERENCE PRESSURE
F,1,FLOW,75.53/32.2 ! INLET MASS FLOW
OUTPR,BASIC,1
OUTPR,NLOAD,1
FINISH
/SOLU    
SOLVE
FINISH
/POST1
ETABLE,RE,NMISC,2
ESORT,RE
*GET,REY,SORT,,MAX
*GET,DELTAP,NODE,1,PRES
*status,parm
*DIM,LABEL,CHAR,2,2
*DIM,VALUE,,2,3
LABEL(1,1) = 'DELTA P ','Re'
LABEL(1,2) = 'lb/ft/ft '
*VFILL,VALUE(1,1),DATA,6160,708
*VFILL,VALUE(1,2),DATA,DELTAP,REY
*VFILL,VALUE(1,3),DATA,ABS(DELTAP/6160),ABS(REY/708)
/OUT,vm123,vrt
/COM
/COM,------------------- VM123 RESULTS COMPARISON -------------
/COM,
/COM,                 |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.0,'  ',F12.0,'   ',1F15.3)
/COM,----------------------------------------------------------
/OUT
FINISH
*LIST,vm123,vrt


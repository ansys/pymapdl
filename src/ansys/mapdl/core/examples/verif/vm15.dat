/COM,ANSYS MEDIA REL. 2023R1 (11/04/2022) REF. VERIF. MANUAL: REL. 2023R1
/VERIFY,VM15
JPGPRF,500,100,1             ! MACRO TO SET PREFS FOR JPEG PLOTS
/SHOW,JPEG
/PREP7   
/TITLE, VM15, BENDING OF A CIRCULAR PLATE USING AXISYMMETRIC SHELL ELEMENTS
C***  CASE 1 - STR. OF MATLS., TIMOSHENKO, PART 2, 3RD ED., PAGE 96, ART. 19
C***  CASE 2 - STR. OF MATLS., TIMOSHENKO, PART 2, 3RD ED., PAGE 103, ART. 21
C***  CASE 3 - STR. OF MATLS., TIMOSHENKO, PART 2, 3RD ED., PAGE 97, ART. 19
ANTYPE,STATIC
ET,1,SHELL208,,,2
SECTYPE,1,SHELL
SECDATA,1,1
SECNUM,1
MP,EX,1,30E6
MP,NUXY,1,.3
N,1
N,11,40
N,6,20
FILL,1,6,4,,,,,20           ! BIAS THE MESH TO ALLOW STRESS RECOVERY NEAR
FILL,6,11,4,,,,,0.05        ! THE CENTERLINE AND EDGE CONSTRAINTS
E,1,2
EGEN,10,1,-1
FINISH
C***  CASE 1 - UNIFORM LOADING - CLAMPED EDGE
/SOLU    
OUTPR,,1
D,1,UX,,,,ROTZ
D,11,ALL
SFE,ALL,1,PRES,,6           ! PRESSURE LOAD = 6 PSI ON ALL LINE SEGMENTS
/OUT,SCRATCH
SOLVE
C***  CASE 2 - CONCENTRATED CENTER LOADING - CLAMPED EDGE
F,1,FY,-7539.82
SFE,ALL,1,PRES,,0           ! REMOVE PRESSURE
SOLVE
C***  CASE 3 - UNIFORM LOADING - SIMPLY SUPPORTED EDGE
DDELE,11,ROTZ               ! DELETE CLAMPED BOUNDARY CONDITION CONSTRAINT
F,1,FY                      ! REMOVE CENTERLINE POINT LOAD
SFE,ALL,1,PRES,,1.5
SOLVE
FINISH
/POST1
/DSCALE,1,35                ! EXAGGERATE DISPLACEMENT SCALING FOR CLARITY
/WINDOW,1,-1,1,-1,-.333
/OUT,
SET,1,1  
PLDISP,1
/WINDOW,1,OFF               ! TURN-OFF WINDOW 1
/WINDOW,2,-1,1,-.333,.333,1
/NOERASE                    ! DON'T ERASE EXISTING DISPLAYS
MID_NODE = NODE ( 0,0,0 )
*GET,DEF_C1,NODE,MID_NODE,U,Y
ETABLE,STRS,S,X
*GET,STRSS_C1,ELEM,10,ETAB,STRS
SET,2,1  
PLDISP
/WINDOW,2,OFF               ! TURN OFF WINDOW 2
/WINDOW,3,-1,1,.333,1,1
*GET,DEF_C2,NODE,MID_NODE,U,Y
ETABLE,STRS,S,X
*GET,STRSS_C2,ELEM,10,ETAB,STRS
SET,3,1  
PLDISP
*GET,DEF_C3,NODE,MID_NODE,U,Y
ETABLE,STRS,S,X
*GET,STRSS_C3,ELEM,1,ETAB,STRS
*DIM,LABEL,CHAR,2,2
*DIM,VALUE_C1,,2,3
*DIM,VALUE_C2,,2,3
*DIM,VALUE_C3,,2,3
LABEL(1,1) = 'DEFLECTI','MAX STRS'
LABEL(1,2) = 'ON (in) ','S (psi) '
*VFILL,VALUE_C1(1,1),DATA,-.08736,7200
*VFILL,VALUE_C1(1,2),DATA,DEF_C1,ABS(STRSS_C1)
*VFILL,VALUE_C1(1,3),DATA,ABS(DEF_C1/.08736 ),ABS(STRSS_C1/7200 )
*VFILL,VALUE_C2(1,1),DATA,-.08736,3600
*VFILL,VALUE_C2(1,2),DATA,DEF_C2,ABS(STRSS_C2)
*VFILL,VALUE_C2(1,3),DATA,ABS(DEF_C2/.08736 ),ABS(STRSS_C2/3600 )
*VFILL,VALUE_C3(1,1),DATA,-.08904,2970
*VFILL,VALUE_C3(1,2),DATA,DEF_C3,ABS(STRSS_C3)
*VFILL,VALUE_C3(1,3),DATA,ABS(DEF_C3/.08904 ),ABS(STRSS_C3/2970 )
/COM
/OUT,vm15,vrt
/COM,------------------- VM15 RESULTS COMPARISON ---------------------
/COM,
/COM,                 |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
/COM,RESULTS FOR CASE 1:
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE_C1(1,1),VALUE_C1(1,2),VALUE_C1(1,3)
(1X,A8,A8,'   ',F10.5,'  ',F15.5,'   ',1F15.3)
/COM,
/COM,RESULTS FOR CASE 2:
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE_C2(1,1),VALUE_C2(1,2),VALUE_C2(1,3)
(1X,A8,A8,'   ',F10.5,'  ',F15.5,'   ',1F15.3)
/COM,
/COM,RESULTS FOR CASE 3:
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE_C3(1,1),VALUE_C3(1,2),VALUE_C3(1,3)
(1X,A8,A8,'   ',F10.5,'  ',F15.5,'   ',1F15.3)
/COM,-----------------------------------------------------------------
/OUT
FINISH
*LIST,vm15,vrt

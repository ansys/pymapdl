/COM,ANSYS MEDIA REL. 2023R1 (11/04/2022) REF. VERIF. MANUAL: REL. 2023R1
/VERIFY,VM306
/TITLE,VM306,SURFACE IMPEDANCE FOR A SINGLE POROELASTIC LAYER
/COM,
/COM, REF: ATALLA, N. ET AL. "A MIXED DISPLACEMENT-PRESSURE 
/COM,      FORMULATION FOR POROELASTIC MATERIALS." JOURNAL OF
/COM,      THE ACOUSTICAL SOCIETY OF AMERICA. (1998): 1444-1452
/COM,

/COM, PARAMETER FOR CREATING THE CDB FILES TO STORE GEOMETRY AND MESH DATA
_GEOMGEN = 0   ! SET TO 1 TO WRITE CDB FILES IF ANY INPUT FILE MODIFICATIONS
/OUT,SCRATCH_vm306

! PREPROCESSING
/PREP7
*IF,_GEOMGEN,EQ,1,THEN

ET,1,220,,7  

RHO=1.213
C0=342.2
RESIS=40E3
PORO=0.94
TORT=1.06
VISL=0.56E-4
THRML=1.10E-4
NUXY=0.
EX=4400E3
DAMP=0.1
RHOS=130
BIOTC=1.0
KS=0
F1=1500
F2=3000

! MATERIAL PROPERTIES
MP,DENS,1,RHO
MP,SONC,1,C0
MP,EX,1,EX
MP,NUXY,1,NUXY
TB,PERF,1,,,PORO
TBFIELD,FREQ,F1
TBDATA,,RESIS,PORO,TORT,VISL,THRML,RHOS
TBDATA,7,DAMP,,BIOTC,KS
TBFIELD,FREQ,F2
TBDATA,,RESIS,PORO,TORT,VISL,THRML,RHOS
TBDATA,7,DAMP,,BIOTC,KS

! CREATE FINITE ELEMENT MODEL
D=0.1     ! HEIGHT 10 CM
H=D/8
A=H
B=H

BLOCK,0,A,0,B,0,-D

TYPE,1
MAT,1
ESIZE,H
VMESH,ALL

ALLS
CDWRITE,db,vm306,cdb
*ELSE
 CDREAD,db,vm306,cdb
*ENDIF

! APPLY BOUNDARY CONDITIONS
NSEL,S,LOC,Z,0
D,ALL,UX,0
D,ALL,UY,0
D,ALL,UZ,0

ALLS

NSEL,ALL
D,ALL,UX,0               ! INFINITE EXTENT IN X AND Y DIRECTIONS
D,ALL,UY,0

! APPLY NORMAL INCIDENCE PLANE WAVE OF UNIT AMPLITUDE
NSEL,S,LOC,Z,-D
D,ALL,PRES,1
ALLS
TBLIST
FINI

/SOLU
EQSLV,SPARSE
ANTYPE,HARMIC          ! HARMONIC ANALYSIS 
HROPT,FULL
HARFRQ,,1200           ! FREQUENCY RANGE
NSUB,4,4,4             
SOLVE
FINI

/OUT
/POST1
*DIM,AMZ,ARRAY,4
*DO,I,1,4
SET,1,I
NSEL,S,LOC,Z,-D
PRAS,SIMP
*GET,AMZ(I),ACUS,0,SIMP
*ENDDO
*DIM,RZ,,4
RZ(1)=1424,1142,935,818
*DIM,RT,,4
*DO,J,1,4
RT(J)=AMZ(J)/RZ(J)
*ENDDO
*DIM,LABEL,CHAR,4
LABEL(1)='300','600','900','1200'
/COM
/OUT,vm306,vrt
/COM,------------ VM306 RESULTS COMPARISON ----------------
/COM,
/COM,        FREQ     |  TARGET   |  Mechanical APDL  |  RATIO
/COM,
*VWRITE,LABEL(1),rz(1),amz(1),RT(1)
(8X,A4,'       ',F10.3,'       ',F10.3,'    ',F10.4)
/COM-------------------------------------------------------
/OUT
FINISH
*LIST,vm306,vrt

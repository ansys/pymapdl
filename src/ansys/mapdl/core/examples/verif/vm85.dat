/COM,ANSYS MEDIA REL. 2023R1 (11/04/2022) REF. VERIF. MANUAL: REL. 2023R1
/VERIFY,VM85
/out,scratch
/PREP7
JPGPRF,500,100,1             ! MACRO TO SET PREFS FOR JPEG PLOTS
/SHOW,JPEG
/TITLE, VM85, TRANSIENT DISPLACEMENTS IN A SUDDENLY STOPPED MOVING BAR
C***      VIBRATION PROBS. IN ENGR., TIMOSHENKO, 3RD. ED., PAGE 305, PROB. NO. 3
ET,1,LINK180
SECTYPE,1,LINK   
SECDATA,1                    ! AREA
MP,EX,1,30E6
MP,DENS,1,.00073
N,1
N,17,10000
FILL
E,1,2
EGEN,16,1,1
ALLSEL,ALL
D,ALL,UZ,0
D,ALL,UY,0
GP,1,20,FX,3E7,.64          ! GAP CONDITION
FINISH
/SOLU
ANTYPE,MODAL
MODOPT,LANB,17,              ! PERFORM MODAL SOLVE USING LANB EIGENSOLVER
MXPAND,17,,,YES
/OUT,SCRATCH
SOLVE
FINISH
/OUT,SCRATCH
/SOLU    
ANTYPE,TRANSIENT
TRNOPT,MSUP,	            ! PERFORM MODE SUPERPOSITION TRANSIENT SOLVE
DELTIM,.0001                ! ITS DEFINITION
KBC,1                       ! STEP BOUNDARY CONDITION
F,1,FX,,,17                 ! DEFINE NULL FORCES ON ALL BAR NODES
SOLVE

TIME,.0004
F,1,FX,57031.25,,17,16      ! FORCES REQUIRED TO ACHIEVE INITIAL VELOCITY
F,2,FX,114062.5,,16
SOLVE

TIME,.06
F,1,FX,,,17                 ! REMOVE FORCES ("COAST")
SOLVE

FINISH
/POST26
FILE,,rdsp                  ! REDUCED DISPLACEMENTS FILE
NSOL,2,1,U,X
NSOL,3,17,U,X
NSOL,5,9,U,X
ADD,4,2,3,,REL_DISP,,,-1    ! COMPUTE RELATIVE DISPLACEMENTS
PRTIME,.053,.057
PRVAR,2,3,4,5
/AXLAB,Y,DISPLACEMENTS
PLVAR,2,3,4,5
DERIV,6,2,,,1 VX            ! COMPUTE VELOCITIES
DERIV,7,3,,,17VX
DERIV,8,5,,,9 VX
/AXLAB,Y,VELOCITY
PLVAR,6,7,8
*GET,D_0544,VARI,4,RTIME,.0544
*GET,D_0557,VARI,4,RTIME,.0557
*DIM,LABEL,CHAR,3,2
*DIM,VALUE,,3,3
LABEL(1,1) = 'D,in(T=.','D,in(T=.','D,in(T=.'
LABEL(1,2) = '05573sec','0544sec)','0557sec)'
*VFILL,VALUE(1,1),DATA,4.9329,0,0 
*VFILL,VALUE(1,2),DATA,0,D_0544,D_0557
*VFILL,VALUE(1,3),DATA,0,ABS(D_0544/4.9329),ABS(D_0557/4.9329)
FINISH
/SOLU    
EXPASS,ON                   ! EXPANSION PASS
EXPSOL,,,0.0557             ! EXPAND SOLUTION AT TIME CLOSEST TO THE THEORETICAL TIME POINT
OUTPR,,1
SOLVE
FINISH
/POST1
SET,LAST
/OUT,
ETABLE,STRS,LS,1
*GET,STRSS,ELEM,1,ETAB,STRS
*DIM,LABEL_2,CHAR,2,2
*DIM,VALUE_2,,2,3
LABEL_2(1,1) = 'SIGX,PSI','SIGX,PSI'
LABEL_2(1,2) = 'T=.05573',' T=.0557'
*VFILL,VALUE_2(1,1),DATA,14799,0 
*VFILL,VALUE_2(1,2),DATA,0,STRSS
*VFILL,VALUE_2(1,3),DATA,0,ABS(STRSS/14799)
/COM
/OUT,vm85,vrt
/COM,------------------- VM85 RESULTS COMPARISON --------------
/COM,
/COM,                 |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
/COM,MODE SUPERPOSITION TRANSIENT DYNAMIC:
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.4,'  ',F14.4,'   ',1F15.3)
/COM,
/COM,EXPANSION PASS:
/COM,
*VWRITE,LABEL_2(1,1),LABEL_2(1,2),VALUE_2(1,1),VALUE_2(1,2),VALUE_2(1,3)
(1X,A8,A8,'   ',F10.0,'  ',F14.0,'   ',1F15.3)
/COM,----------------------------------------------------------
/OUT
FINISH
*LIST,vm85,vrt

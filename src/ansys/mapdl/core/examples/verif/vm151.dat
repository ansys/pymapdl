/COM,ANSYS MEDIA REL. 2023R1 (11/04/2022) REF. VERIF. MANUAL: REL. 2023R1
/VERIFY,VM151
/PREP7  
MP,PRXY,,0.3
JPGPRF,500,100,1             ! MACRO TO SET PREFS FOR JPEG PLOTS
/SHOW,JPEG
/TITLE, VM151, NONAXISYMMETRIC VIBRATION OF A CIRCULAR PLATE
C***          FORMULAS FOR NATURAL FREQUENCY AND MODE SHAPE, BLEVINS, PAGE 240
ET,1,SHELL61
R,1,.05            ! THICKNESS OF PLATE
MP,EX,1,30E6
MP,DENS,1,.00073
MP,PRXY,1,0.3
K,1
K,2,3
L,1,2
LESIZE,1,,,9
LMESH,1
NSEL,S,LOC,X,0
D,ALL,UX
NSEL,S,LOC,X,3     ! SELECT NODE AT R=3 AND CONSTRAIN
D,ALL,UX,,,,,UY
NSEL,ALL
D,ALL,UZ           ! CONSTRAIN TORSIONAL DOF'S
FINISH
/SOLU    
ANTYPE,MODAL       ! MODE FREQUENCY ANALYSIS
MXPAND,3           ! EXPAND FIRST 3 MODE SHAPES
MODOPT,LANB,9
OUTPR,BASIC,ALL
MODE,0             ! ZERO HARMONIC MODE
/OUT,SCRATCH
SOLVE
FINISH
/POST1
/WINDOW,1,LTOP
/OUT,
SET,1,1
PLDISP,1
/NOERASE
/WINDOW,1,OFF
*GET,F1,MODE,0,FREQ
FINISH
/SOLU    
MODE,1             ! FIRST HARMONIC MODE
/OUT,SCRATCH
SOLVE
/OUT,
FINISH
/POST1
/WINDOW,2,RTOP
SET,1,1
PLDISP,1
/WINDOW,2,OFF
*GET,F2,MODE,1,FREQ
FINISH
/SOLU    
MODE,2             ! SECOND HARMONIC MODE
/OUT,SCRATCH
SOLVE
/OUT,
FINISH
/POST1
/WINDOW,3,BOT
SET,1,1
PLDISP,1
*GET,F3,MODE,1,FREQ
*status,parm
*DIM,LABEL,CHAR,3,2
*DIM,VALUE,,3,3
LABEL(1,1) = 'F(0,1) ','F(1,1) ','F(1,2) '
LABEL(1,2) = 'Hz ','Hz ','Hz '
*VFILL,VALUE(1,1),DATA,269.96,756.13,1391.3
*VFILL,VALUE(1,2),DATA,F1,F2,F3
*VFILL,VALUE(1,3),DATA,ABS(F1/269.96),ABS(F2/756.13),ABS(F3/1391.3)
/COM
/OUT,vm151,vrt
/COM,------------------- VM151 RESULTS COMPARISON -------------
/COM,
/COM,                 |    TARGET   |    Mechanical APDL   |    RATIO
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F11.2,'  ',F14.2,'   ',1F16.3)
/COM,----------------------------------------------------------
/OUT
FINISH
*LIST,vm151,vrt

/COM,ANSYS MEDIA REL. 2023R1 (11/04/2022) REF. VERIF. MANUAL: REL. 2023R1
/VERIFY,VM125
/PREP7
/TITLE, VM125, RADIATION HEAT TRANSFER BETWEEN CONCENTRIC CYLINDERS
C*** PRINCIPLES OF HEAT TRANSFER, KREITH, 2ND PRINTING, PAGE 260
ANTYPE,STATIC
ET,1,LINK33,,,,,,,1    ! HEAT CONDUCTING BAR; SUPPRESS SOLUTION OUTPUT
R,1,1                  ! UNIT CROSS-SECTIONAL AREA (ARBITRARY)
MP,KXX,1,1             ! ARBITRARY CONDUCTIVITY DEFINED FOR
MP,KXX,2,1             !        INNER AND OUTER CYLINDERS
K,1
K,2,0,0,-1
K,3,-5
K,4,0,0,1
CIRCLE,1,1,2,3,,18     ! INNER CIRCLE; GENERATED CLOCKWISE
MAT,1
ESIZE,,1
LMESH,ALL
CIRCLE,1,2,4,3,,18     ! OUTER CIRCLE; GENERATED ANTI-CLOCKWISE
MAT,2
LMESH,19,36
FINISH
/AUX12
EMIS,1,.7
EMIS,2,.5
VTYPE,0                ! HIDDEN PROCEDURE FOR VIEW FACTORS
GEOM,1                 ! GEOMETRY SPECIFICATION 2-D
WRITE,VM125            ! WRITE RADIATION MATRIX TO FILE VM125.SUB
FINISH
/PREP7
DOF,TEMP
ET,2,MATRIX50,1,,,,,1  ! SUPERELEMENT (RADIATION MATRIX)
TYPE,2
SE,VM125
TOFFST,460.0           ! TEMPERATURE OFFSET FOR ABSOLUTE SCALE
LSEL,S,LINE,,1,18 
NSLL,S,1               ! SELECT INNER CYLINDER NODES
D,ALL,TEMP,540         ! T1 = 540 + 460 = 1000 DEG. R.
LSEL,S,LINE,,19,36
NSLL,S,1               ! SELECT OUTER CYLINDER NODES
D,ALL,TEMP,0.0         ! T2 = 460 DEG. R.
NSEL,ALL
LSEL,ALL
FINISH
/SOLU    
SOLVE
FINISH
/POST1
ESEL,S,ELEM,,1,18      ! SELECT INNER ELEMENTS
NSLE                   ! AND ASSOCIATED NODES
PRRSOL                 ! PRINT HEAT FLOW FROM INNER TO OUTER CYLINDER
NSEL,INVE
PRRSOL                 ! PRINT HEAT FLOW FROM OUTER TO INNER CYLINDER
ESEL,ALL
FSUM,HEAT
*GET,Q,FSUM,0,ITEM,HEAT
*DIM,LABEL,CHAR,1,2
*DIM,VALUE,,1,3
LABEL(1,1) = 'Q(BTU/hr'
LABEL(1,2) = '-in)'
*VFILL,VALUE(1,1),DATA,37
*VFILL,VALUE(1,2),DATA,Q
*VFILL,VALUE(1,3),DATA,ABS(Q/37)
/COM
/OUT,vm125,vrt
/COM,------------------- VM125 RESULTS COMPARISON --------------
/COM,
/COM,                 |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.1,'  ',F12.1,'   ',1F15.3)
/COM,-----------------------------------------------------------
/COM,
/OUT
FINISH
/DELETE,VM125,sub
*LIST,vm125,vrt


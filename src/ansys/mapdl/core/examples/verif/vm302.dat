/COM,ANSYS MEDIA REL. 2023R1 (11/04/2022) REF. VERIF. MANUAL: REL. 2023R1
/VERIFY,VM302
/COM,   REF: GIANCARLO GENTA, "DYNAMICS OF ROTATING SYSTEMS."
/COM,   2005: P. 535.
/COM
/TITLE, VM302, ROTATING CIRCULAR RING VIBRATION

/COM, ***** USING SHELL281
/UNITS,SI
JPGPRF,500,100,1

/PREP7

/COM, SPIN IN RPM
NBSTEP = 2
*DIM,SPIN,,NBSTEP
SPIN(1) = 0
SPIN(2) = 12000
RATIO   = ACOS(-1)/30

/COM, GEOMETRY PARAMETERS
OD    = 0.176
L     = 0.02
THICK = 0.00088
RMID  = (OD - THICK)/2

/COM, MATERIAL PROPERTIES - STEEL
E   = 2.1E11
RHO = 7850
NU  = 0.3
G   = E/(2*(1+NU))

/COM, TARGET RESULTS - REFERENCE FORMULAS
*SET,IY,L*(THICK**3)/12
*SET,C,0.3*THICK**3*L**3/(THICK**2+L**2)
*SET,M,RHO*THICK*L
*SET,D,2*ACOS(-1)
*SET,XI,E*IY/(G*C)
*DIM,REF,,9,2,NBSTEP
*DO,I,2,5                                   ! FREQUENCIES AT 0 RPM
   REF(I,1,1) = I*(I**2-1)*SQRT(E*IY/(M*(I**2+XI)))/(D*RMID**2)
   REF(I,2,1) = I*(I**2-1)*SQRT(E*IY/(M*(I**2+XI)))/(D*RMID**2)
*ENDDO
*DO,SP,2,NBSTEP
   FLAG = SPIN(SP)*ACOS(-1)/30
   *DO,I,2,5                                    ! FREQUENCIES AT GIVEN RPM
      A=2*I*FLAG/(I**2+1)
      B=I**2*(1-I**2)**2/((1+I**2)**2)
      REF(I,1,SP) = ABS(A-REF(I,1,1)*D*SQRT(1+(B*(FLAG/(D*REF(I,1,1)))**2)))/D
      REF(I,2,SP) = ABS(A+REF(I,1,1)*D*SQRT(1+(B*(FLAG/(D*REF(I,1,1)))**2)))/D
   *ENDDO
*ENDDO

PARSAV,ALL

/COM, GEOMETRY, MESH & BCS
ET,1,281                ! ELEMENT TYPE #1 : RING
MP,EX,1,E
MP,DENS,1,RHO
MP,NUXY,1,NU
SECTYPE,,SHELL
SECDATA,THICK

ET,2,170
R,2
ET,3,175
KEYOPT,3,2,2                ! MPC
KEYOPT,3,4,1                ! FORCE-DISTRIBUTED CONSTRAINT
KEYOPT,3,12,5               ! BONDED

K,1,0,0,0               ! CREATE RING GEOMETRY
K,2,0,0,L
CIRCLE,1,RMID,,,360
CIRCLE,2,RMID,,,360

TYPE,1
MAT,1
SECNUM,1
ESIZE,0.004
ASKIN,1,5
ASKIN,2,6
ASKIN,3,7
ASKIN,4,8
MSHAPE,0,2D
AMESH,ALL
CM,ALLELEM,ELEM         ! DEFINE THE ROTATING COMPONENT

N,10000,0,0,L/2
NSEL, ,LOC,Z,0
NSEL,A,LOC,Z,L
TYPE,3
REAL,2
ESURF
ALLSEL
TYPE,2
REAL,2
TSHAP,PILO
E,10000
TSHAP
FINISH

/OUT,SCRATCH_vm302
*DO,I,1,NBSTEP
   /SOLU
   ANTYPE,STATIC                    ! PERFORM STATIC ANALYSIS.

   NSUB,2,20
   NLGEOM,ON

   D,10000,ALL,0

   CORIOLIS,ON                      ! CORIOLIS ON IN A ROTATING REFERENCE FRAME
   CMOMEGA,ALLELEM,,,SPIN(I)*RATIO
   CAMPBELL,RSTP                    ! CAMPBELL FOR LP MODAL

   SOLVE
   FINI

   /SOLU
   ANTYPE,STATIC, RESTART,,,PERTURB             ! PERFORM A STATIC RESTART WITH
                            ! PERTURBATION FROM THE LAST LOAD STEP AND
                            ! SUBSTEP OF THE PREVIOUS STATIC SOLVE
   PERTURB, MODAL                           ! SET THE ANALYSIS OPTIONS FOR PERTURBED
                            ! MODAL ANALYSIS - DEFAULT TO INNERKEEP
   SOLVE, ELFORM                                ! REFORM ELEMENT MATRICES

   MODOPT,DAMP,30,10                    ! DAMP EIGENSOLVER
   MXPAND,30
   SOLVE
   FINI
*ENDDO

/POST1
FILE,,rstp

/SHOW,JPEG,REV
SET,2,21
LOCAL,12,0,0,0,0,0,30
DSYS,12
PLNSOL,U,SUM
/SHOW,CLOSE

PRCAMP,OFF,,RPM,,ALLELEM,,YES,,1                    ! PRINT CAMPBELL VALUES, UNIT= RPM
*GET,MODE1,CAMP,1,FREQ,1
*GET,MODE2,CAMP,2,FREQ,1
*GET,MODE3,CAMP,5,FREQ,1
*GET,MODE4,CAMP,6,FREQ,1
*GET,MODE5,CAMP,7,FREQ,1
*GET,MODE6,CAMP,8,FREQ,1
*GET,MODE7,CAMP,9,FREQ,1
*GET,MODE8,CAMP,10,FREQ,1

*GET,MODE21,CAMP,1,FREQ,2
*GET,MODE22,CAMP,3,FREQ,2
*GET,MODE23,CAMP,2,FREQ,2
*GET,MODE24,CAMP,6,FREQ,2
*GET,MODE25,CAMP,7,FREQ,2
*GET,MODE26,CAMP,9,FREQ,2
*GET,MODE27,CAMP,11,FREQ,2
*GET,MODE28,CAMP,12,FREQ,2

*DIM,LABEL,CHAR,1,16
*DIM,VALUE,,16,3
LABEL(1,1) = 'MODE1'
LABEL(1,2) = 'MODE2'
LABEL(1,3) = 'MODE3'
LABEL(1,4) = 'MODE4'
LABEL(1,5) = 'MODE5'
LABEL(1,6) = 'MODE6'
LABEL(1,7) = 'MODE7'
LABEL(1,8) = 'MODE8'
LABEL(1,9) = 'MODE1'
LABEL(1,10) = 'MODE2'
LABEL(1,11) = 'MODE3'
LABEL(1,12) = 'MODE4'
LABEL(1,13) = 'MODE5'
LABEL(1,14) = 'MODE6'
LABEL(1,15) = 'MODE7'
LABEL(1,16) = 'MODE8'

/COM,
/COM, FREQUENCIES AT 0 RPM
/COM,
*VFILL,VALUE(1,1),DATA,REF(2,1,1)
*VFILL,VALUE(1,2),DATA,MODE1
*VFILL,VALUE(1,3),DATA,ABS(MODE1/REF(2,1,1))
*VFILL,VALUE(2,1),DATA,REF(2,2,1)
*VFILL,VALUE(2,2),DATA,MODE2
*VFILL,VALUE(2,3),DATA,ABS(MODE2/REF(2,2,1))
*VFILL,VALUE(3,1),DATA,REF(3,1,1)
*VFILL,VALUE(3,2),DATA,MODE3
*VFILL,VALUE(3,3),DATA,ABS(MODE3/REF(3,1,1))
*VFILL,VALUE(4,1),DATA,REF(3,2,1)
*VFILL,VALUE(4,2),DATA,MODE4
*VFILL,VALUE(4,3),DATA,ABS(MODE4/REF(3,2,1))
*VFILL,VALUE(5,1),DATA,REF(4,1,1)
*VFILL,VALUE(5,2),DATA,MODE5
*VFILL,VALUE(5,3),DATA,ABS(MODE5/REF(4,1,1))
*VFILL,VALUE(6,1),DATA,REF(4,2,1)
*VFILL,VALUE(6,2),DATA,MODE6
*VFILL,VALUE(6,3),DATA,ABS(MODE6/REF(4,2,1))
*VFILL,VALUE(7,1),DATA,REF(5,1,1)
*VFILL,VALUE(7,2),DATA,MODE7
*VFILL,VALUE(7,3),DATA,ABS(MODE7/REF(5,1,1))
*VFILL,VALUE(8,1),DATA,REF(5,2,1)
*VFILL,VALUE(8,2),DATA,MODE8
*VFILL,VALUE(8,3),DATA,ABS(MODE8/REF(5,2,1))
/COM,
/COM, FREQUENCIES FOR SPIN = 12000 RPM
/COM,
*VFILL,VALUE(9,1),DATA,REF(2,1,2)
*VFILL,VALUE(9,2),DATA,MODE21
*VFILL,VALUE(9,3),DATA,ABS(MODE21/REF(2,1,2))
*VFILL,VALUE(10,1),DATA,REF(2,2,2)
*VFILL,VALUE(10,2),DATA,MODE22
*VFILL,VALUE(10,3),DATA,ABS(MODE22/REF(2,2,2))
*VFILL,VALUE(11,1),DATA,REF(3,1,2)
*VFILL,VALUE(11,2),DATA,MODE23
*VFILL,VALUE(11,3),DATA,ABS(MODE23/REF(3,1,2))
*VFILL,VALUE(12,1),DATA,REF(3,2,2)
*VFILL,VALUE(12,2),DATA,MODE24
*VFILL,VALUE(12,3),DATA,ABS(MODE24/REF(3,2,2))
*VFILL,VALUE(13,1),DATA,REF(4,1,2)
*VFILL,VALUE(13,2),DATA,MODE25
*VFILL,VALUE(13,3),DATA,ABS(MODE25/REF(4,1,2))
*VFILL,VALUE(14,1),DATA,REF(4,2,2)
*VFILL,VALUE(14,2),DATA,MODE26
*VFILL,VALUE(14,3),DATA,ABS(MODE26/REF(4,2,2))
*VFILL,VALUE(15,1),DATA,REF(5,1,2)
*VFILL,VALUE(15,2),DATA,MODE27
*VFILL,VALUE(15,3),DATA,ABS(MODE27/REF(5,1,2))
*VFILL,VALUE(16,1),DATA,REF(5,2,2)
*VFILL,VALUE(16,2),DATA,MODE28
*VFILL,VALUE(16,3),DATA,ABS(MODE28/REF(5,2,2))
SAVE,TABLE_1
/OUT,
FINI
/CLEAR,NOSTART
/TITLE, VM302, ROTATING CIRCULAR RING VIBRATION

/COM, ***** USING SOLID186
/UNITS,SI
JPGPRF,500,100,1

/PREP7

PARRES,CHANGE                   ! RETREIVE PARAMETERS
NDVW = 5
NDVC = 35

/COM, GEOMETRY, MESH & BCS
ET,1,186
MP,EX,1,E
MP,DENS,1,RHO
MP,NUXY,1,NU

ET,2,170
R,2
ET,3,175
KEYOPT,3,2,2                ! MPC
KEYOPT,3,4,1                ! FORCE-DISTRIBUTED CONSTRAINT
KEYOPT,3,12,5               ! BONDED

CYLIND,OD/2-THICK,OD/2,0,L
TYPE,1
MAT,1
LSEL,S,LINE,,17,20,1
LESIZE,ALL,,,NDVW
LSEL,S,LINE,,1,16,1
LESIZE,ALL,,,NDVC
VSWEEP,ALL
ALLSEL
CM,ALLELEM,ELEM             ! DEFINE THE ROTATING COMPONENT

N,10000,0,0,L/2

NSEL,,LOC,Z,0
NSEL,A,LOC,Z,L
TYPE,3
REAL,2
ESURF
ALLSEL
TYPE,2
REAL,2
TSHAP,PILO
E,10000
TSHAP
FINISH

/OUT,SCRATCH_vm302,,,append
*DO,I,1,NBSTEP
   /SOLU
   ANTYPE,STATIC            ! PERFORM STATIC ANALYSIS

   NSUB,2,20
   NLGEOM,ON

   D,10000,ALL,0

   CORIOLIS,ON              ! CORIOLIS ON IN A ROTATING REFERENCE FRAME
   CMOMEGA,ALLELEM,,,SPIN(I)*RATIO
   CAMPBELL,RSTP            ! CAMPBELL FOR LP MODAL

   SOLVE
   FINI

   /SOLU
   ANTYPE,STATIC, RESTART,,,PERTURB     ! PERFORM A STATIC RESTART WITH
                    ! PERTURBATION FROM THE LAST LOAD STEP AND
                    ! SUBSTEP OF THE PREVIOUS STATIC SOLVE
   PERTURB, MODAL                   ! SET THE ANALYSIS OPTIONS FOR PERTURBED
                    ! MODAL ANALYSIS - DEFAULT TO INNERKEEP
   SOLVE, ELFORM                    ! REFORM ELEMENT MATRICES

   MODOPT,DAMP,30,10            ! DAMP EIGENSOLVER
   MXPAND,30
   SOLVE
   FINI
*ENDDO

/POST1
FILE,,rstp

/SHOW,JPEG,REV
SET,2,21
LOCAL,12,0,0,0,0,0,30
DSYS,12
PLNSOL,U,SUM
/SHOW,CLOSE

PRCAMP,OFF,,RPM,,ALLELEM,,YES,,1        ! PRINT CAMPBELL VALUES, UNIT= RPM
*GET,MODE1,CAMP,1,FREQ,1
*GET,MODE2,CAMP,2,FREQ,1
*GET,MODE3,CAMP,5,FREQ,1
*GET,MODE4,CAMP,6,FREQ,1
*GET,MODE5,CAMP,7,FREQ,1
*GET,MODE6,CAMP,8,FREQ,1
*GET,MODE7,CAMP,9,FREQ,1
*GET,MODE8,CAMP,10,FREQ,1

*GET,MODE21,CAMP,1,FREQ,2
*GET,MODE22,CAMP,3,FREQ,2
*GET,MODE23,CAMP,2,FREQ,2
*GET,MODE24,CAMP,6,FREQ,2
*GET,MODE25,CAMP,7,FREQ,2
*GET,MODE26,CAMP,9,FREQ,2
*GET,MODE27,CAMP,11,FREQ,2
*GET,MODE28,CAMP,12,FREQ,2

*DIM,LABEL,CHAR,1,16
*DIM,VALUE,,16,3
LABEL(1,1) = 'MODE1'
LABEL(1,2) = 'MODE2'
LABEL(1,3) = 'MODE3'
LABEL(1,4) = 'MODE4'
LABEL(1,5) = 'MODE5'
LABEL(1,6) = 'MODE6'
LABEL(1,7) = 'MODE7'
LABEL(1,8) = 'MODE8'
LABEL(1,9) = 'MODE1'
LABEL(1,10) = 'MODE2'
LABEL(1,11) = 'MODE3'
LABEL(1,12) = 'MODE4'
LABEL(1,13) = 'MODE5'
LABEL(1,14) = 'MODE6'
LABEL(1,15) = 'MODE7'
LABEL(1,16) = 'MODE8'

/COM,
/COM, FREQUENCIES AT 0 RPM
/COM,
*VFILL,VALUE(1,1),DATA,REF(2,1,1)
*VFILL,VALUE(1,2),DATA,MODE1
*VFILL,VALUE(1,3),DATA,ABS(MODE1/REF(2,1,1))
*VFILL,VALUE(2,1),DATA,REF(2,2,1)
*VFILL,VALUE(2,2),DATA,MODE2
*VFILL,VALUE(2,3),DATA,ABS(MODE2/REF(2,2,1))
*VFILL,VALUE(3,1),DATA,REF(3,1,1)
*VFILL,VALUE(3,2),DATA,MODE3
*VFILL,VALUE(3,3),DATA,ABS(MODE3/REF(3,1,1))
*VFILL,VALUE(4,1),DATA,REF(3,2,1)
*VFILL,VALUE(4,2),DATA,MODE4
*VFILL,VALUE(4,3),DATA,ABS(MODE4/REF(3,2,1))
*VFILL,VALUE(5,1),DATA,REF(4,1,1)
*VFILL,VALUE(5,2),DATA,MODE5
*VFILL,VALUE(5,3),DATA,ABS(MODE5/REF(4,1,1))
*VFILL,VALUE(6,1),DATA,REF(4,2,1)
*VFILL,VALUE(6,2),DATA,MODE6
*VFILL,VALUE(6,3),DATA,ABS(MODE6/REF(4,2,1))
*VFILL,VALUE(7,1),DATA,REF(5,1,1)
*VFILL,VALUE(7,2),DATA,MODE7
*VFILL,VALUE(7,3),DATA,ABS(MODE7/REF(5,1,1))
*VFILL,VALUE(8,1),DATA,REF(5,2,1)
*VFILL,VALUE(8,2),DATA,MODE8
*VFILL,VALUE(8,3),DATA,ABS(MODE8/REF(5,2,1))
/COM,
/COM, FREQUENCIES AT SPIN = 12000 RPM
/COM,
*VFILL,VALUE(9,1),DATA,REF(2,1,2)
*VFILL,VALUE(9,2),DATA,MODE21
*VFILL,VALUE(9,3),DATA,ABS(MODE21/REF(2,1,2))
*VFILL,VALUE(10,1),DATA,REF(2,2,2)
*VFILL,VALUE(10,2),DATA,MODE22
*VFILL,VALUE(10,3),DATA,ABS(MODE22/REF(2,2,2))
*VFILL,VALUE(11,1),DATA,REF(3,1,2)
*VFILL,VALUE(11,2),DATA,MODE23
*VFILL,VALUE(11,3),DATA,ABS(MODE23/REF(3,1,2))
*VFILL,VALUE(12,1),DATA,REF(3,2,2)
*VFILL,VALUE(12,2),DATA,MODE24
*VFILL,VALUE(12,3),DATA,ABS(MODE24/REF(3,2,2))
*VFILL,VALUE(13,1),DATA,REF(4,1,2)
*VFILL,VALUE(13,2),DATA,MODE25
*VFILL,VALUE(13,3),DATA,ABS(MODE25/REF(4,1,2))
*VFILL,VALUE(14,1),DATA,REF(4,2,2)
*VFILL,VALUE(14,2),DATA,MODE26
*VFILL,VALUE(14,3),DATA,ABS(MODE26/REF(4,2,2))
*VFILL,VALUE(15,1),DATA,REF(5,1,2)
*VFILL,VALUE(15,2),DATA,MODE27
*VFILL,VALUE(15,3),DATA,ABS(MODE27/REF(5,1,2))
*VFILL,VALUE(16,1),DATA,REF(5,2,2)
*VFILL,VALUE(16,2),DATA,MODE28
*VFILL,VALUE(16,3),DATA,ABS(MODE28/REF(5,2,2))
SAVE,TABLE_2
/OUT,
FINI

/CLEAR,NOSTART
/NOPR,
/OUT,vm302,vrt
/COM,------------ VM302 RESULTS COMPARISON --------------
/COM,
RESUME,TABLE_1
/COM,            ***** USING SHELL281 *****
/com,
/COM,        |   TARGET   |   MECHANICAL APDL   |   RATIO
/COM,
/COM, ---------------------------------------
/COM, FREQUENCIES AT 0 RPM
/COM, ---------------------------------------
*VWRITE,LABEL(1,1),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,'   ',F10.4,'  ',F14.4,'   ',F15.3)
*VWRITE,LABEL(1,2),VALUE(2,1),VALUE(2,2),VALUE(2,3)
(1X,A8,'   ',F10.4,'  ',F14.4,'   ',F15.3)
*VWRITE,LABEL(1,3),VALUE(3,1),VALUE(3,2),VALUE(3,3)
(1X,A8,'   ',F10.4,'  ',F14.4,'   ',F15.3)
*VWRITE,LABEL(1,4),VALUE(4,1),VALUE(4,2),VALUE(4,3)
(1X,A8,'   ',F10.4,'  ',F14.4,'   ',F15.3)
*VWRITE,LABEL(1,5),VALUE(5,1),VALUE(5,2),VALUE(5,3)
(1X,A8,'   ',F10.4,'  ',F14.4,'   ',F15.3)
*VWRITE,LABEL(1,6),VALUE(6,1),VALUE(6,2),VALUE(6,3)
(1X,A8,'   ',F10.4,'  ',F14.4,'   ',F15.3)
*VWRITE,LABEL(1,7),VALUE(7,1),VALUE(7,2),VALUE(7,3)
(1X,A8,'   ',F10.4,'  ',F14.4,'   ',F15.3)
*VWRITE,LABEL(1,8),VALUE(8,1),VALUE(8,2),VALUE(8,3)
(1X,A8,'   ',F10.4,'  ',F14.4,'   ',F15.3)
/COM,
/COM, -------------------------------------
/COM, FREQUENCIES AT SPIN = 12 000 RPM
/COM, -------------------------------------
*VWRITE,LABEL(1,9),VALUE(9,1),VALUE(9,2),VALUE(9,3)
(1X,A8,'   ',F10.4,'  ',F14.4,'   ',F15.3)
*VWRITE,LABEL(1,10),VALUE(10,1),VALUE(10,2),VALUE(10,3)
(1X,A8,'   ',F10.4,'  ',F14.4,'   ',F15.3)
*VWRITE,LABEL(1,11),VALUE(11,1),VALUE(11,2),VALUE(11,3)
(1X,A8,'   ',F10.4,'  ',F14.4,'   ',F15.3)
*VWRITE,LABEL(1,12),VALUE(12,1),VALUE(12,2),VALUE(12,3)
(1X,A8,'   ',F10.4,'  ',F14.4,'   ',F15.3)
*VWRITE,LABEL(1,13),VALUE(13,1),VALUE(13,2),VALUE(13,3)
(1X,A8,'   ',F10.4,'  ',F14.4,'   ',F15.3)
*VWRITE,LABEL(1,14),VALUE(14,1),VALUE(14,2),VALUE(14,3)
(1X,A8,'   ',F10.4,'  ',F14.4,'   ',F15.3)
*VWRITE,LABEL(1,15),VALUE(15,1),VALUE(15,2),VALUE(15,3)
(1X,A8,'   ',F10.4,'  ',F14.4,'   ',F15.3)
*VWRITE,LABEL(1,16),VALUE(16,1),VALUE(16,2),VALUE(16,3)
(1X,A8,'   ',F10.4,'  ',F14.4,'   ',F15.3)
/COM, ---------------------------------------------------
/COM,
/COM,
RESUME,TABLE_2
/COM,------------ VM302 RESULTS COMPARISON --------------
/COM,
/COM,            ***** USING SOLID186 *****
/com,
/COM,        |   TARGET   |   MECHANICAL APDL   |   RATIO
/COM,
/COM, ---------------------------------------
/COM, FREQUENCIES AT 0 RPM
/COM, ---------------------------------------
*VWRITE,LABEL(1,1),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,'   ',F10.4,'  ',F14.4,'   ',F15.3)
*VWRITE,LABEL(1,2),VALUE(2,1),VALUE(2,2),VALUE(2,3)
(1X,A8,'   ',F10.4,'  ',F14.4,'   ',F15.3)
*VWRITE,LABEL(1,3),VALUE(3,1),VALUE(3,2),VALUE(3,3)
(1X,A8,'   ',F10.4,'  ',F14.4,'   ',F15.3)
*VWRITE,LABEL(1,4),VALUE(4,1),VALUE(4,2),VALUE(4,3)
(1X,A8,'   ',F10.4,'  ',F14.4,'   ',F15.3)
*VWRITE,LABEL(1,5),VALUE(5,1),VALUE(5,2),VALUE(5,3)
(1X,A8,'   ',F10.4,'  ',F14.4,'   ',F15.3)
*VWRITE,LABEL(1,6),VALUE(6,1),VALUE(6,2),VALUE(6,3)
(1X,A8,'   ',F10.4,'  ',F14.4,'   ',F15.3)
*VWRITE,LABEL(1,7),VALUE(7,1),VALUE(7,2),VALUE(7,3)
(1X,A8,'   ',F10.4,'  ',F14.4,'   ',F15.3)
*VWRITE,LABEL(1,8),VALUE(8,1),VALUE(8,2),VALUE(8,3)
(1X,A8,'   ',F10.4,'  ',F14.4,'   ',F15.3)
/COM,
/COM, -------------------------------------
/COM, FREQUENCIES AT SPIN = 12000 RPM
/COM, -------------------------------------
*VWRITE,LABEL(1,9),VALUE(9,1),VALUE(9,2),VALUE(9,3)
(1X,A8,'   ',F10.4,'  ',F14.4,'   ',F15.3)
*VWRITE,LABEL(1,10),VALUE(10,1),VALUE(10,2),VALUE(10,3)
(1X,A8,'   ',F10.4,'  ',F14.4,'   ',F15.3)
*VWRITE,LABEL(1,11),VALUE(11,1),VALUE(11,2),VALUE(11,3)
(1X,A8,'   ',F10.4,'  ',F14.4,'   ',F15.3)
*VWRITE,LABEL(1,12),VALUE(12,1),VALUE(12,2),VALUE(12,3)
(1X,A8,'   ',F10.4,'  ',F14.4,'   ',F15.3)
*VWRITE,LABEL(1,13),VALUE(13,1),VALUE(13,2),VALUE(13,3)
(1X,A8,'   ',F10.4,'  ',F14.4,'   ',F15.3)
*VWRITE,LABEL(1,14),VALUE(14,1),VALUE(14,2),VALUE(14,3)
(1X,A8,'   ',F10.4,'  ',F14.4,'   ',F15.3)
*VWRITE,LABEL(1,15),VALUE(15,1),VALUE(15,2),VALUE(15,3)
(1X,A8,'   ',F10.4,'  ',F14.4,'   ',F15.3)
*VWRITE,LABEL(1,16),VALUE(16,1),VALUE(16,2),VALUE(16,3)
(1X,A8,'   ',F10.4,'  ',F14.4,'   ',F15.3)
/COM, --------------------------------------------------
/OUT
FINISH
*LIST,vm302,vrt
/EXIT,NOSAVE

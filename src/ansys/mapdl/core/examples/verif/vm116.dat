/COM,ANSYS MEDIA REL. 150 (11/8/2013) REF. VERIF. MANUAL: REL. 150
/VERIFY,VM116
JPGPRF,500,100,1             ! MACRO TO SET PREFS FOR JPEG PLOTS
/SHOW,JPEG
/PREP7 
/TITLE, VM116, HEAT CONDUCTING PLATE WITH SUDDEN COOLING
C***    PRINCIPLES OF HEAT TRANSFER, KREITH, 2ND. PRINTING, PAGE 161, EX. 4-11
ANTYPE,TRANS
ET,1,LINK34,,3     ! CONVECTION LINK, USE (TI-TJ) FOR HF EVALUATION
ET,2,LINK33        ! HEAT CONDUCTION BAR
R,1,1              ! UNIT AREA ASSUMED
MP,KXX,1,2         ! CONDUCTIVITY, DENSITY AND SPECIFIC HEAT
MP,DENS,1,800      !  INPUT USED BY CONDUCTION ELEMENTS
MP,C,1,0.833
MP,HF,1,2,.02      ! TEMPERATURE DEPENDENT HF (USED FOR ELEM 1)
N,1                ! NODES 1 AND 2 DEFINE THE CONVECTION LINK
N,2                !  (ARBIRARY LENGTH)
N,10,(8/12)        ! CONDUCTION LENGTH IN FT.
FILL
E,1,2              ! ELEMENT 1 IS CONVECTION LINK
TYPE,2             ! ELEMENTS 2 THROUGH 9 ARE CONDUCTION BARS
E,2,3
EGEN,8,1,-1
FINISH
/SOLU    
D,2,TEMP,500       ! INITIAL SURFACE TEMPERATURES
D,10,TEMP,100
OUTPR,,LAST 
OUTRES,,ALL
TIMINT,OFF         ! TURN OFF TIME INTEGRATION FOR
TIME,0.001         !  INITIAL STEADY-STATE CONDITION
SOLVE
TIMINT,ON          ! TURN ON TIME INTEGRATION ON FOR
TIME,7             !  TRANSIENT OVER 7 HRS
DDELE,2,TEMP       ! DELETE NODAL TEMPERATURE
D,1,TEMP,100       ! ENVIRONMENT TEMPERATURE IS DECREASED
KBC,1              !  SUDDENLY
AUTOTS,ON
NSUBST,20
SOLVE
FINISH

/POST26
NSOL,2,2,TEMP
PRVAR,2            ! PRINT TEMPERATURE HISTORY AT NODE 2
/AXLAB,Y,TEMP
/GRID,1
PLVAR,2
FINISH

/POST1
ETABLE,TI,SMISC,2  ! NODAL TEMPERATURES FOR CONDUCTION ELEMENTS
ETABLE,TJ,SMISC,3
PLLS,TI,TJ         ! DISPLAY TEMPERATURE VARIATION ACROSS PLATE (AT 7 HRS)
PRNSOL,TEMP
*GET,TN2,NODE,2,TEMP
*DIM,LABEL,CHAR,1,2
*DIM,VALUE,,1,3
LABEL(1,1) = 'T,F(AT X'
LABEL(1,2) = '=0.0 in)'
*VFILL,VALUE(1,1),DATA,285
*VFILL,VALUE(1,2),DATA,TN2
*VFILL,VALUE(1,3),DATA,ABS(TN2/285)
/COM
/OUT,vm116,vrt
/COM,------------------- VM116 RESULTS COMPARISON -------------
/COM,
/COM,                 |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.0,'  ',F12.0,'   ',1F15.2)
/COM,----------------------------------------------------------
/OUT
FINISH
*LIST,vm116,vrt


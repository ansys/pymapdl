/COM,ANSYS MEDIA REL. 2023R1 (11/04/2022) REF. VERIF. MANUAL: REL. 2023R1
/VERIFY,VM75
/out,scratch
/PREP7                    
JPGPRF,500,100,1             ! MACRO TO SET PREFS FOR JPEG PLOTS
/SHOW,JPEG
/TITLE, VM75, TRANSIENT RESPONSE TO A STEP EXCITATION
C*** VIBRATION THEORY AND APPLICATIONS, THOMSON, 2ND PRINTING, PAGE 102, ART 4.3
ET,1,COMBIN40,,,2,,,2     ! Y DOF ELEMENTS, MASS AT NODE J
R,1,200,,.5               ! TWO DAMPING RATIOS
R,2,200,10,.5
N,1
N,4
FILL
E,1,2
REAL,2
E,3,4
D,1,UY,,,3,2
FINISH

/SOLU
ANTYPE,MODAL                ! PERFORM MODAL SOLVE
MODOPT,QRDAMP,2             ! USE QRDAMP EIGENSOLVER
MXPAND,2,,,YES
SOLVE
FINISH

/SOLU
ANTYPE,TRANSIENT
TRNOPT,MSUP,2             ! PERFORM MODE SUPERPOSITION TRANSIENT SOLVE 
DELTIM,25E-4              ! INTEGRATION TIME STEP SIZE
KBC,1                     ! STEP BOUNDARY CONDITIONS
OUTPR,BASIC,1
OUTRES,NSOL,1
F,2,FY,,,4,2
SOLVE

TIME,.205                 ! TIME AT END OF LOAD STEP
F,2,FY,200,,4,2
SOLVE
FINISH

/POST26
FILE,,rdsp
NSOL,2,2,U,Y,2UY          ! STORE UY DISPLACEMENTS OF APPROPRIATE NODES
NSOL,3,4,U,Y,4UY
NPRINT,10                 ! PRINT EVERY 10 POINTS
PRVAR,2,3                 ! PRINT VARIABLES 2 AND 3
/GRID,1                   ! TURN GRID ON
/AXLAB,Y,DISP             ! Y-AXIS LABEL AS DISP
PLVAR,2,3                 ! DISPLAY VARIABLES 2 AND 3
*GET,UMAX,VARI,2,RTIME,.1575
*GET,U0,VARI,2,RTIME,.2
*GET,U5,VARI,3,RTIME,.2
*DIM,LABEL_1,CHAR,1,2
*DIM,VALUE_1,,1,3
LABEL_1(1,1) = 'UMAX,   '
LABEL_1(1,2) = 'in      '
*VFILL,VALUE_1(1,1),DATA,2 
*VFILL,VALUE_1(1,2),DATA,UMAX
*VFILL,VALUE_1(1,3),DATA,ABS(UMAX/2)
*DIM,LABEL_2,CHAR,2,2
*DIM,VALUE_2,,2,3
LABEL_2(1,1) = 'U,in(DAM','U,in(DAM'
LABEL_2(1,2) = 'PING=0) ','PING=.5)'
*VFILL,VALUE_2(1,1),DATA,1.6536,1.1531
*VFILL,VALUE_2(1,2),DATA,U0,U5
*VFILL,VALUE_2(1,3),DATA,ABS(U0/1.6536),ABS(U5/1.1531)
/COM
/OUT,vm75,vrt
/COM,------------------- VM75 RESULTS COMPARISON --------------
/COM,
/COM,                 |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
/COM,TIME = 0.1575 SEC:
*VWRITE,LABEL_1(1,1),LABEL_1(1,2),VALUE_1(1,1),VALUE_1(1,2),VALUE_1(1,3)
(1X,A8,A8,'   ',F10.4,'  ',F14.4,'   ',1F15.3)
/COM,
/COM,TIME = 0.20 SEC:
*VWRITE,LABEL_2(1,1),LABEL_2(1,2),VALUE_2(1,1),VALUE_2(1,2),VALUE_2(1,3)
(1X,A8,A8,'   ',F10.4,'  ',F14.4,'   ',1F15.3)
/COM,
/COM,----------------------------------------------------------
/OUT
FINISH
*LIST,vm75,vrt

/COM,ANSYS MEDIA REL. 150 (11/8/2013) REF. VERIF. MANUAL: REL. 150
/VERIFY,VM67
/PREP7
/TITLE, VM67, RADIAL VIBRATIONS OF A CIRCULAR RING FROM AN AXISYMMETRIC MODEL
C***         VIBRATION PROBS. IN ENGR., TIMOSHENKO, 3RD. ED., PAGE  425, ART. 68
C***         (AXISYMMETRIC) AND 2
ANTYPE,MODAL          ! MODE-FREQUENCY ANALYSIS
ET,1,PLANE25
MXPAND,1              ! EXPAND FIRST MODE
MP,EX,1,30E6
MP,DENS,1,73E-5
MP,NUXY,1,0           ! POISSON'S RATIO IS ZERO
LOCAL,11,,9.975       ! DEFINE LOCAL C.S. AT INSIDE SURFACE OF THE RING
N,1
N,2,,.05
NGEN,2,2,1,2,1,.05
E,1,3,4,2
CP,1,UX,1,2           ! COUPLE RADIAL DOF'S
FINISH
/SOLU    
OUTPR,ALL,1
D,ALL,UZ,0
D,1,UY,0
MODE,0,1
MODOPT,LANB,2
SOLVE
*GET,FREQ0,MODE,1,FREQ
FINISH
/SOLU    
OUTPR,ALL,1
DDELE,ALL             ! DELETE DISPLACEMENT CONSTRAINTS
D,1,UY
MODE,2,1              ! SYMMETRIC LOADING FOR MODE 2
MXPAND,1,0,100        ! RANGE OF FREQUENCIES OF INTEREST
SOLVE
*GET,FREQ2,MODE,1,FREQ
*DIM,LABEL,CHAR,2,2
*DIM,VALUE,,2,3
LABEL(1,1) = '     f0,','     f2,'
LABEL(1,2) = ' Hz     ',' Hz     '
*VFILL,VALUE(1,1),DATA,3226.4,12.496
*VFILL,VALUE(1,2),DATA,FREQ0,FREQ2
*VFILL,VALUE(1,3),DATA,ABS(FREQ0/3226.4 ) ,ABS( FREQ2/12.496)
/COM
/OUT,vm67,vrt
/COM,------------------- VM67 RESULTS COMPARISON --------------
/COM,
/COM,                 |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.3,'  ',F14.3,'   ',1F15.3)
/COM,----------------------------------------------------------
/OUT
FINISH
*LIST,vm67,vrt

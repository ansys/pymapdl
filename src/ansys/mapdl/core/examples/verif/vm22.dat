/COM,ANSYS MEDIA REL. 2023R1 (11/04/2022) REF. VERIF. MANUAL: REL. 2023R1
/VERIFY,VM22
/PREP7
/TITLE, VM22, SMALL DEFLECTION OF A BELLEVILLE SPRING
C*** STR. OF MATL., TIMOSHENKO, PART 2, 3RD ED., PAGE 143, PROB. 2
ANTYPE,STATIC
ET,1,SHELL208,,,2
SECTYPE,1,SHELL
SECDATA,0.2
SECNUM,1
MP,EX,1,3E7
MP,NUXY,1,0
N,1,1,(.5*TAN(.12217))      ! DEFINE NODES
N,2,1.5
E,1,2                       ! DEFINE ELEMENT
D,2,UY                      ! BOUNDARY CONDITIONS AND LOADS
F,1,FY,-628.31853
FINISH
/SOLU    
SOLVE
FINISH
/POST1
SET,1,1
NSEL,S,NODE,,1
PRNSOL,U,COMP               ! DISPLACEMENTS AT NODE 1
*GET,DEF,NODE,1,U,Y
*DIM,LABEL,CHAR,1,1
*DIM,VALUE,,1,3
LABEL(1,1) = 'UY (in)'
*VFILL,VALUE(1,1),DATA,-.0028205
*VFILL,VALUE(1,2),DATA,DEF
*VFILL,VALUE(1,3),DATA,ABS( DEF /.0028205)
/COM
/OUT,vm22,vrt
/COM,------------------- VM22 RESULTS COMPARISON ---------------------
/COM,
/COM,         |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
*VWRITE,LABEL(1,1),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,'   ',F10.7,'  ',F14.7,'   ',1F15.3)
/COM,-----------------------------------------------------------------
/OUT
FINISH
*LIST,vm22,vrt

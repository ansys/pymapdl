! Copyright (C) 2024 ANSYS, Inc. and/or its affiliates.
! SPDX-License-Identifier: MIT
! 
!
! Permission is hereby granted, free of charge, to any person obtaining a copy
! of this software and associated documentation files (the "Software"), to deal
! in the Software without restriction, including without limitation the rights
! to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
! copies of the Software, and to permit persons to whom the Software is
! furnished to do so, subject to the following conditions:
!
! The above copyright notice and this permission notice shall be included in all
! copies or substantial portions of the Software.
!
! THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
! IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
! FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
! AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
! LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
! OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
! SOFTWARE.

/COM,ANSYS MEDIA REL. 2023R2 (05/12/2023) REF. VERIF. MANUAL: REL. 2023R2
/VERIFY,vmr049-pl5b-183
/TITLE, vmr049-pl5b-183, PRESSURISED CYLINDER PLASTICITY BENCHMARK
/COM, REFERENCE: NAFEMS REPORT:R0049. TEST PL-5
/SHOW,JPEG
JPGPRF,500,100,1
/PREP7
ET,1,183,,,
KEYOPT,1,3,1    
N,1,100,0,,
N,2,100,50,,
N,3,100,100,,
N,4,125,0,,
N,5,125,100,,
N,6,150,0,,
N,7,150,50,,
N,8,150,100,,
N,9,175,0,,
N,10,175,100,,
N,11,200,0,,
N,12,200,50,,
N,13,200,100,,
E, 1,6,8,3,4,7,5,2
E, 6,11,13,8,9,12,10,7
MP,EX,1,21E3,
MP,NUXY,1,0.3,
EX=21E3
ET=4200
EP=EX*ET/(EX-ET)
TB,PLAS,1,,,BISO
TBDATA,1,24.0,EP
NSEL,U,LOC,Y,50	
D,ALL,UY,0.0
NSEL,ALL
FINISH

/SOLU
NLGEOM,ON
NSEL,S,LOC,X,100
SF,ALL,PRES,10.0
NSEL,ALL,
NSUBST,10,,,
OUTRES,ALL,ALL
/OUT,SCRATCH
SOLVE
NSEL,S,LOC,X,100
SF,ALL,PRES,14.0
NSEL,ALL,
NSUBST,10,,,
OUTRES,ALL,ALL
SOLVE
NSEL,S,LOC,X,100
SF,ALL,PRES,24.0
NSEL, ALL,
NSUBST,10,,,
SOLVE
NSEL,S,LOC,X,100
SF,ALL,PRES,34.0
NSEL,ALL,
NSUBST,10,,,
SOLVE
SAVE

/POST26
/GROPT,VIEW,0
/GTHK,CURVE,1
/GROPT,FILL,OFF
/GRID,1
/GTHK,GRID,1
/GROPT,CGRID,1
/AXLAB,X,STEP
/AXLAB,Y,STRESS
/GTHK,AXIS,1
/GRTYP,0
/XRANGE,0,4
/YRANGE,-40,60
/OUT,
ESOL,2,1,1,S,X,
ESOL,3,1,1,S,Y,
ESOL,4,1,1,S,Z,
ESOL,5,1,1,EPEL,X,
PLVAR,2,3,4
PRVAR,2,3,4,5

*DIM,VALUEX,ARRAY,4,1
*DO,JJ,1,4,1
*GET,VALUEX(JJ,1),VARI,2,RTIME,JJ
*ENDDO
R1=VALUEX(1,1)/(-8.603)
R2=VALUEX(2,1)/(-10.603)
R3=VALUEX(3,1)/(-19.307)
R4=VALUEX(4,1)/(-27.025)
*DIM,VALUEY,ARRAY,4,1
*DO,JJ,1,4,1
*GET,VALUEY(JJ,1),VARI,3,RTIME,JJ
*ENDDO
R5=VALUEY(1,1)/1.598
R6=VALUEY(2,1)/2.094
R7=VALUEY(3,1)/.964
R8=VALUEY(4,1)/3.632
*DIM,VALUEZ,ARRAY,4,1
*DO,JJ,1,4,1
*GET,VALUEZ(JJ,1),VARI,4,RTIME,JJ
*ENDDO
R9=VALUEZ(1,1)/15.706
R10=VALUEZ(2,1)/18.193
R11=VALUEZ(3,1)/22.820
R12=VALUEZ(4,1)/37.778
*DIM,STEP,CHAR,10
*DIM,TARGETX,CHAR,10
*DIM,TARGETY,CHAR,10
*DIM,TARGETZ,CHAR,10
*DIM,RATIOX,,4,1
*DIM,RATIOY,,4,1
*DIM,RATIOZ,,4,1
STEP(1)='1.0','2.0','3.0','4.0'
TARGETX(1)='-8.603','-10.603','-19.307','-27.025'
TARGETY(1)='1.598','2.094','0.964','3.632'
TARGETZ(1)='15.706','18.193','22.820','37.778'
*VFILL,RATIOX,DATA,R1,R2,R3,R4
*VFILL,RATIOY,DATA,R5,R6,R7,R8
*VFILL,RATIOZ,DATA,R9,R10,R11,R12
/COM,
/COM, ----------- vmr049-pl5b-183 RESULTS COMPARISON --------------
/COM,
/COM, vmr049-pl5b-183.jpeg RESULTS SHOULD MATCH R0049 NAFEMS MANUAL
/COM, GRAPH RESULTS ON PAGE 61 FIGURE 2.23(C).
/COM, THE RESULTS DISPLAYED ARE INCREMENTED FOR THIS PURPOSE.
/COM,
/COM,
/COM, ------ vmr049-pl5b-183 RADIAL STRESS RESULTS -------
/COM,
/COM, |  STEP  |  TARGET  |  Mechanical APDL  |  RATIO
/COM,
*VWRITE,STEP(1),TARGETX(1),VALUEX(1,1),RATIOX(1,1)
(1X,A8,'   ',1x,A8,'   ',F14.3,'   ',F12.3,'   ')
/COM,
/COM, ------ vmr049-pl5b-183 AXIAL STRESS RESULTS -------
/COM,
/COM, |  STEP  |  TARGET  |  Mechanical APDL  |  RATIO
/COM,
*VWRITE,STEP(1),TARGETY(1),VALUEY(1,1),RATIOY(1,1)
(1X,A8,'   ',1x,A8,'   ',F14.3,'   ',F12.3,'   ')
/COM,
/COM,
/COM, ------ vmr049-pl5b-183 HOOP STRESS RESULTS -------
/COM,
/COM, |  STEP  |  TARGET  |  Mechanical APDL  |  RATIO
/COM,
*VWRITE,STEP(1),TARGETZ(1),VALUEZ(1,1),RATIOZ(1,1)
(1X,A8,'   ',1x,A8,'   ',F14.3,'   ',F12.3,'   ')
/COM,
/COM,
FINISH

/POST26
*DIM,LABEL1,CHAR,3
*DIM,VALUE1,,3,3
LABEL1(1) = ' SRAD ',' SAXI ',' SHOOP '
*VFILL,VALUE1(1,1),DATA,VALUEX(4,1),VALUEY(4,1),VALUEZ(4,1)
*VFILL,VALUE1(1,2),DATA,R4,R8,R12
*DIM,LABEL2,CHAR,2
LABEL2(1) = 'vmr049-','pl5b-183'

/OUT,vmr049-pl5b-183,vrt
/COM
/COM,------------------- vmr049-pl5b RESULTS COMPARISON ---------------------
/COM,
/COM,            |   Mechanical APDL   |   RATIO  |         INPUT         |
/COM,
/COM, PLANE183
*VWRITE,LABEL1(1),VALUE1(1,1),VALUE1(1,2),LABEL2(1),LABEL2(2)
(1X,A8,'      ',F14.4,'      ',F9.4,'     ',A7,A8)
*VWRITE,LABEL1(2),VALUE1(2,1),VALUE1(2,2),LABEL2(1),LABEL2(2)
(1X,A8,'      ',F14.4,'      ',F9.4,'     ',A7,A8)
*VWRITE,LABEL1(3),VALUE1(3,1),VALUE1(3,2),LABEL2(1),LABEL2(2)
(1X,A8,'      ',F14.4,'      ',F9.4,'     ',A7,A8)
/COM,
/COM,----------------------------------------------------------------
/OUT

FINISH
*LIST,vmr049-pl5b-183,vrt

/COM,ANSYS MEDIA REL. 2023R1 (11/04/2022) REF. VERIF. MANUAL: REL. 2023R1
/VERIFY,VM10
/PREP7
MP,PRXY,,0.3
/TITLE, VM10, BENDING OF A TEE SHAPED BEAM
C***    MECHANICS OF SOLIDS, CRANDALL AND DAHL, 1959, PAGE 294, EX. 7.2
ANTYPE,STATIC
ET,1,BEAM188,,,3
SECT,1,BEAM,T
SECD,9,20,4,1.5
MP,EX,1,30E6
N,1
N,2,100
N,3,,1
E,1,2,3
D,1,ALL
F,2,MZ,100000
D,ALL,UZ,,,,,ROTX,ROTY
FINISH
/SOLU    
NSUBST,1
OUTPR,ALL,1
SOLVE
FINISH
/POST1
ETABLE,STRS_B,LS,1
ETABLE,STRS_T,LS,31
*GET,STRSS_B,ELEM,1,ETAB,STRS_B
*GET,STRSS_T,ELEM,1,ETAB,STRS_T
*DIM,LABEL,CHAR,2,2
*DIM,VALUE,,2,3
LABEL(1,1) = 'STRSS,(B','STRSS,(T'
LABEL(1,2) = 'OT) psi ','OP) psi '
*VFILL,VALUE(1,1),DATA,300,-700 
*VFILL,VALUE(1,2),DATA,STRSS_B,STRSS_T
*VFILL,VALUE(1,3),DATA,ABS(STRSS_B/300) ,ABS(STRSS_T/700 )
/COM
/OUT,vm10,vrt
/COM,------------------- VM10 RESULTS COMPARISON ---------------------
/COM,
/COM,                 |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.0,'  ',F10.0,'   ',1F5.3)
/COM,-----------------------------------------------------------------
/OUT

FINISH
*LIST,vm10,vrt

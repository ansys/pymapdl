! Copyright (C) 2024 ANSYS, Inc. and/or its affiliates.
! SPDX-License-Identifier: MIT
! 
!
! Permission is hereby granted, free of charge, to any person obtaining a copy
! of this software and associated documentation files (the "Software"), to deal
! in the Software without restriction, including without limitation the rights
! to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
! copies of the Software, and to permit persons to whom the Software is
! furnished to do so, subject to the following conditions:
!
! The above copyright notice and this permission notice shall be included in all
! copies or substantial portions of the Software.
!
! THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
! IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
! FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
! AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
! LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
! OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
! SOFTWARE.

/COM,ANSYS MEDIA REL. 2023R2 (05/12/2023) REF. VERIF. MANUAL: REL. 2023R2
/VERIFY,VM135
/TITLE, VM135, BENDING OF A BEAM ON AN ELASTIC FOUNDATION
C***    STR. OF MATL., TIMOSHENKO, PART 2, 3RD ED., PAGE 12
/PREP7
ET,1,BEAM189             ! BEAM     
SECTYPE,1,BEAM,RECT
SECDATA,5,4.224
MP,EX  ,1,30E6
MP,PRXY,1,0.3
ET,2,156,,,,,1            ! SURFACE EFFECT ELEMENT; NO ORIENTATION NODE
R,2,1515.15               ! FOUNDATION STIFFNESS

TYPE,1
REAL,1
N,1
N,27,286
FILL
*DO,i,1,25,2
 E,i,i+2,i+1
*ENDDO
TYPE,2
REAL,2
*DO,i,1,25,2
 E,i,i+2,i+1
*ENDDO
F,1,FY,-1000             ! APPLY DOWNWARD FORCE
F,1,MZ,10000             ! APPLY END MOMENT
D,1,UX
DSYM,SYMM,Z
OUTPR,,1
FINISH
/SOLU    
/OUT,SCRATCH
SOLVE
/OUT,
*GET,UY,NODE,1,U,Y
*status,parm
*DIM,LABEL,CHAR,1,2
*DIM,VALUE,,1,3
LABEL(1,1) = 'END DISP  '
LABEL(1,2) = ' in '
*VFILL,VALUE(1,1),DATA,-.03762
*VFILL,VALUE(1,2),DATA,UY
*VFILL,VALUE(1,3),DATA,ABS(UY/.03762)
/COM
/OUT,vm135,vrt
/COM,------------------- VM135 RESULTS COMPARISON -------------
/COM,
/COM,                 |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.5,'  ',F12.5,'   ',1F15.3)
/COM,----------------------------------------------------------
/OUT
/NOPR
FINISH
*LIST,vm135,vrt


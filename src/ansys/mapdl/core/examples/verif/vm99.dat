! Copyright (C) 2024 ANSYS, Inc. and/or its affiliates.
! SPDX-License-Identifier: MIT
! 
!
! Permission is hereby granted, free of charge, to any person obtaining a copy
! of this software and associated documentation files (the "Software"), to deal
! in the Software without restriction, including without limitation the rights
! to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
! copies of the Software, and to permit persons to whom the Software is
! furnished to do so, subject to the following conditions:
!
! The above copyright notice and this permission notice shall be included in all
! copies or substantial portions of the Software.
!
! THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
! IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
! FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
! AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
! LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
! OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
! SOFTWARE.

/COM,ANSYS MEDIA REL. 2023R2 (05/12/2023) REF. VERIF. MANUAL: REL. 2023R2
/VERIFY,VM99
/PREP7
/TITLE, VM99, TEMPERATURE DISTRIBUTION IN A TRAPEZOIDAL FIN
C***      CONDUCTION HEAT TRANSFER, SCHNEIDER, 2ND. PRINTING, PAGE 164, ART. 7-8
ANTYPE,STATIC
ET,1,PLANE55
MP,KXX,1,18
W=(.96/12)                         ! FIN LENGTH
N,7 
N,8,(W/2) 
N,3,W
N,1,W,-((2*W)/6)
FILL,1,3
N,6,,-(W/6)
FILL,2,7,1,5
FILL,1,6,1,4
E,6,4,5 
E,7,6,5
E,7,5,8 
E,5,3,8
E,5,2,3 
E,4,2,5
E,4,1,2
NSEL,S,LOC,X,W  
D,ALL,TEMP,100                     ! DEFINE WALL TEMPERATURE
NSEL,S,NODE,,1,4,3
NSEL,A,NODE,,6,7
SF,ALL,CONV,500,0.0
NSEL,ALL
FINISH
/SOLU    
SOLVE
FINISH
/POST1
PRNSOL,TEMP                        ! PRINT NODAL TEMPERATURES
PRNLD,HEAT                         ! PRINT HEAT FLOW RATES
FSUM,,,
*GET,TN4,NODE,4,TEMP
*GET,TN5,NODE,5,TEMP
*GET,TN6,NODE,6,TEMP
*GET,TN7,NODE,7,TEMP
*GET,HEAT,FSUM,0,ITEM,HEAT

*DIM,LABEL,CHAR,5,2
*DIM,VALUE,,5,3
LABEL(1,1) = 'T,F (AT ','T,F (AT ','T,F (AT ','T,F (AT ','q,Btu/hr'
LABEL(1,2) = 'NODE 4) ','NODE 5) ','NODE 6) ','NODE 7) '
*VFILL,VALUE(1,1),DATA,27.6,32.7,9.5,10.7,3545
*VFILL,VALUE(1,2),DATA,TN4,TN5,TN6,TN7,-1*HEAT*2
*VFILL,VALUE(1,3),DATA,ABS(TN4/27.6),ABS(TN5/32.7),ABS(TN6/9.5),ABS(TN7/10.7),ABS(-1*HEAT*2/3545)
/COM
/OUT,vm99,vrt
/COM,------------------- VM99 RESULTS COMPARISON ---------------
/COM,
/COM,                 |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.1,'  ',F14.1,'   ',1F15.3)
/COM,-----------------------------------------------------------
/COM,
/OUT
FINISH
*LIST,vm99,vrt

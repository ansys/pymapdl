/COM,ANSYS MEDIA REL. 150 (11/8/2013) REF. VERIF. MANUAL: REL. 150
/VERIFY,VM44
JPGPRF,500,100,1             ! MACRO TO SET PREFS FOR JPEG PLOTS
/SHOW,JPEG
/PREP7    
/TITLE, VM44, BENDING OF AN AXISYMMETRIC THIN PIPE UNDER GRAVITY LOADING
C***          FORMULAS FOR STRESS AND STRAIN, ROARK, 4TH ED., PAGE 112, NO. 33
ANTYPE,STATIC
ET,1,SHELL61,,,,,,1     ! PRINT DISP. AT ELEMENT ENDS AS WELL AS MIDPOINT
R,1,.1                  ! DEFINE WALL THICKNESS
MP,EX,1,30.E6           ! DEFINE MATERIAL PROPERTIES
MP,DENS,1,.00073        ! DEFINE DENSITY
MP,NUXY,1,0             ! DEFINE NUXY AS 0.0 
N,1,1                   ! BEGIN NODE DEFINITION
N,8,1,125
FILL                    ! PLACE NODES 2 THRU 7 BETWEEN NODES 1 & 8
E,1,2                   ! BEGIN ELEMENT DEFINITION
EGEN,7,1,1              ! GENERATE NEXT 6 ELEMENTS
CE,1,,2,UY,1,2,ROTZ,-1  ! DEFINE FIRST CONSTRAINT EQN. (UY(2) = ROTZ(2))
*REPEAT,6,1,,1,,,1      ! REPEAT FOR NODES 3 TO 7
CE,7,,2,UX,1,2,UZ,1     ! UX(2) = -UZ(2) AND FOR 6 INTERIOR NODES
*REPEAT,6,1,,1,,,1
OUTPR,ALL,ALL           
ACEL,386,,-386          ! GRAVITY AS THE SUM OF TWO HARMONICALLY VARYING LOADS
MODE,1,1                ! MODE NUMBER 1, SYMMETRIC LOADING
D,1,ALL                 ! FIXED END
NSEL,S,LOC,Y,125
DSYM,SYMM,Y             ! CENTER PLANE END
NSEL,ALL
FINISH
/SOLU    
SOLVE
FINISH
/POST1
SET,1,1,,,,0.0          ! GET RESULTS AT 0 DEGREES
/VUP,1,X                ! DEFINE X AXIS AS VERTICAL FOR VIEWING
/WINDOW,1,TOP           ! DEFINE WINDOW 1 AS TOP HALF OF SCREEN 
PLDISP,1                ! DISPLAY BOTH DISTORTED AND UNDISTORTED GEOMETRY
PRNSOL,DOF              ! PRINT DEGREE OF FREEDOM VALUES
LCOPER,LPRIN            ! CALCULATE PRINCIPAL STRESSES 
PRESOL,ELEM             ! PRINT ELEMENT SOLUTION RESULTS 
ETABLE,STRS,NMISC,11
*GET,STRSS,ELEM,1,ETAB,STRS
*GET,DEF_X,NODE,8,U,X
SET,1,1,,,,90.0         ! STUDY RESULTS AT 90.0 DEGREES
/WINDOW,1,OFF           ! TURN-OFF WINDOW 1
/WINDOW,2,BOT           ! DEFINE WINDOW 2 AS BOTTOM HALF OF SCREEN
/NOERASE                ! OVERLAY NEXT DISPLAY (DON'T ERASE WINDOW 1)
/VUP,2,X
PLDISP                  ! DISPLAY ONLY DISTORTED GEOMETRY
PRNSOL,U,COMP           ! PRINT DISPLACEMENTS
*GET,DEF_Z,NODE,8,U,Z
*DIM,LABEL,CHAR,3,2
*DIM,VALUE,,3,3
LABEL(1,1) = 'UX, in  ','UZ, in  ','SIGMX,ps'
LABEL(1,2) = ' (ANG=0)','(ANG=90)','i(ANG=90'
*VFILL,VALUE(1,1),DATA,-.19062,.19062,3074.3
*VFILL,VALUE(1,2),DATA,DEF_X,DEF_Z,STRSS
*VFILL,VALUE(1,3),DATA,ABS(DEF_X/.19062),ABS(DEF_Z/.19062),ABS(STRSS/3074.3)
/COM
/OUT,vm44,vrt
/COM,------------------- VM44 RESULTS COMPARISON ---------------
/COM,
/COM,                 |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.5,'  ',F14.5,'   ',1F15.3)
/COM,-----------------------------------------------------------

/OUT
FINISH
*LIST,vm44,vrt


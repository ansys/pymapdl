! Copyright (C) 2024 ANSYS, Inc. and/or its affiliates.
! SPDX-License-Identifier: MIT
! 
!
! Permission is hereby granted, free of charge, to any person obtaining a copy
! of this software and associated documentation files (the "Software"), to deal
! in the Software without restriction, including without limitation the rights
! to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
! copies of the Software, and to permit persons to whom the Software is
! furnished to do so, subject to the following conditions:
!
! The above copyright notice and this permission notice shall be included in all
! copies or substantial portions of the Software.
!
! THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
! IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
! FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
! AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
! LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
! OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
! SOFTWARE.

/COM,ANSYS MEDIA REL. 2023R2 (05/12/2023) REF. VERIF. MANUAL: REL. 2023R2
/VERIFY,VMC4
/NOPR
/TITLE, VMC4, SIMPLY-SUPPORTED THIN ANNULAR PLATE
    /COM,   SEE "SELECTED BENCHMARKS FOR NATURAL FREQUENCY ANALYSIS", REPORT TO
    /COM,   NAFEMS BY W.S. ATKINS ENGINEERING SCIENCES, REPORT NO. 20939.01, ISSUE
    /COM,   JUNE 1987, TEST CASE NO. 14. (MODIFIED)
    /COM,
	
_GEOMGEN = 0   ! SET TO 1 TO WRITE CDB FILES IF ANY INPUT FILE MODIFICATIONS

/OUT,vmc4_scratch

*DIM,NARAY,TABLE,6,13          ! 2-D NARAY FOR RESULTS INFO

*DIM,frequency,array,9,1
frequency(1,1)=1.870,5.137,5.137,9.673,9.673,14.850,15.573,15.573,18.382

*DO,I,1,6                     !INITIALISE MESH PARAMETERS AND ETYP
    *IF,I,EQ,1,THEN
        ARG1 = 3
        ARG2 = 16
        ETYP = 181
        MKEY = 0
        !VMC2 - QUADRILATERAL MESH: (N1 = 3, N2 = 16)
    *ELSEIF,I,EQ,2,THEN
        ARG1 = 5
        ARG2 = 32
        ETYP = 181
        MKEY = 0
        !VMC2 - QUADRILATERAL MESH: (N1 = 5, N2 = 32)
    *ELSEIF,I,EQ,3,THEN
        ARG1 = 3
        ARG2 = 16
        ETYP = 281
        MKEY = 0
        !VMC2 - QUADRILATERAL MESH: (N1 = 5, N2 = 16)
    *ELSEIF,I,EQ,4,THEN
        ARG1 = 5
        ARG2 = 32
        ETYP = 281
        MKEY = 0
        !VMC2 - QUADRILATERAL MESH: (N1 = 5, N2 = 32)
    *ELSEIF,I,EQ,5,THEN
        ARG1 = 3
        ARG2 = 16
        ETYP = 281
        MKEY = 1
        !VMC2 - TRIANGLE MESH: (N1 = 3, N2 = 16)
    *ELSEIF,I,EQ,6,THEN
        ARG1 = 5
        ARG2 = 32
        ETYP = 281
        MKEY = 1
        !VMC2 - TRIANGLE MESH: (N1 = 5, N2 = 32)
    *ENDIF

    PARSAV,ALL
    /CLEAR, NOSTART
    PARRES,CHANGE
    /PREP7 $SMRT,OFF

    !TO SUPPLY THE TITLES TO THE IMAGES

    *IF,I,LE,2,THEN
        /TITLE,VMC2 - SHELL181 QUADRILATERAL MESH: (N1 = %ARG1%, N2 = %ARG2%)
    *ELSEIF,I,GT,2,AND,I,LE,4,THEN
        /TITLE,VMC2 - SHELL281 QUADRILATERAL MESH: (N1 = %ARG1%, N2 = %ARG2%)
    *ELSEIF,I,GT,4,AND,I,LE,6,THEN
        /TITLE,VMC2 - SHELL281 TRIANGLE MESH: (N1 = %ARG1%, N2 = %ARG2%)
    *ENDIF

    *IF,_GEOMGEN,EQ,1,THEN
	
    ANTYPE,MODAL                 ! MODAL ANALYSIS
    MODOPT,LANB,9                ! LANB EXTRACTION METHOD
    ET,1,ETYP                    ! DEFINE ELEMENT TYPE PARAMETRICALLY
    NARAY(I,1)=ETYP              ! STORE ETYPE
    SECTYPE,1,SHELL
    SECDATA,0.06,1,0,3
    MP,EX,1,200E9                ! SPECIFY MATERIAL PROPERTIES
    MP,NUXY,1,.3
    MP,DENS,1,8000
    CSYS,1                       ! SPECIFY CYLINDRICAL COORDINATES
    K,1,1.8
    K,2,6                        ! DEFINE KEYPOINTS (FIRST QUADRANT)
    K,3,6,90
    K,4,1.8,90
    L,4,1                        ! DEFINE LINES SEGMENTS
    L,2,3
    LSEL,S,LINE,,1,2,
    LESIZE,ALL,,,ARG2/4
    csys,0                       ! SPECIFY CARTESIAN COORDINATE SYSTEM
    L,3,4
    L,1,2
    LSEL,S,LINE,,3,4,
    LESIZE,ALL,,,ARG1
    LSEL,ALL
    MSHAPE,MKEY,2D
    A,4,3,2,1                    ! DEFINE AREA
    MAT,1
    SECNUM,1
    AMESH,1                      ! MESH AREA
    ARSYM,1,1                    ! REFLECT AREA AND MESH ABOUT Y AXIS
    ARSYM,2,1,2                  ! REFLECT AREAS AND MESH ABOUT X AXIS
    NUMMRG,ALL                   ! MERGE ALL ENTITIES
    D,ALL,UX,0,,,,UY,ROTZ        ! FIX ALL IN-PLANE DISP. AND ROTATIONS
    CSYS,1                       ! SPECIFY CYLINDRICAL COORDINATE SYSTEM
    NSEL,S,LOC,X,6               ! SELECT NODES AT OUTER RADIUS
    NROTAT,ALL                   ! ROTATE NODES INTO CYLINDRICAL C.S.
    D,ALL,UZ,0,,,,ROTX           ! FIX UZ AND ROTX IN CSYS,1
    NSEL,ALL
	
    cdwrite,db,vmc4-%I%,cdb
    *ELSE
    cdread,db,vmc4-%I%,cdb
    *ENDIF
	
    NARAY(I,2)=ARG1              ! STORE N1
    NARAY(I,3)=ARG2              ! STORE N2

    *GET,MAXN,NODE,,NUM,MAX      ! GET MAX NODE NUMBER
    NARAY(I,4)=MAXN*6            ! CALCULATE NO. DEGREES OF FREEDOM

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  !!  SETUP AND PLOT ELEMENTS FOR DOCUMENTATION !!
  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

    /VIEW,1,,,1
    /ANG,1
    /AUTO,1
    /DEVICE,VECTOR,1
    /DEVICE,DITHER,1
    /TYPE,ALL,4
    /COLOR,NUM,BLUE,1

    /SHOW,PNG,REV
    EPLOT
    /SHOW,CLOSE

    /TITLE, VMC4, SIMPLY-SUPPORTED THIN ANNULAR PLATE

    FINISH
    /SOLU
    SOLVE
    FINISH
    /POST1

    *DO,J,1,9
        SET,1,J
        *GET,MODJ,MODE,J,FREQ              ! GET MODE FREQUENCIES
        NARAY(I,J+4)=MODJ/frequency(J,1)   ! CALCULATE NORMALIZED FREQUENCY
        /VIEW,1,0.9,0,0.369
        /ANG,1,270
        /TITLE,VMC 4 - MODE J
        PLDISP,0
    *ENDDO
    PARSAV,,PARAM
    FINISH
*ENDDO

  /OUT,
  /OUT,vmc4,vrt
  /COM
  /COM,------------------- VMC4 RESULTS LISTING ------------------------------------
  /COM,
  /COM,| ETYP | N1 | N2 | DOF |  RAT1 | RAT2 | RAT3 |
  /COM,
  *VWRITE,naray(1,1),naray(1,2),naray(1,3),NARAY(1,4),NARAY(1,5),NARAY(1,6),NARAY(1,7)
  (F5.0,' ',F5.0,' ',F6.0,' ',F5.0,' ',F6.3,' ',F6.3,' ',F7.3)
  /COM,-----------------------------------------------------------------------------
  /COM,
  /COM,------------------- VMC4 RESULTS CONT... ------------------------------------
  /COM,
  /COM,| RAT4  | RAT5  | RAT6  | RAT7  | RAT8  | RAT9  |
  /COM,
  *VWRITE,NARAY(1,8),NARAY(1,9),NARAY(1,10),naray(1,11),naray(1,12),naray(1,13)
  (F7.3,' ',F7.3,' ',F7.3,' ',F7.3,' ',F7.3,' ',F7.3)

  /COM,-----------------------------------------------------------------------------
  /OUT
*LIST,vmc4,vrt
FINISH
/EXIT,NOSAVE

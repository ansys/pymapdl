/COM,ANSYS MEDIA REL. 150 (11/8/2013) REF. VERIF. MANUAL: REL. 150
/VERIFY,VMC4
/SHOW
  *DIM,NARAY,TABLE,6,13          ! 2-D NARAY FOR RESULTS INFO
  *DO,I,1,6
    *DO,J,1,13
      NARAY(I,J)=0.               ! INITIALIZE NARAY
    *ENDDO
  *ENDDO
*CREATE,base,
  PARSAV,ALL
  /clear, nostart
  PARRES,CHANGE
  JPGPRF,500,100,1             ! MACRO TO SET PREFS FOR JPEG PLOTS
  /SHOW,JPEG
  /PREP7
smrt,off
  /TITLE, VMC4, SIMPLY-SUPPORTED THIN ANNULAR PLATE
/STITLE,1,REASON COMPARE DIFFERENCES ARE ACCEPTABLE
/STITLE,2,ACCURACY
  /COM,   SEE "SELECTED BENCHMARKS FOR NATURAL FREQUENCY ANALYSIS", REPORT TO
  /COM,   NAFEMS BY W.S. ATKINS ENGINEERING SCIENCES, REPORT NO. 20939.01, ISSUE
  /COM,   JUNE 1987, TEST CASE NO. 14. (MODIFIED)
  /COM,
  /OUT,SCRATCH
  ANTYPE,MODAL
  MODOPT,LANB,9			    ! LANB EXTRACTION METHOD
  ET,1,ARG1                       ! DEFINE ELEMENT TYPE PARAMETRICALLY
  NARAY(ARG5,1)=ARG1              ! STORE ETYPE
  SECTYPE,1,SHELL
  SECDATA,0.06,1,0,3
  MP,EX,1,200E9                   ! SPECIFY MATERIAL PROPERTIES
  MP,NUXY,1,.3
  MP,DENS,1,8000
  CSYS,1                          ! SPECIFY CYLINDRICAL COORDINATES
  K,1,1.8
  K,2,6                           ! DEFINE KEYPOINTS (FIRST QUADRANT)
  K,3,6,90
  K,4,1.8,90
  L,1,2
  L,2,3                           ! DEFINE LINES SEGMENTS
  L,3,4
  L,4,1
  LSEL,S,LINE,,1,3,2
  LESIZE,ALL,,,ARG2
  LSEL,INVE
  LESIZE,ALL,,,ARG3/4
  LSEL,ALL
  MSHAPE,ARG4,2D
  A,1,2,3,4                       ! DEFINE AREA
  MAT,1
  SECNUM,1
  AMESH,1                         ! MESH AREA
  CSYS,0                          ! SPECIFY CARTESIAN COORDINATE SYSTEM
  ARSYM,1,1                       ! REFLECT AREA AND MESH ABOUT Y AXIS
  ARSYM,2,1,2                     ! REFLECT AREAS AND MESH ABOUT X AXIS
  NUMMRG,ALL                      ! MERGE ALL ENTITIES
  D,ALL,UX,0,,,,UY,ROTZ           ! FIX ALL IN-PLANE DISP. AND ROTATIONS
  CSYS,1                          ! SPECIFY CYLINDRICAL COORDINATE SYSTEM
  NSEL,S,LOC,X,6                  ! SELECT NODES AT OUTER RADIUS
  NROTAT,ALL                      ! ROTATE NODES INTO CYLINDRICAL C.S.
  D,ALL,UZ,0,,,,ROTX              ! FIX UZ AND ROTX IN CSYS,1
  NSEL,ALL
  NARAY(ARG5,2)=ARG2              ! STORE N1
  NARAY(ARG5,3)=ARG3              ! STORE N2
  *GET,MAXN,NODE,,NUM,MAX         ! GET MAX NODE NUMBER
  NARAY(ARG5,4)=MAXN*6            ! CALCULATE NO. DEGREES OF FREEDOM
  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  !!  SETUP AND PLOT ELEMENTS FOR DOCUMENTATION !!
  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  /VIEW,1,,,1
  /ANG,1
  /AUTO,1
  /DEVICE,VECTOR,1
  /DEVICE,DITHER,1
  /TYPE,ALL,4
  /COLOR,NUM,BLUE,1
  *IF,ARG1,EQ,181,THEN
  *IF,ARG2,EQ,3,THEN
  *IF,ARG3,EQ,16,THEN
  *IF,ARG4,EQ,0,THEN
  /TITLE,VMC4 - QUADRILATERAL MESH (N1 = 3, N2 = 16)
  EPLOT
  *ENDIF
  *ENDIF
  *ENDIF
  *ENDIF
  *IF,ARG1,EQ,181,THEN
  *IF,ARG2,EQ,5,THEN
  *IF,ARG3,EQ,32,THEN
  *IF,ARG4,EQ,0,THEN
  /TITLE,VMC4 - QUADRILATERAL MESH (N1 = 5, N2 = 32)
  EPLOT
  *ENDIF
  *ENDIF
  *ENDIF
  *ENDIF
  *IF,ARG1,EQ,181,THEN
  *IF,ARG2,EQ,3,THEN
  *IF,ARG3,EQ,16,THEN
  *IF,ARG4,EQ,1,THEN
  /TITLE,VMC4 - TRIANGLE MESH (N1 = 3, N2 = 16)
  EPLOT
  *ENDIF
  *ENDIF
  *ENDIF
  *ENDIF
  *IF,ARG1,EQ,181,THEN
  *IF,ARG2,EQ,5,THEN
  *IF,ARG3,EQ,32,THEN
  *IF,ARG4,EQ,1,THEN
  /TITLE,VMC4 - TRIANGLE MESH (N1 = 5, N2 = 32)
  EPLOT
  *ENDIF
  *ENDIF
  *ENDIF
  *ENDIF
  /TITLE, VMC4, SIMPLY-SUPPORTED THIN ANNULAR PLATE

  FINISH
  /SOLU
  SOLVE
  FINISH
  /POST1
  /DEVICE,VECTOR,1
  /DEVICE,DITHER,1
  /TYPE,ALL,4
  /COLOR,NUM,BLUE,1
  *GET,MOD1,MODE,1,FREQ           ! GET MODE 1 FREQUENCY
  NARAY(ARG5,5)=MOD1/1.870        ! CALCULATE NORMALIZED FREQUENCY
  /VIEW,1,0.9,0,0.369
  /ANG,1,270  
  *IF,I,EQ,1,THEN
  SET,1,1
  /TITLE,VMC 4 - MODE 1
  PLDISP,0
  *ENDIF
  *GET,MOD2,MODE,2,FREQ           ! GET MODE 2 FREQUENCY
  NARAY(ARG5,6)=MOD2/5.137        ! CALCULATE NORMALIZED FREQUENCY
  *IF,I,EQ,2,THEN  
  SET,1,2
  /TITLE,VMC4 - MODE 2
  PLDISP,0
  *ENDIF

  *GET,MOD3,MODE,3,FREQ           ! GET MODE 3 FREQUENCY
  NARAY(ARG5,7)=MOD3/5.137        ! CALCULATE NORMALIZED FREQUENCY
  *IF,I,EQ,3,THEN
  SET,1,3
  /TITLE,VMC4 - MODE 3
  PLDISP,0
  *ENDIF
  *GET,MOD4,MODE,4,FREQ           ! GET MODE 4 FREQUENCY
  NARAY(ARG5,8)=MOD4/9.673        ! CALCULATE NORMALIZED FREQUENCY
  *IF,I,EQ,4,THEN
  SET,1,4
  /TITLE,VMC4 - MODE 4
  PLDISP,0
  *ENDIF
  *GET,MOD5,MODE,5,FREQ           ! GET MODE 5 FREQUENCY
  NARAY(ARG5,9)=MOD5/9.673        ! CALCULATE NORMALIZED FREQUENCY
  *IF,I,EQ,5,THEN
  SET,1,5
  /TITLE,VMC4 - MODE 5
  PLDISP,0
  *ENDIF
  *GET,MOD6,MODE,6,FREQ           ! GET MODE 6 FREQUENCY
  NARAY(ARG5,10)=MOD6/14.850      ! CALCULATE NORMALIZED FREQUENCY
  *IF,I,EQ,6,THEN
  SET,1,6
  /TITLE,VMC4 - MODE 6
  PLDISP,0
  *ENDIF
  *GET,MOD7,MODE,7,FREQ           ! GET MODE 7 FREQUENCY
  NARAY(ARG5,11)=MOD7/15.573      ! CALCULATE NORMALIZED FREQUENCY
  *IF,I,EQ,7,THEN
  SET,1,7
  /TITLE,VMC4 - MODE 7
  PLDISP,0
  *ENDIF
  *GET,MOD8,MODE,8,FREQ           ! GET MODE 8 FREQUENCY
  NARAY(ARG5,12)=MOD8/15.573      ! CALCULATE NORMALIZED FREQUENCY
  *IF,I,EQ,8,THEN
  SET,1,8
  /TITLE,VMC4 - MODE 8
  PLDISP,0
  *ENDIF
  *GET,MOD9,MODE,9,FREQ           ! GET MODE 9 FREQUENCY
  NARAY(ARG5,13)=MOD9/18.382      ! CALCULATE NORMALIZED FREQUENCY
  *IF,I,EQ,9,THEN
  SET,1,9
  /TITLE,VMC4 - MODE 9
  PLDISP
  *ENDIF
  /TITLE, VMC4, SIMPLY-SUPPORTED THIN ANNULAR PLATE 
  PARSAV,,PARAM
  FINISH
*END
  /COM, COMMENT:  CALL MACRO TO LOOP THROUGH DIFFERENT ELEMENT TYPES, MESHES
  *DO,I,1,6
    *IF,I,EQ,1,THEN
      *USE,base,181,3,16,0,I,       ! QUAD MESH
    *ELSEIF,I,EQ,2,THEN
      *USE,base,181,5,32,0,I,      ! QUAD MESH
    *ELSEIF,I,EQ,3,THEN
      *USE,base,281,3,16,0,I,       ! QUAD MESH
    *ELSEIF,I,EQ,4,THEN
      *USE,base,281,5,32,0,I,       ! QUAD MESH
    *ELSEIF,I,EQ,5,THEN
      *USE,base,281,3,16,1,I       ! TRI MESH
    *ELSEIF,I,EQ,6,THEN
      *USE,base,281,5,32,1,I       ! TRI MESH
    *ENDIF
  *ENDDO
  /OUT,
  /OUT,vmc4,vrt
  /COM
  /COM,------------------- VMC4 RESULTS LISTING ------------------------------------
  /COM,
  /COM,| ETYP | N1 | N2 | DOF |  RAT1 | RAT2 | RAT3 | 
  /COM,
  *VWRITE,naray(1,1),naray(1,2),naray(1,3),NARAY(1,4),NARAY(1,5),NARAY(1,6),NARAY(1,7)
  (F5.0,' ',F5.0,' ',F6.0,' ',F5.0,' ',F6.3,' ',F6.3,' ',F7.3)
  /COM,-----------------------------------------------------------------------------
  /COM,
  /COM,------------------- VMC4 RESULTS CONT... ------------------------------------
  /COM,
  /COM,| RAT4  | RAT5  | RAT6  | RAT7  | RAT8  | RAT9  |
  /COM,
  *VWRITE,NARAY(1,8),NARAY(1,9),NARAY(1,10),naray(1,11),naray(1,12),naray(1,13)
  (F7.3,' ',F7.3,' ',F7.3,' ',F7.3,' ',F7.3,' ',F7.3)

  /COM,-----------------------------------------------------------------------------
  /OUT
*LIST,vmc4,vrt
FINISH

! Copyright (C) 2024 ANSYS, Inc. and/or its affiliates.
! SPDX-License-Identifier: MIT
! 
!
! Permission is hereby granted, free of charge, to any person obtaining a copy
! of this software and associated documentation files (the "Software"), to deal
! in the Software without restriction, including without limitation the rights
! to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
! copies of the Software, and to permit persons to whom the Software is
! furnished to do so, subject to the following conditions:
!
! The above copyright notice and this permission notice shall be included in all
! copies or substantial portions of the Software.
!
! THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
! IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
! FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
! AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
! LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
! OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
! SOFTWARE.

/COM,ANSYS MEDIA REL. 2023R2 (05/12/2023) REF. VERIF. MANUAL: REL. 2023R2
/VERIFY,VMC2
/NOPR
/TITLE, VMC2, ELLIPTIC MEMBRANE UNDER A UNIFORMLY-DISTRIBUTED LOAD
  /COM,   SEE "SELECTED FE BENCHMARKS FOR STRUCTURAL AND THERMAL ANALYSIS",
  /COM,   NAFEMS REPORT NO. FEBSTA, REV. 1, OCT. 1986, TEST NO. LE1
  /COM,
*DIM,NARAY,TABLE,12,6          ! 2-D NARAY FOR RESULTS INFO

*DO,I,1,12                     !INITIALISE MESH PARAMETERS AND ETYP
    *IF,I,EQ,1,THEN
        ARG1 = 2
        ARG2 = 3
        ETYP = 182
        MKEY = 1
        !VMC2 - TRIANGLE MESH: (N1 = 2, N2 = 3)
    *ELSEIF,I,EQ,2,THEN
        ARG1 = 5
        ARG2 = 7
        ETYP = 182
        MKEY = 1
        !VMC2 - TRIANGLE MESH: (N1 = 5, N2 = 7)
    *ELSEIF,I,EQ,3,THEN
        ARG1 = 10
        ARG2 = 12
        ETYP = 182
        MKEY = 1
        !VMC2 - TRIANGLE MESH: (N1 = 10, N2 = 12)
    *ELSEIF,I,EQ,4,THEN
        ARG1 = 2
        ARG2 = 3
        ETYP = 182
        MKEY = 0
        !VMC2 - QUADRILATERAL MESH: (N1 = 2, N2 = 3)
    *ELSEIF,I,EQ,5,THEN
        ARG1 = 5
        ARG2 = 7
        ETYP = 182
        MKEY = 0
        !VMC2 - QUADRILATERAL MESH: (N1 = 5, N2 = 7)
    *ELSEIF,I,EQ,6,THEN
        ARG1 = 10
        ARG2 = 12
        ETYP = 182
        MKEY = 0
        !VMC2 - QUADRILATERAL MESH: (N1 = 10, N2 = 12)
    *ELSEIF,I,EQ,7,THEN
        ARG1 = 2
        ARG2 = 3
        ETYP = 183
        MKEY = 1
        !VMC2 - TRIANGLE MESH: (N1 = 2, N2 = 3)
    *ELSEIF,I,EQ,8,THEN
        ARG1 = 5
        ARG2 = 7
        ETYP = 183
        MKEY = 1
        !VMC2 - TRIANGLE MESH: (N1 = 5, N2 = 7)
    *ELSEIF,I,EQ,9,THEN
        ARG1 = 10
        ARG2 = 12
        ETYP = 183
        MKEY = 1
        !VMC2 - TRIANGLE MESH: (N1 = 10, N2 = 12)
    *ELSEIF,I,EQ,10,THEN
        ARG1 = 2
        ARG2 = 3
        ETYP = 183
        MKEY = 0
        !VMC2 - QUADRILATERAL MESH: (N1 = 2, N2 = 3)
    *ELSEIF,I,EQ,11,THEN
        ARG1 = 5
        ARG2 = 7
        ETYP = 183
        MKEY = 0
        !VMC2 - QUADRILATERAL MESH: (N1 = 5, N2 = 7)
    *ELSEIF,I,EQ,12,THEN
        ARG1 = 10
        ARG2 = 12
        ETYP = 183
        MKEY = 0
        !VMC2 - QUADRILATERAL MESH: (N1 = 10, N2 = 12)
    *ENDIF


  PARSAV,ALL
  /CLEAR, NOSTART
  PARRES,CHANGE
  /PREP7 $SMRT,OFF


  !TO SUPPLY THE TITLES TO THE IMAGES
  
	*IF,I,LE,3,THEN
		/TITLE,VMC2 - TRIANGLE MESH: (N1 = %ARG1%, N2 = %ARG2%)	
	*ELSEIF,I,GT,3,AND,I,LE,6,THEN
		/TITLE,VMC2 - QUADRILATERAL MESH: (N1 = %ARG1%, N2 = %ARG2%)
	*ELSEIF,I,GT,6,AND,I,LE,9,THEN
		/TITLE,VMC2 - TRIANGLE MESH: (N1 = %ARG1%, N2 = %ARG2%)
	*ELSEIF,I,GT,9,THEN
		/TITLE,VMC2 - QUADRILATERAL MESH: (N1 = %ARG1%, N2 = %ARG2%)
	*ENDIF	
  
  
  
  
  
  


    ET,1,ETYP,,,3                  ! DEFINE ELEMENT TYPE (PARAMETRICALLY)
    MP,EX,1,210E9                  ! DEFINE MATERIAL PROPERTIES
    MP,NUXY,1,.3
    R,1,0.1                        ! SET THICKNESS
    LOCAL,11,1,,,,,,,0.5           ! DEFINE ELLIPTICAL COORD. SYSTEM
    K,1,2,90
    K,4,2,0                        ! CREATE MODEL GEOMETRY
    K,5,1.165,20
    KMOVE,5,0,1.165,U,0.0,11,2.0,U,0.0
    K,8,2.0,5.0
    KMOVE,8,0,U,0.453,0.0,11,E,U,0.0
    L,1,5
    L,5,8
    L,8,4
    LOCAL,12,1,,,,,,,0.8461585
    K,2,3.25,90
    K,3,3.25,0.0
    K,6,3.25,67
    KMOVE,6,0,1.783,U,0.0,12,E,U,0.0
    K,7,3.25,25
    KMOVE,7,0,U,1.348,0.0,12,E,U,0.0
    L,2,6
    L,6,7
    L,7,3
    LESIZE,ALL,,,ARG1
    CSYS,0
    L,1,2,
    L,4,3,
    LSEL,S,LINE,,7,8
    LESIZE,ALL,,,ARG2
    LSEL,ALL
    A,4,3,7,8
    A,8,7,6,5
    A,5,6,2,1
    ESIZE,,ARG2
    MSHAPE,MKEY
    MSHKEY,1
    AMESH,ALL                      ! MESH ALL AREAS


  *GET,MAXN,NODE,,NUM,MAX          ! GET MAX NODE NUMBER
  NARAY(I,2)=ARG1                  ! STORE N1
  NARAY(I,3)=ARG2                  ! STORE N2
  NARAY(I,4)=MAXN*2                ! CALCULATE NO. DEGREES OF FREEDOM
  NSEL,S,LOC,Y,0
  DSYM,SYMM,Y,1                    ! APPLY BOUNDARY CONDITIONS
  NSEL,S,LOC,X,0
  DSYM,SYMM,Y,1
  CSYS,12
  NSEL,S,LOC,X,3.25
  SF,ALL,PRES,-10E6                ! APPLY PRESSURE LOAD
  NSEL,ALL
  *IF,ETYP,EQ,182,THEN             !TO PRINT THE MESH ONLY ONCE
     /VIEW,1,,,1
     /ANG,1
     /AUTO,1
     /DEVICE,VECTOR,1
     /DEVICE,DITHER,1
     /TYPE,ALL,4
     /COLOR,NUM,BLUE,1

     /SHOW,PNG,REV
     EPLOT
     /SHOW,CLOSE
  *ENDIF
  /TITLE, VMC2, ELLIPTIC MEMBRANE UNDER A UNIFORMLY-DISTRIBUTED LOAD

  OUTRES,STRS
  OUTPR,BASIC
  FINISH
  /SOLU
  SOLVE
  FINISH
  /POST1
  NSEL,S,LOC,Y,0,.1
  NSEL,R,LOC,X,2.0
  NSORT,S,Y
  *GET,MAXN,NODE,,NUM,MAX          ! GET MAX NODE NUMBER
  *GET,SYM,NODE,MAXN,S,Y           ! GET DESIRED SY STRESS VALUE
  TARG=92.7E6                      ! TARGET SY VALUE
  NARAY(I,6)=SYM/TARG              ! NORMALIZED SY VALUE
  *GET,NARAY(I,5),PRERR,,SEPC      ! STORE PERCENT ENERGY ERROR NORM
  PARSAV,,PARAM
  FINISH
*ENDDO

  *DO,COL,1,3                      ! INITIALIZE COLUMN 1 WITH ELEMENT TYPES
    NARAY(COL,1)= 182
    NARAY(COL+3,1)= 182
    NARAY(COL+6,1)= 183
    NARAY(COL+9,1)= 183
  *ENDDO

  SAVE
  /GROPT,VIEW,1
  /SHOW,PNG,REV
  /GRID,1
  /AXLAB,X,NO. DOF'S
  /AXLAB,Y,% ERROR IN ENERGY NORM
  /GTHK,AXIS,2
  /GTHK,CURVE,3
  /GROPT,LOGX,ON
  /GROPT,LOGY,ON
  /XRANGE,10,1E5
  /YRANGE,0,1.25
  *VLEN,3,1
  *VPLOT,NARAY(1,4),NARAY(1,5)
  *VLEN,3,1
  *VPLOT,NARAY(4,4),NARAY(4,5)
  *VLEN,3,1
  *VPLOT,NARAY(7,4),NARAY(7,5)
  *VLEN,3,1
  *VPLOT,NARAY(10,4),NARAY(10,5)
  /AXLAB,Y,SY RATIO
  /GROPT,LOGY,OFF
  *VLEN,3,1
  *VPLOT,NARAY(1,4),NARAY(1,6)
  *VLEN,3,1
  *VPLOT,NARAY(4,4),NARAY(4,6)
  *VLEN,3,1
  *VPLOT,NARAY(7,4),NARAY(7,6)
  *VLEN,3,1
  *VPLOT,NARAY(10,4),NARAY(10,6)
  /SHOW,CLOSE
  FINISH

  RESUME,
  /OUT,vmc2,vrt
  /COM
  /COM,------------------- VMC2 RESULTS LISTING ------------------------------------
  /COM,
  /COM,| ETYP | N1  | N2   |   DOF  | %ERR NM  | SY RAT |
  /COM,
  *VWRITE,NARAY(1,1),NARAY(1,2),NARAY(1,3),NARAY(1,4),NARAY(1,5),NARAY(1,6)
  (F5.0,'  ',F5.0,'  ',F5.0,' ',F10.0,'  ',F7.3,'     ',F5.3)
  /COM,-----------------------------------------------------------------------------
  /OUT
  *LIST,vmc2,vrt

FINISH

/COM,ANSYS MEDIA REL. 2023R1 (11/04/2022) REF. VERIF. MANUAL: REL. 2023R1
/VERIFY,VM4
/PREP7
/TITLE, VM4, DEFLECTION OF A HINGED SUPPORT
C***     STR. OF MATL., TIMOSHENKO, PART 1, 3RD ED., PAGE 10, PROB. 2
L=15*12                         ! LENGTH OF BAR IN INCHES
*AFUN,DEG                       ! TRIG FUNCTIONS IN DEGREES 
THETA=30                        ! ANGLE TO BE USED TO CALCULATE A AND B
A=2*L*COS(THETA)                ! CALCULATED X LOCATION - NODE 3
B=L*SIN(THETA)                  ! CALCULATED Y LOCATION - NODE 2
ET,1,LINK180
SECTYPE,1,LINK   
SECDATA,0.5
MP,EX,1,30E6
N,1
N,2,A/2,-B                      ! X LOCATION = A/2; A AND B AS ABOVE
N,3,A                           
E,1,2
E,2,3
D,1,ALL,,,3,2
F,2,FY,-5000
OUTPR,,1
FINISH
/SOLU    
SOLVE
FINISH
/POST1 
MID_NODE = NODE (A/2,-B,0 )
*GET,DISP,NODE,MID_NODE,U,Y 
LEFT_EL = ENEARN (MID_NODE) 
ETABLE,STRS,LS,1  
*GET,STRSS,ELEM,LEFT_EL,ETAB,STRS 

*DIM,LABEL,CHAR,2
*DIM,VALUE,,2,3
LABEL(1) = 'STRS_psi','DEF_in'
*VFILL,VALUE(1,1),DATA,10000,-0.120
*VFILL,VALUE(1,2),DATA,STRSS,DISP
*VFILL,VALUE(1,3),DATA,ABS(STRSS /10000 ) ,ABS( DISP /0.120 )
/OUT,vm4,vrt
/COM
/COM,------------------- VM4 RESULTS COMPARISON ---------------------
/COM,
/COM,         |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
*VWRITE,LABEL(1),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,'   ',F10.3,'  ',F14.3,'   ',1F15.3)
/COM,----------------------------------------------------------------
/OUT

FINISH
*LIST,vm4,vrt

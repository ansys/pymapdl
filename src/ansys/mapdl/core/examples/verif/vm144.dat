/COM,ANSYS MEDIA REL. 2023R1 (11/04/2022) REF. VERIF. MANUAL: REL. 2023R1
/VERIFY,VM144
/PREP7
/TITLE, VM144, BENDING OF A COMPOSITE BEAM
C***    FORMULAS FOR STRESS AND STRAIN, ROARK, 5TH ED.
C***    USING LAYERED SOLID ELEMENTS (SOLID185)
ANTYPE,STATIC
ET,1,SOLID185               ! LAYERED SOLID ELEMENT
KEYOPT,1,2,2                ! ENHANCED STRAIN FORMULATION
KEYOPT,1,3,1                ! LAYERED SOLID
KEYOPT,1,8,1                ! WRITE LAYER RESULTS
SECTYPE,1,SHELL
SECDATA,0.2,1               ! LAYER 1: 0.2 THK 
SECDATA,0.1,2               ! LAYER 2: 0.1 THK
MP,EX,1,1.2E6               ! MATERIAL 1 PROPERTIES
MP,NUXY,1,0
MP,ALPX,1,1.8E-4
MP,ALPY,1,0.0
MP,ALPZ,1,0.0
MP,EX,2,0.4E6               ! MATERIAL 2 PROPERTIES
MP,NUXY,2,0
MP,ALPX,2,0.6E-4
MP,ALPY,2,0.0
MP,ALPZ,2,0.0
N,1
N,9,8
FILL
NGEN,2,10,1,9,1,,.5
NGEN,2,20,1,19,1,,,.3
E,1,2,12,11,21,22,32,31
EGEN,8,1,-1                 ! 8 ELEMENTS ALONG LENGTH
D,1,ALL,,,31,10             ! FIXED END
F,9,FX,-(50/3),,19,10       ! APPLY NODAL FORCES TO GENERATE MOMENT
F,29,FX,(50/3),,39,10
BFUNIF,TEMP,100             ! ELEVATED TEMPERATURE LOAD
FINISH
/SOLU    
SOLVE
FINISH
/POST1
NSEL,S,LOC,X,8
PRNSOL,U,Z                  ! PRINT FREE END DISPLACEMENTS
*GET,U3,NODE,9,U,Z
NSEL,S,LOC,Z,0.3
PRNSOL,S,COMP               ! PRINT STRESSES ALONG TOP SURFACE
*GET,ST3,NODE,21,S,X
NSEL,S,LOC,Z
PRNSOL,S,COMP               ! PRINT STRESSES ALONG BOTTOM SURFACE
*GET,SB3,NODE,1,S,X
*DIM,LABEL,CHAR,3,2
*DIM,VALUE,,3,3
LABEL(1,1) = 'DISP ','PRS TP ','PRS BTM '
LABEL(1,2) = 'in','psi','psi'
*VFILL,VALUE(1,1),DATA,.832,2258,1731
*VFILL,VALUE(1,2),DATA,U3,ST3,SB3
*VFILL,VALUE(1,3),DATA,ABS(U3/.832),ABS(ST3/2258),ABS(SB3/1731)
SAVE, TABLE_1
FINISH
/CLEAR,NOSTART 
/PREP7
C***    USING LAYERED SOLID ELEMENTS (SOLID186)
ANTYPE,STATIC
ET,1,SOLID186               ! LAYERED SOLID ELEMENT
KEYOPT,1,3,1                ! LAYERED SOLID
KEYOPT,1,8,1                ! WRITE LAYER RESULTS  
SECTYPE,1,SHELL
SECDATA,0.2,1               ! LAYER 1: 0.2 THK 
SECDATA,0.1,2               ! LAYER 2: 0.1 THK
MP,EX,1,1.2E6               ! MATERIAL 1 PROPERTIES
MP,NUXY,1,0
MP,ALPX,1,1.8E-4
MP,ALPY,1,0.0
MP,ALPZ,1,0.0
MP,EX,2,0.4E6               ! MATERIAL 2 PROPERTIES
MP,NUXY,2,0
MP,ALPX,2,0.6E-4
MP,ALPY,2,0.0
MP,ALPZ,2,0.0
N,1
N,9,8
FILL
NGEN,2,10,1,9,1,,.5
NGEN,2,20,1,19,1,,,.3
E,1,2,12,11,21,22,32,31
EGEN,8,1,-1                 ! 8 ELEMENTS ALONG LENGTH
EMID
NSEL,S,LOC,X
D,ALL,ALL,                  ! FIXED END
NSEL,ALL
NLIST,ALL
SFE,8,3,PRES,,4000/3,4000/3,-4000/3,-4000/3  ! TAPERED PRESSURE TO APPLY MOMENT ON FACE
BFUNIF,TEMP,100             ! ELEVATED TEMPERATURE LOAD
FINISH
/SOLU    
OUTPR,NSOL,1
OUTPR,RSOL,1
SOLVE
FINISH
/POST1
NSEL,S,LOC,X,8
PRNSOL,U,Z                  ! PRINT FREE END DISPLACEMENTS
*GET,U3,NODE,9,U,Z
NSEL,S,LOC,Z,0.3
PRNSOL,S,COMP               ! PRINT STRESSES ALONG TOP SURFACE
*GET,ST3,NODE,21,S,X
NSEL,S,LOC,Z
PRNSOL,S,COMP               ! PRINT STRESSES ALONG BOTTOM SURFACE
*GET,SB3,NODE,1,S,X
*DIM,LABEL,CHAR,3,2
*DIM,VALUE,,3,3
LABEL(1,1) = 'DISP ','PRS TP ','PRS BTM '
LABEL(1,2) = 'in','psi','psi'
*VFILL,VALUE(1,1),DATA,.832,2258,1731
*VFILL,VALUE(1,2),DATA,U3,ST3,SB3
*VFILL,VALUE(1,3),DATA,ABS(U3/.832),ABS(ST3/2258),ABS(SB3/1731)
SAVE, TABLE_2
FINISH
/CLEAR,NOSTART 
/PREP7
C***    USING LAYERED SOLID ELEMENTS (SOLSH190)
ANTYPE,STATIC
ET,1,SOLSH190               ! LAYERED SOLID-SHELL ELEMENT
KEYOPT,1,8,1                ! WRITE LAYER RESULTS
SECTYPE,1,SHELL
SECDATA,0.2,1               ! LAYER 1: 0.2 THK 
SECDATA,0.1,2               ! LAYER 2: 0.1 THK
MP,EX,1,1.2E6               ! MATERIAL 1 PROPERTIES
MP,NUXY,1,0
MP,ALPX,1,1.8E-4
MP,ALPY,1,0.0
MP,ALPZ,1,0.0
MP,EX,2,0.4E6               ! MATERIAL 2 PROPERTIES
MP,NUXY,2,0
MP,ALPX,2,0.6E-4
MP,ALPY,2,0.0
MP,ALPZ,2,0.0
N,1
N,9,8
FILL
NGEN,2,10,1,9,1,,.5
NGEN,2,20,1,19,1,,,.3
E,1,2,12,11,21,22,32,31
EGEN,8,1,-1                 ! 8 ELEMENTS ALONG LENGTH
D,1,ALL,,,31,10             ! FIXED END
F,9,FX,-(50/3),,19,10       ! APPLY NODAL FORCES TO GENERATE MOMENT
F,29,FX,(50/3),,39,10
BFUNIF,TEMP,100             ! ELEVATED TEMPERATURE LOAD
FINISH
/SOLU    
SOLVE
FINISH
/POST1
NSEL,S,LOC,X,8
PRNSOL,U,Z                  ! PRINT FREE END DISPLACEMENTS
*GET,U3,NODE,9,U,Z
NSEL,S,LOC,Z,0.3
PRNSOL,S,COMP               ! PRINT STRESSES ALONG TOP SURFACE
*GET,ST3,NODE,21,S,X
NSEL,S,LOC,Z
PRNSOL,S,COMP               ! PRINT STRESSES ALONG BOTTOM SURFACE
*GET,SB3,NODE,1,S,X
*DIM,LABEL,CHAR,3,2
*DIM,VALUE,,3,3
LABEL(1,1) = 'DISP ','PRS TP ','PRS BTM '
LABEL(1,2) = 'in','psi','psi'
*VFILL,VALUE(1,1),DATA,.832,2258,1731
*VFILL,VALUE(1,2),DATA,U3,ST3,SB3
*VFILL,VALUE(1,3),DATA,ABS(U3/.832),ABS(ST3/2258),ABS(SB3/1731)
SAVE, TABLE_3
FINISH
/CLEAR,NOSTART 
/PREP7
C***    USING LAYERED SHELL ELEMENTS (SHELL281)
ANTYPE,STATIC
ET,1,SHELL281               ! 8 NODE LAYERED SHELL ELEMENT             
KEYOPT,1,8,1                ! WRITE LAYER RESULTS
SECTYPE,1,SHELL
SECDATA,0.2,1               ! LAYER 1: 0.2 THK 
SECDATA,0.1,2               ! LAYER 2: 0.1 THK
MP,EX,1,1.2E6               ! MATERIAL 1 PROPERTIES
MP,NUXY,1,0
MP,ALPX,1,18E-5
MP,ALPY,1,0.0
MP,EX,2,0.4E6               ! MATERIAL 2 PROPERTIES
MP,NUXY,2,0
MP,ALPX,2,6E-5
MP,ALPY,2,0
N,1
N,9,8
FILL
NGEN,3,10,1,9,,,.25
E,1,3,23,21,2,13,22,11
EGEN,4,2,-1                 ! 4 ELEMENTS ALONG BEAM LENGTH
CP,1,ROTY,9,19,29           ! COUPLE FREE END NODES FOR ROTATION
D,1,ALL,,,21,10             ! FIXED END
F,19,MY,10                  ! APPLY BENDING MOMENT AT FREE EDGE
BFUNIF,TEMP,100             ! ELEVATED TEMPERATURE LOAD
FINISH
/SOLU    
SOLVE
FINISH
/POST1
NSEL,S,LOC,X,8              ! SELECT FREE EDGE
PRNSOL,U,Z                  ! PRINT DISPLACEMENTS
*GET,U1,NODE,9,U,Z
NSEL,S,LOC,Y                ! SELECT NODES ALONG LENGTH
SHELL,TOP
PRNSOL,S,COMP               ! PRINT TOP STRESSES
*GET,ST1,NODE,1,S,X
SHELL,BOT
PRNSOL,S,COMP               ! PRINT BOTTOM STRESSES
*GET,SB1,NODE,1,S,X 
ALLSEL  
*DIM,LABEL,CHAR,3,2
*DIM,VALUE,,3,3
LABEL(1,1) = 'DISP ','PRS TP ','PRS BTM '
LABEL(1,2) = 'in','psi','psi'
*VFILL,VALUE(1,1),DATA,.832,2258,1731
*VFILL,VALUE(1,2),DATA,U1,ST1,SB1
*VFILL,VALUE(1,3),DATA,ABS(U1/.832),ABS(ST1/2258),ABS(SB1/1731)
SAVE,TABLE_4
RESUME,TABLE_1
/COM
/OUT,vm144,vrt
/COM,------------------- VM144 RESULTS COMPARISON --------------
/COM,
/COM,                 |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
/COM,RESULTS USING LAYERED SOLID 185
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.3,'  ',F12.3,'   ',1F15.3)
/NOPR
RESUME,TABLE_2
/GOPR
/COM,
/COM,RESULTS USING LAYERED SOLID186
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.3,'  ',F12.3,'   ',1F15.3)
/NOPR
RESUME, TABLE_3
/GOPR
/COM,RESULTS USING LAYERED SOLSH190
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.3,'  ',F12.3,'   ',1F15.3)
/NOPR
RESUME, TABLE_4
/GOPR
/COM,RESULTS USING LAYERED SHELL281
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.3,'  ',F12.3,'   ',1F15.3)
/COM,-----------------------------------------------------------
/OUT
FINISH
*LIST,vm144,vrt

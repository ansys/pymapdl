/COM,ANSYS MEDIA REL. 150 (11/8/2013) REF. VERIF. MANUAL: REL. 150
/VERIFY,VM173
/PREP7
/TITLE, VM173, CENTERLINE TEMPERATURE OF AN ELECTRICAL WIRE
C***      HEAT, MASS AND MOMENTUM TRANS., ROHSENOW AND CHOI, 2ND. PR., PAGE 106,
C***      EX. 6.5, USING SOLID5 ELEMENTS
ET,1,SOLID5,1                    ! SOLID5, TEMP,VOLT,MAG DOF OPTION
MP,KXX,1,13                      ! THERMAL CONDUCTIVITY
MP,RSVX,1,8.983782E-8            ! ELECTRICAL RESISTIVITY
CSYS,1
N,1,1E-10,-5                     ! MOVE AWAY FROM ORIGIN FOR THETA SPEC.
N,6,.03125,-5
FILL
NGEN,2,10,1,6,1,,10              ! MODEL 10 DEG. SECTOR
NGEN,2,20,1,16,1,,,-(1/12)       ! ARBITRARY Z-LENGTH OF 1 INCH
NUMMRG,NODE                      ! MERGE COINCIDENT NODES AT ORIGIN
E,2,12,1,1,22,32,21,21           ! GENERATE ELEMENTS
E,2,3,13,12,22,23,33,32
EGEN,4,1,2
CP,1,TEMP,1,21                   ! COUPLING TO ENSURE AXIAL SYMMETRY
CP,2,TEMP,2,12,22,32             ! COUPLING TO ENSURE CIRCUMFERENTIAL SYMMETRY
CPSGEN,5,1,2
NSEL,S,LOC,Z,0
D,ALL,VOLT,0                     ! SET VOLTAGES
NSEL,INVE
D,ALL,VOLT,-(.1/12)              ! .1 VOLT/FT OVER 1 IN LENGTH
NSEL,S,LOC,X,.03125
SF,ALL,CONV,5,70
NSEL,ALL
FINISH
/SOLU    
SOLVE
FINISH
/POST1
NSEL,S,LOC,X,0
PRNSOL,TEMP                      ! RESULTS AT CENTERLINE
*GET,T,NODE,1,TEMP
NSEL,S,LOC,X,.03125
PRNSOL,TEMP                      ! RESULTS AT OUTER RADIUS
*GET,TEMP,NODE,6,TEMP        ! GET TEMPERATURE AT SURFACE NODE
PI=2*ASIN(1)
LENG=2*(0.375/12)*SIN(PI/36) ! LENGTH ALONG 10 DEG ON OUTER FACE
AREA=LENG*36                 ! COMPUTE AREA OF OUTER FACE (360 DEG)
HRATE=AREA*5.0*(TEMP-70)     ! TOTAL HEAT DISSIPATION RATE
*status,parm                      ! SHOW PARAMETER STATUS
*DIM,LABEL,CHAR,3,2
*DIM,VALUE,,3,3
LABEL(1,1) = 'T(CL) ','T(S) ','Q '
LABEL(1,2) = 'DEG F','DEG F','BTU/hr/ft'
*VFILL,VALUE(1,1),DATA,419.9,417.9,341.5
*VFILL,VALUE(1,2),DATA,T,TEMP,HRATE
*VFILL,VALUE(1,3),DATA,ABS(T/419.9),ABS(TEMP/417.9),ABS(HRATE/341.5)
/COM
/OUT,vm173,vrt
/COM,------------------- VM173 RESULTS COMPARISON -------------
/COM,
/COM,                 |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.1,'  ',F14.1,'   ',1F15.3)
/COM,----------------------------------------------------------
/OUT

FINISH
*LIST,vm173,vrt


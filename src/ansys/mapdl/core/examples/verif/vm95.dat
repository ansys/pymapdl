/COM,ANSYS MEDIA REL. 2023R1 (11/04/2022) REF. VERIF. MANUAL: REL. 2023R1
/VERIFY,VM95
/PREP7
/TITLE, VM95, HEAT TRANSFER FROM A COOLING SPINE
C***  PRINCIPLES OF HEAT TRANSFER, KREITH, 2ND. PRINTING, PAGE 48, EQNS. 2-44,45
C***  USING LINK33 AND LINK34 ELEMENTS
ANTYPE,STATIC
ET,1,LINK33
ET,2,LINK34
R,1,(1/144)                     ! CONVERT AREAS INTO SQUARE FT. UNITS
R,2,(1/72)
R,3,(4/144)
MP,KXX,1,25
MP,HF,1,1
N,1  
N,9,(8/12)
FILL
N,11  
N,19
FILL
E,1,2                           ! DEFINE ELEMENTS
TYPE,2 
REAL,2 
E,1,11      
TYPE,1 
REAL,1 
E,2,3
TYPE,2 
REAL,3 
E,2,12
EGEN,7,1,3,4
REAL,2 
E,9,19
D,1,TEMP,100                    ! DEFINE WALL AND TIP TEMPERATURES
D,11,TEMP,,,19                 
FINISH
/SOLU    
SOLVE
FINISH
/POST1
PRNSOL,TEMP                     ! PRINT NODAL TEMPERATURES
NSEL,S,NODE,,1                  ! SELECT NODE 1
PRNLD,HEAT                      ! PRINT NODAL HEAT FLOWS
FSUM                            ! PRINT HEAT FLOW SUMMATION
ALLSEL
*GET,TL,NODE,9,TEMP
/POST26
ESOL,2,2,1,HEAT,,HEAT
ESOL,3,1,1,HEAT,,HEAT
STORE
*GET,HEAT,VARI,2,EXTREM,VMAX
*GET,HEAT2,VARI,3,EXTREM,VMAX
HTTOT=(ABS(HEAT+HEAT2))
*DIM,LABEL,CHAR,2,2
*DIM,VALUE,,2,3
LABEL(1,1) = '      TL','Q,  BTU/'
LABEL(1,2) = ', F     ','hr	 '
*VFILL,VALUE(1,1),DATA,68.594,17.504
*VFILL,VALUE(1,2),DATA,TL,HTTOT
*VFILL,VALUE(1,3),DATA,ABS(TL/68.594),ABS(HTTOT/17.504)
SAVE,TABLE1
FINISH

/CLEAR, NOSTART ! CLEAR DATABASE BEFORE STARTING PART 2
/PREP7
C***  USING SOLID70 ELEMENTS
ANTYPE,STATIC
ET,1,SOLID70
MP,KXX,1,25
LOCAL,11,0,,(-.5/12),(-.5/12)
N,1  
N,9,(8/12)
FILL
NGEN,2,10,1,9,1,,,(1/12)
NGEN,2,20,1,19,1,,(1/12)
E,1,2,22,21,11,12,32,31
EGEN,8,1,1
CP,1,TEMP,2,12,22,32             ! COUPLE APPROPRIATE NODAL TEMPERATURES
CPSGEN,8,1,1                     ! GENERATE 8 COUPLED SETS
NSEL,S,LOC,X,0  
D,ALL,TEMP,100
NSEL,ALL
SFE,ALL,1,CONV,,1
SFE,ALL,2,CONV,,1
SFE,ALL,4,CONV,,1
SFE,ALL,6,CONV,,1
sfe,all,1,conv,2,0.0			! bulk temperature
sfe,all,2,conv,2,0.0			! bulk temperature
sfe,all,4,conv,2,0.0			! bulk temperature
sfe,all,6,conv,2,0.0			! bulk temperature
FINISH
/SOLU    
SOLVE
FINISH
/POST1
NSEL,S,LOC,X,(8/12)              ! SELECT NODES AT X=L
PRNSOL,TEMP 
NSEL,ALL                         ! PRINT NODAL TEMPERATURES
PRNLD,HEAT                       ! PRINT NODAL HEAT FLOWS AT WALL
NSEL,S,LOC,X,0                   ! SELECT NODES AT X=0
FSUM                             ! PRINT HEAT FLOW SUMMATION
ALLSEL
*GET,TL,NODE,9,TEMP
/POST26
ESOL,2,1,11,HEAT,,HEAT
ESOL,3,1,1,HEAT,,HEAT
ESOL,4,1,21,HEAT,,HEAT
ESOL,5,1,31,HEAT,,HEAT
STORE
*GET,HEAT,VARI,2,EXTREM,VMAX
*GET,HEAT2,VARI,3,EXTREM,VMAX
*GET,HEAT3,VARI,4,EXTREM,VMAX
*GET,HEAT4,VARI,5,EXTREM,VMAX
HTTOT=(ABS(HEAT+HEAT2+HEAT3+HEAT4))
*DIM,LABEL,CHAR,2,2
*DIM,VALUE,,2,3
LABEL(1,1) = '      TL','Q,  BTU/'
LABEL(1,2) = ', F     ','hr	 '
*VFILL,VALUE(1,1),DATA,68.594,17.504
*VFILL,VALUE(1,2),DATA,TL,HTTOT
*VFILL,VALUE(1,3),DATA,ABS(TL/68.594),ABS(HTTOT/17.504)
SAVE,TABLE2
RESUME,TABLE1
/COM
/OUT,vm95,vrt
/COM,------------------- VM95 RESULTS COMPARISON --------------
/COM,
/COM,                 |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
/COM,LINK33&LINK34:
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.3,'  ',F14.3,'   ',1F15.3)
/NOPR
RESUME,TABLE2
/GOPR
/COM,
/COM,SOLID70:
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.3,'  ',F14.3,'   ',1F15.3)
/COM,----------------------------------------------------------
/OUT
FINISH
*LIST,vm95,vrt

! Copyright (C) 2024 ANSYS, Inc. and/or its affiliates.
! SPDX-License-Identifier: MIT
! 
!
! Permission is hereby granted, free of charge, to any person obtaining a copy
! of this software and associated documentation files (the "Software"), to deal
! in the Software without restriction, including without limitation the rights
! to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
! copies of the Software, and to permit persons to whom the Software is
! furnished to do so, subject to the following conditions:
!
! The above copyright notice and this permission notice shall be included in all
! copies or substantial portions of the Software.
!
! THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
! IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
! FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
! AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
! LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
! OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
! SOFTWARE.

/COM,ANSYS MEDIA REL. 2023R2 (05/12/2023) REF. VERIF. MANUAL: REL. 2023R2
/VERIFY,vmr029-t1-190
/TITLE,vmr029-t1-190,ELASTIC LARGE DEFLECTION RESPONSE OF A Z-SHAPED CANTILEVER UNDER END LOAD
/COM, REFERENCE: NAFEMS GEOMETRIC NONLINEAR BEHAVIOUR OF 3D BEAMS AND SHELLS
/COM, MANUAL TEST 3DNLG-1
/SHOW,JPEG
JPGPRF,500,100,1

/PREP7
MP,EX,1,2.0E5
MP,NUXY,1,0.3
ET,1,190,,0
KEYOPT,1,8,1
LENGTH = 60.0
THICK = 1.7
WIDTH = 20.0
MY_FORCE  = 2000.0
K,1,0,0,THICK
K,2,0,0,0
k,3,LENGTH,0,0
k,4,LENGTH,0,THICK
K,5,2.0*LENGTH,0,30.0
K,6,2.0*LENGTH,0,30.0+THICK
K,7,3.0*LENGTH,0,30.0
K,8,3.0*LENGTH,0,30.0+THICK
K,9, 0,WIDTH,THICK
K,10,0,WIDTH,0
K,11,LENGTH,WIDTH,0
K,12,LENGTH,WIDTH,THICK
K,13,2.0*LENGTH,WIDTH,30.0
K,14,2.0*LENGTH,WIDTH,30.0+THICK
K,15,3.0*LENGTH,WIDTH,30.0
K,16,3.0*LENGTH,WIDTH,30.0+THICK
V,2,3,11,10,1,4,12,9
V,3,5,13,11,4,6,14,12
V,5,7,15,13,6,8,16,14
VEORIENT,1,THIN
VEORIENT,2,THIN
VEORIENT,3,THIN
ESIZE,10.0
VMESH,1
VMESH,2
VMESH,3
NSEL,S,LOC,X
D,ALL,ALL
NSEL,ALL
NSEL,S,LOC, X,3.0*LENGTH
NSEL,R,LOC, Y,0.0
NSEL,R,LOC, Z,30.0+THICK
F,ALL,FZ,MY_FORCE
NSEL,S,LOC, X,3.0*LENGTH
NSEL,R,LOC, Y,WIDTH
NSEL,R,LOC, Z, 30.0+THICK
F,ALL,FZ,MY_FORCE
NSEL,ALL
FINI

/SOLU
NLGEOM,ON
NSUB,200
OUTRES,ALL,ALL
/OUT,SCRATCH
SOLVE
FINI

/POST26
NUMVAR,20
/GOLIST
/GROPT,VIEW,0
/GTHK,CURVE,1
/GROPT,FILL,OFF
/GRID,1
/GTHK,GRID,1
/GROPT,CGRID,1
/AXLAB,X,TIP DEFLECTION
/AXLAB,Y,LOAD
/GTHK,AXIS,1
/GRTYP,0
/GROPT,ASCAL,ON
/GROPT,LOGX,OFF
/GROPT,LOGY,OFF
/GROPT,AXDV,1
/GROPT,AXNM,ON
/GROPT,AXNSC,1,
/GROPT,DIG1,4,
/GROPT,DIG2,3,
/XRANGE,0,150
/YRANGE,0,4000
/GOPR
/OUT,
NSOL,2,85,U,Z
RFORCE,3,1,F,Z,
RFORCE,4,10,F,Z,
RFORCE,5,16,F,Z,
RFORCE,6,23,F,Z,
RFORCE,7,31,F,Z,
RFORCE,8,37,F,Z,
ADD,9,3,4,5
ADD,10,6,7,8
ADD,11,9,10
PROD,12,11, , , , , ,-1,1,1,
XVAR,2
/SHOW,,jpeg
PLVAR,12
PRVAR,12,2
FINISH

/POST1
SET,LIST,2
SET,LAST
*GET,UY1,NODE,85,U,Z
HF1=UY1/143.43
/GOPR
*STATUS,PARM
FINISH
*DIM,VALUE,,1,3
*DIM,LABEL,CHAR,2
LABEL(1) = 'vmr029-','t1-190'
*VFILL,VALUE(1,1),DATA,4000
*VFILL,VALUE(1,2),DATA,UY1
*VFILL,VALUE(1,3),DATA,HF1
/OUT,vmr029-t1-190,vrt
/COM
/COM,-------------- VMR029-T1 RESULTS COMPARISON--------------
/COM,
/COM,|  LOAD  |      Mechanical APDL      |   RATIO   |      TEST      |
/COM,
/COM,SOLSH190
*VWRITE,VALUE(1,1), VALUE(1,2), VALUE(1,3), LABEL(1),LABEL(2)
(1X,F8.2,'     ',F12.2,'      ',F12.2,'      ',A7,A8)
/COM,
/COM,----------------------------------------------------------
/OUT
FINISH
*LIST,vmr029-t1-190,vrt

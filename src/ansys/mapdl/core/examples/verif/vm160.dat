! Copyright (C) 2024 ANSYS, Inc. and/or its affiliates.
! SPDX-License-Identifier: MIT
! 
!
! Permission is hereby granted, free of charge, to any person obtaining a copy
! of this software and associated documentation files (the "Software"), to deal
! in the Software without restriction, including without limitation the rights
! to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
! copies of the Software, and to permit persons to whom the Software is
! furnished to do so, subject to the following conditions:
!
! The above copyright notice and this permission notice shall be included in all
! copies or substantial portions of the Software.
!
! THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
! IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
! FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
! AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
! LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
! OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
! SOFTWARE.

/COM,ANSYS MEDIA REL. 2023R2 (05/12/2023) REF. VERIF. MANUAL: REL. 2023R2
/VERIFY,VM160
/PREP7
/TITLE, VM160, SOLID CYLINDER WITH HARMONIC TEMPERATURE LOAD
C***    HILDEBRAND, ADVANCED CALCULUS FOR APPLICATIONS, PAGE 447
ET,1,PLANE78               ! AXISYMMETRIC THERMAL SOLID
MP,KXX,1,1
N,1
N,9,20
FILL
NGEN,3,10,1,9,1,,2.5
E,1,3,23,21,2,13,22,11
EGEN,4,2,1
D,9,TEMP,80,,29,10         ! PEAK TEMPERATURE AT THETA=0
MODE,2,1                   ! SYMMETRIC MODE WITH 2 WAVES AROUND PERIPHERY
FINISH
/SOLU    
OUTPR,ALL,LAST             ! PRINTOUT ELEMENT SOLUTION
/OUT,SCRATCH
SOLVE
FINISH
/POST1
SET,1,1,,,,0.0             ! STORE SOLUTION DATA AT 0 DEGREES
/OUT,
NSEL,S,NODE,,1,9           ! SELECT NODES 1-9
*GET,T1,NODE,1,TEMP
*GET,T2,NODE,3,TEMP
*GET,T3,NODE,5,TEMP
*GET,T4,NODE,7,TEMP
PRNSOL,TEMP                ! PRINT TEMPERATURE
SET,1,1,,,,90.0            ! STORE SOLUTION DATA AT 90 DEGREES
*GET,T5,NODE,1,TEMP
*GET,T6,NODE,3,TEMP
*GET,T7,NODE,5,TEMP
*GET,T8,NODE,7,TEMP
PRNSOL,TEMP                ! PRINT TEMPERATURE
*status,parm
*DIM,LABEL,CHAR,4,2
*DIM,VALUE,,4,3
LABEL(1,1) = 'T (ND=1) ','T (ND=3) ','T (ND=5) ','T (ND=7)'
LABEL(1,2) = 'F','F','F','F'
*VFILL,VALUE(1,1),DATA,0,5,20,45
*VFILL,VALUE(1,2),DATA,T1,T2,T3,T4
*VFILL,VALUE(1,3),DATA,ABS(0),ABS(T2/5),ABS(T3/20),ABS(T4/45)
/COM
/OUT,vm160,vrt
/COM,------------------- VM160 RESULTS COMPARISON -------------
/COM,
/COM,THETA=0       |      TARGET   |     Mechanical APDL   |   RATIO
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.1,'  ',F13.1,'   ',1F18.3)
/NOPR
*VFILL,VALUE(1,1),DATA,0,-5,-20,-45
*VFILL,VALUE(1,2),DATA,T5,T6,T7,T8
*VFILL,VALUE(1,3),DATA,ABS(0),ABS(T6/5),ABS(T7/20),ABS(T8/45)
/GOPR
/COM,
/COM,THETA=90      |      TARGET   |     Mechanical APDL   |   RATIO
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.1,'  ',F13.1,'   ',1F18.3)
/COM,---------------------------------------------------------
/OUT
FINISH
*LIST,vm160,vrt

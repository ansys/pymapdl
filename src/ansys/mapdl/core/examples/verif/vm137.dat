/COM,ANSYS MEDIA REL. 2023R1 (11/04/2022) REF. VERIF. MANUAL: REL. 2023R1
/VERIFY,VM137
/PREP7
/TITLE, VM137, LARGE DEFLECTION OF A CIRCULAR MEMBRANE
C***    THEORY OF PLATES AND SHELLS, TIMOSHENKO, P. 404, EQ. 236
ET,1,SHELL208,,,2
SECTYPE,1,SHELL
SECDATA,0.0001
SECNUM,1
MP,EX,1,30E6
MP,NUXY,1,.25
MP,ALPX,1,1E-5
N,1
N,11,10
FILL
E,1,2
EGEN,10,1,1
FINISH
/SOLU
ANTYPE,STATIC
NLGEOM,ON          ! LARGE DEFLECTION OPTION
CNVTOL,F,,,,1      ! USE FORCE CONVERGENCE ONLY, MINIMUM REFERENCE VALUE OF 1
OUTPR,,LAST
TUNIF,-50          ! THERMAL PRESTRESS TO START
NSEL,S,LOC,X,0
DSYM,SYMM,X        ! SYMMETRY B.C. AT X=0
NSEL,ALL
D,11,ALL
/OUT,SCRATCH
SOLVE
KBC,1              ! STEP B.C.
SF,ALL,PRES,.1     ! APPLY PRESSURE LOAD
SOLVE
TUNIF,0            ! REMOVE THERMAL PRESSURES
SOLVE
/POST1
/OUT,
SET,3
SHELL,MID
*GET,UY,NODE,1,U,Y
ESEL,S,ELEM,,1,1
ETABLE,CENT,S,X
ESORT,CENT
*GET,PRSCNT,SORT,,MAX
ESEL,S,ELEM,,10,10
ETABLE,CEN,S,X
ESORT,CEN
*GET,PRSOUT,SORT,,MAX
*STATUS,PARM
*DIM,LABEL,CHAR,3,2
*DIM,VALUE,,3,3
LABEL(1,1) = 'Y DISP ','PRES CENT ','PRES RIM'
LABEL(1,2) = ' in',' psi',' psi'
*VFILL,VALUE(1,1),DATA,-.459,61010,47310
*VFILL,VALUE(1,2),DATA,UY,PRSCNT,PRSOUT
*VFILL,VALUE(1,3),DATA,ABS(UY/.459),ABS(PRSCNT/61010),ABS(PRSOUT/47310)
/COM
/OUT,vm137,vrt
/COM,------------------- VM137 RESULTS COMPARISON --------------
/COM,
/COM,   LOAD STEP 3   |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.3,'  ',F12.3,'   ',1F15.3)
/COM,-----------------------------------------------------------
/OUT
FINISH
*LIST,vm137,vrt

/COM,ANSYS MEDIA REL. 2023R1 (11/04/2022) REF. VERIF. MANUAL: REL. 2023R1
/VERIFY,VM174
/PREP7
MP,PRXY,,0.3
MP,PRXY,,0.3
SMRT,OFF
JPGPRF,500,100,1             ! MACRO TO SET PREFS FOR JPEG PLOTS
/SHOW,JPEG
/TITLE, VM174, BIMETALLIC BEAM UNDER THERMAL LOAD
/COM, THEORY OF THERMAL STRESS, BOLEY AND WEINER,
/COM, 1985 PRINTING, PG. 429
ANTYPE,STATIC                   ! COUPLED FIELD STATIC ANALYSIS
NLGEOM,ON                       ! LARGE DEFLECTION
ET,1,PLANE13,4,,2               ! 2-D COUPLED FIELD SOLID, PLANE STRESS
MP,EX,1,10E6
MP,EX,2,10E6
MP,ALPX,1,14.5E-6
MP,ALPX,2,2.5E-6
MP,KXX,1,5                      ! THERMAL CONDUCTIVITY
MP,KXX,2,5
MP,PRXY,1,0.3
MP,PRXY,2,0.3
K,1                             ! DEFINE GEOMETRY
K,2,5
KGEN,3,1,2,1,,.05
L,1,2
*REPEAT,3,2,2
LESIZE,ALL,,,5
A,1,2,4,3
AATT,2
A,3,4,6,5
ESIZE,,1
AMESH,ALL
NSEL,S,LOC,X,0
NSEL,R,LOC,Y,.05
D,ALL,UY
NSEL,S,LOC,Y,.1
D,ALL,TEMP,400.0                ! APPLY TOP SURFACE TEMPERATURE
NSEL,S,LOC,Y,0
D,ALL,TEMP,400.0                ! APPLY BOTTOM SURFACE TEMPERATURE
NSEL,S,LOC,X,5
DSYM,SYMM,0,X
NSEL,ALL
FINISH
/SOLU
CNVTOL,F,,,,0.1                 ! CONVERGENCE BASED ON FORCE ONLY
SOLVE
FINISH
/POST1
SET,1
/DSCALE,1,1                     ! TRUE SCALING OPTION
PLDISP,1                        ! DISPLAY DEFLECTED AND UNDEFLECTED SHAPE
NSEL,S,LOC,X,5
NSEL,R,LOC,Y,.05
PRNSOL,U,COMP                   ! PRINT DISPLACEMENTS
NPOST=NODE(5,0.05,0)
*GET,Y,NODE,NPOST,U,Y
PRNSOL,TEMP                     ! PRINT TEMPERATURES
*GET,T,NODE,NPOST,TEMP
*DIM,LABEL,CHAR,2,2
*DIM,VALUE,,2,3
LABEL(1,1) = 'Y ','TEMP,'
LABEL(1,2) = 'IN',' F'
*VFILL,VALUE(1,1),DATA,.9,400
*VFILL,VALUE(1,2),DATA,Y,T
*VFILL,VALUE(1,3),DATA,ABS(Y/.9),ABS(T/400)
/COM
/OUT,vm174,vrt
/COM,------------------- VM174 RESULTS COMPARISON -------------
/COM,
/COM,                 |   TARGET   |   Mechanical APDL   |   RATIO
/COM,PLANE13
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.3,'  ',F14.3,'   ',1F15.3)
/OUT
FINISH
!
LABEL=
VALUE=
!
/PREP7
DDELE,ALL,ALL
ACLEAR,ALL
ET,1,PLANE223,11,,0             ! 2-D COUPLED FIELD SOLID, PLANE STRESS
AMESH,ALL
NSEL,S,LOC,X,0
NSEL,R,LOC,Y,.05
D,ALL,UY
NSEL,S,LOC,Y,.1
D,ALL,TEMP,400.0                ! APPLY TOP SURFACE TEMPERATURE
NSEL,S,LOC,Y,0
D,ALL,TEMP,400.0                ! APPLY BOTTOM SURFACE TEMPERATURE
NSEL,S,LOC,X,5
DSYM,SYMM,0,X
NSEL,ALL
FINISH
/SOLU
CNVTOL,F,,,,0.1                 ! CONVERGENCE BASED ON FORCE ONLY
SOLVE
FINISH
/POST1
SET,1
/DSCALE,1,1                     ! TRUE SCALING OPTION
PLDISP,1                        ! DISPLAY DEFLECTED AND UNDEFLECTED SHAPE
NSEL,S,LOC,X,5
NSEL,R,LOC,Y,.05
PRNSOL,U,COMP                   ! PRINT DISPLACEMENTS
NPOST=NODE(5,0.05,0)
*GET,Y,NODE,NPOST,U,Y
PRNSOL,TEMP                     ! PRINT TEMPERATURES
*GET,T,NODE,NPOST,TEMP
*DIM,LABEL,CHAR,2,2
*DIM,VALUE,,2,3
LABEL(1,1) = 'Y ','TEMP,'
LABEL(1,2) = 'IN',' F'
*VFILL,VALUE(1,1),DATA,.9,400
*VFILL,VALUE(1,2),DATA,Y,T
*VFILL,VALUE(1,3),DATA,ABS(Y/.9),ABS(T/400)
/COM
/OUT,vm174,vrt,,APPEND
/COM,PLANE223
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.3,'  ',F14.3,'   ',1F15.3)
/COM,----------------------------------------------------------
/OUT
FINISH
*LIST,vm174,vrt

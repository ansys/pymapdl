/COM,ANSYS MEDIA REL. 2023R1 (11/04/2022) REF. VERIF. MANUAL: REL. 2023R1
/VERIFY,VM159
JPGPRF,500,100,1             ! MACRO TO SET PREFS FOR JPEG PLOTS
/SHOW,JPEG
/PREP7
/TITLE, VM159, TEMPERATURE CONTROLLED HEATER
C***            REFERENCE - SELF-CHECKING (RESPONSE FOLLOWS INPUT REQUEST)
ANTYPE,TRANS
ET,1,MASS71,,,1            ! THERMAL MASS
ET,2,LINK34                ! CONVECTION ELEMENT
ET,3,COMBIN37,,,8,,1       ! CONTROL ELEMENT
R,1,2.7046E-4              ! THERMAL CAPACITANCE OF HEATER
R,2,2.7046E-3              ! THERMAL CAPACITANCE OF BOX
R,3,8.1812E-3              ! SURFACE AREA OF HEATER
R,4,4.1666E-2              ! SURFACE AREA OF BOX
R,5,,,,100,125,-10         ! CONTROL TEMPERATURES, HEAT FLOW
RMORE,,1                   ! INITIAL CONTROL STATUS (ON)
MP,HF,1,4
N,1
*REPEAT,4,1
E,1                        ! HEATER
TYPE,2
REAL,3
E,1,2                      ! CONVECTION LINK
TYPE,1
REAL,2
E,2                        ! BOX
TYPE,3
REAL,5
E,4,1,2                    ! CONTROL
TYPE,2
REAL,4
E,2,3                      ! CONVECTION LINK
FINISH
/SOLU    
TIME,.2
IC,1,TEMP,70               ! UNIFORM STARTING TEMPERATURE
IC,2,TEMP,70
KBC,1
D,3,TEMP,70
D,4,TEMP,0
AUTOTS,ON
OUTPR,,10
OUTRES,,ALL
TINTP,,,,0.5,0.5,0.2
DELTIM,0.10000E-02,0.10000E-02,0.20000
/OUT,SCRATCH
SOLVE
FINISH
/POST26
NSOL,2,1,TEMP
NSOL,3,2,TEMP
ESOL,4,4,,NMISC,1,STAT
/OUT,
PRVAR,2,3,4
/GRID,1                   ! DISPLAY BOX TEMP VS. TIME
/AXLAB,Y,TEMP
PLVAR,3
/GRID,0                   ! DISPLAY STATUS VS. TIME
/AXLAB,Y,STAT
PLVAR,4
FINISH
/OUT,vm159,vrt
/COM,-----------------------------------------------------------
/COM,NOTE: THERE ARE VERIFIED RESULTS IN VM159  NOT CONTAINED IN
/COM,THIS TABLE
/COM,-----------------------------------------------------------
/OUT
FINISH
*LIST,vm159,vrt

/COM,ANSYS MEDIA REL. 2023R1 (11/04/2022) REF. VERIF. MANUAL: REL. 2023R1
/VERIFY,VM84
JPGPRF,500,100,1             ! MACRO TO SET PREFS FOR JPEG PLOTS
/SHOW,JPEG
/PREP7                  
/TITLE, VM84, DISPLACEMENT PROPAGATION ALONG A BAR WITH FREE ENDS
C***      VIBRATION PROBS. IN ENGR., TIMOSHENKO, 3RD. ED., PAGE 311, PROB. 2
ET,1,LINK180
SECTYPE,1,LINK   
SECDATA,2                ! AREA = 2
MP,EX,1,3E7
MP,DENS,1,7202E-7
N,1
N,17,48E3
FILL
E,1,2
EGEN,16,1,1
D,ALL,UZ
D,ALL,UY
FINISH
/OUT,SCRATCH
/SOLU
ANTYPE,MODAL
MODOPT,LANB,17          ! PERFORM MODAL SOLVE USING LANB EIGENSOLVER
MXPAND,17
SOLVE
FINISH
/SOLU    
ANTYPE,TRANSIENT
TRNOPT,MSUP,17           ! PERFORM MODE SUPERPOSITION TRANSIENT SOLVE
DELTIM,5E-3              ! INTEGRATION TIME STEP SIZE
KBC,1                    ! STEP LOADING CONDITION
OUTPR,BASIC,LAST
F,17,FX                  ! DEFINE NULL FX LOAD AT NODE 17
SOLVE

OUTPR,BASIC,2
OUTRES,NSOL,1 
TIME,.24                 ! FINAL TIME INCLUDES 1/2 OF THE FUNDAMENTAL PERIOD
F,17,FX,6000             ! APPLY FULL LOAD TO NODE 17
SOLVE
FINISH

/OUT,
/POST26
FILE,,rdsp               ! REDUCED DISPLACEMENTS FILE
NSOL,2,1,U,X,1UX         ! STORE APPROPRIATE NODAL DISPLACEMENTS
NSOL,3,9,U,X,9UX
NSOL,4,17,U,X,17UX
DERIV,5,2,,,1VX          ! COMPUTE VELOCITIES
DERIV,6,3,,,9VX
DERIV,7,4,,,17VX
/GRID,1                  ! TURN GRID ON
/AXLAB,Y,DISP            ! Y-AXIS LABEL DISP
PLVAR,2,3,4              ! DISPLAY VARIABLES 2, 3 AND 4
/AXLAB,Y,VELO            ! Y-AXIS LABEL VELO
PLVAR,5,6,7              ! DISPLAY VARIABLES 5, 6 AND 7
PRTIME,0.230,0.240       ! APPROPRIATE TIME RANGE (.23 TO .24)
PRVAR,4                  ! PRINT VARIABLE 4 (UX AT NODE 17)
*GET,DEF,VARI,4,RTIME,.240
*DIM,LABEL,CHAR,1,2
*DIM,VALUE,,1,3
LABEL(1,1) = '    DEF,'
LABEL(1,2) = 'in      '
*VFILL,VALUE(1,1),DATA,4.8 
*VFILL,VALUE(1,2),DATA,DEF 
*VFILL,VALUE(1,3),DATA,ABS(DEF/4.8)
/COM
/OUT,vm84,vrt
/COM,------------------- VM84 RESULTS COMPARISON --------------
/COM,
/COM,                 |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.4,'  ',F14.4,'   ',1F15.3)
/COM,----------------------------------------------------------
/OUT
FINISH
*LIST,vm84,vrt

! Copyright (C) 2024 ANSYS, Inc. and/or its affiliates.
! SPDX-License-Identifier: MIT
! 
!
! Permission is hereby granted, free of charge, to any person obtaining a copy
! of this software and associated documentation files (the "Software"), to deal
! in the Software without restriction, including without limitation the rights
! to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
! copies of the Software, and to permit persons to whom the Software is
! furnished to do so, subject to the following conditions:
!
! The above copyright notice and this permission notice shall be included in all
! copies or substantial portions of the Software.
!
! THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
! IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
! FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
! AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
! LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
! OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
! SOFTWARE.

/COM,ANSYS MEDIA REL. 2023R2 (05/12/2023) REF. VERIF. MANUAL: REL. 2023R2
/VERIFY,vmr049-cr2-183
/TITLE,vmr049-cr2-183,CONSTANT-DISPLACEMENT CREEP BENCHMARK
/COM, REFERENCE: TEST CR-2A FROM NAFEMS REPORT 0049.
/SHOW,JPEG
JPGPRF,500,100,1

/PREP7
C*** PARAMETRIC INPUT FOR CREEP CONSTANTS (TIME HARDENING)!***
*SET,C1,1.5625E-14
*SET,C2,5.0
*SET,C3,-0.5
*SET,C4,0
C***  TIME PARAMETER
*SET,HOUR,1000
C***   ELASTIC CONSTANT
MP,EX,1,200E3
MP,NUXY,1,0.3
TUNIF,HOT
TOFF,OFFS
TB,CREEP,1,,,6
TBDATA,1,C1,C2,C3,C4
C*** PARAMETRIC INPUT FOR CREEP CONSTANTS (STRAIN HARDENING)!***
*SET,C1,1.5625E-14
*SET,C2,5
*SET,C3,-0.5
MP,EX,2,200E3
MP,NUXY,2,0.3
TB,CREEP,2,,,7
TBDATA,1,C1,C2,C3
SAVE
N,1
N,2,100
N,3,100,100
N,4,0,100
N,5,50,0
N,6,100,50
N,7,50,100
N,8,0,50
ET,1,183,,,
E,1,2,3,4,5,6,7,8
NSEL,ALL
NSEL,S,LOC,X
D,ALL,UX
NSEL,S,LOC,Y
D,ALL,UY
NSEL,S,LOC,X,100
D,ALL,UX,0.1
NSEL,S,LOC,Y,100
D,ALL,UY,0.1
NSEL,ALL
*DO,I,1,2
*STATUS,I

/PREP7
*IF,I,EQ,1,THEN
MAT,1
*ELSEIF,I,EQ,2,THEN
MPCHG,2,ALL
*ENDIF
FINI

/SOLU
RATE, OFF
DELT,1.0E-8,1.0E-9,1.0E-8
TIME, 1.0E-8
/OUTPUT,SCRATCH
OUTRES,ALL,LAST
SOLVE

/OUT
RATE, ON
DELT,0.10,0.099,0.10
TIME, 0.10
/OUTPUT,SCRATCH
OUTRES,ALL,LAST
SOLVE
/OUT
RATE, ON
DELT,1.0,0.99,1.0
TIME, 1.0
/OUTPUT,SCRATCH
OUTRES,ALL,LAST
SOLVE
/OUT
RATE, ON
DELT,5.0,4.99,5.0
TIME, 5.0
/OUTPUT,SCRATCH
OUTRES,ALL,LAST
SOLVE
/OUT
RATE, ON
DELT,10.0,9.99,10.0
TIME, 10.0
/OUTPUT,SCRATCH
OUTRES,ALL,LAST
SOLVE
/OUT
RATE, ON, ON
DELT,10,1,100
AUTOS,OFF
TIME, 1000
/OUT,SCRATCH
OUTRES,ALL,ALL
OUTPR,ALL,LAST
SOLVE
/OUT
FINISH

/POST26
*IF,I,EQ,1,THEN
ESOL,2,1,,S,EQV,TIMEHARD
PRVAR,2
/AXLAB,X,TIME
/AXLAB,Y,STRESS
/YRANGE,0,400
PLVAR,2
/NOERASE

/POST1
SET,1,1
*GET,SH1,NODE,3,S,X
R1=SH1/285.714
SET,6,19
*GET,SH2,NODE,3,S,X
R2=SH2/46.612
SET,6,39
*GET,SH3,NODE,3,S,X
R3=SH3/42.206
SET,6,59
*GET,SH4,NODE,3,S,X
R4=SH4/39.893
SET,6,79
*GET,SH5,NODE,3,S,X
R5=SH5/38.353
SET,6,99
*GET,SH6,NODE,3,S,X
R6=SH6/37.211
*DIM,VALUE,,6,3
*DIM,LABEL,CHAR,10
*DIM,TARGET,CHAR,10
*DIM,RATIO,,6,4
LABEL(1) = '0','200.0','400.0','600.0','800.0','1000'
TARGET(1) = '285.714','46.612','42.206','39.893','38.353','37.211'
*VFILL,VALUE(1,1),DATA,SH1,SH2,SH3,SH4,SH5,SH6
*VFILL,RATIO(1,1),DATA,R1,R2,R3,R4,R5,R6

*ELSEIF,I,EQ,2,THEN
ESOL,3,1,,S,EQV,STRAINHA
PRVAR,3
/AXLAB,X,TIME
/AXLAB,Y,STRESS
/COLOR,CURVE,BLUE,1
/YRANGE,0,400
PLVAR,3

/POST1
SET,1,1
*GET,SH1_1,NODE,3,S,X
R7=SH1_1/285.714
SET,6,19
*GET,SH2_1,NODE,3,S,X
R8=SH2_1/62.014
SET,6,39
*GET,SH3_1,NODE,3,S,X
R9=SH3_1/57.251
SET,6,59
*GET,SH4_1,NODE,3,S,X
R10=SH4_1/54.690
SET,6,79
*GET,SH5_1,NODE,3,S,X
R11=SH5_1/52.959
SET,6,99
*GET,SH6_1,NODE,3,S,X
R12=SH6_1/51.661
*DIM,VALUE1,,6,3
*DIM,LABEL1,CHAR,10
*DIM,TARGET1,CHAR,10
*DIM,RATIO1,,6,3
LABEL1(1) = '0','200.0','400.0','600.0','800.0','1000'
TARGET1(1) = '285.714','62.014','57.251','54.690','52.959','51.661'
*VFILL,VALUE1(1,1),DATA,SH1_1,SH2_1,SH3_1,SH4_1,SH5_1,SH6_1
*VFILL,RATIO1(1,1),DATA,R7,R8,R9,R10,R11,R12
/COM
/COM,--------- vmr049-cr2-183 STRESS RESULTS COMPARISON (TIME HARDENING)-----------
/COM,
/COM, COMPARE NUMERICAL VALUES LISTED BELOW WITH FIGURE 3.10(A)
/COM, ON PAGE 103 AS NO NUMERICAL RESULTS ARE PRESENTED.
/COM,
/COM,|   TIME   |   TARGET  |  Mechanical APDL  |  RATIO
/COM,
*VWRITE,LABEL(1), TARGET(1), VALUE(1,1), RATIO(1,1)
(1X,A8,'   ',1X,A8,'   ',F14.3,'   ',1F12.3)
/COM,
/COM,
/COM,--------- vmr049-cr2-183 STRESS RESULTS COMPARISON (STRAIN HARDENING)-----------
/COM,
/COM,|   TIME   |   TARGET  |  Mechanical APDL  |  RATIO
/COM,
*VWRITE,LABEL(1), TARGET1(1), VALUE1(1,1), RATIO1(1,1)
(1X,A8,'   ',1X,A8,'   ',F14.3,'   ',1F12.3)
*ENDIF
*ENDDO
FINISH


/POST26
*DIM,LABEL3,CHAR,2
*DIM,VALUE2,,2,3
LABEL3(1) = ' S6 ',' S7 '
*VFILL,VALUE2(1,1),DATA,SH6,SH6_1
*VFILL,VALUE2(1,2),DATA,R6,R12
*DIM,LABEL2,CHAR,2
LABEL2(1) = 'vmr049-','cr2-183'

/OUT,vmr049-cr2-183,vrt
/COM
/COM,------------------- vmr049-cr2 RESULTS COMPARISON ---------------------
/COM,
/COM,            |   Mechanical APDL   |   RATIO  |         INPUT         |
/COM,
/COM, PLANE183
*VWRITE,LABEL3(1),VALUE2(1,1),VALUE2(1,2),LABEL2(1),LABEL2(2)
(1X,A8,'      ',F14.3,'      ',F9.4,'     ',A7,A8)
*VWRITE,LABEL3(2),VALUE2(2,1),VALUE2(2,2),LABEL2(1),LABEL2(2)
(1X,A8,'      ',F14.3,'      ',F9.4,'     ',A7,A8)
/COM,
/COM,----------------------------------------------------------------
/OUT

FINISH
*LIST,vmr049-cr2-183,vrt

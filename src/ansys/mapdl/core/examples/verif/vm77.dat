/COM,ANSYS MEDIA REL. 2023R1 (11/04/2022) REF. VERIF. MANUAL: REL. 2023R1
/VERIFY,VM77
/OUT,SCRATCH
/PREP7
/TITLE, VM77, TRANSIENT RESPONSE TO A CONSTANT FORCE WITH A FINITE RISE TIME
C***           INTRODUCTION TO STRUCT. DYNAMICS, BIGGS, PAGE 50, EXAMPLE E
C***           DISPLACEMENT PASS USING BEAM188 AND MASS21 ELEMENTS
ET,1,BEAM188,,,3        ! BEAM USING CUBIC OPTION
SECTYPE,1,BEAM,RECT
SECDATA,18,1.647
SECCONTROL,,,2e10       ! OVERRIDE THE PROGRAM-CALCULATED TRANSVERSE SHEAR STIFFNESS
ET,2,MASS21,,,4         ! STRUCTURAL MASS
R,2,.0259067            ! MASS
MP,EX  ,1,30E3
MP,PRXY,1,0.3
N,1
N,3,240
FILL
E,1,2                   ! BEAM ELEMENTS
EGEN,2,1,1
TYPE,2
REAL,2
E,2                     ! TYPE 2 ELEMENT WITH REAL CONSTANT 2
D,1,UY
D,3,UX,,,,,UY
DSYM,SYMM,Z             ! PREVENT OUT-OF-PLANE DISPLACEMENT
FINISH

/SOLU
ANTYPE,MODAL
MODOPT,LANPCG,5         ! USE LANPCG EIGENSOLVER
MXPAND,5
SOLVE
FINISH

/SOLU
ANTYPE,TRANSIENT
TRNOPT,MSUP,5           ! PERFORM MODE SUPERPOSITION TRANSIENT SOLVE
DELTIM,.004             ! INTEGRATION TIME STEP SIZE
OUTPR,BASIC,1
OUTRES,ALL,1
F,2,FY,0                ! FORCE = 0 AT TIME = 0
SOLVE

TIME,.075               ! TIME AT END OF LOAD STEP
F,2,FY,20               ! FORCE IS RAMPED (KBC,0 IS DEFAULT) TO 20
SOLVE  

TIME,.1                 ! CONSTANT FORCE UNTIL TIME = 0.1
SOLVE
FINISH

/SOLU    
C***    EXPANSION PASS USING BEAM188 AND MASS21 ELEMENTS
EXPASS,ON               ! EXPANSION PASS ON
EXPSOL,,,0.092          ! TIME OF MAXIMUM RESPONSE
SOLVE            
FINISH

/OUT,
/POST1
ESEL,S,TYPE,,1
ETABLE,STRS,SMISC,33
*GET,STRSS,ELEM,2,ETAB,STRS
ALLSEL,ALL
FINISH

/POST26
NSOL,2,2,U,Y
STORE
*GET,TMAX,VARI,2,EXTREM,TMAX
*GET,YMAX,VARI,2,EXTREM,VMAX
*DIM,LABEL_1,CHAR,2,2
*DIM,VALUE_1,,2,3
LABEL_1(1,1) = 'T_MAX, s','Y_MAX, i'
LABEL_1(1,2) = 'ec      ','n       '
*VFILL,VALUE_1(1,1),DATA,.092,.331 
*VFILL,VALUE_1(1,2),DATA,TMAX,YMAX
*VFILL,VALUE_1(1,3),DATA,ABS(TMAX/.092),ABS(YMAX/.331)
*DIM,LABEL_2,CHAR,1,2
*DIM,VALUE_2,,1,3
LABEL_2(1,1) = 'SIG_BEND'
LABEL_2(1,2) = ', KSI   '
*VFILL,VALUE_2(1,1),DATA,-18.6
*VFILL,VALUE_2(1,2),DATA,STRSS
*VFILL,VALUE_2(1,3),DATA,ABS(STRSS/18.6) 
/COM
/OUT,vm77,vrt
/COM,------------------- VM77 RESULTS COMPARISON --------------
/COM,
/COM,                 |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
/COM,TRANSIENT:
/COM,
*VWRITE,LABEL_1(1,1),LABEL_1(1,2),VALUE_1(1,1),VALUE_1(1,2),VALUE_1(1,3)
(1X,A8,A8,'   ',F10.3,'  ',F14.3,'   ',1F15.2)
/COM,
/COM,EXPANSION PASS:
/COM,
*VWRITE,LABEL_2(1,1),LABEL_2(1,2),VALUE_2(1,1),VALUE_2(1,2),VALUE_2(1,3)
(1X,A8,A8,'   ',F10.1,'  ',F14.1,'   ',1F15.2)
/COM,----------------------------------------------------------
/OUT
FINISH
*LIST,vm77,vrt

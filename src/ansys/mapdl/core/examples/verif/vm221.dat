/COM,ANSYS MEDIA REL. 150 (11/8/2013) REF. VERIF. MANUAL: REL. 150
/VERIFY,VM221
JPGPRF,500,100,1
/SHOW,JPEG
/TITLE,VM221, SIMULATION OF SHAPE MEMORY ALLOY EFFECT
/COM,   REF: ANGELA C.SOUZA, EDGAR N.MAMIYA, NESTOR ZOUAIN,
/COM,   "THREE-DIMENSIONAL MODEL FOR SOLIDS UNDERGOING
/COM,   STRESS-INDUCED PHASE TRANSFORMATIONS"
/COM,   EUR. J. MECH. A/SOLIDS. 17 (1998) 789-806
/COM,
/PREP7
ET,1,SOLID185    !3D 8-NODE STRUCTURAL SOLID ELEMENT

/COM, DEFINING SMA MATERIAL PROPERTIES
MP,EX,1,70E3     !MPA, [AUSTENITE MODULUS]
MP,PRXY,1,0.33

C1=500 		!MPA [HARDENING PARAMETER]
C2=253.15	!K   [REF TEMP]
C3=45		!MPA  [ELASTIC LIMIT]
C4=7.5		!MPA
C5=0.03		![MAX TRANSFORMATION STRAIN]
C6=70E3		!MPA, [MARTENSITE MODULUS]
C7=0            !M = 0, SYMMETRICAL BEHAVIOR

TB,SMA,1,,7,MEFF
TBDATA,1,C1,C2,C3,C4,C5,C6,C7

BLOCK,0.00,5.00,0.00,5.00,0.00,5.00
ESIZE,5
TYPE,1
MAT,1
VMESH,1

NSEL,S,LOC,X
D,ALL,UX
NSEL,S,LOC,Y
D,ALL,UY
NSEL,S,LOC,Z
D,ALL,UZ
NSEL,ALL
BFUNIF,TEMP,253.15
FINISH
/OUT,SCRATCH
/SOLU
NROPT,UNSYM
OUTRES,ALL,ALL
NSUBST,50,100,30
LD=-70                     !PRESSURE LOADING 70MPA

TIME,1
NSEL,S,LOC,X,5
SF,ALL,PRES,LD*0.75        ! LOADING IN ELASTIC STAGE
ALLSEL
SOLVE

TIME,2
NSEL,S,LOC,X,5
SF,ALL,PRES,LD             ! LOAD TO PHASE TRANSFORMATION
ALLSEL
SOLVE

TIME,3
NSEL,S,LOC,X,5
SF,ALL,PRES,0.0            ! UNLOAD TO ZERO
ALLSEL
SOLVE

TIME,4
BFUNIF,TEMP,259.15         !   TEMPERATURE LOADING TO RECOVER RESIDUAL STRAIN
ALLSEL
SOLVE
FINISH

/POST1
SET,LAST
*GET,S_Y,NODE,NODE(5,5,0),S,X
*GET,Ee,NODE,NODE(5,5,0),EPEL,X
*GET,Ep,NODE,NODE(5,5,0),EPPL,X

*DIM,LABEL,CHAR,3,1
*DIM,VALUE,,3,2
LABEL(1,1) = 'SY','Ee_X','Ep_X'
*VFILL,VALUE(1,1),DATA,0.0,0.0,0.0
*VFILL,VALUE(1,2),DATA,S_Y,Ee_X,Ep_X
/COM
/OUT,vm221,vrt
/COM,------------------- VM221 RESULTS COMPARISON --------------
/COM,
/COM,               |  TARGET   |   Mechanical APDL  
/COM,
*VWRITE,LABEL(1,1),VALUE(1,1),VALUE(1,2)
(1X,A8,'      ',F14.3,'  ',F14.3)
/COM,-----------------------------------------------------------
/OUT
FINISH
*LIST,vm221,vrt

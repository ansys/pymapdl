/COM,ANSYS MEDIA REL. 2023R1 (11/04/2022) REF. VERIF. MANUAL: REL. 2023R1
/VERIFY,VM74
/PREP7
/TITLE, VM74, TRANSIENT RESPONSE TO AN IMPULSIVE EXCITATION
C*** VIBRATION THEORY AND APPLICATIONS, THOMSON, 2ND PRINTING, PAGE 99, ART. 4.1
ET,1,COMBIN40,,,2,,,2    ! Y DOF ELEMENTS, MASS AT NODE J
R,1,200,,.5              ! TWO DAMPING RATIOS
R,2,200,14,.5
N,1
N,4
FILL
E,1,2
REAL,2
E,3,4
D,1,UY,,,3,2
FINISH

/SOLU
ANTYPE,MODAL            ! PERFORM MODAL SOLVE
MODOPT,QRDAMP,2         ! USE QRDAMP EIGENSOLVER
MXPAND,2
/OUT,vm74_SCRATCH
SOLVE
*DIM,CFREQ,,2,2
*GET,CFREQ(1,1),MODE,1,FREQ
*GET,CFREQ(1,2),MODE,1,DFRQ
*GET,CFREQ(2,1),MODE,2,FREQ
*GET,CFREQ(2,2),MODE,2,DFRQ
*IF,CFREQ(1,1),LT,CFREQ(2,1),THEN
    F1A=CFREQ(2,1)
    CFREQ(2,1)=CFREQ(1,1)
    CFREQ(1,1)=F1A
    DF1A=CFREQ(2,2)
    CFREQ(2,2)=CFREQ(1,2)
    CFREQ(1,2)=DF1A
    *DEL,F1A
    *DEL,DF1A
*ENDIF
/OUT
*STATUS,CFREQ
FINISH

/SOLU
ANTYPE,TRANSIENT
TRNOPT,MSUP,2           ! PERFORM MODE SUPERPOSITION TRANSIENT SOLVE
F,2,FY,0,,4,2
DELTIM,25E-4             ! INTEGRATION TIME STEP
KBC,1                    ! STEP BOUNDARY CONDITIONS
OUTPR,BASIC,1
/OUT,vm74_SCRATCH,,,APPEND
SOLVE                   ! PSEUDO STATIC SOLVE
/OUT

TIME,25E-4
F,2,FY,4000,,4,2        ! IMPULSE FORCE
KBC,1
/OUT,vm74_SCRATCH,,,APPEND
SOLVE
/OUT

TIME,.105                ! TIME TO ALLOW THE MASSES TO REACH LARGEST DEFLECTIONS
F,2,FY,,,4,2             ! REMOVE FORCE
/OUT,vm74_SCRATCH,,,APPEND
SOLVE
/OUT
FINISH
/POST26
FILE,,rdsp
NSOL,2,2,U,Y,2UY          ! STORE UY DISPLACEMENTS OF APPROPRIATE NODES
NSOL,4,4,U,Y,4UY
NPRINT,.25
PRVAR,2,4
*GET,Y1,VARI,2,RTIME,.08
*GET,Y2,VARI,2,RTIME,.1
*GET,Y3,VARI,4,RTIME,.1
*DIM,LABEL_1,CHAR,1,2
*DIM,VALUE_1,,1,3
LABEL_1(1,1) = 'Y,MAX in'
LABEL_1(1,2) = ' NODE=2 '
*VFILL,VALUE_1(1,1),DATA,.99957
*VFILL,VALUE_1(1,2),DATA,Y1
*VFILL,VALUE_1(1,3),DATA,ABS(Y1/.99957)
*DIM,LABEL_2,CHAR,2,2
*DIM,VALUE_2,,2,3
LABEL_2(1,1) = 'Y, in   ','Y, in   '
LABEL_2(1,2) = 'node=2  ','node=4  '
*VFILL,VALUE_2(1,1),DATA,.90930,.34180
*VFILL,VALUE_2(1,2),DATA,Y2,Y3
*VFILL,VALUE_2(1,3),DATA,ABS(Y2/.90930),ABS(Y3/.34180)
/COM
/OUT,vm74,vrt
/COM,------------------- VM74 RESULTS COMPARISON --------------
/COM,
/COM,                 |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
/COM,TIME=.08 sec
/COM,
*VWRITE,LABEL_1(1,1),LABEL_1(1,2),VALUE_1(1,1),VALUE_1(1,2),VALUE_1(1,3)
(1X,A8,A8,'   ',F10.5,'  ',F14.5,'   ',1F15.3)
/COM,
/COM,TIME=.1 sec
/COM,
*VWRITE,LABEL_2(1,1),LABEL_2(1,2),VALUE_2(1,1),VALUE_2(1,2),VALUE_2(1,3)
(1X,A8,A8,'   ',F10.5,'  ',F14.5,'   ',1F15.3)
/COM,----------------------------------------------------------
/OUT
FINISH
*LIST,vm74,vrt
/DEL,SCRATCH

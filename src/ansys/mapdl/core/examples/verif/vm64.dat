/COM,ANSYS MEDIA REL. 150 (11/8/2013) REF. VERIF. MANUAL: REL. 150
/VERIFY,VM64
/PREP7
/TITLE, VM64, THERMAL EXPANSION TO CLOSE A GAP AT A RIGID SURFACE
C***          INTRO. TO STRESS ANALYSIS, HARRIS, 1ST PRINTING, PAGE 58, PROB. 8
C***          USING CONTAC26 AND PLANE182 ELEMENTS
ANTYPE,STATIC                ! STATIC ANALYSIS
ET,1,CONTA175                ! CONTACT ELEMENT
R,1                          ! USE DEFAULTS
KEYOPT,1,12,4                ! NO SEPARATION
ET,2,PLANE182,2,,3
R,2,1                        ! THICKNESS = 1
ET,3,TARGE169                ! TARGET SURFACE
MP,EX,1,10.5E6
MP,ALPX,1,12.5E-6
MP,NUXY,1,0
N,1,2,1
N,2,3,1
N,3,3,4
N,4,2,4
N,11,1,0.998
N,12,4,0.998
TYPE,1
REAL,1
E,1                          ! CONTACT ELEMENTS
E,2
TYPE,3
REAL,1
TSHAP,LINE
E,11,12                      ! TARGET ELEMENT
TYPE,2  
REAL,2
E,1,2,3,4                    ! BAR
TREF,70
BFUNIF,TEMP,170
D,3,ALL,,,4
D,1,UX,,,2,1
OUTPR,BASIC,LAST
FINISH
/SOLU    
/OUT,SCRATCH
SOLVE
FINISH
/POST1
/OUT,
ETABLE,STRSX,S,X
ETABLE,STRSY,S,Y
*GET,STRSSX,ELEM,4,ETAB,STRSX
*GET,STRSSY,ELEM,4,ETAB,STRSY
*DIM,LABEL,CHAR,2,2
*DIM,VALUE,,2,3
LABEL(1,1) = 'SIGX, (p','SIGY, (p'
LABEL(1,2) = 'si)     ','si)     '
*VFILL,VALUE(1,1),DATA,-13125,-6125
*VFILL,VALUE(1,2),DATA,STRSSX,STRSSY
*VFILL,VALUE(1,3),DATA,ABS(STRSSX/13125) ,ABS(STRSSY/6125)
/COM
/OUT,vm64,vrt
/COM,------------------- VM64 RESULTS COMPARISON --------------
/COM,
/COM,                 |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.0,'  ',F14.0,'   ',1F15.3)
/COM,----------------------------------------------------------
/OUT
FINISH
*LIST,vm64,vrt

! Copyright (C) 2024 ANSYS, Inc. and/or its affiliates.
! SPDX-License-Identifier: MIT
! 
!
! Permission is hereby granted, free of charge, to any person obtaining a copy
! of this software and associated documentation files (the "Software"), to deal
! in the Software without restriction, including without limitation the rights
! to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
! copies of the Software, and to permit persons to whom the Software is
! furnished to do so, subject to the following conditions:
!
! The above copyright notice and this permission notice shall be included in all
! copies or substantial portions of the Software.
!
! THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
! IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
! FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
! AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
! LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
! OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
! SOFTWARE.

/COM,ANSYS MEDIA REL. 2023R2 (05/12/2023) REF. VERIF. MANUAL: REL. 2023R2
/VERIFY,vmfebsta-le1-181
/TITLE,vmfebsta-le1-181,LINEAR ELASTIC ANALYSIS IN ELLIPTICAL MEMBRANE
/COM, REFERENCE:" DAVIS ET AL, SELECTED FE BENCHMARKS IN STRUCTURAL AND
/COM,		  THERMAL ANALYSIS,FEBSTA,AUG 1986,1-7-86/1
/COM, ORIGINIAL TEST NUMBER: LE1
/OUT,SCRATCH
/PREP7

_geomgen = 0
*if,_geomgen,eq,1,then

ET,1,SHELL181                         	! SHELL 181 ELEMENT
KEYOPT,1,1,1                     		! MEMBRANE STIFFNESS
KEYOPT,1,3,2			 		! FULL INTEGRATION

SECTYPE,1,SHELL
SECDATA,0.1,1

MP,EX,1,210e9
MP,NUXY,1,0.3

K,1,0,1,0
K,2,0.25,0.9921,0
K,3,0.5,0.9682,0
K,4,0.75,0.92702,
K,5,1,0.8660254
K,6,1.25,0.78062,
K,7,1.5,0.661437
K,8,1.75,0.4841229
K,9,2.0,0
K,10,2.25,0,
K,11,2.50,0,
K,12,2.75,0,
K,13,3.0,0,
K,14,3.25,0,
K,15,3.0,1.05769
K,16,2.75,1.46558
K,17,2.50,1.7571
K,18,2.25,1.9844
K,19,2.00,2.1676
K,20,1.75,2.3172
K,21,1.50,2.4395
K,22,1.25,2.5384
K,23,1.00,2.6165
K,24,0.75,2.6757
K,25,0.50,2.7172
K,26,0.25,2.7418
K,27,0,2.75,0

A,1,3,4,5,6,7,8,9,14,15,16,17,19,21,23,25,27


LSEL,S,LINE,,17
LSEL,A,LINE,,8
LESIZE,ALL,,,5

LSEL,S,LINE,,7,9,2
LESIZE,ALL,,,5

LSEL,S,LINE,,1,6,1
LSEL,A,LINE,,10,16,1
LESIZE,ALL,,,2

AMESH,ALL
ALLSEL,ALL

LSEL,S,LINE,,9,16,1
NSLL,S,1
SF,ALL,PRES,-10E6*0.1
LSEL,ALL

NSEL,S,LOC,X,0
D,ALL,UX,0
NSEL,ALL


NSEL,S,LOC,Y,0
D,ALL,UY,0
NSEL,ALL

D,ALL,UZ,0
ALLSEL,ALL
  cdwrite,db,vmfebsta-le1-181,cdb
*else
  cdread,db,vmfebsta-le1-181,cdb
*endif
FINI

/SOLU
ANTYPE,STATIC
NSUBS,10,10,10
OUTRES,ALL,ALL
SOLVE
FINI

/POST1
SET,LAST
N1 = NODE(2,0,0)
*GET,SY1,NODE,N1,S,Y		     		! STRESS FROM Mechanical APDL IN PA
*DIM,LABEL,CHAR,1
*DIM,VALUE,,1,3
LABEL(1,1) = 'TANG_STRESS'
*VFILL,VALUE(1,1),DATA,92.7
*VFILL,VALUE(1,2),DATA,(SY1/1000000)
*VFILL,VALUE(1,3),DATA,((SY1/1000000)/(92.7))
/OUT,vmfebsta-le1-181,vrt
/COM,------------------- vmfebsta-le1-181 RESULTS COMPARISON --------------
/COM,
/COM,           |  TARGET  |   Mechanical APDL  |  RATIO
/COM,
/COM, 
*VWRITE,LABEL(1,1),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,'   ',F10.4,'  ',F14.4,'   ',F15.3)
/COM,------------------------------------------------------------------------
/OUT,
FINISH
*LIST,vmfebsta-le1-181,vrt



! Copyright (C) 2024 ANSYS, Inc. and/or its affiliates.
! SPDX-License-Identifier: MIT
! 
!
! Permission is hereby granted, free of charge, to any person obtaining a copy
! of this software and associated documentation files (the "Software"), to deal
! in the Software without restriction, including without limitation the rights
! to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
! copies of the Software, and to permit persons to whom the Software is
! furnished to do so, subject to the following conditions:
!
! The above copyright notice and this permission notice shall be included in all
! copies or substantial portions of the Software.
!
! THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
! IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
! FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
! AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
! LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
! OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
! SOFTWARE.

/COM,ANSYS MEDIA REL. 2023R2 (05/12/2023) REF. VERIF. MANUAL: REL. 2023R2
/VERIFY,VMR029-T4-281
/TITLE,VMR029-T4-281,LATERAL-TORSIONAL BUCKLING OF AN ELASTIC CANTILEVER SUBJECTED TO TRANSVERSE END LOAD
/COM,REFERENCE NAFEMS 3D BEAMS AND SHELLS MANUAL TEST 3DNLG-4
/SHOW,JPEG
JPGPRF,500,100,1
/FILNAME,vmr029-t4-281

_GEOMGEN = 0                        ! SET THIS KEY=1 TO REGENERATE THE GEOMETRY
_EXIT = 0                           ! SET THIS KEY=1 TO EXIT WITHOUT SOLVING
                        
*IF,_GEOMGEN,EQ,1,THEN


/PREP7
ET,  1,281
R,1,0.2,0.2,0.2,0.2
MP,EX,1,1E4
MP,NUXY,1,0
MP,GXY,1,5000
BLOCK,0,100,0,5,0,5
LSEL,S,LINE,,9,10
LESIZE,ALL, , ,6
LSEL,S,LINE,,4,5
LESIZE,ALL, , ,10
TYPE,1
REAL,1
MAT,1
AMESH,3
NSEL,S,LOC,X,0
D,ALL,ALL
NSEL,S,LOC,X,100
NSEL,R,LOC,Z,2.5
*GET,NTIP,NODE,0,NUM,MAX
NSEL,S,LOC,X,100
NSEL,R,LOC,Z,0
*GET,ND1,NODE,0,NUM,MAX
NSEL,S,LOC,X,100
NSEL,R,LOC,Z,5
*GET,ND2,NODE,0,NUM,MAX
ALLSEL,ALL
F,NTIP,FZ,-1
ALLSEL,ALL
CDWRITE,db,vmr029-t4-281,cdb
FINISH

*IF,_EXIT,EQ,1,THEN
/EXIT,NOSAVE
*ENDIF

*ELSE

/PREP7
CDREAD,db,vmr029-t4-281,cdb
FINISH


/SOLU
ANTYPE,STATIC
RESCONTROL,LINEAR,ALL,1         ! RESTART FILES FOR SUBSEQUENT LINEAR PERTURBATION 
EQSLV,SPARSE
SOLVE
FINISH

/SOLU
ANTYPE,STATIC,RESTART,,,PERTURB ! PERFORM A PERTURBATION ANALYSIS
PERTURB,BUCKLE,,,ALLKEEP        ! PERTURBED BUCKLING SOLVE 
SOLVE,ELFORM  

OUTRES,ALL,ALL
OUTRES,ALL,ALL
BUCOPT,SUBSPACE,4,0,,RANGE
MXPAND,4,,,YES
SOLVE
FINISH

/PREP7
UPGEOM,0.0015,1,1,vmr029-t4-281,rstp
NSEL,S,NODE,,ND1
NSEL,A,NODE,,ND2
NLIST
NSEL,ALL
FINISH

/SOLU
ANTYPE,STATIC
OUTRES,ALL,ALL
NLGEOM,ON
ARCLEN,ON,2,0.0001
ARCTRM,U,1.0,NTIP,UY
NSUBST,1000
CNVTOL,F,,1E-6,,1E-6
CNVTOL,M,,1E-6,,1E-6
/OUT,SCRATCH
SOLVE
FINISH

/POST26
NSOL,2,NTIP,U,Y,TIPDISP
ADD,3,1,,,APLOAD,,,1,
/XRANGE,0,0.06
/YRANGE,0,0.02
/GROPT,DIVX,6
/AXLAB,X,TIP LATERAL DISP
/AXLAB,Y,LOAD
/COLOR,CURVE,YGRE
XVAR,2
PLVAR,3
/OUT,
PRVAR,2,4
FINISH

/POST1
SET,1,12
*GET,APLOAD,TIME
R=APLOAD/0.01892
*DIM,VALUE,,1,3
*DIM,LABEL,CHAR,3
LABEL(1) = 'PCR','vmr029-','t4-281'
*VFILL,VALUE(1,1),DATA,APLOAD
*VFILL,VALUE(1,2),DATA,R
/OUT,vmr029-t4-281,vrt
/COM
/COM,-------------- VMR029-T4 RESULTS COMPARISON ---------------
/COM,
/COM,         |   Mechanical APDL   |   RATIO   |      TEST      |
/COM,
/COM,SHELL281
*VWRITE,LABEL(1), VALUE(1,1), VALUE(1,2),LABEL(2),LABEL(3)
(1X,' ',A4,'       ',F10.5,'      ',F13.4,'      ',A7,A8)
/COM,
/COM,----------------------------------------------------------------
/OUT
FINISH
*LIST,vmr029-t4-281,vrt
/OUT,SCRATCH,,,APPEND

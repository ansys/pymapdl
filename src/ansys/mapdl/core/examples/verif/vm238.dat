/COM,ANSYS MEDIA REL. 2023R1 (11/04/2022) REF. VERIF. MANUAL: REL. 2023R1
/VERIFY,VM238
/TITLE,VM238,WHEATSTONE BRIDGE CONNECTION OF PIEZORESISTORS, UMKSV SYSTEM OF UNITS
JPGPRF,500,100,1             ! MACRO TO SET PREFS FOR JPEG PLOTS
/SHOW,JPEG
/COM,
/COM,  GEOMETRIC PARAMETERS:
/COM,
L=180                         ! LENGTH OF THE BEAM, UM
W=120                         ! WIDTH OF THE BEAM, UM
A=100                         ! LENGTH OF PIEZORESISTORS, UM
B=20                          ! WIDTH OF PIEZORESISTORS, UM
D=10                          ! DISTANCE FROM THE EDGE, UM
/COM,
/COM,  MATERIAL PROPERTIES (SI):
/COM,
/COM,  YOUNG MODULUS, MPA
E=165E3
/COM,  POISSON RATIO
NU=0.25
/COM,
/COM,  RESISTIVITY (P-TYPE SI), TOHM*UM
RHO= 7.8E-8
/COM,
/COM,  PIEZORESISTIVE COEFFICIENTS (P-TYPE SI), (MPA)^1
/COM,   [P11 P12 P12  0 ]
/COM,   [P12 P11 P12  0 ]
/COM,   [P12 P12 P11  0 ]
/COM,   [ 0   0   0  P44]
/COM,
P11=6.5E-5
P12=-1.1E-5
P44=138.1E-5
/COM,
/COM,  PRESSURE LOAD, MPA
P=1
/COM,  SUPPLY VOLTAGE, VOLT
VS=5
/NOPR

/PREP7
ET,1,PLANE223,101            ! PIEZORESISTIVE ELEMENT TYPE
ET,2,PLANE183                 ! STRUCTURAL ELEMENT TYPE
                             ! SPECIFY MATERIAL ORIENTATION:
LOCAL,11
LOCAL,12,,,,,45              ! X-AXIS ALONG <110> DIRECTION
MP,EX,1,E                    ! ELASTIC PROPERTIES
MP,NUXY,1,NU
MP,RSVX,1,RHO                ! RESISTIVITY
TB,PZRS,1                    ! PIEZORESISTIVE STRESS MATRIX
TBDATA,1,P11,P12,P12
TBDATA,7,P12,P11,P12
TBDATA,13,P12,P12,P11
TBDATA,22,P44
CSYS,11
RECT,-L/2,L/2,-W/2,W/2       ! DEFINE BEAM
RECT,-(L/2-D),-(L/2-D-B),-A/2,A/2   ! RESISTOR 1
RECT,-A/2,A/2,W/2-D-B,W/2-D         ! RESISTOR 2
RECT,-A/2,A/2,-(W/2-D),-(W/2-D-B)   ! RESISTOR 3
RECT,L/2-D-B,L/2-D,-A/2,A/2         ! RESISTOR 4
AOVLAP,ALL                   ! MESH AREAS
ESYS,12
TYPE,1
ESIZE,B/3
AMESH,2,5                    ! MESH RESISTOR AREAS
TYPE,2
MSHAP,1,2-D
ESIZE,B/2
AMESH,6                      ! MESH REST OF THE BEAM
                             ! APPLY ELECTRICAL BC:
LSEL,S,LINE,,5               ! DEFINE SUPPLY VOLTAGE CONTACT
LSEL,A,LINE,,16
NSLL,S,1
CP,1,VOLT,ALL
*GET,NS,NODE,0,NUM,MIN
D,NS,VOLT,VS
LSEL,S,LINE,,10              ! DEFINE GROUND CONTACT
LSEL,A,LINE,,19
NSLL,S,1
CP,2,VOLT,ALL
*GET,NG,NODE,0,NUM,MIN
D,NG,VOLT,0
LSEL,S,LINE,,7               ! DEFINE FIRST OUTPUT CONTACT
LSEL,A,LINE,,12
NSLL,S,1
CP,3,VOLT,ALL
*GET,NO1,NODE,0,NUM,MIN
LSEL,S,LINE,,14              ! DEFINE SECOND OUTPUT CONTACT
LSEL,A,LINE,,17
NSLL,S,1
CP,4,VOLT,ALL
*GET,NO2,NODE,0,NUM,MIN
NSEL,ALL
LSEL,ALL
                             ! APPLY STRUCTURAL BC
NSEL,S,LOC,X,-L/2
D,ALL,UX,0
NSEL,R,LOC,Y,-W/2
D,ALL,UY,0
NSEL,S,LOC,X,L/2
SF,ALL,PRES,-P               ! PRESSURE LOAD
NSEL,ALL
/PBC,U,,1
/PBC,VOLT,,1
/PBC,CP,,1
/PNUM,TYPE,1
/NUMBER,1
EPLOT
FINI
/SOLU                        ! SOLUTION
ANTYPE,STATIC
CNVTOL,AMPS,1,1.E-3
/OUT,SCRATCH
SOLVE
FINI
/POST1
!!! CALCULATE RESULTS
/OUT,
SX=P
VOC=P44/(2+(P11+P12)*SX)*SX*VS*1.E3   ! CALCULATE ACTUAL RESULT
VOA=ABS(VOLT(NO1)-VOLT(NO2))*1.E3     ! CALCULATE Mechanical APDL RESULT
*DIM,LABEL,CHAR,1
*DIM,VALUE,,1,3
LABEL(1) = 'VO (MV)'
*VFILL,VALUE(1,1),DATA,VOC
*VFILL,VALUE(1,2),DATA,VOA
*VFILL,VALUE(1,3),DATA,ABS(VOA/VOC)
/OUT,vm238,vrt
/COM
/COM,------------------- VM238 RESULTS COMPARISON ---------------------
/COM,
/COM,           |  TARGET  |   Mechanical APDL   |   RATIO
/COM,
*VWRITE,LABEL(1),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,'       ',F7.4,'  ',F14.4,'     ',1F15.3)
/COM,----------------------------------------------------------------
/OUT
FINISH
*LIST,vm238,vrt


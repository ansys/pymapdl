/COM,ANSYS MEDIA REL. 150 (11/8/2013) REF. VERIF. MANUAL: REL. 150
/VERIFY,VM271
JPGPRF,500,100,1
/SHOW,JPEG
/TITLE,VM271,CONVECTION TREATMENT PROBLEM FOR A HOLLOW CYLINDER WITH FLUID FLOW
/COM, REFERENCE: " INTRODUCTION TO HEAT TRANSFER" - VEDAT S.ARPACI, AHMET SELAMET
/COM,			 SHU-HSIN KAO, 2000,PG:90-100
/COM, 
R1 = 0.01105				! INNER RADIUS OF THE CYLINDER (M)
R2 = 0.02					! OUTER RADIUS OF THE CYLINDER (M)
Z1 = 0
Z2 = 0.1
THETA1 = 0
THETA2 = 180
LENGTH = Z2-Z1				! LENGTH OF THE FLUID ELEMENT (M)
DIAMETER = 2*R1				! DIAMETER (M)
FILMAREA = ACOS(-1)*DIAMETER*LENGTH	! FILMAREA (M^2)
CROSSAREA = 0.25*ACOS(-1)*DIAMETER**2	
FILM = 300					! FILM COEFFICIENT FOR SURFACE ELEMENTS (W/M^2 C)
CP   = 0.5374				! SPECIFIC HEAT FOR FLUID (J/KG C)
COND = 1.0E-16				! THERMAL CONDUCTIVITY FOR FLUID (W/M C)
TINLET = 700				! INLET TEMPERATURE (DEGREE)
TBULK  = 2000				! BULK TEMPERATURE (DEGREE)
MDOT   = 0.002*60*60			! MASS FLOW RATE FOR FLUID  (KG/SEC)
CPS = 1					! SPECIFIC HEAT FOR CYLINDER (J/KG C)
CONDS = 10000				! THERMAL CONDUCTIVITY FOR CYLINDER (W/M C)

/PREP7
ET,1,FLUID116
KEYOPT,1,1,1				! TEMPERATURE DOF ONLY
KEYOPT,1,9,2				! UPWIND DIFFERENCE SCHEME
R,1,DIAMETER,CROSSAREA			! DIA FOR CONVECTION,CROSS SECTION AREA FOR CONDUCTION
MP,KXX,1,COND
MP,C,1,CP

ET,2,SURF152
KEYOPT,2,4,1				! NO MIDSIDE NODES
KEYOPT,2,5,0
KEYOPT,2,8,2				! CONVECTION

ET,3,SOLID70
MP,KXX,3,CONDS

K,1,0,0,Z1
K,2,0,0,Z2
L,1,2
LESIZE,1,,,3
TYPE,1
MAT,1
REAL,1
LMESH,ALL

*GET,DLEN1,NODE,1,LOC,Z
*GET,DLEN2,NODE,3,LOC,Z
*GET,DLEN3,NODE,4,LOC,Z
*GET,DLEN4,NODE,2,LOC,Z

CYLIND,R1,R2,Z1,Z2,THETA1,THETA2
CYLIND,R1,R2,Z1,Z2,THETA1+180,THETA2+180
VGLUE,1,2
MSHAP,TRIOPT,3D
LSEL,S,LINE,,2,4,2
LSEL,A,LINE,,7,9,2
LESIZE,ALL,,,3
LSEL,S,LINE,,10,13,1			
LESIZE,ALL,,,18
LSEL,S,LINE,,6,8,2
LSEL,A,LINE,,28,29,1
LSEL,A,LINE,,3,5,2
LSEL,A,LINE,,26,27,1
LESIZE,ALL,,,24
TYPE,3
MAT,3
REAL,3
VMESH,ALL

CSYS,1
NSEL,S,LOC,X,R1
ESLN,S
TYPE,2
MAT,2
REAL,2
ESURF
KEYOPT,2,5,2				! TWO NODES 5TH,6TH NODES
CSYS,0
ALLSEL,ALL

ESEL,S,TYPE,,1
CM,COM116,ELEM				! COMPONENT WITH FLUID116 ELEMENTS
ESEL,S,TYPE,,2
CM,COM152,ELEM				! COMPONENT WITH SURF152 ELEMENTS
ALLSEL,ALL
MSTOLE,1,'COM152','COM116'		! MAP 152 TO 116 AND CREATE THE 5/6 NODES OF 152
ALLSEL,ALL

KSEL,S,,,1
NSLK,S
D,ALL,TEMP,TINLET				! INLET TEMPERATURE
ALLSEL,ALL

ESEL,S,TYPE,,1
SFE,ALL,,HFLUX,,MDOT			! MASS FLOW RATE FOR FLUID ELEMENT
ESEL,ALL

CSYS,1
NSEL,S,LOC,X,R2
D,ALL,TEMP,TBULK				! BULK TEMPERATURE ON THE CYLINDER
CSYS,0
ALLSEL,ALL

ESEL,S,TYPE,,2
SFE,ALL,1,CONV,0,FILM			! FILM COEFFICIENT FOR SURFACE ELEMENT
SFE,ALL,1,CONV,2,0			! SET BULK TEMP
ESEL,ALL
EPLOT
FINISH

/SOLU
ANTYPE,STATIC				! STEADY STATE ANALYSIS
OUTRES,ALL,ALL
SOLVE
SAVE
FINI

/POST1
SET,LAST
*GET,TEMP1,NODE,1,TEMP			! TEMPERATURE AT NODE 1 OF FLUID116 ELEMENT
*GET,TEMP2,NODE,2,TEMP			! TEMPERATURE AT NODE 2 OF FLUID116 ELEMENT
*GET,TEMP3,NODE,3,TEMP			! TEMPERATURE AT NODE 3 OF FLUID116 ELEMENT	
*GET,TEMP4,NODE,4,TEMP			! TEMPERATURE AT NODE 4 OF FLUID116 ELEMENT

/OUT,SCRATCH
*DIM,LABEL,CHAR,1,4
*DIM,LEN,,1,4
*DIM,VALUE,,4,3

LABEL(1,1) = 'NODE1'
LABEL(1,2) = 'NODE3'
LABEL(1,3) = 'NODE4'
LABEL(1,4) = 'NODE2'



MODULUS = (FILM*ACOS(-1)*DIAMETER*LENGTH)/(MDOT*CP)

/COM, ANALYTICAL VALUES CALCULATED
/COM,	USING EQUATION MENTIONED IN THE REFERENCE BOOK

POSITION1 = (DLEN1- DLEN1)/LENGTH
POSITION2 = (DLEN2- DLEN1)/LENGTH
POSITION3 = (DLEN3- DLEN1)/LENGTH
POSITION4 = (DLEN4- DLEN1)/LENGTH

ATEMP1 = (TINLET-TBULK)*EXP(-MODULUS*POSITION1)+TBULK
ATEMP2 = (TINLET-TBULK)*EXP(-MODULUS*POSITION2)+TBULK
ATEMP3 = (TINLET-TBULK)*EXP(-MODULUS*POSITION3)+TBULK
ATEMP4 = (TINLET-TBULK)*EXP(-MODULUS*POSITION4)+TBULK


*VFILL,LEN(1,1),DATA,POSITION1		
*VFILL,LEN(1,2),DATA,POSITION2
*VFILL,LEN(1,3),DATA,POSITION3
*VFILL,LEN(1,4),DATA,POSITION4

*VFILL,VALUE(1,1),DATA,ATEMP1
*VFILL,VALUE(1,2),DATA,TEMP1
*VFILL,VALUE(1,3),DATA,ABS(ATEMP1/TEMP1)
*VFILL,VALUE(2,1),DATA,ATEMP2
*VFILL,VALUE(2,2),DATA,TEMP3
*VFILL,VALUE(2,3),DATA,ABS(ATEMP2/TEMP3)
*VFILL,VALUE(3,1),DATA,ATEMP3
*VFILL,VALUE(3,2),DATA,TEMP4
*VFILL,VALUE(3,3),DATA,ABS(ATEMP3/TEMP4)
*VFILL,VALUE(4,1),DATA,ATEMP4
*VFILL,VALUE(4,2),DATA,TEMP2
*VFILL,VALUE(4,3),DATA,ABS(ATEMP4/TEMP2)

SAVE,TABLE_1
FINISH
RESUME,TABLE_1
/COM,
/OUT,vm271,vrt
/COM,-----------------------VM271 RESULTS COMPARISON -----------------------
/COM,
/COM,		  	      LENGTH |  TARGET  |  Mechanical APDL |  RATIO
/COM,	
/COM,
/COM, TEMPERATURE COMPUTED ON THE NODES OF FLUID116 ELEMENT
/COM, ARE COMPARED WITH ANALYTICAL VALUES
/COM,
*VWRITE,LABEL(1,1),LEN(1,1),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A5,'  ',F9.3,'  ',F10.3,'   ',F14.3,'   ',F15.3)
/COM,
*VWRITE,LABEL(1,2),LEN(1,2),VALUE(2,1),VALUE(2,2),VALUE(2,3)
(1X,A5,'  ',F9.3,'  ',F10.3,'   ',F14.3,'   ',F15.3)
/COM,
*VWRITE,LABEL(1,3),LEN(1,3),VALUE(3,1),VALUE(3,2),VALUE(3,3)
(1X,A5,'  ',F9.3,'  ',F10.3,'   ',F14.3,'   ',F15.3)
/COM,
*VWRITE,LABEL(1,4),LEN(1,4),VALUE(4,1),VALUE(4,2),VALUE(4,3)
(1X,A5,'  ',F9.3,'  ',F10.3,'   ',F14.3,'   ',F15.3)
/COM,
/COM,-------------------------------------------------------------------------
/OUT
*LIST,vm271,vrt
FINISH


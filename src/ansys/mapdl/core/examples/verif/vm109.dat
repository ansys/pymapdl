! Copyright (C) 2024 ANSYS, Inc. and/or its affiliates.
! SPDX-License-Identifier: MIT
! 
!
! Permission is hereby granted, free of charge, to any person obtaining a copy
! of this software and associated documentation files (the "Software"), to deal
! in the Software without restriction, including without limitation the rights
! to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
! copies of the Software, and to permit persons to whom the Software is
! furnished to do so, subject to the following conditions:
!
! The above copyright notice and this permission notice shall be included in all
! copies or substantial portions of the Software.
!
! THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
! IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
! FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
! AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
! LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
! OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
! SOFTWARE.

/COM,ANSYS MEDIA REL. 2023R2 (05/12/2023) REF. VERIF. MANUAL: REL. 2023R2
/VERIFY,VM109
JPGPRF,500,100,1             ! MACRO TO SET PREFS FOR JPEG PLOTS
/SHOW,JPEG
/PREP7
/TITLE, VM109, TEMPERATURE RESPONSE OF A SUDDENLY COOLED WIRE
C***    PRINCIPLES OF HEAT TRANSFER, KREITH, 2ND. PRINTING, PAGE 120, EX. 4-1
ANTYPE,TRANS            ! THERMAL ANALYSIS
ET,1,MASS71,,,1
ET,2,LINK34
R,1,2.7046E-4           ! THERMAL CAPACITANCE PER UNIT LENGTH
R,2,0.0081812           ! SURFACE AREA PER UNIT LENGTH
MP,HF,1,2               ! FILM COEFFICIENT
N,1                     ! COINCIDENT NODES AT ORIGIN
N,2
E,1
TYPE,2
REAL,2
E,1,2
FINISH
/SOLU    
OUTRES,,ALL  
AUTOTS,ON
OUTPR,BASIC,LAST
DELTIM,0.00125
TIME,.0125
BFUNIF,TEMP,300         ! UNIFORM INITIAL TEMPERATURE
KBC,1
D,2,TEMP,100            ! AIR TEMPERATURE
SOLVE
TIME,.0325
SOLVE
TIME,0.05
SOLVE
FINISH
/POST26
NSOL,2,1,TEMP
PRVAR,2                 ! PRINT TEMPERATURE
/GRID,1
/AXLAB,Y,TEMP
PLVAR,2                 ! DISPLAY TEMP OF NODE 1 VS. TIME
*GET,T1,VARI,2,RTIME,.0125
*GET,T2,VARI,2,RTIME,.0325
*GET,T3,VARI,2,RTIME,.05
*DIM,LABEL,CHAR,3,2
*DIM,VALUE,,3,3
LABEL(1,1) = 'T,F AT 0','T,F AT 0','T,F AT 0'
LABEL(1,2) = '.0125 hr','.0325 hr','.05   hr'
*VFILL,VALUE(1,1),DATA,193.89,128,109.71
*VFILL,VALUE(1,2),DATA,T1,T2,T3
*VFILL,VALUE(1,3),DATA,ABS(T1/193.89),ABS(T2/128),ABS(T3/109.71)
/COM
/OUT,vm109,vrt
/COM,------------------- VM109 RESULTS COMPARISON --------------
/COM,
/COM,                 |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.2,'  ',F12.2,'   ',1F15.3)
/COM,----------------------------------------------------------
/OUT
FINISH
*LIST,vm109,vrt


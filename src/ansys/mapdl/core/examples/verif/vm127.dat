/COM,ANSYS MEDIA REL. 150 (11/8/2013) REF. VERIF. MANUAL: REL. 150
/VERIFY,VM127
/PREP7
MP,PRXY,,0.3
/TITLE, VM127, BUCKLING OF A BAR WITH HINGED SOLVES (LINE ELEMENTS)
C***    STR. OF MATL., TIMOSHENKO, PART 2, 3RD ED., PAGE 148
ET,1,BEAM188              ! BEAM ELEMENT
KEYOPT,1,3,3
SECT,1,BEAM,RECT
SECD,0.5,0.5
MP,EX,1,30E6
N,1
N,11,,100
FILL
E,1,2
EGEN,10,1,1
D,ALL,UZ,,,,,ROTX,ROTY
SAVE,,DB
FINISH

/COM
/COM,***BUCKLING ANALYSIS USING PSTRES COMMAND******
/SOLUTION
ANTYPE,STATIC                   ! STATIC ANALYSIS
PSTRES,ON                       ! CALCULATE PRESTRESS EFFECTS
D,1,ALL                         ! FIX SYMMETRY END
F,11,FY,-1                      ! UNIT LOAD AT FREE END
SOLVE
FINISH

/SOLU 
ANTYPE,BUCKLE                   ! BUCKLING ANALYSIS
BUCOPT,LANB,1                   ! USE BLOCK LANCZOS EIGENVALUE EXTRACTION METHOD
MXPAND,1
SOLVE
*GET,FCR1,MODE,1,FREQ
*status,parm
*DIM,LABEL,CHAR,1,2
*DIM,VALUE,,1,3
LABEL(1,1) = 'Fcr '
LABEL(1,2) = 'lb '
*VFILL,VALUE(1,1),DATA,38.553
*VFILL,VALUE(1,2),DATA,FCR1
*VFILL,VALUE(1,3),DATA,ABS(FCR1/38.553)
SAVE,TABLE_1
FINISH
/CLEAR,NOSTART

/COM,
/COM, ****USING LINEAR PERTURBATION METHOD*************
/COM,
/PREP7
RESUME,,DB                      ! RESUME THE DATABASE
FINISH

/SOLU    
ANTYPE,STATIC                   ! STATIC ANALYSIS
RESCONTROL,LINEAR,ALL,1         ! RESTART FILES FOR SUBSEQUENT LINEAR PERTURBATION 
D,1,ALL                         ! FIX SYMMETRY END
F,11,FY,-1                      ! UNIT LOAD AT FREE END
SOLVE
FINISH
/SOLU 
ANTYPE,STATIC,RESTART,,,PERTURB ! PERFORM A PERTURBATION ANALYSIS
PERTURB,BUCKLE,,,ALLKEEP        ! PERTURBED BUCKLING SOLVE 
SOLVE,ELFORM                    ! REFORM ELEMENT MATRICES
BUCOPT,LANB,1,,,RANGE           ! USE BLOCK LANCZOS EIGENVALUE EXTRACTION METHOD, EXTRACT 1 MODE
MXPAND,1                        ! EXPAND 1 MODE SHAPE
SOLVE
*GET,FCR2,MODE,1,FREQ
*status,parm
*DIM,LABEL,CHAR,1,2
*DIM,VALUE,,1,3
LABEL(1,1) = 'Fcr '
LABEL(1,2) = 'lb '
*VFILL,VALUE(1,1),DATA,38.553
*VFILL,VALUE(1,2),DATA,FCR2
*VFILL,VALUE(1,3),DATA,ABS(FCR2/38.553)
SAVE,TABLE_2
FINI
/CLEAR,NOSTART

/NOPR
/COM
/OUT,vm127,vrt
/COM,------------------- VM127 RESULTS COMPARISON -------------
/COM,
/COM,                 |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
/COM,
RESUME,TABLE_1
/COM,USING PSTRES COMMAND
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.3,'  ',F12.3,'   ',1F15.3)
/COM,
/COM,
RESUME,TABLE_2
/COM,USING LINEAR PERTURBATION METHOD
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.3,'  ',F12.3,'   ',1F15.3)
/COM,
/COM,----------------------------------------------------------
/OUT
*LIST,vm127,vrt
FINISH

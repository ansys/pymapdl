! Copyright (C) 2024 ANSYS, Inc. and/or its affiliates.
! SPDX-License-Identifier: MIT
! 
!
! Permission is hereby granted, free of charge, to any person obtaining a copy
! of this software and associated documentation files (the "Software"), to deal
! in the Software without restriction, including without limitation the rights
! to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
! copies of the Software, and to permit persons to whom the Software is
! furnished to do so, subject to the following conditions:
!
! The above copyright notice and this permission notice shall be included in all
! copies or substantial portions of the Software.
!
! THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
! IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
! FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
! AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
! LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
! OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
! SOFTWARE.

/COM,ANSYS MEDIA REL. 2023R2 (05/12/2023) REF. VERIF. MANUAL: REL. 2023R2
/VERIFY,VMR029-T4-189
/TITLE,VMR029-T4-189,LATERAL-TORSIONAL BUCKLING OF AN ELASTIC CANTILEVER SUBJECTED TO TRANSVERSE END LOAD
/COM,REFERENCE NAFEMS 3D BEAMS AND SHELLS MANUAL TEST 3DNLG-4
/SHOW,JPEG
JPGPRF,500,100,1
/FILNAME,vmr029-t4-189

/PREP7
MP,EX,1,1E4
MP,NUXY,1,0
MP,GXY,1,5000
ET,1,189
SECT,1,BEAM,RECT
SECD,.2,5.0
N,1
N,2,10
N,3,20
N,4,30
N,5,40
N,6,50
N,7,60
N,8,70
N,9,80
N,10,90
N,11,100
N,101,50,50
TYPE,1
MAT,1
SECN,1
E,1,3,2,101
E,3,5,4,101
E,5,7,6,101
E,7,9,8,101
E,9,11,10,101
NSEL,S,LOC,X,100
*GET,NTIP,NODE,0,NUM,MAX
NSEL,ALL
D,1,ALL
F,NTIP,FY,1.0
FINISH

/SOLU
ANTYPE,STATIC
RESCONTROL,LINEAR,ALL,1             ! RESTART FILES FOR SUBSEQUENT LINEAR PERTURBATION 
SOLVE
FINISH

/SOLU
ANTYPE,STATIC,RESTART,,,PERTURB ! PERFORM A PERTURBATION ANALYSIS
PERTURB,BUCKLE,,,ALLKEEP        ! PERTURBED BUCKLING SOLVE 
SOLVE,ELFORM  

OUTRES,ALL,ALL
bucopt,LANB,4,,,range
MXPAND,4,,,YES
SOLVE
FINISH

/PREP7
UPGEOM,0.001/3,1,1,vmr029-t4-189,rstp
FINISH

/SOLU
ANTYPE,STATIC
OUTRES,ALL,ALL
NLGEOM,ON
ARCLEN,ON,1,0.0001
ARCTRM,U,1.0,NTIP,UZ
NSUBST,10000
/OUT,SCRATCH
SOLVE
FINISH

/POST26
/SHOW,,jpeg
NSOL,2,NTIP,U,Z,TIPDISP
ADD,3,1,,,APLOAD,,,1,
/XRANGE,0,0.06
/YRANGE,0,0.02
/GROPT,DIVX,6
/AXLAB,X,TIP LATERAL DISP
/AXLAB,Y,LOAD
/COLOR,CURVE,YGRE
XVAR,2
PLVAR,3
/OUT,
PRVAR,2,4
FINISH

/POST1
SET,1,595
*GET,APLOAD,TIME
R=APLOAD/0.01892
*DIM,VALUE,,1,3
*DIM,LABEL,CHAR,3
LABEL(1) = 'PCR','vmr029-','t4-189'
*VFILL,VALUE(1,1),DATA,APLOAD
*VFILL,VALUE(1,2),DATA,R
/OUT,vmr029-t4-189,vrt
/COM
/COM,-------------- VMR029-T4 RESULTS COMPARISON ---------------
/COM,
/COM,         |   Mechanical APDL   |   RATIO   |      TEST      |
/COM,
/COM,BEAM189
*VWRITE,LABEL(1), VALUE(1,1), VALUE(1,2),LABEL(2),LABEL(3)
(1X,' ',A4,'       ',F10.5,'      ',F13.4,'      ',A7,A8)
/COM,
/COM,----------------------------------------------------------------
/OUT
FINISH
*LIST,vmr029-t4-189,vrt

! Copyright (C) 2024 ANSYS, Inc. and/or its affiliates.
! SPDX-License-Identifier: MIT
! 
!
! Permission is hereby granted, free of charge, to any person obtaining a copy
! of this software and associated documentation files (the "Software"), to deal
! in the Software without restriction, including without limitation the rights
! to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
! copies of the Software, and to permit persons to whom the Software is
! furnished to do so, subject to the following conditions:
!
! The above copyright notice and this permission notice shall be included in all
! copies or substantial portions of the Software.
!
! THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
! IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
! FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
! AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
! LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
! OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
! SOFTWARE.

/COM,ANSYS MEDIA REL. 2023R2 (05/12/2023) REF. VERIF. MANUAL: REL. 2023R2
/VERIFY,VM111
/PREP7
/TITLE, VM111, COOLING OF A SPHERICAL BODY
!       PRINCIPLES OF HEAT TRANSFER, KREITH, 2ND. PRINTING, PAGE 143, EX. 4-5
!       USING PLANE55 ELEMENTS (QUAD. ELEMENTS)
ANTYPE,TRANS
ET,1,PLANE55,,,1      ! AXISYMMETRIC ELEMENTS
MP,KXX,1,(1/3)
MP,DENS,1,62
MP,C,1,1.0752677
CSYS,1                ! CYLINDRICAL COORDINATE SYSTEM
N,1
N,2,(1/18),-7.5
N,4,(1/6),-7.5
FILL
NGEN,2,20,1,4,1,,15
E,2,22,1,1
E,3,23,22,2
EGEN,2,1,2
CP,1,TEMP,2,22        ! COUPLING TO ENSURE CIRCUMFERENTIAL SYMMETRY
CPSGEN,3,1,1
AUTOTS,ON             ! USE AUTOMATIC TIME STEPPING
DELTIM,.01            ! MIN TIME STEP SIZE
OUTPR,BASIC,LAST      ! PRINT LAST SUBSTEP
TIME,6
TUNIF,65
KBC,1                 ! STEP BOUNDARY CONDITIONS
NSEL,S,LOC,X,(1/6)
SF,ALL,CONV,2,25      ! CONVECTION ON ELEMENT SURFACE
NSEL,ALL
FINISH
/SOLU    
SOLVE
FINISH
/POST1
PRNSOL,TEMP           ! PRINT NODAL TEMPERATURES
*GET,TEMP,NODE,1,TEMP
*DIM,LABEL,CHAR,1,2
*DIM,VALUE,,1,3
LABEL(1,1) = '       T'
LABEL(1,2) = ', F     '
*VFILL,VALUE(1,1),DATA,28
*VFILL,VALUE(1,2),DATA,TEMP 
*VFILL,VALUE(1,3),DATA,ABS(TEMP/28)
/COM
/OUT,vm111,vrt
/COM,------------------- VM111 RESULTS COMPARISON --------------
/COM,
/COM,                 |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.1,'  ',F12.2,'   ',1F15.3)
/COM,-----------------------------------------------------------
/OUT
FINISH
*LIST,vm111,vrt


/COM,ANSYS MEDIA REL. 2023R1 (11/04/2022) REF. VERIF. MANUAL: REL. 2023R1
/VERIFY,VM235
/PREP7
JPGPRF,500,100,1             ! MACRO TO SET PREFS FOR JPEG PLOTS
/SHOW,JPEG
/TITLE,VM235, FREQUENCY RESPONSE OF A PRE-STRESSED BEAM
/COM,
/COM, REFERENCE: " FORMULAS FOR NATURAL FREQUENCY AND MODE SHAPES"
/COM,			 PG:144, EQUATION 8-20, R.D. BLEVINS, VAN NOSTRAND
/COM,			 REINHOLD CO. 1979
/COM
L=150                       ! BEAM LENGTH (MICROMETERS)
B=4                         ! BEAM WIDTH
H=2                         ! BEAM HEIGTH
I=B*H**3/12                 ! BEAM MOMENT OF INERTIA
E=169E3                     ! MODULUS  ( MICRO NEWTONS/MICROMETER**2)
P=10                        ! MICRO NEWTONS	
DENS=2332E-18               ! DENSITY (KG/MICROMETER**3)
M=DENS*B*H                  ! MASS/LENGTH (KG/MICROMETER)
PER0=8.85E-6                ! FREE-SPACE PERMITTIVITY (PF/MICROMETER)
PLATEA=100                  ! CAPACITOR PLATE AREA (MICROMETER**2)

GAPI=1                      ! INITIAL GAP (MICROMETERS)
GAP=GAPI-P*L/E/B/H          ! APPROX DEFLECTED GAP (IGNORE CAP STIFFNESS)  (MICROMETER)
C3=PER0*PLATEA              ! TRANDUCER REAL CONTANT
C3P=C3/(GAP**2)             ! DERIVITIVE OF C3
C3PP=2*C3/(GAP**3)          ! SECOND DERIVITIVE OF C3
VLT=SQRT(2*P/C3P)           ! APPLIED VOLTAGE TO PLATE
KUU=C3PP*VLT**2/2           ! GAP STIFFNESS
KBEAM=E*PLATEA/L            ! BEAM STIFFNESS (NOTE: GAP STIFFNESS ASSUMED << BEAM STIFFNESS) 
UX2=P*L/B/H/E               ! DESIRED DEFLECTION 

*DIM,FREQ,,5                ! ARRAY PARAMETER FOR BEAM FREQUENCY
*DIM,PFREQ,,5               ! ARRAY PARMETER FOR BEAM PRE-STRESSED FREQUENCY

PI=4*ATAN(1)
!! CALCULATE ANALYTICAL SOLUTION !!
*DO,J,1,5
LAMDA=J*PI
LAMDAP2=LAMDA**2*SQRT((1 + P*L**2/(E*I*LAMDA**2)))
LAMDAP=SQRT(LAMDAP2)
FREQ(J) = LAMDA**2/(2*PI*L**2)*SQRT(E*I/M)
PFREQ(J) = LAMDAP**2/(2*PI*L**2)*SQRT(E*I/M)
*ENDDO

ET,1,188					! BEAM188
KEYOPT,1,3,3
SECT,1,BEAM,ASEC			! BEAM PROPERTIES
SECD,B*H,I,,I,,1
MP,EX,1,E					! SET EX TO E 
MP,DENS,1,DENS				! SET DENSITY TO DENS
MP,PRXY,,0.3				! SET PRXY TO 0.3
ET,2,126					! TRANS126 FOR ELEMENT 2
C3=PER0*PLATEA				! SET INPUT CAPACITANCE FOR TRANSDUCER
R,2,0,0,1,0,C3		    
RMORE,C3
N,1,-10						! SETUP MODEL NODES
N,2,0
N,22,L
FILL
TYPE,2
REAL,2
E,1,2						! CREATE TRANSDUCER
TYPE,1
SECN,1
E,2,3						! CREATE BEAM
*REPEAT,20,1,1
NSEL,S,LOC,X,-10
NSEL,A,LOC,X,L
D,ALL,UX,0,,,,UY
NSEL,S,LOC,X,0
D,ALL,UY,0
D,ALL,VOLT,VLT
IC,ALL,VOLT,VLT
NSEL,S,LOC,X,-10
D,ALL,VOLT,0
NSEL,ALL
ESEL,S,TYPE,,1
NSLE
D,ALL,UZ,,,,,ROTX,ROTY
ALLS,ALL
EPLOT						! PLOT ELEMENTS
FINISH
/out,scratch
/SOLUTION
ANTYP,STATIC
PSTRES,ON					! PRESTRESSED MODAL ANALYSIS
SOLVE
FINISH
/out
/POST1
FINISH

/SOLUTION
ANTYP,MODAL
MODOPT,UNSYM,3   			! EXTRACT 3 MODES
MXPAND,,,,YES
PSTRES,ON
SOLVE
FINISH
/POST1
/NOPR						! SETUP RESULTS TABLE DATA
SET,1,1
PLDISP,1					! PLOT MODE SHAPE
*GET,FP1,ACTIVE,,SET,FREQ
SET,1,2
PLDISP,1					! PLOT MODE SHAPE
*GET,FP2,ACTIVE,,SET,FREQ
SET,1,3
PLDISP,1					! PLOT MODE SHAPE
*GET,FP3,ACTIVE,,SET,FREQ
FINISH

PFREQ1=PFREQ(1)
PFREQ2=PFREQ(2)
PFREQ3=PFREQ(3)

*DIM,LABEL,CHAR,3
*DIM,VALUE,,3,3
LABEL(1) = 'FREQ 1' , 'FREQ 2' , 'FREQ 3'
*VFILL,VALUE(1,1),DATA,PFREQ1,PFREQ2,PFREQ3
*VFILL,VALUE(1,2),DATA,ABS(FP1),ABS(FP2),ABS(FP3)
*VFILL,VALUE(1,3),DATA,ABS(PFREQ1/FP1),ABS(PFREQ2/FP2),ABS(PFREQ3/FP3)
/OUT,vm235,vrt
/COM
/COM,------------------- VM235 RESULTS COMPARISON ---------------------
/COM,
/COM,         |    TARGET   |   Mechanical APDL   |   RATIO
/COM,
*VWRITE,LABEL(1),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,'   ',F11.3,'  ',F15.3,'   ',1F15.3)
/COM,----------------------------------------------------------------
/OUT
/GOPR
FINISH
*LIST,vm235,vrt

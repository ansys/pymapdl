/COM,ANSYS MEDIA REL. 2023R1 (11/04/2022) REF. VERIF. MANUAL: REL. 2023R1
/VERIFY,VM305
/TITLE,VM305,SIMULATION OF FUNCTIONALLY GRADED MATERIAL
/COM,
/COM,   REF: V.N.BURLAYENKO, H. ALTENBACH, T. SADOWSKI, S.D. DIMITROVA, A. BHASKAR
/COM,   "MODELLING FUNCTIONALLY GRADED MATERIALS IN HEAT TRANSFER AND THERMAL
/COM,   STRESS ANALYSIS BY MEANS OF GRADED FINITE ELEMENTS"
/COM,   APPLIED MATHEMATICAL MODELLING. 45 (2017) 422-438
/COM,
/COM, PARAMETER FOR CREATING THE CDB FILES TO STORE GEOMETRY AND MESH DATA
_GEOMGEN = 0   ! SET TO 1 TO WRITE CDB FILES IF ANY INPUT FILE MODIFICATIONS
/OUT,SCRATCH_vm305

! PREPROCESSING
/PREP7
*IF,_GEOMGEN,EQ,1,THEN

XX1=0                    ! DEFINE THE SUPPORTING POINTS ALONG X
XX3=0.25
XX5=0.5
XX7=0.75
XX9=1

H1=0                     ! DEFINE THE SUPPORTING POINTS ALONG Y
H2=0.5
H3=1

E1=EXP(LOG(8)*XX1)       ! ASSIGN YOUNG'S MODULUS FOR THE SUPPORTING POINTS
E3=EXP(LOG(8)*XX3)
E5=EXP(LOG(8)*XX5)
E7=EXP(LOG(8)*XX7)
E9=EXP(LOG(8)*XX9)

ET,1,182                 ! PLANE182
KEYOPT,1,3,2             ! PLANE STRAIN

RECTNG,0,1,0,1
ESIZE,0.05
AMESH,ALL

ALLS
CDWRITE,db,vm305,cdb
*ELSE
 CDREAD,db,vm305,cdb
*ENDIF

/OUT,

TB,ELAS,1                ! DEFINITE THE MATERIAL PROPERTIES
*DO, I, 1, 9, 2
*DO, J, 1, 3
 TBFIELD, XCOR,     XX%I%
 TBFIELD, YCOR,     H%J%
 TBFIELD, ZCOR,     0
 TBDATA,   1,       E%I% ,  0.30
*ENDDO
*ENDDO

TBIN,ALGO

NSEL,S,LOC,Y,1           ! UNIAXIAL TENSION IN Y
D,ALL,UY,0.2
ALLSEL

NSEL,S,LOC,Y,0
D,ALL,UY,0
ALLSEL
NSEL,S,LOC,X,0
D,ALL,UX,0
ALLSEL
FINISH

/SOLU
ERESX,NO
NSUBST,1,1,1
SOLV
FINISH

/OUT,SCRATCH_vm305,,,APPEND
/POST1
*GET, S1Y, NODE, NODE(0,0,0), S, Y
*GET, S2Y, NODE, NODE(0.5, 0, 0), S, Y
*GET, S3Y, NODE, NODE(1, 0, 0), S, Y
S1Y_REF=1/0.91*0.2*EXP(LOG(8)*0)
S2Y_REF=1/0.91*0.2*EXP(LOG(8)*0.5)
S3Y_REF=1/0.91*0.2*EXP(LOG(8)*1)
R1=S1Y/S1Y_REF
R2=S2Y/S2Y_REF
R3=S3Y/S3Y_REF

*DIM,LABEL,CHAR,3,2
*DIM,VALUE,,3,3
LABEL(1,1) = 'X=','X=','X='
LABEL(1,2) = '0.0','0.5','1.0'
*VFILL,VALUE(1,1),DATA,S1Y_REF, S2Y_REF, S3Y_REF
*VFILL,VALUE(1,2),DATA,S1Y, S2Y, S3Y
*VFILL,VALUE(1,3),DATA,R1, R2, R3

/COM
/OUT,vm305,vrt
/COM,------------------- VM305 RESULTS COMPARISON --------------
/COM,
/COM,-------------------------------------------------------------------
/COM, RESULTS OF STRESS_Y
/COM,-------------------------------------------------------------------
/COM,
/COM,               |  TARGET   |   MECHANICAL APDL  |  RATIO
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A2,A8,'   ',F10.3,'     ',1F10.3,'            ',1F6.3)
/COM,-----------------------------------------------------------
/OUT
*LIST,vm305,vrt
FINISH

/EXIT,NOSAVE

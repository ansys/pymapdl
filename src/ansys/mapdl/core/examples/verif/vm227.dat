/COM,ANSYS MEDIA REL. 2023R1 (11/04/2022) REF. VERIF. MANUAL: REL. 2023R1
/VERIFY,VM227
JPGPRF,500,100,1             ! MACRO TO SET PREFS FOR JPEG PLOTS
/SHOW,JPEG
/TITLE,VM227, Radiation Between Finite Coaxial Cylinders
/COM,* EXPECTED RESULTS:
! RESULTS DERIVED FROM MODEST, RADIATIVE HEAT TRANSFER, P.791
! VIEW FACTOR EVALUATIONS 44, 45
/COM,* FOR INSIDE CYLINDER-INSIDE CYLINDER VFAVG1=0
/COM,* FOR OUTSIDE CYLINDER-INSIDE CYLINDER VFAVG2=0.288
/COM,* FOR OUTSIDE CYLINDER-OUTSIDE CYLINDER VFAVG3=0.503
*SET,NDIV,20          ! ADJUSTED TO DETERMINE SPACING FOR PROBLEM.
*SET,L1,10    
*SET,R1,1   
*SET,R2,3   
/PREP7  
BLC4,0,0,R1,L1  
BLC4,R2,0,R1,L1 
ET,1,PLANE77                        !CREATE 2D THERMAL ELEMENTS
KEYOPT,1,1,0
KEYOPT,1,3,1
LESIZE,ALL,,,10 
MSHAPE,0,2D 
MSHKEY,0
AMESH,ALL
SFL,2,RDSF,1, ,1,
SFL,8,RDSF,1, ,1,
FINISH  
/AUX12    
STEF,0.119E-10                      !SET STEFAN-BOLTZMAN CONSTANT FOR MODEL
hemiopt,,,,,,,,,,,,,,,,,,,0
TOFFST,100                          !SET TEMPERATURE OFFSET
RADOPT,,0.1,2,1000,0.1,0.1  !SET RADIOSITY OPTIONS
SPCTEMP,1,0.E+00                    !SET TEMPERATURE FOR RADIATION TO SPACE
                                    !(NO RADIATION WILL APPEAR IN THIS MODEL)
HEMIOPT,1000,0.01  !SET HEMICUBE OPTIONS
V2DOPT,1,NDIV,0.E+00,200            !SET 2D CALCULATIONS TO AXISYMMETRIC
VFOPT,NEW   
!VFCALC   !CALCULATE RADIOSITY VIEW FACTORS
ASEL,S, , ,       1 
ESLA,S  
CM,INSIDE,ELEM  
ASEL,S, , ,       2 
ESLA,S  
CM,OUTSIDE,ELEM 
CMPL
allsel
vfopt,read
VFQUERY,INSIDE,INSIDE !EXTRACT VIEW FACTOR FROM INTERIOR CYLINDER ELEMENTS DUE TO INTERIOR CYLINDER
*GET,VFAVG1,RAD,,VFAVG   !DETERMINE VIEW FACTOR FOR ENTIRE SURFACE
VFQUERY,OUTSIDE,INSIDE   !EXTRACT VIEW FACTOR FROM EXTERIOR CYLINDER ELEMENTS
                         !DUE TO INTERIOR CYLINDER
*GET,VFAVG2,RAD,,VFAVG   !DETERMINE VIEW FACTOR FOR ENTIRE SURFACE
VFQUERY,OUTSIDE,OUTSIDE  !EXTRACT VIEW FACTOR FROM EXTERIOR CYLINDER ELEMENTS 
			 !DUE TO EXTERIOR CYLINDER
*GET,VFAVG3,RAD,,VFAVG   !DETERMINE VIEW FACTOR FOR ENTIRE SURFACE
*status,parm
*DIM,VALUE,,3,3
*VFILL,VALUE(1,1),DATA,0,0.288,0.503
*VFILL,VALUE(1,2),DATA,VFAVG1,VFAVG2,VFAVG3
*VFILL,VALUE(1,3),DATA,0.000,VFAVG2/0.288,VFAVG3/0.503
*DIM,LABEL,CHAR,3,2
LABEL(1,1) = 'VF(1-1)','VF(2-1)','VF(2-2)'
/OUT,vm227,vrt
/COM,
/COM,------------------- VM227 RESULTS COMPARISON --------------
/COM,
/COM,               |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F8.3,'  ',F14.3,'   ',1F17.2)
/COM,-----------------------------------------------------------
/OUT
FINISH
*LIST,vm227,vrt


! Copyright (C) 2024 ANSYS, Inc. and/or its affiliates.
! SPDX-License-Identifier: MIT
! 
!
! Permission is hereby granted, free of charge, to any person obtaining a copy
! of this software and associated documentation files (the "Software"), to deal
! in the Software without restriction, including without limitation the rights
! to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
! copies of the Software, and to permit persons to whom the Software is
! furnished to do so, subject to the following conditions:
!
! The above copyright notice and this permission notice shall be included in all
! copies or substantial portions of the Software.
!
! THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
! IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
! FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
! AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
! LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
! OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
! SOFTWARE.

/COM,ANSYS MEDIA REL. 2023R2 (05/12/2023) REF. VERIF. MANUAL: REL. 2023R2
/VERIFY,VM320
/COM,   REF: J. GUILLEN, 1999
/COM,   "STUDIES OF THE DYNAMICS OF DRY-FRICTION-DAMPED BLADE ASSEMBLIES.",
/COM,   MECHANICS [PHYSICS.MED-PH]. UNIVERSITY OF MICHIGAN-ANN ARBOR.
/COM,
/TITLE, VM320, NONLINEAR HARMONIC ANALYSIS OF A FRICTIONAL DAMPER
/OUT,vm320_scratch

JPGPRF,500,100,1

/PREP7

/COM, MODEL PARAMETERS
PI  = ACOS(-1)
M   = 944.6         ! MASS (KG)
C   = 7700          ! DAMPING (KG/S)
K   = 2.4E+7        ! LINEAR SPRING CONSTANT (N/M)
KD  = 2.4E+7        ! NORMAL/TANGENTIAL CONTACT STIFFNESS (N/M)
FD  = 246.048       ! NORMAL FORCE (N)
U0  = FD/KD         ! ENFORCED NORMAL DISPLACEMENT
F   = 0.5*FD        ! EXCITATION MAGNITUDE

/COM, HARMONIC BALANCE METHOD PARAMETERS
FSTART  = 20/(2*PI)  ! STARTING FREQUENCY (HZ)
FEND    = 350/(2*PI) ! ENDING   FREQUENCY (HZ)
DS      = 5.0        ! INITIAL ARC LENGTH
NH      = 1          ! NUMBER OF HBM HARMONICS
NT      = 64         ! NUMBER OF TIME POINTS PER PERIOD

/COM, GEOMETRY AND MESH
ET,1,178
KEYOPT,1,1,0  ! UNIDIRECTIONAL
KEYOPT,1,2,1  ! PENALTY-BASED METHOD
KEYOPT,1,5,3  ! Z-DIRECTION FOR CONTACT NORMAL

MP,MU,1,1.0   ! FRICTION COEFFICIENT

ET,2,21
KEYO,2,3,4    ! 2D MASS

ET,3,14
KEYO,3,3,2

R,1, -KD,,,-KD
R,2,M
R,3,K,C

N,1,0,0,0
N,2,1,0,U0    ! INITIAL PENETRATION BASED ON NODE LOCATION

TYPE,2
REAL,2
E,2

TYPE,3
REAL,3
E,1,2

TYPE,1
REAL,1
E,2,1

FINISH

/SOLU
ANTYPE,HARMIC

HROPT,HBM,NH
HARFRQ,FSTART,FEND

HBMOPT,AFT,,NT
HBMOPT,CONTSET,,DS
HBMOPT,SCAL,VALU,1e-5,200

/COM, BOUNDARY CONDITIONS AND LOADING
KBC,1

D,1,ALL
D,2,UY

F,2,FX,F,0

SOLVE
FINISH

/COM, HBM POST-PROCESSING

/post26
*GET,JOBN,ACTIVE,,JOBNAM
FILE,%JOBN%_1hi0

NSOL,2,2,U,X,UX     ! AMPLITUDE
ABS,2,2

PROD,3,1,,,,,,2*PI  ! FREQUENCY IN RAD/S

XVAR,3
/GMARKER,1,4
/AXLAB,X,FREQUENCY (RAD/S)
/AXLAB,Y,HARMONIC 1 AMPLITUDE (M)
/XRANGE,0,2*PI*FEND
/YRANGE,0.0,1.6e-5
/GROPT,DIVX,7
/GROPT,DIVY,8
/SHOW,JPEG,REV
    PLVAR,2
/SHOW,CLOSE

*GET,FMAX   ,VARI,2,EXTREM,TMAX
*GET,AMPLMAX,VARI,2,EXTREM,VMAX
OMGMAX = 2*PI*FMAX

FINISH

*DIM,LABEL,CHAR,1,2
LABEL(1,1) = 'OMG_MAX'
LABEL(1,2) = 'AMPL_MAX'

*DIM,VALUE,ARRAY,2,3
VALUE(1,1) = 209            ! REFERENCE VALUES
VALUE(2,1) = 1.50e-5
VALUE(1,2) = OMGMAX         ! TEST VALUES
VALUE(2,2) = AMPLMAX
VALUE(1,3) = OMGMAX/209     ! RATIO
VALUE(2,3) = AMPLMAX/1.50e-5

/NOPR
/OUT,vm320,vrt
/COM,------------ VM320 RESULTS COMPARISON --------------
/COM,
/COM,
/COM,        |   TARGET   |   MECHANICAL APDL   |   RATIO
/COM,
*VWRITE,LABEL(1,1),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,'   ',F10.4,'  ',F10.4,'   ',F8.3)
*VWRITE,LABEL(1,2),VALUE(2,1),VALUE(2,2),VALUE(2,3)
(1X,A8,'   ',E10.4,'  ',E10.4,'   ',F8.3)
/COM, --------------------------------------------------
/COM,
/COM,
/OUT
*LIST,vm320,vrt

/EXIT,NOSAVE

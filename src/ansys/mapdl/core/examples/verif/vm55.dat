! Copyright (C) 2024 ANSYS, Inc. and/or its affiliates.
! SPDX-License-Identifier: MIT
! 
!
! Permission is hereby granted, free of charge, to any person obtaining a copy
! of this software and associated documentation files (the "Software"), to deal
! in the Software without restriction, including without limitation the rights
! to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
! copies of the Software, and to permit persons to whom the Software is
! furnished to do so, subject to the following conditions:
!
! The above copyright notice and this permission notice shall be included in all
! copies or substantial portions of the Software.
!
! THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
! IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
! FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
! AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
! LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
! OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
! SOFTWARE.

/COM,ANSYS MEDIA REL. 2023R2 (05/12/2023) REF. VERIF. MANUAL: REL. 2023R2
/VERIFY,VM55
/PREP7
MP,PRXY,,0.3
/TITLE, VM55, VIBRATION OF A STRETCHED CIRCULAR MEMBRANE
C***        VIBRATION PROBS. IN ENGR., TIMOSHENKO, 3RD. ED., PAGE  439, EQN. 182
ANTYPE,STATIC        ! STATIC ANALYSIS
ET,1,SHELL208,,,2
MP,EX,1,30E6
MP,DENS,1,73E-5
SECTYPE,1,SHELL
SECDATA,0.01
SECNUM,1
N,1
N,10,15
FILL
E,1,2
EGEN,9,1,1
D,1,UX,,,,,ROTZ
D,10,UY
F,10,FX,9424.778
FINISH
/SOLU 
RESCONTROL,LINEAR,ALL,1  			! NEEDED FOR PERTURBATION ANALYSIS 
OUTPR,BASIC,1
/OUT,SCRATCH
SOLVE
FINISH
/POST1
/OUT,
ETABLE,STRS,S,X
*GET,STRSS,ELEM,9,ETAB,STRS
PARSAV,ALL
FINI
/SOLU    
ANTYPE,STATIC,RESTART,,,PERTURB		! RESTART STATIC SOLVE FROM LAST LOAD STEP AND SUB STEP
PERTURB,MODAL,,,PARKEEP				! PERFORM PERTURBED MODAL SOLVE
/OUT,SCRATCH
SOLVE,ELFORM					! REFORM ELEMENT MATRICES
/OUT,

PARRES,CHANGE
MXPAND,3             ! EXPAND FIRST 3 MODES
MODOPT,LANB,9        ! EXTRACT FIRST 9 MODES
/OUT,SCRATCH
SOLVE
/OUT,
*GET,FREQ1,MODE,1,FREQ
*GET,FREQ2,MODE,2,FREQ
*GET,FREQ3,MODE,3,FREQ
*DIM,LABEL,CHAR,4,2
*DIM,VALUE,,4,3
LABEL(1,1) = 'SIG,R  p','f1,     ','f2,     ','f3,     '
LABEL(1,2) = 'si      ','Hz      ','Hz      ','Hz      '
*VFILL,VALUE(1,1),DATA,10000,94.406,216.77,339.85
*VFILL,VALUE(1,2),DATA,STRSS,FREQ1,FREQ2,FREQ3
*VFILL,VALUE(1,3),DATA,(STRSS/10000),(FREQ1/94.406),(FREQ2/216.77),(FREQ3/339.85)
/COM
/OUT,vm55,vrt
/COM,------------------- VM55 RESULTS COMPARISON ---------------
/COM,
/COM,                 |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.3,'  ',F14.3,'   ',1F15.3)
/COM,-----------------------------------------------------------
/OUT
FINISH
*LIST,vm55,vrt

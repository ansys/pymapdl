! Copyright (C) 2024 ANSYS, Inc. and/or its affiliates.
! SPDX-License-Identifier: MIT
! 
!
! Permission is hereby granted, free of charge, to any person obtaining a copy
! of this software and associated documentation files (the "Software"), to deal
! in the Software without restriction, including without limitation the rights
! to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
! copies of the Software, and to permit persons to whom the Software is
! furnished to do so, subject to the following conditions:
!
! The above copyright notice and this permission notice shall be included in all
! copies or substantial portions of the Software.
!
! THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
! IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
! FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
! AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
! LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
! OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
! SOFTWARE.

/COM,ANSYS MEDIA REL. 2023R2 (05/12/2023) REF. VERIF. MANUAL: REL. 2023R2
/VERIFY,VM145
/PREP7
/TITLE, VM145, STRETCHING OF AN ORTHOTROPIC SOLID
C***    MECHANICS OF SOLIDS, CRANDALL AND DAHL, 1959, PAGE 225
ANTYPE,STATIC              ! STATIC ANALYSIS
ET,1,SOLID185              ! ANISOTROPIC SOLID
MP,EX,2,10E6               ! LABELED MATERIAL PROPERTY INPUT
MP,EY,2,20E6
MP,EZ,2,40E6
MP,NUXY,2,.1
MP,NUYZ,2,.2
MP,NUXZ,2,.3
MP,GXY,2,10E6
MP,GYZ,2,10E6
MP,GXZ,2,10E6
TB,ANEL,1,,,1
TBDATA,1,.1E-6,-.5E-8,-.75E-8    ! UNINVERTED MATERIAL PROPERTY MATRIX INPUT
TBDATA,7,.5E-7,-.5E-8,,,,.25E-7
TBDATA,16,.1E-6,,,.1E-6,,.1E-6
N,1
N,2,1
NGEN,2,2,1,2,,,,1
NGEN,4,4,1,4,,,1
E,1,2,6,5,3,4,8,7
MAT,2
E,9,10,14,13,11,12,16,15
OUTPR,,1
D,1,ALL,,,9,8
D,3,UX,,,7,2
D,11,UX,,,15,2
D,2,UY,,,4
D,10,UY,,,12
D,2,UZ,,,14,4
D,5,UZ,,,13,8
F,2,FX,25,,16,2
F,5,FY,50,,8
F,13,FY,50,,16
FINISH
/SOLU    
/OUT,SCRATCH
SOLVE
/POST1
/OUT,
*GET,UX,NODE,8,U,X
*GET,UY,NODE,8,U,Y
*GET,UZ,NODE,8,U,Z
*DIM,LABEL,CHAR,3,2
*DIM,VALUE,,3,3
LABEL(1,1) = 'X DISP ','Y DISP ','Z DISP '
LABEL(1,2) = ' in',' in',' in'
*VFILL,VALUE(1,1),DATA,.9E-5,.95E-5,-.175E-5
*VFILL,VALUE(1,2),DATA,UX,UY,UZ
*VFILL,VALUE(1,3),DATA,ABS(UX/(.9E-5)),ABS(UY/(.95E-5)),ABS(UZ/(-.175E-5))
/COM
/OUT,vm145,vrt
/COM,------------------- VM145 RESULTS COMPARISON ---------------
/COM,
/COM,                 |     TARGET   |     Mechanical APDL   |   RATIO
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F12.8,'  ',F17.8,'   ',1F15.3)
/COM,------------------------------------------------------------
/OUT
FINISH
*LIST,vm145,vrt

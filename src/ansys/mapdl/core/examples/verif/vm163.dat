! Copyright (C) 2024 ANSYS, Inc. and/or its affiliates.
! SPDX-License-Identifier: MIT
! 
!
! Permission is hereby granted, free of charge, to any person obtaining a copy
! of this software and associated documentation files (the "Software"), to deal
! in the Software without restriction, including without limitation the rights
! to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
! copies of the Software, and to permit persons to whom the Software is
! furnished to do so, subject to the following conditions:
!
! The above copyright notice and this permission notice shall be included in all
! copies or substantial portions of the Software.
!
! THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
! IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
! FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
! AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
! LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
! OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
! SOFTWARE.

/COM,ANSYS MEDIA REL. 2023R2 (05/12/2023) REF. VERIF. MANUAL: REL. 2023R2
/VERIFY,VM163
JPGPRF,500,100,1             ! MACRO TO SET PREFS FOR JPEG PLOTS
/SHOW,JPEG
/PREP7
SMRT,OFF
/TITLE, VM163, GROUNDWATER SEEPAGE
C***          A SIMPLE GUIDE TO FINITE ELEMENTS, R.J.OWEN AND E. HINTON, P.89
ANTYPE,STATIC             ! THERMAL ANALYSIS           
ET,1,PLANE55,,,1,,,,1     ! AXISYMMETRIC, SUPRESS ALL PRINTOUT
MP,KXX,1,0.864            ! PERMEABILITY
K,1
*REPEAT,3,1,,3.5
KGEN,2,1,3,1,8.0 
KGEN,2,1,2,1,18.0 
K,9,18,10 
K,10,8,10 
K,11,8.0,3.5
L,1,4                     ! DEFINE LINE SEGMENTS AND MESH DIVISIONS
*REPEAT,3,1,1
L,10,9
L,11,8
L,4,7
LESIZE,ALL,,,8
A,1,4,5,2 
A,2,5,6,3 
A,4,7,8,5 
A,11,8,9,10 
ESIZE,,5
MSHK,2                    ! MAPPED AREA MESH IF POSSIBLE
MSHA,0,2D                 ! USING QUADS 
AMESH,ALL                 ! MESH AREAS
NUMMRG,NODE               ! MERGE NODES AT BOTTOM OF CAISSON
NSEL,S,LOC,Y,7.0
D,ALL,TEMP,0 
NSEL,S,LOC,Y,10 
D,ALL,TEMP,3              ! PRESSURE HEAD 
NSEL,ALL
FINISH
/SOLU     
SOLVE
FINISH
/POST1
/CLABEL,,1
/CONTOUR,,20
/EDGE,,1
PLNSOL,TEMP          ! DISPLAY PRESSURE CONTOURS
/VSCALE,,-1
PLVECT,TG            ! DISPLAY THERMAL GRADIENT VECTORS 
NSEL,S,LOC,Y,7.0
PRRSOL,HEAT          ! PRINT FLOWRATE THROUGH SOIL
FSUM,HEAT
*GET,Q1,FSUM,0,ITEM,HEAT
*SET,Q,(Q1/(2*3.14159265))
*DIM,LABEL,CHAR,1,2
*DIM,VALUE,,1,3
LABEL(1,1) = 'q '
LABEL(1,2) = 'CUBm/DAY'
*VFILL,VALUE(1,1),DATA,8.6
*VFILL,VALUE(1,2),DATA,Q
*VFILL,VALUE(1,3),DATA,ABS(Q/8.6)
/COM
/OUT,vm163,vrt
/COM,------------------- VM163 RESULTS COMPARISON -------------
/COM,
/COM,                 |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.1,'  ',F13.1,'   ',1F15.3)
/COM,----------------------------------------------------------
/OUT
FINISH
*LIST,vm163,vrt

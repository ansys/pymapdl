/COM,ANSYS MEDIA REL. 2023R1 (11/04/2022) REF. VERIF. MANUAL: REL. 2023R1
/VERIFY,VM153
JPGPRF,500,100,1             ! MACRO TO SET PREFS FOR JPEG PLOTS
/SHOW,JPEG
/TITLE, VM153, NONAXISYM. VIBR. OF A STRETCHED CIRCULAR MEMBRANE 
C***           VIBRATION PROBS. IN ENGR., TIMOSHENKO, PAGE 439, ARTICLE 69
C*** USING SHELL181 MEMBRANE SHELL OPTION
/PREP7
ET,1,SHELL181,1          ! MEMBRANE STIFFNESS ONLY
SECT,1,SHELL
SECD,.00005,1            ! PLATE THICKNESS
MP,EX,1,30E6
MP,ALPX,1,1E-5
MP,DENS,1,.00073
MP,NUXY,1,0
CSYS,1
N,1,.001
N,10,3
FILL
NGEN,4,10,1,10,1,,10
NROTAT,ALL               ! ROTATE ALL NODES INTO CYLINDRICAL SYSTEM
E,1,2,12,11
EGEN,3,10,-1
EGEN,9,1,-3
NSEL,S,LOC,Y,0           ! DEFINE EDGE COMPONENTS FOR CYCLIC SYMM. MACRO
CM,RIGHT,NODE            ! RIGHT EDGE OF THE SECTOR MODEL
NSEL,S,LOC,Y,30
CM,LEFT,NODE             ! LEFT EDGE OF THE SECTOR MODEL
NSEL,ALL
NSEL,S,LOC,X,3
CM,INNER,NODE
NSEL,ALL
/NOPR
CYCLIC              
/GOPR
D,ALL,ALL,0              ! FIX ALL DISPLACEMENTS TO APPLY PRESTRESS
TREF,0                   ! REFERENCE TEMPERATURE
BFUNIF,TEMP,-6.66666     ! COOL DOWN TO INDUCE PRESTRESS
FINISH

/SOLUTION                ! SOLVE FOR STATIC SOLUTION
/OUTPUT,SCRATCH
ANTYPE,STATIC
RESCONTROL,LINEAR,ALL,1  ! LINEAR STATIC ANALYSIS TO PRESTRESS MODEL
CYCOPT,HINDX,0,0
SOLVE
FINISH

/TITLE, VM153, NONAXISYM. VIBR. OF A STRETCHED CIRCULAR MEMBRANE (MODAL)

/SOLUTION
ANTYPE,STATIC,RESTART,,,PERTURB ! PERFORM LP MODAL ANALYSIS
PERTURB,MODAL,,,NOKEEP          ! DO NOT RETAIN ANY BOUNDARY CONDITIONS
DDOPTION,CYCHI,,2               ! USE HARMONIC INDEX DISTRIBUTED SOLVING
SOLVE,ELFORM                    ! REFORM THE MATRICES

CMSEL,S,LEFT,NODE
CMSEL,A,RIGHT,NODE
D,ALL,UX,,,,,UY
NSEL,ALL

CMSEL,S,INNER,NODE
D,ALL,ALL,0
NSEL,ALL

MODOPT,LANB,4,1,1000     ! USE BLOCK LANCZOS ITER, EXTRACT 4 MODES IN 1 TO 1000 HZ
MXPAND,4                 ! EXPAND 4 MODES
CYCOPT,HINDX,0,1         ! NODAL DIAMETER 0 TO 1, WITH 30 DEG. SECTORS
SOLVE
FINISH

/POST1
file,,rstp
/OUTPUT
SET,1,1
*GET,F01_1,ACTIVE,,SET,FREQ   ! NATURAL FREQ. FOR 1ST MODE, 0 MODAL DIAM.
SET,1,2
*GET,F02_1,ACTIVE,,SET,FREQ   ! NATURAL FREQ. FOR 2ND MODE, 0 MODAL DIAM.
SET,2,1
*GET,F11_1,ACTIVE,,SET,FREQ   ! NATURAL FREQ. FOR 1ST MODE, 1 MODAL DIAM.
SET,2,2
*GET,F11_2,ACTIVE,,SET,FREQ   ! REPEATED FREQ. FOR 1ST MODE,1 MODAL DIAM.
SET,2,3
*GET,F12_1,ACTIVE,,SET,FREQ   ! NATURAL FREQ. FOR 2ND MODE, 1 MODAL DIAM.
SET,2,4
*GET,F12_2,ACTIVE,,SET,FREQ   ! REPEATED FREQ. FOR 2ND MODE,1 MODAL DIAM.
FINISH
/NOPR

/POST1
file,,rstp
/CYCEXPAND,,ON
/VIEW,,1,1,1
/VUP,1,Z
/GLINE,,-1               ! NO ELEMENT OUTLINE
/TRIAD,OFF
SET,1,2
/TITLE, VM153, STRETCHED CIRCULAR MEMBRANE - NODAL DIAM 0, MODE 2
PLNSOL,U,Z
SET,2,2
/TITLE, VM153, STRETCHED CIRCULAR MEMBRANE - NODAL DIAM 1, MODE 2
PLNSOL,U,Z
FINISH
*DIM,LABEL,CHAR,4,2
*DIM,VALUE,,4,3
LABEL(1,1) = 'F(0,1) ','F(0,2) ','F(1,1) ','F(1,2) '
LABEL(1,2) = 'Hz ','Hz ','Hz ','Hz '
*VFILL,VALUE(1,1),DATA,211.1,484.7,336.5,616.1
*VFILL,VALUE(1,2),DATA,F01_1,F02_1,F11_1,F12_1
*VFILL,VALUE(1,3),DATA,ABS(F01_1/211.1),ABS(F02_1/484.7),ABS(F11_1/336.5),ABS(F12_1/616.1)
FINISH
SAVE,TABLE_1
/COM
/OUT,vm153,vrt
/COM,------------------- VM153 RESULTS COMPARISON -------------
/COM,
/COM,                 |    TARGET   |    Mechanical APDL   |    RATIO
/COM,
/COM,
/COM, SHELL181
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F11.1,'  ',F16.1,'   ',1F16.3)
/NOPR
/COM,----------------------------------------------------------
/OUT
*LIST,vm153,vrt

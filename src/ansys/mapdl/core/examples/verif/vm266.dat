/COM,ANSYS MEDIA REL. 2023R1 (11/04/2022) REF. VERIF. MANUAL: REL. 2023R1
/VERIFY,VM266
/PREP7
JPGPRF,500,100,1                ! MACRO TO SET PREFS FOR JPEG PLOTS
/SHOW,JPEG
/TITLE, VM266, 3D CROSSING BEAMS IN CONTACT WITH FRICTION
C***     G.ZAVARISE AND P. WRIGGERS   
C***     "CONTACT WITH FRICTION BETWEEN BEAMS IN 3-D SPACE"   
C***     INTERNATIONAL JOURNAL FOR NUMERICAL METHODS IN ENGINEERING, VOL.49,977-1006 (2000)
/OUT,SCRATCH
/PREP7 
ANTYPE,0                        ! STATIC ANALYSIS                
ET,1,BEAM188                    ! 3-D 2-NODE BEAM
KEYO,1,3,2                      ! QUADRATIC SHAPE FUNCTIONS ALONG THE LENGTH
ET,2,BEAM188                    ! 3-D 2-NODE BEAM
KEYO,2,3,2                      ! QUADRATIC SHAPE FUNCTIONS ALONG THE LENGTH
SECTYPE,1,BEAM,CTUBE            ! CIRCULAR TUBE SECTION TYPE FOR BEAMS
SECDATA,0.06,0.12793            ! INNER AND OUTER RADIUS
R,2,0.2,0.2                     ! RADIUS ON THE TARGET AND CONTACT SIDES
RMODIF,2,3,-1.0E+4              ! NORMAL CONTACT STIFFNESS-ABSOLUTE VALUE
RMODIF,2,12,-1.0E+4             ! TANGENTIAL CONTACT STIFFNESS-ABSOLUTE VALUE
ET,3,CONTA177                   ! 3D LINE-TO-SURFACE CONTACT
KEYOPT,3,2,1                    ! PENALTY FUNCTION
KEYOPT,3,3,4                    ! CROSSING BEAMS(FORCE-BASED)
ET,4,TARGE170                   ! 3D TARGET SEGMENT
MP,EX,1,1.0E+8                  ! YOUNG'S MODULUS 
MP,NUXY,1,0.0                   ! POISSON'S RATIO
MP,MU,1,0.1                     ! FRICTION COEFFICIENT
K,1,0,0,0                       ! GEOMETRY OF BEAMS
K,2,14,0,0
K,3,4,-5,1
K,4,4,5,1
L,1,2
L,3,4
TYPE,1                          ! HORIZONTAL BEAM
MAT,1
SECNUM,1
LESIZE,1,,,16
LMESH,1
TYPE,2                          ! VERTICAL BEAM
MAT,1
SECNUM,1
LESIZE,2,,,9
LMESH,2
TYPE,3                          ! CONTACT ON HORIZONTAL BEAM
REAL,2
MAT,1
ESEL,S,TYPE,,1
NSLE
ESURF
TYPE,4                          ! TARGET ON VERTICAL BEAM
REAL,2
MAT,1
ESEL,S,TYPE,,2
NSLE
ESURF
ALLSEL,ALL
DK,2,ALL                        ! CLAMPED RIGHT END OF HORIZONTAL BEAM
DK,3,ALL                        ! CLAMPED BOTH ENDS OF VERTICAL BEAM
DK,4,ALL
DK,2,UX,0.18                    ! HORIZONTAL DISPLACEMENT OF RIGHT END
DK,2,UZ,1.8                     ! OUT-OF-PLANE DISPLACEMENT OF RIGHT END
FINI
/SOLU
NLGEOM,ON
NSUBST,60,100,60
OUTRES,ALL,ALL
SOLVE
FINI
/POST1
ESEL,S,ELEM,,30                 ! ELEMENT IN CONTACT
SET,1,30                        ! TIME WHEN CONTACT IS INITIATED
ETABLE,PRES3,CONT,PRES          ! NORMAL CONTACT FORCE AT SUBSTEP30
ETABLE,SFRIC3,CONT,SFRIC        ! FRICTIONAL CONTACT FORCE AT SUBSTEP30
*GET,NFORCE3,ETAB,1,ELEM,30     ! NORMAL CONTACT FORCE AT SUBSTEP30-COMPARE WITH STEP 3 IN REFERENCE
*GET,TFORCE3,ETAB,2,ELEM,30     ! FRICTIONAL CONTACT FORCE AT SUBSTEP30-COMPARE WITH STEP 3 IN REFERENCE
SET,1,40                    
ETABLE,PRES4,CONT,PRES          ! NORMAL CONTACT FORCE AT SUBSTEP40
ETABLE,SFRIC4,CONT,SFRIC        ! FRICTIONAL CONTACT FORCE AT SUBSTEP40
*GET,NFORCE4,ETAB,3,ELEM,30     ! NORMAL CONTACT FORCE AT SUBSTEP40-COMPARE WITH STEP 4 IN REFERENCE
*GET,TFORCE4,ETAB,4,ELEM,30     ! FRICTIONAL CONTACT FORCE AT SUBSTEP40-COMPARE WITH STEP 4 IN REFERENCE
SET,1,50                     
ETABLE,PRES5,CONT,PRES          ! NORMAL CONTACT FORCE AT SUBSTEP50
ETABLE,SFRIC5,CONT,SFRIC        ! FRICTIONAL CONTACT FORCE AT SUBSTEP50
*GET,NFORCE5,ETAB,5,ELEM,30     ! NORMAL CONTACT FORCE AT SUBSTEP50-COMPARE WITH STEP 5 IN REFERENCE
*GET,TFORCE5,ETAB,6,ELEM,30     ! FRICTIONAL CONTACT FORCE AT SUBSTEP50-COMPARE WITH STEP 5 IN REFERENCE                
SET,1,60                        ! LAST SUBSTEP
ETABLE,PRES6,CONT,PRES          ! NORMAL CONTACT FORCE AT SUBSTEP60 (LAST)
ETABLE,SFRIC6,CONT,SFRIC        ! FRICTIONAL CONTACT FORCE AT SUBSTEP60(LAST)
*GET,NFORCE6,ETAB,7,ELEM,30     ! NORMAL CONTACT FORCE AT SUBSTEP60-COMPARE WITH STEP 6 IN REFERENCE
*GET,TFORCE6,ETAB,8,ELEM,30     ! FRICTIONAL CONTACT FORCE AT SUBSTEP60-COMPARE WITH STEP 6 IN REFERENCE
ESEL,ALL
/OUT,
*DIM,LABEL2,CHAR,4,1
*DIM,VALUE2,,4,3
LABEL2(1,1) = 'NFORCE3  ','NFORCE4 ','NFORCE5 ','NFORCE6 '
*VFILL,VALUE2(1,1),DATA,17.0,33.8,50.4,67.0
*VFILL,VALUE2(1,2),DATA,NFORCE3,NFORCE4,NFORCE5,NFORCE6
*VFILL,VALUE2(1,3),DATA,(NFORCE3/17.0),(NFORCE4/33.8),(NFORCE5/50.4),(NFORCE6/67.0)
*DIM,LABEL1,CHAR,4,1
*DIM,VALUE1,,4,3
LABEL1(1,1) = 'TFORCE3  ','TFORCE4 ','TFORCE5 ','TFORCE6 '
*VFILL,VALUE1(1,1),DATA,1.7,3.38,5.04,6.7
*VFILL,VALUE1(1,2),DATA,TFORCE3,TFORCE4,TFORCE5,TFORCE6
*VFILL,VALUE1(1,3),DATA,(TFORCE3/1.7),(TFORCE4/3.38),(TFORCE5/5.04),(TFORCE6/6.7)
FINISH
/COM
/OUT,vm266,vrt
/COM,----------------- VM266 RESULTS COMPARISON ---------------
/COM,
/COM,
/COM,FORCE-BASED CONTACT: K(3)=4
/COM,NORMAL-CONTACT FORCE   |  TARGET   |   Mechanical APDL   |   RATIO
/COM,
*VWRITE,LABEL2(1),VALUE2(1,1),VALUE2(1,2),VALUE2(1,3)
(1X,A20,'   ',F10.4,'  ',F14.4,'    ',1F15.3)
/COM,
/COM,
/COM,TANG-CONTACT FORCE     |  TARGET   |   Mechanical APDL   |   RATIO
/COM,
*VWRITE,LABEL1(1),VALUE1(1,1),VALUE1(1,2),VALUE1(1,3)
(1X,A20,'   ',F10.4,'  ',F14.4,'    ',1F15.3)
/COM,
/COM, --------------------------------------------------------
/OUT,
FINISH
/CLEAR,NOSTART

/OUT,SCRATCH
/PREP7 
ANTYPE,0                        ! STATIC ANALYSIS                
ET,1,BEAM188                    ! 3-D 2-NODE BEAM
KEYO,1,3,2                      ! QUADRATIC SHAPE FUNCTIONS ALONG THE LENGTH
ET,2,BEAM188                    ! 3-D 2-NODE BEAM
KEYO,2,3,2                      ! QUADRATIC SHAPE FUNCTIONS ALONG THE LENGTH
SECTYPE,1,BEAM,CTUBE            ! CIRCULAR TUBE SECTION TYPE FOR BEAMS
SECDATA,0.06,0.12793            ! INNER AND OUTER RADIUS
R,2,0.2,0.2                     ! RADIUS ON THE TARGET AND CONTACT SIDES
T_RATIO=5.71425                 ! AREA RATIO TO CONVERT TO TRACTION CONTACT
T_STIFF=(-1.0E4)*T_RATIO        ! SCALED ABSOLUTE VALUE OF CONTACT STIFFNESS
RMODIF,2,3,T_STIFF              ! APPLY NORMAL CONTACT STIFFNESS-ABSOLUTE VALUE
RMODIF,2,12,T_STIFF             ! APPLY TANGENTIAL CONTACT STIFFNESS-ABSOLUTE VALUE
ET,3,CONTA177                   ! 3D LINE-TO-SURFACE CONTACT
KEYOPT,3,2,1                    ! PENALTY FUNCTION
KEYOPT,3,3,3                    ! CROSSING BEAMS WITH TRACTION CONTACT
ET,4,TARGE170                   ! 3D TARGET SEGMENT
MP,EX,1,1.0E+8                  ! YOUNG'S MODULUS 
MP,NUXY,1,0.0                   ! POISSON'S RATIO
MP,MU,1,0.1                     ! FRICTION COEFFICIENT
K,1,0,0,0                       ! GEOMETRY OF BEAMS
K,2,14,0,0
K,3,4,-5,1
K,4,4,5,1
L,1,2
L,3,4
TYPE,1                          ! HORIZONTAL BEAM
MAT,1
SECNUM,1
LESIZE,1,,,16
LMESH,1
TYPE,2                          ! VERTICAL BEAM
MAT,1
SECNUM,1
LESIZE,2,,,9
LMESH,2
TYPE,3                          ! CONTACT ON HORIZONTAL BEAM
REAL,2
MAT,1
ESEL,S,TYPE,,1
NSLE
ESURF
TYPE,4                          ! TARGET ON VERTICAL BEAM
REAL,2
MAT,1
ESEL,S,TYPE,,2
NSLE
ESURF
ALLSEL,ALL
DK,2,ALL                        ! CLAMPED RIGHT END OF HORIZONTAL BEAM
DK,3,ALL                        ! CLAMPED BOTH ENDS OF VERTICAL BEAM
DK,4,ALL
DK,2,UX,0.18                    ! HORIZONTAL DISPLACEMENT OF RIGHT END
DK,2,UZ,1.8                     ! OUT-OF-PLANE DISPLACEMENT OF RIGHT END
FINI
/SOLUTION
NLGEOM,ON
NSUBST,60,100,60
OUTRES,ALL,ALL
/OUT,SCRATCH
SOLVE
FINISH
/POST1
ESEL,S,ELEM,,30                 ! ELEMENT IN CONTACT
SET,1,30                        ! TIME WHEN CONTACT IS INITIATED
ETABLE,PRES3,CONT,PRES          ! NORMAL CONTACT PRESSURE AT SUBSTEP30
ETABLE,SFRIC3,CONT,SFRIC        ! FRICTIONAL CONTACT PRESSURE AT SUBSTEP30
*GET,NPRES3,ETAB,1,ELEM,30      ! NORMAL CONTACT PRESSURE AT SUBSTEP30-COMPARE WITH STEP 3 IN REFERENCE
*GET,TPRES3,ETAB,2,ELEM,30      ! FRICTIONAL CONTACT PRESSURE AT SUBSTEP30-COMPARE WITH STEP 3 IN REFERENCE
SET,1,40                    
ETABLE,PRES4,CONT,PRES          ! NORMAL CONTACT PRESSURE AT SUBSTEP40
ETABLE,SFRIC4,CONT,SFRIC        ! FRICTIONAL CONTACT PRESSURE AT SUBSTEP40
*GET,NPRES4,ETAB,3,ELEM,30      ! NORMAL CONTACT PRESSURE AT SUBSTEP40-COMPARE WITH STEP 4 IN REFERENCE
*GET,TPRES4,ETAB,4,ELEM,30      ! FRICTIONAL CONTACT PRESSURE AT SUBSTEP40-COMPARE WITH STEP 4 IN REFERENCE
SET,1,50                     
ETABLE,PRES5,CONT,PRES          ! NORMAL CONTACT PRESSURE AT SUBSTEP50
ETABLE,SFRIC5,CONT,SFRIC        ! FRICTIONAL CONTACT PRESSURE AT SUBSTEP50
*GET,NPRES5,ETAB,5,ELEM,30      ! NORMAL CONTACT PRESSURE AT SUBSTEP50-COMPARE WITH STEP 5 IN REFERENCE
*GET,TPRES5,ETAB,6,ELEM,30      ! FRICTIONAL CONTACT PRESSURE AT SUBSTEP50-COMPARE WITH STEP 5 IN REFERENCE                
SET,1,60                        ! LAST SUBSTEP
ETABLE,PRES6,CONT,PRES          ! NORMAL CONTACT PRESSURE AT SUBSTEP60 (LAST)
ETABLE,SFRIC6,CONT,SFRIC        ! FRICTIONAL CONTACT PRESSURE AT SUBSTEP60(LAST)
*GET,NPRES6,ETAB,7,ELEM,30      ! NORMAL CONTACT PRESSURE AT SUBSTEP60-COMPARE WITH STEP 6 IN REFERENCE
*GET,TPRES6,ETAB,8,ELEM,30      ! FRICTIONAL CONTACT PRESSURE AT SUBSTEP60-COMPARE WITH STEP 6 IN REFERENCE
ESEL,ALL
/OUT,
*DIM,LABEL2,CHAR,4,1
*DIM,VALUE2,,4,3
LABEL2(1,1) = 'NPRES3  ','NPRES4 ','NPRES5 ','NPRES6 '
*VFILL,VALUE2(1,1),DATA,17.0*T_RATIO,33.8*T_RATIO,50.4*T_RATIO,67.0*T_RATIO
*VFILL,VALUE2(1,2),DATA,NPRES3,NPRES4,NPRES5,NPRES6
*VFILL,VALUE2(1,3),DATA,(NPRES3/97.14),(NPRES4/193),(NPRES5/288),(NPRES6/383)
*DIM,LABEL1,CHAR,4,1
*DIM,VALUE1,,4,3
LABEL1(1,1) = 'TPRES3  ','TPRES4 ','TPRES5 ','TPRES6 '
*VFILL,VALUE1(1,1),DATA,1.7*T_RATIO,3.38*T_RATIO,5.04*T_RATIO,6.7*T_RATIO
*VFILL,VALUE1(1,2),DATA,TPRES3,TPRES4,TPRES5,TPRES6
*VFILL,VALUE1(1,3),DATA,(TPRES3/9.714),(TPRES4/19.3),(TPRES5/28.8),(TPRES6/38.3)
FINISH
/COM --------------------------------------------------------------------------
/OUT,vm266,vrt,,APPEND
/COM,
/COM,
/COM,TRACTION-BASED CONTACT: K(3)=3
/COM,NORMAL-CONTACT PRESSURE |  TARGET   |   Mechanical APDL   |   RATIO
/COM,
*VWRITE,LABEL2(1),VALUE2(1,1),VALUE2(1,2),VALUE2(1,3)
(1X,A20,'   ',F10.4,'  ',F14.4,'    ',1F15.3)
/COM,
/COM,
/COM,TANG-CONTACT PRESSURE   |  TARGET   |   Mechanical APDL   |   RATIO
/COM,
*VWRITE,LABEL1(1),VALUE1(1,1),VALUE1(1,2),VALUE1(1,3)
(1X,A20,'   ',F10.4,'  ',F14.4,'    ',1F15.3)
/COM,
/COM, --------------------------------------------------------
/OUT
FINISH
*LIST,vm266,vrt

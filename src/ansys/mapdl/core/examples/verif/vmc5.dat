/COM,ANSYS MEDIA REL. 150 (11/8/2013) REF. VERIF. MANUAL: REL. 150
/VERIFY,VMC5
/SHOW
/DEVICE,VECTOR,ON
  *DIM,NARAY,TABLE,2,10        ! 2-D NARAY FOR RESULTS INFO
  *DO,I,1,2
    *DO,J,1,10
      NARAY(I,J)=0.             ! INITIALIZE NARAY
    *ENDDO
  *ENDDO
  
*CREATE,base,
  PARSAV,ALL
  /clear, nostart
  JPGPRF,500,100,1             ! MACRO TO SET PREFS FOR JPEG PLOTS
  /SHOW,JPEG
  PARRES,CHANGE
  /PREP7
smrt,off
  /TITLE, VMC5, SIMPLY-SUPPORTED SOLID SQUARE PLATE
/stitle,1,Reason COMPARE differences are acceptable:
/stitle,2,  Accuracy
  /COM,   SEE NAFEMS "THE STANDARD NAFEMS BENCHMARKS",
  /COM,   REV. NO. TSNB, NATIONAL ENGNG. LABORATORY, UK
  /COM,   AUG. 1989, TEST NO. FV52
  /COM,
 /OUT,SCRATCH
  ANTYPE,MODAL                  ! MODE-FREQUENCY ANALYSIS
  ET,1,ARG1                     ! ELEMENT TYPE PARAMETRICALLY
  *IF,ARG1,EQ,185,THEN
    KEYOPT,1,2,3
  *ELSE
    KEYOPT,1,6,0
  *ENDIF
  NARAY(ARG5,1)=ARG1            ! STORE ETYPE
  MODOPT,LANB,10              	! BLOCK LANCZOS EXTRACTION
  MXPAND,10                     ! EXPAND FIRST 10 MODES
  MP,EX,1,200E9                 ! DEFINE MATERIAL PROPERTIES
  MP,NUXY,1,.3
  MP,DENS,1,8000
  K,1
  K,2,10
  K,3,10,10
  K,4,,10                       ! DEFINE KEYPOINTS
  KGEN,2,1,4,1,,,1
  L,1,5
  *REPEAT,4,1,1                 ! DEFINE LINE SEGMENTS AND DIVISIONS
  LESIZE,ALL,,,ARG3
  V,1,2,3,4,5,6,7,8             ! DEFINE VOLUME
  ESIZE,,ARG2                   ! SET NUMVER OF ELEMENT DIVISIONS
  NARAY(ARG5,2)=ARG2            ! STORE N1
  NARAY(ARG5,3)=ARG3            ! STORE N2
  VMESH,1 			            ! MESH VOLUME
                          
  NSEL,S,LOC,Y,0                ! SELECT NODES FOR CONSTRAINING
  NSEL,A,LOC,Y,10
  NSEL,A,LOC,X,0
  NSEL,A,LOC,X,10
  NSEL,R,LOC,Z,0
  D,ALL,UZ,0                    ! CONSTRAIN NODES
  NSEL,ALL
  WAVES
  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  !!  SETUP AND PLOT ELEMENTS FOR DOCUMENTATION !!
  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  /VIEW,1,0.5,-0.5,0.5
  /ANG,1,-63
  /AUTO,1
  /DEVICE,VECTOR,1
  /DEVICE,DITHER,1
  /TYPE,ALL,4
  /COLOR,NUM,BLUE,1
  *IF,I,EQ,1,THEN
  *IF,ARG1,EQ,185,THEN
  *IF,ARG2,EQ,8,THEN
  *IF,ARG3,EQ,3,THEN
  /TITLE,VMC5 - BRICK MESH (N1 = 8, N2 = 3)
  EPLOT
  *ENDIF
  *ENDIF
  *ENDIF
  *ENDIF
  *IF,I,EQ,2,THEN
  *IF,ARG1,EQ,187,THEN
  *IF,ARG2,EQ,6,THEN
  *IF,ARG3,EQ,1,THEN
  /TITLE,VMC5 - TETRAHEDRAL MESH (N1 = 6, N2 = 1)
  EPLOT
  *ENDIF
  *ENDIF
  *ENDIF
  *ENDIF

  FINISH
  /SOLU
  SOLVE
  *GET,F1,MODE,1,FREQ           ! GET MODE 1 FREQUENCY
  *GET,F2,MODE,2,FREQ           ! GET MODE 2 FREQUENCY
  *GET,F3,MODE,3,FREQ           ! GET MODE 3 FREQUENCY
  *GET,F4,MODE,4,FREQ           ! GET MODE 4 FREQUENCY
  NARAY(ARG5,4)=F4/45.897       ! CALCULATE NORMALIZED FREQUENCY
  *GET,F5,MODE,5,FREQ           ! GET MODE 5 FREQUENCY
  NARAY(ARG5,5)=F5/109.44       ! CALCULATE NORMALIZED FREQUENCY
  *GET,F6,MODE,6,FREQ           ! GET MODE 6 FREQUENCY
  NARAY(ARG5,6)=F6/109.44       ! CALCULATE NORMALIZED FREQUENCY
  *GET,F7,MODE,7,FREQ           ! GET MODE 7 FREQUENCY
  NARAY(ARG5,7)=F7/167.89       ! CALCULATE NORMALIZED FREQUENCY
  *GET,F8,MODE,8,FREQ           ! GET MODE 8 FREQUENCY
  NARAY(ARG5,8)=F8/193.59      ! CALCULATE NORMALIZED FREQUENCY
  *GET,F9,MODE,9,FREQ           ! GET MODE 9 FREQUENCY
  NARAY(ARG5,9)=F9/206.19      ! CALCULATE NORMALIZED FREQUENCY
  *GET,F10,MODE,10,FREQ         ! GET MODE 10 FREQUENCY
  NARAY(ARG5,10)=F10/206.19     ! CALCULATE NORMALIZED FREQUENCY
  PARSAV,,PARAM
  FINISH
  /POST1
  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  !!!  SET UP POST TO PRODUCE PLDISP PLOTS  !!!
  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

  /VIEW,1,,,-1
  /ANG,1
  /DEVICE,VECTOR,1
  /DEVICE,DITHER,1
  /TYPE,ALL,4
  /COLOR,NUM,BLUE,1
  *IF,I,EQ,2,THEN
  SET,1,1
  /TITLE,VMC5 - RIGID BODY - MODE 1
  PLDISP,1
  *ENDIF
  *IF,I,EQ,2,THEN
  SET,1,2
  /TITLE,VMC5 - RIGID BODY - MODE 2
  PLDISP,1
  *ENDIF
  *IF,I,EQ,2,THEN
  SET,1,3
  /TITLE,VMC5 - RIGID BODY - MODE 3
  PLDISP,1
  *ENDIF
  /VIEW,1,-0.677530527371,-0.68876415506,0.257985122023 
  /ANG,1,76.7942822618
  *IF,I,EQ,2,THEN
  SET,1,4
  /TITLE,VMC5 - OUT OF PLANE - MODE 4
  PLDISP,0
  *ENDIF
  *IF,I,EQ,2,THEN
  SET,1,5
  /TITLE,VMC5 - OUT OF PLANE - MODE 5
  PLDISP,0
  *ENDIF
  *IF,I,EQ,2,THEN
  SET,1,6
  /TITLE,VMC5 - OUT OF PLANE - MODE 6
  PLDISP,0
  *ENDIF
  *IF,I,EQ,2,THEN
  SET,1,7
  /TITLE,VMC5 - OUT OF PLANE - MODE 7
  PLDISP,0
  *ENDIF
  /VIEW,1,,,-1
  /ANG,1
  *IF,I,EQ,2,THEN
  SET,1,8
  /TITLE,VMC5 - IN PLANE - MODE 8
  PLDISP,0
  *ENDIF
  *IF,I,EQ,2,THEN
  SET,1,9
  /TITLE,VMC5 - IN PLANE - MODE 9
  PLDISP,0
  *ENDIF
  *IF,I,EQ,2,THEN
  SET,1,10
  /TITLE,VMC5 - IN PLANE - MODE 10
  PLDISP,0
  *ENDIF
  /TITLE, VMC5, SIMPLY-SUPPORTED SOLID SQUARE PLATE
  FINISH
*END
  /COM, COMMENT:  CALL MACRO TO LOOP THROUGH DIFFERENT ELEMENT TYPES
  *DO,I,1,2
    *IF,I,EQ,1,THEN
      *USE,base,185,8,3,,I,
    *ELSEIF,I,EQ,2,THEN
      *USE,base,187,6,1,,I,
    *ENDIF
  *ENDDO
  /OUT,
  !*STAT,NARAY
  /OUT,vmc5,vrt
  /COM,
  /COM,---------------------- VMC5 RESULTS LISTING ------------------------
  /COM,
  /COM,| ETYP | N1 |  N2 |  RAT4 |  RAT5 |
  /COM,
  *VWRITE,NARAY(1,1),NARAY(1,2),NARAY(1,3),NARAY(1,4),NARAY(1,5)
  (F5.0,'  ',F5.0,'  ',F5.0,'  ',F5.3,'  ',F5.3)
  /COM,
  /COM,--------------------------------------------------------------------
  /COM,
  /COM,---------------------- VMC5 RESULTS CONT.... -----------------------
  /COM,
  /COM,| RAT6 |  RAT7 |  RAT8 |  RAT9 |  RAT10 
  /COM,
  *VWRITE,NARAY(1,6),NARAY(1,7),NARAY(1,8),NARAY(1,9),NARAY(1,10)
  ('  ',F5.3,'   ',F5.3,'   ',F5.3,'   ',F5.3,'   ',F5.3,'   ')
  /COM,
  /COM,--------------------------------------------------------------------
  /OUT
FINISH
/delete,PARAM
/delete,base
*LIST,vmc5,vrt
finish



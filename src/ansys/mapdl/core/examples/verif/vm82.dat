/COM,ANSYS MEDIA REL. 150 (11/8/2013) REF. VERIF. MANUAL: REL. 150
/VERIFY,VM82
/OUT,SCRATCH
/PREP7
SMRT,OFF
/TITLE, VM82, SIMPLY SUPPORTED LAMINATED PLATE UNDER PRESSURE
C***    EXACT SOLUTIONS OF MODERATELY THICK LAMINATED SHELLS,
C***    J.N. REDDY, JNL. OF ENGR. MECHANICS, VOL 110, NO.5, MAY'84.
C*** USING SOLID185 WITH LAYERS
ANTYPE,STATIC
ET,1,185
KEYOPT,1,3,1		    ! LAYERED SOLID ELEMENTS
KEYOPT,1,2,2                ! ENHANCED STRAIN FORMULATION
KEYOPT,1,8,1		    ! STORE DATA FOR ALL LAYERS
SECTYPE,1,SHELL
SECDATA,0.025,1,0           ! LAYER 1: 0.025 THK, THETA 0 
SECDATA,0.025,1,90          ! LAYER 2: 0.025 THK, THETA 90
SECDATA,0.025,1,90          ! LAYER 3: 0.025 THK, THETA 90
SECDATA,0.025,1,0           ! LAYER 4: 0.025 THK, THETA 0
MP,EX,1,25E6                ! ORTHOTROPIC MATERIAL PROPERTIES
MP,EY,1,1E6
MP,EZ,1,1E6                 ! EZ=EY ASSUMED
MP,GXY,1,5E5
MP,GYZ,1,2E5
MP,GXZ,1,5E5
MP,PRXY,1,0.25              ! MAJOR POISSONS RATIO
MP,PRYZ,1,0.01              ! MAJOR POISSONS RATIO
MP,PRXZ,1,0.25              ! MAJOR POISSONS RATIO
K,1                         ! CORNER KEYPOINTS OF QUADRANT (VOLUME)
K,2,5
K,3,5,5
K,4,,5
KGEN,2,1,4,1,,,0.1
L,1,5
*REPEAT,4,1,1
LESIZE,ALL,,,1
V,1,2,3,4,5,6,7,8          
ESIZE,,6                    ! 6X6 MESH USING QUARTER SYMMETRY
VMESH,1
NSEL,S,LOC,X,0
DSYM,SYMM,X
NSEL,S,LOC,Y,0
DSYM,SYMM,Y
NSEL,S,LOC,X,5              ! FREELY SUPPORTED B.C.
D,ALL,UZ,,,,,UY
NSEL,S,LOC,Y,5
D,ALL,UZ,,,,,UX
NSEL,ALL
SFE,ALL,6,PRES,,1           ! APPLY UNIFORM PRESSURE ON TOP SURFACE
OUTPR,,1
FINISH
/OUT,SCRATCH
/SOLU    
SOLVE
FINISH
/OUT
/POST1
NSEL,S,LOC,X                ! SELECT CENTER NODES
NSEL,R,LOC,Y
PRNSOL,U,Z                  ! PRINT CENTER DEFLECTION
*GET,DEF46,NODE,1,U,Z
*DIM,LABEL,CHAR,1,2
*DIM,VALUE,,1,3
LABEL(1,1) = 'DEF,m (S'
LABEL(1,2) = 'OLID185) '
*VFILL,VALUE(1,1),DATA,.0683
*VFILL,VALUE(1,2),DATA,ABS(DEF46)
*VFILL,VALUE(1,3),DATA,ABS(DEF46/.0683)
SAVE,INF1
FINISH
/CLEAR,NOSTART
/OUT,SCRATCH
/PREP7
SMRT,OFF
/TITLE, VM82, SIMPLY SUPPORTED LAMINATED PLATE UNDER PRESSURE
C*** USING SOLSH190
ANTYPE,STATIC
ET,1,SOLSH190               ! 8 NODE LAYERED SOLID-SHELL
KEYOPT,1,8,1                ! WRITE LAYER RESULTS
SECTYPE,1,SHELL
SECDATA,0.025,1,0           ! LAYER 1: 0.025 THK, THETA 0 
SECDATA,0.025,1,90          ! LAYER 2: 0.025 THK, THETA 90
SECDATA,0.025,1,90          ! LAYER 3: 0.025 THK, THETA 90
SECDATA,0.025,1,0           ! LAYER 4: 0.025 THK, THETA 0
MP,EX,1,25E6                ! ORTHOTROPIC MATERIAL PROPERTIES
MP,EY,1,1E6
MP,EZ,1,1E6                 ! EZ=EY ASSUMED
MP,GXY,1,5E5
MP,GYZ,1,2E5
MP,GXZ,1,5E5
MP,PRXY,1,0.25              ! MAJOR POISSONS RATIO
MP,PRYZ,1,0.01              ! MAJOR POISSONS RATIO
MP,PRXZ,1,0.25              ! MAJOR POISSONS RATIO
K,1                         ! CORNER KEYPOINTS OF QUADRANT (VOLUME)
K,2,5
K,3,5,5
K,4,,5
KGEN,2,1,4,1,,,0.1
L,1,5
*REPEAT,4,1,1
LESIZE,ALL,,,1
V,1,2,3,4,5,6,7,8          
VEORIENT,1,THIN
ESIZE,,6                    ! 6X6 MESH USING QUARTER SYMMETRY
VMESH,1
NSEL,S,LOC,X,0
DSYM,SYMM,X
NSEL,S,LOC,Y,0
DSYM,SYMM,Y
NSEL,S,LOC,X,5              ! FREELY SUPPORTED B.C.
D,ALL,UZ,,,,,UY
NSEL,S,LOC,Y,5
D,ALL,UZ,,,,,UX
NSEL,ALL
SFE,ALL,6,PRES,,1           ! APPLY UNIFORM PRESSURE ON TOP SURFACE
OUTPR,NSOL,1
OUTPR,RSOL,1
FINISH
/OUT,SCRATCH
/SOLU    
SOLVE
FINISH
/OUT
/POST1
NSEL,S,LOC,X                ! SELECT CENTER NODES
NSEL,R,LOC,Y
PRNSOL,U,Z                  ! PRINT CENTER DEFLECTION
*GET,DEF46,NODE,1,U,Z
*DIM,LABEL,CHAR,1,2
*DIM,VALUE,,1,3
LABEL(1,1) = 'DEF,m (S'
LABEL(1,2) = 'OLID190) '
*VFILL,VALUE(1,1),DATA,.0683
*VFILL,VALUE(1,2),DATA,ABS(DEF46)
*VFILL,VALUE(1,3),DATA,ABS(DEF46/.0683)
SAVE,INF2
FINISH
/CLEAR,NOSTART
/OUT,SCRATCH
/PREP7
SMRT,OFF
/TITLE, VM82, SIMPLY SUPPORTED LAMINATED PLATE UNDER PRESSURE
C*** USING SOLID186
ANTYPE,STATIC
ET,1,SOLID186               ! 20-NODE SOLID ELEMENT
KEYOPT,1,3,1                ! LAYERED SOLID ELEMENT   
KEYOPT,1,8,1                ! WRITE LAYER RESULTS
SECDATA,0.025,1,0           ! LAYER 1: 0.025 THK, THETA 0 
SECDATA,0.025,1,90          ! LAYER 2: 0.025 THK, THETA 90
SECDATA,0.025,1,90          ! LAYER 3: 0.025 THK, THETA 90
SECDATA,0.025,1,0           ! LAYER 4: 0.025 THK, THETA 0
MP,EX,1,25E6                ! ORTHOTROPIC MATERIAL PROPERTIES
MP,EY,1,1E6
MP,EZ,1,1E6                 ! EZ=EY ASSUMED
MP,GXY,1,5E5
MP,GYZ,1,2E5
MP,GXZ,1,5E5
MP,PRXY,1,0.25              ! MAJOR POISSONS RATIO
MP,PRYZ,1,0.01              ! MAJOR POISSONS RATIO
MP,PRXZ,1,0.25              ! MAJOR POISSONS RATIO
K,1                         ! CORNER KEYPOINTS OF QUADRANT (VOLUME)
K,2,5
K,3,5,5
K,4,,5
KGEN,2,1,4,1,,,0.1
L,1,5
*REPEAT,4,1,1
LESIZE,ALL,,,1
V,1,2,3,4,5,6,7,8          
ESIZE,,6                    ! 6X6 MESH USING QUARTER SYMMETRY
VMESH,1
NSEL,S,LOC,X,0
DSYM,SYMM,X
NSEL,S,LOC,Y,0
DSYM,SYMM,Y
NSEL,S,LOC,X,5              ! FREELY SUPPORTED B.C.
D,ALL,UZ,,,,,UY
NSEL,S,LOC,Y,5
D,ALL,UZ,,,,,UX
NSEL,ALL
SFE,ALL,6,PRES,,1           ! APPLY UNIFORM PRESSURE ON TOP SURFACE
OUTPR,NSOL,1
OUTPR,RSOL,1
FINISH
/SOLU    
SOLVE
FINISH
/OUT
/POST1
NSEL,S,LOC,X                ! SELECT CENTER NODES
NSEL,R,LOC,Y
PRNSOL,U,Z                  ! PRINT CENTER DEFLECTION
*GET,DEF191,NODE,1,U,Z
*DIM,LABEL,CHAR,1,2
*DIM,VALUE,,1,3
LABEL(1,1) = 'DEF,m (S'
LABEL(1,2) = 'OLID186) '
*VFILL,VALUE(1,1),DATA,.0683
*VFILL,VALUE(1,2),DATA,ABS(DEF191)
*VFILL,VALUE(1,3),DATA,ABS(DEF191/.0683)
SAVE,INF3
FINISH
/CLEAR,NOSTART
/OUT,SCRATCH
/PREP7
SMRT,OFF
/TITLE, VM82, SIMPLY SUPPORTED LAMINATED PLATE UNDER PRESSURE
C*** USING SHELL181
/OUT,SCRATCH
/PREP7
SMRT,OFF
ANTYPE,STATIC
ET,1,SHELL181               ! 4 NODE LAYERED SHELL
KEYOPT,1,3,2                ! FULL INTEGRATION
KEYOPT,1,8,1                ! WRITE LAYER RESULTS
SECTYPE,1,SHELL
SECDATA,0.025,1,0           ! LAYER 1: 0.025 THK, THETA 0 
SECDATA,0.025,1,90          ! LAYER 2: 0.025 THK, THETA 90
SECDATA,0.025,1,90          ! LAYER 3: 0.025 THK, THETA 90
SECDATA,0.025,1,0           ! LAYER 4: 0.025 THK, THETA 0
MP,EX,1,25E6                ! ORTHOTROPIC MATERIAL PROPERTIES
MP,EY,1,1E6
MP,EZ,1,1E6                 ! EZ=EY ASSUMED
MP,GXY,1,5E5
MP,GYZ,1,2E5
MP,GXZ,1,5E5
MP,PRXY,1,0.25              ! MAJOR POISSONS RATIO
MP,PRYZ,1,0.01              ! MAJOR POISSONS RATIO
MP,PRXZ,1,0.25              ! MAJOR POISSONS RATIO
K,1                         ! CORNER KEYPOINTS OF QUADRANT (AREA)
K,2,5
K,3,5,5
K,4,,5
A,1,2,3,4
ESIZE,,6                    ! 6X6 MESH USING QUARTER SYMMETRY
AMESH,1
NSEL,S,LOC,X,0
DSYM,SYMM,X
NSEL,S,LOC,Y,0
DSYM,SYMM,Y
NSEL,S,LOC,X,5              ! APPLY FREELY SUPPORTED B.C.
D,ALL,UZ,,,,,UY
NSEL,S,LOC,Y,5
D,ALL,UZ,,,,,UX
NSEL,ALL
SFE,ALL,2,PRES,,1           ! APPLY UNIFORM PRESSURE 
OUTPR,NSOL,1
OUTPR,RSOL,1
FINISH
/SOLU    
SOLVE
FINISH
/OUT
/POST1
NSEL,S,LOC,X                ! SELECT CENTER NODE 
NSEL,R,LOC,Y
PRNSOL,U,Z                  ! PRINT CENTER DEFLECTION
*GET,DEF99,NODE,1,U,Z
*DIM,LABEL,CHAR,1,2
*DIM,VALUE,,1,3
LABEL(1,1) = 'DEF,m (S'
LABEL(1,2) = 'HELL181)'
*VFILL,VALUE(1,1),DATA,.0683 
*VFILL,VALUE(1,2),DATA,ABS(DEF99)
*VFILL,VALUE(1,3),DATA,ABS(DEF99/.0683)
SAVE,INF4
FINISH
FINISH
/CLEAR, NOSTART 
/PREP7
SMRT,OFF
/TITLE, VM82, SIMPLY SUPPORTED LAMINATED PLATE UNDER PRESSURE
C*** USING SHELL281
/OUT,SCRATCH
/PREP7
SMRT,OFF
ANTYPE,STATIC
ET,1,SHELL281               ! 8 NODE LAYERED SHELL
KEYOPT,1,8,1                ! WRITE LAYER RESULTS
SECTYPE,1,SHELL
SECDATA,0.025,1,0           ! LAYER 1: 0.025 THK, THETA 0 
SECDATA,0.025,1,90          ! LAYER 2: 0.025 THK, THETA 90
SECDATA,0.025,1,90          ! LAYER 3: 0.025 THK, THETA 90
SECDATA,0.025,1,0           ! LAYER 4: 0.025 THK, THETA 0
MP,EX,1,25E6                ! ORTHOTROPIC MATERIAL PROPERTIES
MP,EY,1,1E6
MP,EZ,1,1E6                 ! EZ=EY ASSUMED
MP,GXY,1,5E5
MP,GYZ,1,2E5
MP,GXZ,1,5E5
MP,PRXY,1,0.25              ! MAJOR POISSONS RATIO
MP,PRYZ,1,0.01              ! MAJOR POISSONS RATIO
MP,PRXZ,1,0.25              ! MAJOR POISSONS RATIO
K,1                         ! CORNER KEYPOINTS OF QUADRANT (AREA)
K,2,5
K,3,5,5
K,4,,5
A,1,2,3,4
ESIZE,,6                    ! 6X6 MESH USING QUARTER SYMMETRY
AMESH,1
NSEL,S,LOC,X,0
DSYM,SYMM,X
NSEL,S,LOC,Y,0
DSYM,SYMM,Y
NSEL,S,LOC,X,5              ! APPLY FREELY SUPPORTED B.C.
D,ALL,UZ,,,,,UY
NSEL,S,LOC,Y,5
D,ALL,UZ,,,,,UX
NSEL,ALL
SFE,ALL,2,PRES,,1           ! APPLY UNIFORM PRESSURE 
OUTPR,NSOL,1
OUTPR,RSOL,1
FINISH
/SOLU    
SOLVE
FINISH
/OUT
/POST1
NSEL,S,LOC,X                ! SELECT CENTER NODE 
NSEL,R,LOC,Y
PRNSOL,U,Z                  ! PRINT CENTER DEFLECTION
*GET,DEF99,NODE,1,U,Z
*DIM,LABEL,CHAR,1,2
*DIM,VALUE,,1,3
LABEL(1,1) = 'DEF,m (S'
LABEL(1,2) = 'HELL281)'
*VFILL,VALUE(1,1),DATA,.0683 
*VFILL,VALUE(1,2),DATA,ABS(DEF99)
*VFILL,VALUE(1,3),DATA,ABS(DEF99/.0683)
SAVE,INF5
RESUME,INF1
/COM
/OUT,vm82,vrt
/COM,------------------- VM82 RESULTS COMPARISON --------------
/COM,
/COM,                 |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.4,'  ',F14.4,'   ',1F15.3)
/NOPR
RESUME,INF2
/GOPR
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.4,'  ',F14.4,'   ',1F15.3)
/NOPR
RESUME,INF3
/GOPR
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.4,'  ',F14.4,'   ',1F15.3)
/NOPR
RESUME,INF4
/GOPR
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.4,'  ',F14.4,'   ',1F15.3)
/NOPR
RESUME,INF5
/GOPR
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.4,'  ',F14.4,'   ',1F15.3)
/COM,----------------------------------------------------------
/OUT
FINISH
*LIST,vm82,vrt
/OUT,SCRATCH
/DELETE,INF1
/DELETE,INF2
/DELETE,INF3
/DELETE,INF4
/DELETE,INF5

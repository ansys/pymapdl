/COM,ANSYS MEDIA REL. 2023R1 (11/04/2022) REF. VERIF. MANUAL: REL. 2023R1
/VERIFY,VM86
/PREP7
/TITLE, VM86, HARMONIC RESPONSE OF A DYNAMIC SYSTEM
C*** VIBRATION THEORY AND APPLICATIONS, THOMSON, 2ND PRINTING, PAGE 56, EX 3.1-2
C***                BY VISCOUS DAMPING APPROACH
ANTYPE,HARMIC       ! HARMONIC RESPONSE ANALYSIS
HROPT,FULL
HROUT,OFF           ! PRINT COMPLEX DISP. AS AMPLITUDES AND PHASE ANGLES
ET,1,COMBIN40,,,3,,,2
R,1,200,6,.5        ! SPRING STIFFNESS = 200, C = 6, M = .5
N,1
N,2
E,1,2
OUTPR,BASIC,1
HARFRQ,,3.1831      ! HARMONIC FREQUENCY RANGE
D,1,UZ
F,2,FZ,10
FINISH
/SOLU    
SOLVE
FINISH
/POST26
FILE,,rst
NSOL,2,2,U,Z,2UX
PRVAR,2
*GET,A,VARI,2,ITIME,3.1831
*DIM,LABEL,CHAR,1,2
*DIM,VALUE,,1,3
LABEL(1,1) = 'AMPLITUD'
LABEL(1,2) = 'E, in   '
*VFILL,VALUE(1,1),DATA,.0833
*VFILL,VALUE(1,2),DATA,ABS(A)
*VFILL,VALUE(1,3),DATA,ABS(A/.0833)
/COM
/OUT,vm86,vrt
/COM,------------------- VM86 RESULTS COMPARISON --------------
/COM,
/COM,                 |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.4,'  ',F14.4,'   ',1F15.3)
/COM,-----------------------------------------------------------
/COM,
/COM,-----------------------------------------------------------
/COM,NOTE: THERE ARE VERIFIED RESULTS IN VM86  NOT CONTAINED IN
/COM,THIS TABLE
/COM,-----------------------------------------------------------
/OUT
FINISH
*LIST,vm86,vrt

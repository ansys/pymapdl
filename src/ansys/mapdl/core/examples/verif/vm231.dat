/COM,ANSYS MEDIA REL. 150 (11/8/2013) REF. VERIF. MANUAL: REL. 150
/VERIFY,vm231
JPGPRF,500,100,1
/SHOW,JPEG
/TITLE, VM231, PIEZOCERAMIC RECTANGLE UNDER PURE BENDING LOAD
/COM,   REF: PARTON,V.Z., KUDRYAVTSEV, B.A. AND SENIK,N.A. (1989)
/COM,   "MECHANICS OF PIEZOELECTRIC MATERIALS" IN "APPLIED MECHANICS:
/COM,   SOVIET REVIEW.", Vol.2: ELECTROMAGNETOELASTICITY,
/COM,   G.K.MIKHAILOV AND V.Z.PARTON (EDS.), HEMISPHERE PUBL. CORP., P.28
/NOPR
/COM,
/COM,       GEOMETRY DATA
/COM,
L=1.E-3                         ! PLATE LENGTH,m
H=0.5E-3                        ! PLATE THICKNESS,m
/COM,
/COM,       LOAD DATA
/COM,
SIG1=-20E9                      ! PRESSURE SLOPE, N/m**3

/PREP7
/COM,
/COM,       MATERTIAL PROPERTIES FOR THE FINITE ELEMENT SOLUTION:
/COM,       CONSTITUTIVE MATRICES FOR PZT-4 (POLAR AXIS ALONG Y)
/COM,
/COM,   [c11 c13 c12  0  0  0 ]        [ 0 e31  0 ]      [ep11  0    0  ]
/COM,   [c13 c33 c13  0  0  0 ]        [ 0 e33  0 ]      [  0  ep33  0  ]
/COM,   [c12 c13 c11  0  0  0 ]        [ 0 e31  0 ]      [  0   0   ep11]
/COM,   [ 0   0   0  c44 0  0 ]        [e15 0   0 ]
/COM,   [ 0   0   0   0 c44 0 ]        [ 0  0  e15]
/COM,   [ 0   0   0   0  0 c66]        [ 0  0   0 ]
/COM,
MP,PERX,1,728.5                 ! PERMITTIVITY AT CONSTANT STRAIN
MP,PERY,1,634.7
MP,PERZ,1,728.5 
TB,ANEL,1                       ! ANISOTROPIC ELASTIC STIFFNESS
TBDA,1,13.9E10,7.43E10,7.78E10  ! c11,c13,c12
TBDA,7,11.5E10,7.43E10          ! c33,c13
TBDA,12,13.9E10                 ! c11
TBDA,16,2.56E10                 ! c44
TBDA,19,2.56E10                 ! c44
TBDA,21,3.06E10                 ! c66
TB,PIEZ,1                       ! PIEZOELECTRIC STRESS COEFFICIENTS
TBDA,2,-5.2                     ! e31
TBDA,5,15.1                     ! e33
TBDA,8,-5.2                     ! e31
TBDA,10,12.7                    ! e15
TBDA,15,12.7                    ! e15
/COM,
/COM,      FINITE ELEMENT MODEL
/COM,
ANTYPE,STATIC
ET,1,PLANE13,7,0,2,0                 ! PLANE13 (UX,UY,VOLT) PLANE STRESS
N,1,0,0
N,2,L,0
N,3,L,H
N,4,0,H
E,1,2,3,4
NSEL,S,LOC,X,0                  ! DEFINE STRUCTURAL B.C.
DSYM,SYMM,X
NSEL,R,LOC,Y,0
D,ALL,UY,0
D,ALL,VOLT,0
NSEL,S,LOC,Y,0
DSYM,ASYMM,Y
NSEL,ALL
SFGRAD,PRES,0,Y,0,-SIG1         ! SPECIFY PRESSURE LOAD GRADIENT
NSEL,S,LOC,X,L
SF,ALL,PRES,0                   ! APPLY PRESSURE LOAD
NSEL,ALL
FINISH
/SOLVE
OUTPR,,LAST
/OUT,SCRATCH
SOLVE
FINISH
/POST1
/OUT,
PRNSOL,S,COMP
PRNSOL,EPEL,COMP
PRNSOL,EF,COMP
PRNSOL,D,COMP
!
!       MATERTIAL PROPERTIES FOR THE ANALYTICAL SOLUTION
!
S11=12.3093E-12                 ! ELASTIC COMPLIANCE COEFFICIENTS
S13=-5.34878E-12
D31=-1.23816E-10                ! PIEZOELECTRIC STRAIN COEFFICIENTS
D33= 2.91296E-10
EP33=11.3063E-9                 ! PERMITTIVITY COEFFICIENT AT CONSTANT STRESS
K31=D31*D31/(S11*EP33)          ! ELECTROMECHANICAL COEFFICIENTS
KS=D33*D31/(S13*EP33)
!
!      ANALYTICAL SOLUTION AT NODE 3
!
UX3=S11*(1-K31)*SIG1*NX(3)*NY(3)
UY3=S13*(1-KS)*SIG1*NY(3)**2/2
UY3=UY3-S11*(1-K31)*SIG1*NX(3)**2/2
VOLT3=D31*SIG1*NY(3)**2/(2*EP33)
SX3=SIG1*NY(3)
EFY3=-D31*SIG1*NY(3)/EP33
!
!      RESULT OUTPUT
!
*GET,SX,NODE,3,S,X
*GET,EFY,NODE,3,EF,Y
*DIM,LABEL,CHAR,5,3
*DIM,VALUE,ARRAY,5,3
LABEL(1,1)='UX, ','UY, ','VOLT, ','SX, ','EFZ, '
LABEL(1,2)='(um)  ','(um)  ','(V) ','(N/mm^2)','(V/mm)'
*VFILL,VALUE(1,1),DATA,UX3*1E6,UY3*1E6,VOLT3,SX3*1E-6,EFY3*1E-3
*VFILL,VALUE(1,2),DATA,UX(3)*1E6,UY(3)*1E6,VOLT(3),SX*1E-6,EFY*1E-3
*VFILL,VALUE(1,3),DATA,ABS(UX(3)/UX3),ABS(UY(3)/UY3),ABS(VOLT(3)/VOLT3)
*VFILL,VALUE(4,3),DATA,ABS(SX/SX3),ABS(EFY/EFY3)
SAVE,table_1
FINISH
/CLEAR,NOSTART

/COM,
/COM,  SOLVING THE PROBLEM USING PLANE223 ELEMENT
/COM,

/NOPR
JPGPRF,500,100,1
/SHOW,JPEG
/COM,
/COM,       GEOMETRY DATA
/COM,
L=1.E-3                         ! PLATE LENGTH,m
H=0.5E-3                        ! PLATE THICKNESS,m
/COM,
/COM,       LOAD DATA
/COM,
SIG1=-20E9                      ! PRESSURE SLOPE, N/m**3

/PREP7
/COM,
/COM,       MATERTIAL PROPERTIES FOR THE FINITE ELEMENT SOLUTION:
/COM,       CONSTITUTIVE MATRICES FOR PZT-4 (POLAR AXIS ALONG Y)
/COM,
/COM,   [c11 c13 c12  0  0  0 ]        [ 0 e31  0 ]      [ep11  0    0  ]
/COM,   [c13 c33 c13  0  0  0 ]        [ 0 e33  0 ]      [  0  ep33  0  ]
/COM,   [c12 c13 c11  0  0  0 ]        [ 0 e31  0 ]      [  0   0   ep11]
/COM,   [ 0   0   0  c44 0  0 ]        [e15 0   0 ]
/COM,   [ 0   0   0   0 c44 0 ]        [ 0  0  e15]
/COM,   [ 0   0   0   0  0 c66]        [ 0  0   0 ]
/COM,
MP,PERX,1,728.5                 ! PERMITTIVITY AT CONSTANT STRAIN
MP,PERY,1,634.7
MP,PERZ,1,728.5 
TB,ANEL,1                       ! ANISOTROPIC ELASTIC STIFFNESS
TBDA,1,13.9E10,7.43E10,7.78E10  ! c11,c13,c12
TBDA,7,11.5E10,7.43E10          ! c33,c13
TBDA,12,13.9E10                 ! c11
TBDA,16,2.56E10                 ! c44
TBDA,19,2.56E10                 ! c44
TBDA,21,3.06E10                 ! c66
TB,PIEZ,1                       ! PIEZOELECTRIC STRESS COEFFICIENTS
TBDA,2,-5.2                     ! e31
TBDA,5,15.1                     ! e33
TBDA,8,-5.2                     ! e31
TBDA,10,12.7                    ! e15
TBDA,15,12.7                    ! e15
/COM,
/COM,      FINITE ELEMENT MODEL
/COM,
ANTYPE,STATIC
ET,1,PLANE223,1001,,0                ! PLANE223 (UX,UY,VOLT) PLANE STRESS
N,1,0,0
N,2,L,0
N,3,L,H
N,4,0,H
E,1,2,3,4
EMID				! MIDSIDE NODES ON
NSEL,S,LOC,X,0                  ! DEFINE STRUCTURAL B.C.
DSYM,SYMM,X
NSEL,R,LOC,Y,0
D,ALL,UY,0
D,ALL,VOLT,0
NSEL,S,LOC,Y,0
DSYM,ASYMM,Y
NSEL,ALL
SFGRAD,PRES,0,Y,0,-SIG1         ! SPECIFY PRESSURE LOAD GRADIENT
NSEL,S,LOC,X,L
SF,ALL,PRES,0                   ! APPLY PRESSURE LOAD
NSEL,ALL
FINISH
/SOLVE
OUTPR,,LAST
/OUT,SCRATCH
SOLVE
FINISH
/POST1
/OUT,
PRNSOL,S,COMP
PRNSOL,EPEL,COMP
PRNSOL,EF,COMP
PRNSOL,D,COMP
!
!       MATERTIAL PROPERTIES FOR THE ANALYTICAL SOLUTION
!
S11=12.3093E-12                 ! ELASTIC COMPLIANCE COEFFICIENTS
S13=-5.34878E-12
D31=-1.23816E-10                ! PIEZOELECTRIC STRAIN COEFFICIENTS
D33= 2.91296E-10
EP33=11.3063E-9                 ! PERMITTIVITY COEFFICIENT AT CONSTANT STRESS
K31=D31*D31/(S11*EP33)          ! ELECTROMECHANICAL COEFFICIENTS
KS=D33*D31/(S13*EP33)
!
!      ANALYTICAL SOLUTION AT NODE 3
!
UX3=S11*(1-K31)*SIG1*NX(3)*NY(3)
UY3=S13*(1-KS)*SIG1*NY(3)**2/2
UY3=UY3-S11*(1-K31)*SIG1*NX(3)**2/2
VOLT3=D31*SIG1*NY(3)**2/(2*EP33)
SX3=SIG1*NY(3)
EFY3=-D31*SIG1*NY(3)/EP33
!
!      RESULT OUTPUT
!
*GET,SX,NODE,3,S,X
*GET,EFY,NODE,3,EF,Y
*DIM,LABEL,CHAR,5,3
*DIM,VALUE,ARRAY,5,3
LABEL(1,1)='UX, ','UY, ','VOLT, ','SX, ','EFZ, '
LABEL(1,2)='(um)  ','(um)  ','(V) ','(N/mm^2)','(V/mm)'
*VFILL,VALUE(1,1),DATA,UX3*1E6,UY3*1E6,VOLT3,SX3*1E-6,EFY3*1E-3
*VFILL,VALUE(1,2),DATA,UX(3)*1E6,UY(3)*1E6,VOLT(3),SX*1E-6,EFY*1E-3
*VFILL,VALUE(1,3),DATA,ABS(UX(3)/UX3),ABS(UY(3)/UY3),ABS(VOLT(3)/VOLT3)
*VFILL,VALUE(4,3),DATA,ABS(SX/SX3),ABS(EFY/EFY3)
SAVE,table_2
FINISH
RESUME,table_1
/COM
/out,vm231,vrt
/COM
/COM,---------------------- VM231 RESULTS COMPARISON ------------------
/COM,
/COM,     NODE 3       |    TARGET    |    Mechanical APDL    |   RATIO
/COM,
/COM,
/COM,  USING PLANE13 ELEMENTS
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1x,A8,A8,'     ',F9.3,'     ',F14.3,'     ',F17.3)
/COM,
/COM,
/NOPR,
RESUME,table_2
/GOPR
/COM,
/COM, USING PLANE223 ELEMENTS
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1x,A8,A8,'     ',F9.3,'     ',F14.3,'     ',F17.3)
/COM,
/COM,---------------------------------------------------------------------
/out,
*list,vm231,vrt
FINISH
/delete,table_1
/delete,table_2

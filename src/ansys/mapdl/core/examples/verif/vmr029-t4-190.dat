! Copyright (C) 2024 ANSYS, Inc. and/or its affiliates.
! SPDX-License-Identifier: MIT
! 
!
! Permission is hereby granted, free of charge, to any person obtaining a copy
! of this software and associated documentation files (the "Software"), to deal
! in the Software without restriction, including without limitation the rights
! to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
! copies of the Software, and to permit persons to whom the Software is
! furnished to do so, subject to the following conditions:
!
! The above copyright notice and this permission notice shall be included in all
! copies or substantial portions of the Software.
!
! THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
! IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
! FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
! AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
! LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
! OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
! SOFTWARE.

/COM,ANSYS MEDIA REL. 2023R2 (05/12/2023) REF. VERIF. MANUAL: REL. 2023R2
/VERIFY,VMR029-T4-190
/TITLE,VMR029-T4-190,LATERAL-TORSIONAL BUCKLING OF AN ELASTIC CANTILEVER SUBJECTED TO TRANSVERSE END LOAD
/COM,REFERENCE NAFEMS 3D BEAMS AND SHELLS MANUAL TEST 3DNLG-4
/SHOW,JPEG
JPGPRF,500,100,1

/FILENAME,vmr029-t4-190

_GEOMGEN = 0                        ! SET THIS KEY=1 TO REGENERATE THE GEOMETRY
_EXIT = 0                           ! SET THIS KEY=1 TO EXIT WITHOUT SOLVING
                        
*IF,_GEOMGEN,EQ,1,THEN

/PREP7
ET,1,SOLSH190                       ! STRUCTURAL SOLID SHELL

MP,EX,1,1.0E4
MP,NUXY,1,0.0

K,1,
K,2,100
K,3,100,0.2
K,4,,0.2

L,1,2
L,2,3
L,3,4
L,4,1
AL,1,2,3,4

VOFFSET,1,-5

LESIZE,1,,,40
LESIZE,2,,,2
LESIZE,11,,,4
MSHKEY,1

TYPE,1
MAT,1
VMESH,1

NSEL,S,LOC,X,0
D,ALL,ALL,
ALLSEL,ALL

F,374,FZ,1

CDWRITE,db,vmr029-t4-190,cdb
FINISH

*IF,_EXIT,EQ,1,THEN
/EXIT,NOSAVE
*ENDIF

*ELSE

/PREP7
CDREAD,db,vmr029-t4-190,cdb
FINISH


/OUT,SCRATCH
/SOLU
ANTYPE,STATIC
RESCONTROL,LINEAR,ALL,1             ! RESTART FILES FOR SUBSEQUENT LINEAR PERTURBATION 
EQSLV,SPARSE
SOLVE
FINISH


/SOLU
ANTYPE,STATIC,RESTART,,,PERTURB ! PERFORM A PERTURBATION ANALYSIS
PERTURB,BUCKLE,,,ALLKEEP        ! PERTURBED BUCKLING SOLVE 
SOLVE,ELFORM  

OUTRES,ALL,ALL
BUCOPT,LANB,4,
MXPAND,4,,,NO
SOLVE
FINISH

/PREP7
UPGEOM,0.001/4,1,1,vmr029-t4-190,rstp
FINISH

/SOLU
ANTYPE,STATIC
OUTRES,ALL,ALL
NLGEOM,ON
ARCLEN,ON,2,0.0001
ARCTRM,U,1.0,374,UY
NSUBST,1000
SOLVE
FINISH

/POST26
/SHOW,,jpeg
NSOL,2,374,U,Y,TIPDISP
ADD,3,1,,,APLOAD,,,1,
/XRANGE,0,0.06
/YRANGE,0,0.020
/GROPT,DIVX,6
/AXLAB,X,TIP LATERAL DISP
/AXLAB,Y,LOAD
/COLOR,CURVE,YGRE
XVAR,2
PLVAR,3
PRVAR,2,3

/POST1
/OUT,
SET,1,11
*GET,APLOAD,TIME
R=APLOAD/0.01892
*DIM,VALUE,,1,3
*DIM,LABEL,CHAR,3
LABEL(1) = 'PCR','vmr029-','t4-190'
*VFILL,VALUE(1,1),DATA,APLOAD
*VFILL,VALUE(1,2),DATA,R
/OUT,vmr029-t4-190,vrt
/COM
/COM,-------------- VMR029-T4 RESULTS COMPARISON ---------------
/COM,
/COM,         |   Mechanical APDL   |   RATIO   |      TEST      |
/COM,
/COM,SOLSH190
*VWRITE,LABEL(1), VALUE(1,1), VALUE(1,2),LABEL(2),LABEL(3)
(1X,' ',A4,'       ',F10.5,'      ',F13.4,'      ',A7,A8)
/COM,
/COM,----------------------------------------------------------------
/OUT
FINISH
*LIST,vmr029-t4-190,vrt
/OUT,SCRATCH
/EXIT,NOSAVE

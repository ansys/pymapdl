/COM,ANSYS MEDIA REL. 2023R1 (11/04/2022) REF. VERIF. MANUAL: REL. 2023R1
/VERIFY,VM25
/PREP7
SMRT,OFF
JPGPRF,500,100,1             ! MACRO TO SET PREFS FOR JPEG PLOTS
/SHOW,JPEG
/TITLE, VM25, STRESSES IN A LONG CYLINDER
C*** STR. OF MATLS., TIMOSHENKO, PART 2, 3RD ED., PAGE 213, PROB. 1
C***  INTERNAL PRESSURE 
ANTYPE,STATIC        ! STATIC ANALYSIS
ET,1,PLANE183,,,1,   ! AXISYMM
MP,EX,1,30E6         ! MATERIAL PROPERTIES
MP,DENS,1,.00073
MP,NUXY,1,0.3        ! DEFINE KEYPOINTS, LINES, AND AREAS
N,1,4 
N,2,4+4/14
N,3,4+4/14,0.5
N,4,4,0.5
N,5,4+4/28
N,6,4+4/14,0.25
N,7,4+4/28,0.5
N,8,4,0.25
E,1,2,3,4,5,6,7,8
EGEN,14,8,1,,,,,,,,4/14  
NUMMRG,NODE
EGEN,2,111,1,14,,,,,,,,0.5
NUMMRG,NODE
SAVE,MODEL           ! SAVE MODEL FOR SECOND LOAD CASE
NSEL,S,LOC,Y,0       ! SET UP LONG CYLINDER EFFECT
D,ALL,UY
NSEL,S,LOC,Y,1   
CP,1,UY,ALL          ! COUPLE AXIAL DISPLACEMENTS AT UNCONSTRAINED Y EDGE
NSEL,ALL
FINISH
/SOLU
NSEL,S,LOC,X,4
SF,,PRES,30000       ! APPLY INTERNAL PRESSURE ON CYLINDER
NSEL,S,LOC,X,8
SF,,PRES,1E-10       ! APPLY DUMMY PRESSURE FOR SURFACE PRINTOUT
NSEL,ALL
OUTPR,,ALL
/OUT,SCRATCH
SOLVE                ! LOAD STEP 1 - INTERNAL PRESSURE
FINISH
/POST1
SET,1,1
LFT_NODE = NODE (4,0,0)
MID_NODE = NODE (6,0,0)
RT_NODE = NODE (8,0,0)
PRNSOL,S,COMP        ! PRINT NODAL STRESS SOLUTION
PATH,STRESS,2,,48    ! DEFINE PATH WITH NAME = "STRESS"
PPATH,1,LFT_NODE     ! DEFINE PATH POINTS BY NODE
PPATH,2,RT_NODE
PLSECT,S,Z,-1        ! DISPLAY SZ STRESSES
PLSECT,S,X,-1        ! DISPLAY SX STRESSES
PRSECT,-1            ! PRINT LINEARIZED STRESSES
/OUT,
*GET,DEF_4,NODE,LFT_NODE,U,X
*GET,RST_4_C1,NODE,LFT_NODE,S,X
*GET,RST_6_C1,NODE,MID_NODE,S,X
*GET,RST_8_C1,NODE,RT_NODE ,S,X
*GET,TST_4_C1,NODE,LFT_NODE,S,Z
*GET,TST_6_C1,NODE,MID_NODE,S,Z
*GET,TST_8_C1,NODE,RT_NODE ,S,Z
*DIM,VALUE_C1,,7,3
*VFILL,VALUE_C1(1,1),DATA,.0078666,-30000,-7778,0,50000,27778,20000
*VFILL,VALUE_C1(1,2),DATA,DEF_4,RST_4_C1,RST_6_C1,RST_8_C1,TST_4_C1,TST_6_C1,TST_8_C1
*VFILL,VALUE_C1(1,3),DATA,ABS(DEF_4/.0078666),ABS(RST_4_C1/30000),ABS(RST_6_C1/7778),0
*VFILL,VALUE_C1(5,3),DATA,ABS(TST_4_C1/50000),ABS(TST_6_C1/27778),ABS(TST_8_C1/20000)
*DIM,LABEL_C1,CHAR,7,2
LABEL_C1(1,1)='DEF (R=4','STRS_R p','STRS_R p','STRS_R p','STRS_T p'
LABEL_C1(6,1)='STRS_T p','STRS_T p'
LABEL_C1(1,2) = ') in    ','si (R=4)','si (R=6)','si (R=8)','si (R=4)'
LABEL_C1(6,2) ='si (R=6)','si (R=8)'
SAVE,TABLE_1
FINISH

/SOLU
RESUME,MODEL
C*** ROTATION ABOUT AXIS
NSEL,S,LOC,Y,0       ! PREVENT RIGID BODY MOTION
NSEL,R,LOC,X,4
D,ALL,UY
NSEL,S,LOC,X,4
SF,,PRES,1E-10       ! LEAVE A SMALL PRESSURE TO ALLOW STRESS PRINTOUT
NSEL,ALL
OMEGA,,1000          ! ROTATE CYLINDER WITH ANGULAR VELOCITY OMEGA
OUTPR,,ALL
/OUT,SCRATCH
SOLVE                ! LOAD STEP 2 - CENTRIFUGAL LOADING
FINISH
/POST1
/OUT,
LFT_NODE = NODE (4,0,0)
XI_NODE  = NODE (5.43,0,0)

*GET,RST_4_C2,NODE,LFT_NODE,S,X
*GET,TST_4_C2,NODE,LFT_NODE,S,Z
*GET,RST_X_C2,NODE,XI_NODE ,S,X
*GET,TST_X_C2,NODE,XI_NODE ,S,Z
*DIM,VALUE_C2,,4,3
*VFILL,VALUE_C2(1,1),DATA,0,40588,4753,29436
*VFILL,VALUE_C2(1,2),DATA,RST_4_C2,TST_4_C2,RST_X_C2,TST_X_C2
*VFILL,VALUE_C2(1,3),DATA,0,ABS(TST_4_C2/40588 ),ABS(RST_X_C2/4753 ),ABS(TST_X_C2/29436)
*DIM,LABEL_C2,CHAR,4,2
LABEL_C2(1,1) ='STRS_R p','STRS_T p','STRS_R p','STRS_T p'
LABEL_C2(1,2) ='si (R=4)','si (R=4)','si R=5.4','si R=5.4'
SAVE,TABLE_2
RESUME,TABLE_1
/COM
/OUT,vm25,vrt
/COM,------------------- VM25 RESULTS COMPARISON ---------------------
/COM,
/COM,                 |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
/COM,RESULTS FOR P = 30,000 PSI:
/COM,
*VWRITE,LABEL_C1(1,1),LABEL_C1(1,2),VALUE_C1(1,1),VALUE_C1(1,2),VALUE_C1(1,3)
(1X,A8,A8,'   ',F14.7,'  ',F17.7,'   ',1F15.3)
/NOPR
RESUME,TABLE_2
/GOPR
/COM,
/COM,RESULTS FOR w = 1000 RAD/SEC
*VWRITE,LABEL_C2(1,1),LABEL_C2(1,2),VALUE_C2(1,1),VALUE_C2(1,2),VALUE_C2(1,3)
(1X,A8,A8,'   ',F14.7,'  ',F17.7,'   ',1F15.3)
/COM,-----------------------------------------------------------------
/OUT
FINISH
*LIST,vm25,vrt


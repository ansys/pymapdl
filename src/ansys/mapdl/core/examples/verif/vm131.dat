/COM,ANSYS MEDIA REL. 2023R1 (11/04/2022) REF. VERIF. MANUAL: REL. 2023R1
/VERIFY,VM131
/PREP7
/TITLE, VM131, ACCELERATION OF A ROTATING CRANE BOOM
C***    VECTOR MECHANICS FOR ENGINEERS, BEER & JOHNSTON, P 616, PROB. 15.13
ANTYPE,STATIC
ET,1,MASS21,,,2      ! GENERALIZED MASS WITHOUT ROTARY INERTIA
R,1,1                ! UNIT MASS
N,1,34.64,20      
E,1
OMEGA,,,.5           ! ANGULAR VELOCITY OF RISING BOOM WRT GLOBAL
CGOMGA,,.3           ! ANGULAR VELOCITY OF CAB WRT REFERENCE SYSTEM
D,1,ALL
OUTRES,,1
OUTPR,RSOL,1
OUTPR,NLOAD,1
FINISH
/out,scratch
/SOLU    
SOLVE
NSEL,S,NODE,,1,1
FINI
/out
/POST1
FSUM
*GET,AX,FSUM,,ITEM,FX
*GET,AY,FSUM,,ITEM,FY
*GET,AZ,FSUM,,ITEM,FZ
*status,parm
*DIM,LABEL,CHAR,3,2
*DIM,VALUE,,3,3
LABEL(1,1) = 'AX ','AY ','AZ '
LABEL(1,2) = 'ft/s/s','ft/s/s','ft/s/s'
*VFILL,VALUE(1,1),DATA,-11.78,-5,6
*VFILL,VALUE(1,2),DATA,(-1)*(AX),(-1)*(AY),(-1)*(AZ)
*VFILL,VALUE(1,3),DATA,ABS((-1)*AX/11.78),ABS((-1)*AY/5),ABS((-1)*AZ/6)
/COM
/OUT,vm131,vrt
/COM,------------------- VM131 RESULTS COMPARISON -------------
/COM,
/COM,                 |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.2,'  ',F12.2,'   ',1F15.2)
/COM,----------------------------------------------------------
/OUT
FINISH
*LIST,vm131,vrt


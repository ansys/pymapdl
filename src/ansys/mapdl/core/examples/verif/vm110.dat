/COM,ANSYS MEDIA REL. 2023R1 (11/04/2022) REF. VERIF. MANUAL: REL. 2023R1
/VERIFY,VM110
/PREP7
/TITLE, VM110, TRANSIENT TEMPERATURE DISTRIBUTION IN A SLAB
C***    PRINCIPLES OF HEAT TRANSFER, KREITH, 2ND. PRINTING, PAGE 140, EX. 4-4
ANTYPE,TRANS              ! TRANSIENT ANALYSIS
ET,1,LINK33               ! HEAT CONDUCTING BAR
ET,2,LINK34               ! CONVECTION LINK
R,1,1                     ! UNIT AREA
MP,KXX,1,.54              ! PROPERTIES OF WALL
MP,DENS,1,144
MP,C,1,.20
MP,HF,1,5                 ! CONVECTION COEFFICIENT
N,1
N,11,1
FILL
N,12,1
E,1,2
EGEN,10,1,1               ! TEN BAR ELEMENTS ACROSS WALL THICKNESS
TYPE,2
E,11,12                   ! ONE CONVECTION LINK AT GAS END
FINISH
/SOLU    
OUTRES,,ALL  
TIME,14.5                 ! TIME AT END OF LOAD STEP
NSUBST,80
BFUNIF,TEMP,100
D,12,TEMP,1600
KBC,1                     ! STEP BOUNDARY CONDITION
AUTOTS,ON
OUTPR,BASIC,LAST
TINTPAR,,,,0.5             ! USE CENTRAL DIFFERENCE
/OUT,vm110_SCRATCH
SOLVE
/OUT
FINISH
/POST26
ESOL,2,11,,SMISC,1,HEAT   ! HEAT RATE FOR ELEMENT 11
INT1,3,2,1,,TOTAL_HT      ! INTEGRATE HEAT RATE OVER TIME SPAN
PRVAR,2,3
*GET,QTOT,VARI,3,RTIME,14.5
FINISH
/POST1
*GET,T1,NODE,1,TEMP
*GET,T3,NODE,3,TEMP
*GET,T5,NODE,5,TEMP
*GET,T7,NODE,7,TEMP
*GET,T9,NODE,9,TEMP
*GET,T11,NODE,11,TEMP
*DIM,LABEL,CHAR,7,2
*DIM,VALUE,,7,3
LABEL(1,1) = 'T, F (NO','T, F (NO','T, F (NO','T, F (NO','T, F (NO','T, F (NO','Q, BTU/f'
LABEL(1,2) = 'DE 1 )  ','DE 3 )  ','DE 5 )  ','DE 7 )  ','DE 9 )  ','DE 11)  ','t^2     '
*VFILL,VALUE(1,1),DATA,505,550,670,865,1135,1435,-20736
*VFILL,VALUE(1,2),DATA,T1,T3,T5,T7,T9,T11,QTOT
*VFILL,VALUE(1,3),DATA,ABS(T1/505),ABS(T3/550),ABS(T5/670),ABS(T7/865),ABS(T9/1135)
*VFILL,VALUE(6,3),DATA,ABS(T11/1435),ABS(QTOT/20736)
/COM
/OUT,vm110,vrt
/COM,------------------- VM110 RESULTS COMPARISON --------------
/COM,
/COM,                 |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.0,'  ',F15.0,'   ',1F15.2)
/COM,-----------------------------------------------------------
/OUT
FINISH
*LIST,vm110,vrt


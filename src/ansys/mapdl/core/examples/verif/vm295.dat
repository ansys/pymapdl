/COM,ANSYS MEDIA REL. 2023R1 (11/04/2022) REF. VERIF. MANUAL: REL. 2023R1
/VERIFY,VM295
/TITLE,VM295,1D TERZAGHI'S CONSOLIDATION PROBLEM WITH PERMEABILITY AS FUNCTION OF DEPTH
JPGPRF,500,100,1
/SHOW,JPEG
/COM, REFERENCE: A POINT INTERPOLATION METHOD FOR SIMULATING DISSIPATION PROCESS
/COM,            OF CONSOLIDATION, J.G.WANG, G.R.LIU, Y.G.WU, COMPUTER METHODS 
/COM,            IN APPLIED MECHANICS AND ENGINEERING 190 (2001),PG: 5907-5922
/PREP7
DAY=24*3600			! SECONDS IN ONE DAY
H = 16				! TOTAL DEPTH OF SOIL IN METERS
W = 1				! WIDTH OF SOIL IN METERS
R = 1E4				! PRESSURE IN PA
E = 4E7				! YOUNG'S MODULUS IN PA
TT = 1*DAY

ET,1,CPT212			! 2D 4 NODE COUPLED PORE PRESSURE ELEMENT
KEYOPT,1,12,1 			         
KEYOPT,1,3,2			! PLANE STRAIN


RECT,0,W,0,H
LESIZE,4,,,16
LESIZE,3,,,1
MSHAPE,0,2D
MSHKEY,1
AMESH,1				! CREATING CPT212 ELEMENTS

MP,EX,1,E
MP,NUXY,1,0.3

FPX = 1.728E-3/DAY/1E4		! PERMEABILITY FROM REFERENCE
ONE = 1.0

TB,PM,1,,,PERM			! DEFINING PERMEABILITY FOR THE SOIL
TBFIELD,YCOR,0			! LOCATION Y = 0
TBDATA,1,FPX,FPX,FPX		! PERMEABILITY VALUES AT LOCATION Y=0
TBFIELD,YCOR,H			! LOCATION Y=16
TBDATA,1,FPX*100,FPX*100,FPX*100! PERMEABILITY VALUES AT LOCATION Y=16, LINEAR VARIABLE PERMEABILITY

TB,PM,1,,,BIOT			! DEFINING BIOT COEFFICINET FOR SOIL
TBDATA,1,ONE			! BIOT COEFFICIENT

D,ALL,UX,0			! CONSTRAINING ALL UX DOF 

NSEL,S,LOC,Y,0
D,ALL,UY,0			! CONSTRAINING UY DOF AT LOCATION Y=0
NSEL,ALL

NSEL,S,LOC,Y,H
D,ALL,PRES,0			! DEFINING THE TOP PORTION OF SOIL AS PERMEABLE
NSEL,ALL
FINISH

/SOLU
ANTYPE,STATIC			! STATIC ANALYSIS
NROPT,UNSYM			! UNSYMMETRIC NEWTON RAPHSON OPTION
TIME,TT				! END TIME
NSEL,S,LOC,Y,H
SF,ALL,PRES,R			! APPLYING PRESSURE LOAD AT TOP OF THE SOIL
NSEL,ALL
NSUBS,350,1000,150		! SUBSTEPS 
OUTRES,ALL,ALL
KBC,1				! STEPPED LOADING
/OUT,SCRATCH
SOLVE				! SOLVE
FINISH

/POST1
SET,LIST
/OUT,
/COM, 
/COM,  EXCESS PORE PRESSURE IN KILOPASCALS AT LOCATION X=1,Y=6 
/COM,  FOR 0.1 DAY (8640 SECONDS),0.2 DAY (17280 SECONDS)
/COM,  0.3 DAY (25920 SECONDS), 0.4 DAY (34560 SECONDS) 
/COM,  AND 0.5 DAY (43200 SECONDS) ARE COMPUTED AND COMPARED
/COM,  AGAINST REFERENCE SOLUTION
/COM, 
/OUT,SCRATCH
*DIM,P,,5
SET,1,16
*GET,P11,NODE,NODE(1,6,0),PRES	
*GET,T11,ACTIVE,0,SET,TIME	
SET,1,17
*GET,P12,NODE,NODE(1,6,0),PRES	
*GET,T12,ACTIVE,0,SET,TIME	
T1=DAY*0.1
/COM
/COM INTERPOLATE THE RESULTS AT LOCATION (1,6,0) FOR TIME=0.1DAY
/COM
P(1)=(P11+(T1-T11)/(T12-T11)*(P12-P11))/1E3

SET,1,31
*GET,P21,NODE,NODE(1,6,0),PRES	
*GET,T21,ACTIVE,0,SET,TIME	
SET,1,32
*GET,P22,NODE,NODE(1,6,0),PRES	
*GET,T22,ACTIVE,0,SET,TIME	
T2=DAY*0.2
/COM
/COM INTERPOLATE THE RESULTS AT LOCATION (1,6,0) FOR TIME=0.2DAY
/COM
P(2)=(P21+(T2-T21)/(T22-T21)*(P22-P21))/1E3


SET,1,46
*GET,P31,NODE,NODE(1,6,0),PRES	
*GET,T31,ACTIVE,0,SET,TIME	
SET,1,47
*GET,P32,NODE,NODE(1,6,0),PRES	
*GET,T32,ACTIVE,0,SET,TIME	
T3=DAY*0.3
/COM
/COM INTERPOLATE THE RESULTS AT LOCATION (1,6,0) FOR TIME=0.3DAY
/COM
P(3)=(P31+(T3-T31)/(T32-T31)*(P32-P31))/1E3

SET,1,61
*GET,P41,NODE,NODE(1,6,0),PRES	
*GET,T41,ACTIVE,0,SET,TIME	
SET,1,62
*GET,P42,NODE,NODE(1,6,0),PRES	
*GET,T42,ACTIVE,0,SET,TIME	
T4=DAY*0.4
/COM
/COM INTERPOLATE THE RESULTS AT LOCATION (1,6,0) FOR TIME=0.4DAY
/COM
P(4)=(P41+(T4-T41)/(T42-T41)*(P42-P41))/1E3

SET,1,76
*GET,P51,NODE,NODE(1,6,0),PRES	
*GET,T51,ACTIVE,0,SET,TIME	
SET,1,77
*GET,P52,NODE,NODE(1,6,0),PRES	
*GET,T52,ACTIVE,0,SET,TIME	
T5=DAY*0.5
/COM
/COM INTERPOLATE THE RESULTS AT LOCATION (1,6,0) FOR TIME=0.5DAY
/COM
P(5)=(P51+(T5-T51)/(T52-T51)*(P52-P51))/1E3

*DIM,CP,,5
CP(1)=5.230,2.970,1.769,1.043,0.632		! REFERENCE RESULTS, FIGURE 5, PG 5916
*DIM,RT,,5
*DO,I,1,5
RT(I)=P(I)/CP(I)
*ENDDO
*DIM,LABEL,CHAR,5
LABEL(1)='0.1','0.2','0.3','0.4','0.5'
/COM
/OUT,vm295,vrt
/COM,------------ vm295 RESULTS COMPARISON --------------
/COM,
/COM,        Time     |  TARGET   |  Mechanical APDL  |  RATIO
/COM,
*VWRITE,LABEL(1),CP(1),P(1),RT(1)
(8X,A4,'       ',F10.3,'       ',F10.3,'    ',F10.2)
/COM,
/COM-------------------------------------------------------
/OUT
FINISH
*LIST,vm295,vrt



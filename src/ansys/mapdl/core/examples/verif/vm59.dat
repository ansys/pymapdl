/COM,ANSYS MEDIA REL. 2023R1 (11/04/2022) REF. VERIF. MANUAL: REL. 2023R1
/VERIFY,VM59
/PREP7
MP,PRXY,,0.3
/TITLE, VM59, LATERAL VIBRATION OF AN AXIALLY LOADED BAR
C***          VIBRATION PROBS. IN ENGR., TIMOSHENKO, 3RD. ED., PAGE 374, ART. 59
ANTYPE,STATIC                ! STATIC ANALYSIS
RESCONTROL,LINEAR,ALL,1      ! NEEDED FOR PERTURBATION ANALYSIS
ET,1,BEAM188,,,3
SECT,1,BEAM,RECT
SECD,2,2
MP,EX,1,30E6
MP,DENS,1,727973E-9
N,1
N,14,80
FILL
E,1,2
EGEN,13,1,1
D,1,UY,,,14,,ROTX,ROTZ       ! B.C.'S AND LOADING
D,1,UX,,,,,UZ
D,14,UZ
F,14,FX,-40E3
FINISH
/out,scratch
/SOLU    
OUTPR,BASIC,1
SOLVE
FINISH
/out
/POST1
ETABLE,STRS,LS,1
*GET,STRSS,ELEM,13,ETAB,STRS
*GET,DEF,NODE,14,U,X
PARSAV,ALL
FINISH
/out,scratch
/SOLU    
ANTYPE,STATIC,RESTART,,,PERTURB	! RESTART STATIC SOLVE FROM LAST LOAD STEP AND SUB STEP
PERTURB,MODAL,,,PARKEEP			! PERFORM PERTURBED MODAL SOLVE
SOLVE,ELFORM				! REFORM ELEMENT MATRICES

PARRES,CHANGE
MXPAND,3                     ! EXPAND FIRST 3 MODES
MODOPT,LANB,3                ! SELECT THE BLOCK LANCZOS EIGENSOLVER
SOLVE
/out
*GET,FREQ1,MODE,1,FREQ
*GET,FREQ2,MODE,2,FREQ
*GET,FREQ3,MODE,3,FREQ

*DIM,LABEL,CHAR,5,2
*DIM,VALUE,,5,3
LABEL(1,1) = 'DEFLECTI','SIGMA,  ','f1,     ','f2,     ','f3,     '
LABEL(1,2) = 'ON, (in)','   (psi)','    (Hz)','    (Hz)','    (Hz)'
*VFILL,VALUE(1,1),DATA,-.026667,-10000,17.055,105.32,249.39
*VFILL,VALUE(1,2),DATA,DEF,STRSS,FREQ1,FREQ2,FREQ3
*VFILL,VALUE(1,3),DATA,ABS(DEF/.026667),ABS(STRSS/10000),ABS(FREQ1/17.055)
*VFILL,VALUE(4,3),DATA,ABS(FREQ2/105.32),ABS(FREQ3/249.39)
/COM
/OUT,vm59,vrt
/COM,------------------- VM59 RESULTS COMPARISON ---------------
/COM,
/COM,                     |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F14.6,'  ',F18.6,'   ',1F15.3)
/COM,-----------------------------------------------------------
/COM,
/COM,-----------------------------------------------------------
/COM,NOTE: THERE ARE VERIFIED RESULTS IN VM59  NOT CONTAINED IN
/COM,THIS TABLE
/COM,-----------------------------------------------------------
/OUT
FINISH
*LIST,vm59,vrt

/COM,ANSYS MEDIA REL. 2023R1 (11/04/2022) REF. VERIF. MANUAL: REL. 2023R1
/VERIFY,VM181
/PREP7
SMRT,OFF
/TITLE, VM181, NATURAL FREQUENCY OF A FLAT CIRCULAR PLATE WITH A CLAMPED EDGE
C***          FORMULAS FOR NATURAL FREQUENCY AND MODE SHAPE, BLEVINS, PAGE 241
ET,1,PLANE183,,,1       ! AXISYMMETRIC ELEMENTS
MP,EX,1,3E7
MP,DENS,1,0.00073
MP,PRXY,1,0.3
K,1
K,2,17
KGEN,2,1,2,1,,.5
L,1,2
L,3,4
LESIZE,ALL,,,10       ! TEN DIVISIONS ALONG LENGTH
ESIZE,,1
A,1,2,4,3
MSHAPE,1,2D
MSHKEY,0
AMESH,1
NSEL,R,LOC,X,0
D,ALL,UX
NSEL,ALL
DK,2,ALL,,,1
DK,4,UX,,,1
FINISH
/SOLU    
ANTYPE,MODAL          ! MODAL ANALYSIS
MODOPT,LANP,9
SOLVE
*GET,F1,MODE,1,FREQ
*GET,F2,MODE,2,FREQ
*GET,F3,MODE,3,FREQ
*DIM,LABEL,CHAR,3,2
*DIM,VALUE,,3,3
LABEL(1,1) = 'F (0,0) ','F (0,1) ','F (0,2) '
LABEL(1,2) = 'Hz','Hz','Hz'
*VFILL,VALUE(1,1),DATA,172.64,671.79,1505.7
*VFILL,VALUE(1,2),DATA,F1,F2,F3
*VFILL,VALUE(1,3),DATA,ABS(F1/172.64),ABS(F2/671.79),ABS(F3/1505.7)
/COM
/OUT,vm181,vrt
/COM,------------------- VM181 RESULTS COMPARISON --------------
/COM,
/COM,              |    TARGET   |        Mechanical APDL   |        RATIO
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.2,'  ',F18.2,'   ',1F15.3)
/COM,-----------------------------------------------------------
/OUT
FINISH
*LIST,vm181,vrt

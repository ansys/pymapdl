/COM,ANSYS MEDIA REL. 2023R1 (11/04/2022) REF. VERIF. MANUAL: REL. 2023R1
/VERIFY,VM170
/PREP7
/TITLE, VM170, MAGNETIC FIELD FROM A SQUARE CURRENT LOOP
/COM,   REF. BOAST, "PRINCIPLES OF ELECTRIC AND MAGNETIC FIELDS"
/COM,   PG. 200
ET,1,LINK68               ! LINK68 ELEMENT
R,1,1                     ! ARBITRARY AREA
MP,RSVX,1,4.0E-8          ! RESISTIVITY
N,1                       ! DEFINE NODES FOR LOOP
N,2,1.5
N,3,1.5,,1.5
N,4,,,1.5
N,5                       ! NODES 1 AND 5 OVERLAP
N,6,,.35                  ! NODE FOR RESULTS EXTRACTION
E,1,2                     ! GENERATE ELEMENTS
EGEN,4,1,-1
D,5,VOLT,0                ! GROUND VOLTAGE IN WIRE
F,1,AMPS,7.5              ! APPLY CURRENT AT "CUT"
FINISH
/SOLU    
OUTPR,ESOL,LAST           ! BASIC ELEMENT PRINTOUT                 
SOLVE                     ! SOLVE ELECTRIC CURRENT CONDUCTION
BIOT,NEW                  ! CALCULATE HS FIELD VIA BIOT-SAVART INTEG.
*GET,HX,NODE,6,HS,X       ! GET HS(X) AS A PARAMETER 
*GET,HY,NODE,6,HS,Y       ! GET HS(Y)
*GET,HZ,NODE,6,HS,Z       ! GET HS(Z)
MUZRO=12.5664E-7          ! DEFINE FREE=SPACE PERMEABILITY
BX=MUZRO*HX               ! CALCULATE FLUX DENSITY
BY=MUZRO*HY
BZ=MUZRO*HZ
*status,parm                   ! SHOW PARAMETER STATUS
*DIM,LABEL,CHAR,3,2
*DIM,VALUE,,3,3
LABEL(1,1) = 'BX ','BY ','BZ '
LABEL(1,2) = 'TESLA','TESLA','TESLA'
*VFILL,VALUE(1,1),DATA,2.010E-6,-.662E-6,2.01E-6
*VFILL,VALUE(1,2),DATA,BX,BY,BZ
*VFILL,VALUE(1,3),DATA,ABS(BX/(2.01E-6)),ABS(BY/.662E-6),ABS(BZ/(2.01E-6))
/COM
/OUT,vm170,vrt
/COM,------------------- VM170 RESULTS COMPARISON -------------
/COM,
/COM,                 |   TARGET     |   Mechanical APDL     |   RATIO
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F12.9,'  ',F16.9,'   ',1F15.3)
/COM,----------------------------------------------------------
/OUT
FINISH
*LIST,vm170,vrt

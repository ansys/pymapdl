/COM,ANSYS MEDIA REL. 2023R1 (11/04/2022) REF. VERIF. MANUAL: REL. 2023R1
/VERIFY,VM183
/PREP7  
JPGPRF,500,100,1             ! MACRO TO SET PREFS FOR JPEG PLOTS
/SHOW,JPEG
/TITLE, VM183,  HARMONIC RESPONSE OF A SPRING-MASS SYSTEM
C***  R.K. VIERCK, "VIBRATION ANALYSIS", 2ND EDITION, SECTION 4-2
ANTYPE,MODAL         ! MODE - FREQUENCY ANALYSIS
MODOPT,LANB,2,,,     
ET,1,COMBIN40,,,2    ! UY DOF
R,1,6,,2             ! K1=6 N/M     M1=2 KG
R,2,16,,2            ! K2=16 N/M    M2=2 KG
N,1
N,2,0,1
N,3,0,2
REAL,1
E,1,2
REAL,2
E,2,3
OUTPR,,ALL
D,3,ALL
FINISH
/SOLU    
SOLVE
FINISH
/SOLU    
ANTYPE,HARMIC        ! HARMONIC ANALYSIS
HROPT,MSUP,2         ! MODE SUPERPOSITION USING TWO MODES
HARFRQ,0.1,1.0       ! RANGE OF FREQUENCIES FROM 0.1 TO 1.0 HZ
F,1,FY,50
KBC,1                ! STEP BOUNDARY CONDITIONS
NSUBST,50
OUTPR,,NONE
OUTRES,,1
SOLVE
FINISH
/POST26
FILE,,rfrq           ! REDUCED FREQUENCIES FILE
NSOL,2,1,U,Y,UY1
NSOL,3,2,U,Y,UY2
/GRID,1
/AXLAB,Y,DISP
PLVAR,2,3            ! DISPLAY DISPLACEMENT RESPONSE VS. FREQUENCY
PRVAR,2,3
*GET,Y1,VARI,2,RTIME,.226
*GET,Y2,VARI,3,RTIME,.226
*GET,Y3,VARI,2,RTIME,.910
*GET,Y4,VARI,3,RTIME,.910
*DIM,LABEL,CHAR,2,2
*DIM,VALUE,,2,3
LABEL(1,1) = 'DISP 1 ','DISP 2 '
LABEL(1,2) = 'm','m'
*VFILL,VALUE(1,1),DATA,-1371.7,-458.08
*VFILL,VALUE(1,2),DATA,Y1,Y2
*VFILL,VALUE(1,3),DATA,ABS(Y1/1371.7),ABS(Y2/458.08)
SAVE,TABLE_1
*VFILL,VALUE(1,1),DATA,-.8539,.1181
*VFILL,VALUE(1,2),DATA,Y3,Y4
*VFILL,VALUE(1,3),DATA,ABS(Y3/.8539),ABS(Y4/.1181),
SAVE,TABLE_2
RESUME,TABLE_1
/COM
/OUT,vm183,vrt
/COM,------------------- VM183 RESULTS COMPARISON --------------
/COM,
/COM,                  |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
/COM,RESULTS AT .226 Hz
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.2,'  ',F14.2,'   ',1F15.3)
/NOPR
RESUME,TABLE_2
/GOPR
/COM,
/COM,RESULTS AT .910 Hz
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.4,'  ',F14.4,'   ',1F15.3)
/COM,-----------------------------------------------------------
/OUT
FINISH
*LIST,vm183,vrt

/DELETE,TABLE_1
/DELETE,TABLE_2
FINISH

/COM,ANSYS MEDIA REL. 2023R1 (11/04/2022) REF. VERIF. MANUAL: REL. 2023R1
/VERIFY,VM309
/TITLE,VM309, TORSIONAL BUCKLING FOR BEAM WITH CRUCIFORM SECTION
/COM,
/COM,  REF: S.P. TIMOSHENKO, J.M. GERE, THEORY OF ELASTIC STABILITY,
/COM,  MCGRAW-HILL, 2ND EDITION, 1961, PP 229
/COM,

/OUT,vm309_SCRATCH
/PREP7
ET,1,183

K,1,0.2,0.2
K,2,0.2,8.0
K,3,-0.2,8.0
K,4,-0.2,0.2
K,6,8.0,0.2
K,7,8.0,-0.2
K,8,0.2,-0.2
K,9,0.2,-8.0
K,10,-0.2,-8.0
K,11,-0.2,-0.2
K,12,-8.0,-0.2
K,13,-8.0,0.2

L,1,2
L,2,3
L,3,4
L,4,1
L,1,6
L,6,7
L,7,8
L,1,8
L,8,9
L,9,10
L,10,11
L,11,8
L,11,12
L,12,13
L,13,4
L,4,11
AL,1,2,3,4
AL,5,6,7,8
AL,9,10,11,12
AL,13,14,15,16
AL,4,8,12,16
ALLSEL,
LSEL,S,LENGTH,,7.8
LESIZE,ALL,,,6
LSEL,INVE,
LESIZE,ALL,,,4
AMESH,ALL

SECWRITE,vm309,sect,,1              ! CREATES AN ASCII FILE CONTAINING USER MESH SECTION
FINISH

/CLEAR,NOSTART
/OUT,
/PREP7

ET,1,189,1,                         ! WARPING DEGREES OF FREEDOM IS ACTIVATED
MP,EX,1,21000
MP,GXY,1,8077
MP,NUXY,1,0.3

SECN,1
SECTYPE,1,BEAM,MESH
SECREAD,vm309,sect,,MESH            ! READS A USER-DEFINED SECTION MESH INTO ANSYS.

N,1,0,0,0
N,51,100,0,0
FILL,1,51
*DO,I,1,50,2,
E,I,I+2,I+1
*ENDDO

NSEL,S,NODE,,1
F,ALL,FX,1
ALLSEL

NSEL,S,NODE,,51
D,ALL,UY,,,,,UZ,UX,ROTX,ROTY,ROTZ,  ! FIX ALL DOF OTHER THAN WARPING
ALLSEL

NSEL,S,NODE,,1
D,ALL,UY,,,,,UZ,ROTX,ROTY,ROTZ,     ! FIX ALL DOF OTHER THAN WARPING AND UX
ALLSEL

/SOLU
PSTRES,ON
SOLVE
FINISH

/SOLU
OUTRES,ALL,ALL
ANTYPE,1
BUCOPT,LANB,3
SUBOPT,0,0,0,0,0,ALL
MXPAND,3,,,YES
SOLVE
FINISH

/POST1
SET,LIST

*DIM,MODES,ARRAY,3,1
*DIM,ANSYS,,3,1
*DIM,SUBSTEP,,3,1
*DO,I,1,3
    *GET,MODES(I,1),MODE,I,FREQ
    *VFILL,ANSYS(I,1),DATA,MODES(I,1)
    *VFILL,SUBSTEP(I,1),DATA,I
*ENDDO

*DIM,ANALYTIC,,3,1
*VFILL,ANALYTIC(1,1),DATA,261.792, 272.232, 289.633
*DIM,RATIO,,3,1
*VFILL,RATIO(1,1),DATA,ABS(ANSYS(1,1) / 261.792 ),ABS( ANSYS(2,1) / 272.232 ), ABS( ANSYS(3,1) / 289.633 )
/OUT,vm309,vrt
/COM
/COM,------------------- VM309 RESULTS COMPARISON ---------------------
/COM,
/COM, MODES  |   TARGET   |   MECHANICAL APDL   |   RATIO
/COM,
*VWRITE,SUBSTEP(1,1),ANALYTIC(1,1),ANSYS(1,1),RATIO(1,1)
('   'F3.0,,'    ',F10.3,'  ',F14.3,'  ',1F15.3)
/COM,----------------------------------------------------------------
/OUT

FINISH
*LIST,vm309,vrt

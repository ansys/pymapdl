/COM,ANSYS MEDIA REL. 2023R1 (11/04/2022) REF. VERIF. MANUAL: REL. 2023R1
/VERIFY,VM216
JPGPRF,500,100,1             ! MACRO TO SET PREFS FOR JPEG PLOTS
/SHOW,JPEG
/TITLE,VM216, LATERAL BUCKLING OF RIGHT-ANGLE FRAME
!  J.C. SIMO, L. VU-QUOC, "THREE-DIMENSIONAL FINITE-STRAIN ROD
!  MODEL",PART II CMAME, VOL 58, 1986, PP 79-116

/PREP7
N,1 ,   ,240		!DEFINE NODES
N,21,240,240
N,41,240,0
FILL,1,21
FILL,21,41
N,101,120,360
N,201,360,120
SECNUM,1
ET,1,188		!USE BEAM188 ELEMENT TYPE
KEYOPT,1,3,2
*DO,I,1,20,1
 E,I,I+1,101
*ENDDO
*DO,I,21,40,1
 E,I,I+1,201
*ENDDO
SECTYPE,1,BEAM,ASEC
SECDATA,18.0,1350.0,0.0,0.54,0.0,2.16
MP,EX,1,71240
MP,NUXY,1,0.31
D,1,ALL
FINISH

/SOLU
NLGEOM,ON
NSUBST,2
F,41,FZ,1.0E-3		!APPLY PERTURBATION FORCE
/OUT,SCRATCH,		!SUPPRESS SOLUTION DATA
SOLVE
OUTRES,ALL,ALL
ARCLEN,ON
ARCTRM,U,60,41,UZ
F,41,FX,1.485		!APPLY END FORCE
NSUBST,20
SOLVE
FINISH

/POST26
/AXLAB,X,TIP DISPLACEMENT
/AXLAB,Y,END FORCE
NSOL,2,41,U,Z,DISP
RFORCE,3,1,F,X,FORCE
PROD,4,3, , ,FORCE , , ,-1.0,1,1,
XVAR,2
/COM,   THE LOAD DEFLECTION CURVE SHOWN IN VM216.GRPH
/COM,   SHOW A CRITICAL LOAD OF APPROX. 1.09
!PRVAR,2,4
PLVAR,4
FINISH

/POST1
SET,2,7,1
NSEL,S,LOC,X,0
FSUM
*GET,CP1,FSUM,,ITEM,FX
*DIM,LABEL1,CHAR,1
*DIM,VALUE1,,1,3
LABEL1(1) = 'FX_CRLD'
*VFILL,VALUE1(1,1),DATA,1.09
*VFILL,VALUE1(1,2),DATA,ABS(CP1)
*VFILL,VALUE1(1,3),DATA,ABS(CP1 / 1.09)
FINISH
SAVE,TABLE_1

/CLEAR,NOSTART			!PERFORM SAME ANALYSIS WITH BEAM189
/OUT
/PREP7
N,1 ,   ,240
N,21,240,240
N,41,240,0
FILL,1,21
FILL,21,41
N,101,120,360
N,201,360,120
SECNUM,1
ET,1,BEAM189                    ! 3-D QUADRATIC BEAM
*DO,I,1,10,1
 I0=(I-1)*2+1
 E,I0,I0+2,I0+1,101
*ENDDO
*DO,I,11,20,1
 I0=(I-1)*2+1
 E,I0,I0+2,I0+1,201
*ENDDO
SECTYPE,1,BEAM,ASEC
SECDATA,18.0,1350.0,0.0,0.54,0.0,2.16
MP,EX,1,71240
MP,NUXY,1,0.31
D,1,ALL
FINISH

/SOLU
NLGEOM,ON
NSUBST,2
F,41,FZ,1.0E-3
/OUT,SCRATCH,,,APPEND
SOLVE
OUTRES,ALL,ALL
ARCLEN,ON
ARCTRM,U,60,41,UZ
F,41,FX,1.485
NSUBST,20
SOLVE
FINISH

/POST26
/AXLAB,X,TIP DISPLACEMENT
/AXLAB,Y,END FORCE
NSOL,2,41,U,Z,DISP
RFORCE,3,1,F,X,FORCE
PROD,4,3, , , , , ,-1.0,1,1,
XVAR,2
!PRVAR,2,4
PLVAR,4
FINISH

/POST1
SET,2,7,1
NSEL,S,LOC,X,0
FSUM
*GET,CP2,FSUM,,ITEM,FX
*DIM,LABEL2,CHAR,1
*DIM,VALUE2,,1,3
LABEL2(1) = 'FX_CRLD'
*VFILL,VALUE2(1,1),DATA,1.09
*VFILL,VALUE2(1,2),DATA,ABS(CP2)
*VFILL,VALUE2(1,3),DATA,ABS(CP2 / 1.09)
SAVE,TABLE_2
RESUME,TABLE_1
/OUT
/COM,
/OUT,vm216,vrt
/COM,
/COM,------------------- VM216 RESULTS COMPARISON ---------------------
/COM,
/COM,				             |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
/COM, USING BEAM 188 ELEMENTS
/COM,
*VWRITE,LABEL1(1),VALUE1(1,1),VALUE1(1,2),VALUE1(1,3)
(1X,A15,'     ',F8.3,'     ',F15.3,'     ',1F15.3)
/COM,
/NOPR
RESUME,TABLE_2
/COM,
/COM, USING BEAM 189 ELEMENTS
/COM,
/COM,
*VWRITE,LABEL2(1),VALUE2(1,1),VALUE2(1,2),VALUE2(1,3)
(1X,A15,'     ',F8.3,'     ',F15.3,'     ',1F15.3)
/COM,
/COM,------------------------------------------------------------------
/OUT
FINISH
*LIST,vm216,vrt
/OUT,SCRATCH,,,APPEND


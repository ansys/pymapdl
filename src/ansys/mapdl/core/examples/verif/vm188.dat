/COM,ANSYS MEDIA REL. 150 (11/8/2013) REF. VERIF. MANUAL: REL. 150
/VERIFY,VM188
JPGPRF,500,100,1             ! MACRO TO SET PREFS FOR JPEG PLOTS
/SHOW,JPEG
/PREP7
SMRT,OFF
/TITLE, VM188, FORCE CALCULATION ON A CURRENT CARRYING CONDUCTOR
/COM,   REF:  MOON, FRANCIS C., MAGNETO-SOLID MECHANICS, PG. 418, 1984
ET,1,PLANE53                       ! 8-NODE QUADRILATERAL MAGNETICS ELEMENT
ET,2,INFIN9                        ! 2-D INFINITE BOUNDARY ELEMENT
EMUNIT,MKS                         ! MKS UNITS
MP,MURX,1,1                        ! MATERIAL 1 RELATIVE PERMEABILITY=1.0
MP,MURX,2,1                        ! MATERIAL 2 RELATIVE PERMEABILITY=1.0
D=.01                              ! DEFINE GEOMETRY IN TERMS OF PARAMETERS
A=.012
T=.002
OB=.04                             ! OUTER BOUNDARY SIZE
X1=D/2-T/2
X2=D/2+T/2
GP=.0002                           ! GAP FOR THIN AIR LAYER NEXT TO CONDUCTOR
RECTNG,0,OB,0,OB                   ! DEFINE BOOLEAN AREAS
RECTNG,0,.012,0,.012
RECTNG,X1,X2,0,A/2
RECTNG,X1-GP,X2+GP,0,A/2+GP
AOVLAP,ALL                         ! OVERLAP AREAS
ASEL,S,AREA,,3
AATT,2                             ! ASSIGN MATERIAL ATTRIBUTE TO CONDUCTOR
ASEL,ALL
KSEL,S,LOC,X,0,.012                ! SELECT KEYPOINTS FOR KESIZE SPEC.
KSEL,R,LOC,Y,0,.012
KESIZE,ALL,A/8                     ! ASSIGN ELEMENT SIZE AT KEYPOINTS
KSEL,INVE
KESIZE,ALL,OB/5
KSEL,ALL
LSEL,S,LOC,X,OB                    ! SELECT FAR-FIELD BOUNDARY LINES 
LSEL,A,LOC,Y,OB
TYPE,2
LMESH,ALL                          ! MESH WITH INFINITE LINE ELMENTS
LSEL,ALL
MSHK,0                             ! FREE AREA MESH
MSHA,0,2D                          ! USING QUADS
TYPE,1
/OUT,MESH,LIS
AMESH,ALL                          ! MESH AREAS WITH PLANE53
/OUT
FINISH
/SOLU
ANTYPE,STATIC                      ! STATIC MAGNETICS ANALYSIS
ESEL,S,MAT,,2                      ! SELECT CONDUCTOR ELEMENTS
BFE,ALL,JS,,,,1E6                  ! APPLY CURRENT DENSITY TO CONDUCTOR
NSLE,S                             ! SELECT NODES IN CONDUCTOR REGION
BF,ALL,MVDI,1                      ! APPLY VIRTUAL WORK DISPLACEMENT = 1
NSEL,INVE                          ! SELECT ALL OTHER NODES
BF,ALL,MVDI,0                      ! APPLY VIRTUAL WORK DISPLACEMENT = 0
NSEL,ALL
ESEL,ALL
SOLVE
FINISH
/POST1
ETABLE,FMAGX,FMAG,X                ! STORE J*B FORCE INFORMATION
ETABLE,FVWX,NMISC,3                ! STORE VIRTUAL WORK FORCE
SSUM                               ! SUM TABLE ENTRIES
*GET,FXL,SSUM,,ITEM,FMAGX          ! GET J*B FORCE AS PARAMETER
FXL=FXL*2                          ! TOTAL LORENTZ FORCE
*GET,FXVW,SSUM,,ITEM,FVWX          ! GET VIRTUAL WORK FORCE AS PARAMETER
FXVW=FXVW*2                        ! TOTAL VIRTUAL WORK FORCE
PATH,MAXWELL,4,,48                 ! DEFINE PATH WITH NAME = "MAXWELL"
PPATH,1,,.012,0,0                  ! DEFINE PATH POINTS BY LOCATION
PPATH,2,,.012,.012,0
PPATH,3,,0,.012,0
PPATH,4,,0,0,0
FOR2D                              ! COMMAND MACRO FOR MAXWELL STRESS FORCE CALC
FXM=FX*2                           ! TOTAL MAXWELL FORCE (SYMMETRY)
*STATUS,PARM                       ! SHOW RESULTS
/PBC,PATH,1                        ! ACTIVATE PATH B.C. FOR DISPLAY
PLF2D                              ! DISPLAY FLUX LINES
*DIM,LABEL,CHAR,3,2
*DIM,VALUE,,3,3
LABEL(1,1) = 'F (LRNZ) ','F (MAXW) ','F (VW) '
LABEL(1,2) = 'N/m','N/m','N/m'
*VFILL,VALUE(1,1),DATA,-9.684E-3,-9.684E-3,-9.684E-3
*VFILL,VALUE(1,2),DATA,FXL,FXM,FXVW
*VFILL,VALUE(1,3),DATA,ABS(FXL/(9.684E-3)),ABS(FXM/(9.684E-3)),ABS(FXVW/(9.684E-3))
/COM
/OUT,vm188,vrt
/COM,------------------- VM188 RESULTS COMPARISON --------------
/COM,
/COM,              |    TARGET   |        Mechanical APDL   |        RATIO
/COM,PLANE53
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.6,'  ',F18.6,'   ',1F15.3)
/OUT
FINISH

/CLEAR,NOSTART
/NOPR
/PREP7
SMRT,OFF
ET,1,PLANE233                      ! 8-NODE QUADRILATERAL EMAG ELEMENT
KEYOPT,1,7,1
ET,2,INFIN9                        ! 2-D INFINITE BOUNDARY ELEMENT
MP,MURX,1,1                        ! MATERIAL 1 RELATIVE PERMEABILITY=1.0
MP,MURX,2,1                        ! MATERIAL 2 RELATIVE PERMEABILITY=1.0
D=.01                              ! DEFINE GEOMETRY IN TERMS OF PARAMETERS
A=.012
T=.002
OB=.04                             ! OUTER BOUNDARY SIZE
X1=D/2-T/2
X2=D/2+T/2
GP=.0002                           ! GAP FOR THIN AIR LAYER NEXT TO CONDUCTOR
RECTNG,0,OB,0,OB                   ! DEFINE BOOLEAN AREAS
RECTNG,0,.012,0,.012
RECTNG,X1,X2,0,A/2
RECTNG,X1-GP,X2+GP,0,A/2+GP
AOVLAP,ALL                         ! OVERLAP AREAS
ASEL,S,AREA,,3
AATT,2                             ! ASSIGN MATERIAL ATTRIBUTE TO CONDUCTOR
ASEL,ALL
KSEL,S,LOC,X,0,.012                ! SELECT KEYPOINTS FOR KESIZE SPEC.
KSEL,R,LOC,Y,0,.012
KESIZE,ALL,A/8                     ! ASSIGN ELEMENT SIZE AT KEYPOINTS
KSEL,INVE
KESIZE,ALL,OB/5
KSEL,ALL
LSEL,S,LOC,X,OB                    ! SELECT FAR-FIELD BOUNDARY LINES 
LSEL,A,LOC,Y,OB
TYPE,2
LMESH,ALL                          ! MESH WITH INFINITE LINE ELEMENTS
LSEL,ALL
MSHK,0                             ! FREE AREA MESH
MSHA,0,2D                          ! USING QUADS
TYPE,1
AMESH,ALL                          ! MESH AREAS
FINISH

/SOLU
ANTYPE,STATIC                      ! STATIC MAGNETICS ANALYSIS
ESEL,S,MAT,,2                      ! SELECT CONDUCTOR ELEMENTS
BFE,ALL,JS,,,,1E6                  ! APPLY CURRENT DENSITY TO CONDUCTOR
ESEL,ALL
SOLVE
FINISH

/POST1
ESEL,S,MAT,,2   
NSLE
ESLN
EMFT                               ! SUM UP MAXWELL FORCES ACTING ON THE CONDUCTOR
FMXW=_FXSUM*2 
ALLS   
FINISH

/PREP7
KEYOP,1,8,1                       ! CALCULATE LORENTZ FORCE
FINISH

/SOLU
SOLVE
FINISH

/POST1
ESEL,S,MAT,,2
NSLE
EMFT                               ! SUM UP LORENTZ FORCES ACTING ON THE CONDUCTOR
FJxB=_FXSUM*2
ALLS
FINI

/POST1
PATH,MAXWELL,4,,48                 ! DEFINE PATH WITH NAME = "MAXWELL"
PPATH,1,,.012,0,0                  ! DEFINE PATH POINTS BY LOCATION
PPATH,2,,.012,.012,0
PPATH,3,,0,.012,0
PPATH,4,,0,0,0
/PBC,PATH,1                        ! ACTIVATE PATH B.C. FOR DISPLAY
PLF2D            

*DIM,LABEL,CHAR,2,2
*DIM,VALUE,,2,3
LABEL(1,1) = 'F (LRNZ) ','F (MAXW) '
LABEL(1,2) = 'N/m','N/m'
*VFILL,VALUE(1,1),DATA,-9.684E-3,-9.684E-3
*VFILL,VALUE(1,2),DATA,FJxB,FMXW
*VFILL,VALUE(1,3),DATA,ABS(FJxB/(9.684E-3)),ABS(FMXW/(9.684E-3))
/COM
/OUT,vm188,vrt,,APPEND
/COM,PLANE233
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.6,'  ',F18.6,'   ',1F15.3)
/COM,-----------------------------------------------------------
/OUT
FINISH
*LIST,vm188,vrt

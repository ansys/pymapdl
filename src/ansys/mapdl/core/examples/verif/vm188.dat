/COM,ANSYS MEDIA REL. 2023R1 (11/04/2022) REF. VERIF. MANUAL: REL. 2023R1
/VERIFY,VM188
/out,scratch
/TITLE,VM188, FORCE CALCULATION ON A CURRENT CARRYING CONDUCTOR
/COM,   REFERENCE:  MOON, FRANCIS C., MAGNETO-SOLID MECHANICS, PG. 418, 1984
/COM,
/COM,
JPGPRF,500,100,1             ! MACRO TO SET PREFS FOR JPEG PLOTS
/SHOW,JPEG
_GEOMGEN_1=0                    ! SET THIS TO 1 TO UPDATE CDB FILE 
_EXIT1=0                       ! SET THIS TO 1 TO EXIT WITHOUT SOLVING
*IF,_GEOMGEN_1,EQ,1,THEN 
/PREP7
SMRT,OFF
ET,1,PLANE233                      ! 8-NODE QUADRILATERAL EMAG ELEMENT
KEYOPT,1,7,1
ET,2,INFIN110                      ! 2-D INFINITE BOUNDARY ELEMENT
MP,MURX,1,1                        ! MATERIAL 1 RELATIVE PERMEABILITY=1.0
MP,MURX,2,1                        ! MATERIAL 2 RELATIVE PERMEABILITY=1.0
D=.01                              ! DEFINE GEOMETRY IN TERMS OF PARAMETERS
A=.012
T=.002
OB=.04                             ! OUTER BOUNDARY SIZE
X1=D/2-T/2
X2=D/2+T/2
GP=.0002                           ! GAP FOR THIN AIR LAYER NEXT TO CONDUCTOR
RECTNG,0,OB,0,OB                   ! DEFINE BOOLEAN AREAS
RECTNG,0,.012,0,.012
RECTNG,X1,X2,0,A/2
RECTNG,X1-GP,X2+GP,0,A/2+GP
AOVLAP,ALL                         ! OVERLAP AREAS
ASEL,S,AREA,,3
AATT,2                             ! ASSIGN MATERIAL ATTRIBUTE TO CONDUCTOR
ASEL,ALL
KSEL,S,LOC,X,0,.012                ! SELECT KEYPOINTS FOR KESIZE SPEC.
KSEL,R,LOC,Y,0,.012
KESIZE,ALL,A/8                     ! ASSIGN ELEMENT SIZE AT KEYPOINTS
KSEL,INVE
KESIZE,ALL,OB/5
KSEL,ALL

CSYS,0
K,22,.08
K,23,.08,.08
K,24,0,.08

NUMSTR,LINE,31
L,2,22
L,3,23
L,4,24
L,22,23
L,23,24

LSEL,S,,,31,33
LESIZE,ALL,,,1
LSEL,S,,,34,35
LESIZE,ALL,,,5
LSEL,ALL

NUMSTR,AREA,11
A,2,22,23,3
A,3,23,24,4

TYPE,2
MAT,1
AMESH,11,12                        ! MESH INFINITE ELEMENTS
ALLSEL,ALL

ASEL,S,AREA,,3,7,1
MSHK,0                             ! FREE AREA MESH
MSHA,0,2D                          ! USING QUADS
TYPE,1
AMESH,ALL                          ! MESH 2D ELECTROMAGNETIC SOLID ELEMENTS
ALLSEL,ALL
LSEL,S,LINE,,34,35
NSLL,S,1
SF,ALL,INF                         ! EXTERIOR SURFACE FLAG FOR INFIN110 ELEMENTS
ALLSEL,ALL
FINISH

CDWRITE,db,vm188,cdb
FINISH
*IF,_EXIT1,EQ,1,THEN
/EXIT,NOSAVE
*ENDIF

*ELSE
CDREAD,db,vm188,cdb
*ENDIF
FINISH

/SOLU
ANTYPE,STATIC                      ! STATIC MAGNETICS ANALYSIS
ESEL,S,MAT,,2                      ! SELECT CONDUCTOR ELEMENTS
BFE,ALL,JS,,,,1E6                  ! APPLY CURRENT DENSITY TO CONDUCTOR
ESEL,ALL
SOLVE
FINISH

/POST1
ESEL,S,MAT,,2   
NSLE
ESLN
EMFT                               ! SUM UP MAXWELL FORCES ACTING ON THE CONDUCTOR
FMXW=_FXSUM*2 
ALLS   
FINISH

/PREP7
KEYOP,1,8,1                       ! CALCULATE LORENTZ FORCE
FINISH

/SOLU
SOLVE
FINISH

/POST1
ESEL,S,MAT,,2
NSLE
EMFT                               ! SUM UP LORENTZ FORCES ACTING ON THE CONDUCTOR
FJxB=_FXSUM*2
ALLS
FINI

/POST1
PATH,MAXWELL,4,,48                 ! DEFINE PATH WITH NAME = "MAXWELL"
PPATH,1,,.012,0,0                  ! DEFINE PATH POINTS BY LOCATION
PPATH,2,,.012,.012,0
PPATH,3,,0,.012,0
PPATH,4,,0,0,0
/PBC,PATH,1                        ! ACTIVATE PATH B.C. FOR DISPLAY
PLF2D            

*DIM,LABEL,CHAR,2,2
*DIM,VALUE,,2,3
LABEL(1,1) = 'F (LRNZ) ','F (MAXW) '
LABEL(1,2) = 'N/m','N/m'
*VFILL,VALUE(1,1),DATA,-9.684E-3,-9.684E-3
*VFILL,VALUE(1,2),DATA,FJxB,FMXW
*VFILL,VALUE(1,3),DATA,ABS(FJxB/(9.684E-3)),ABS(FMXW/(9.684E-3))
/COM
/OUT,vm188,vrt
/COM,PLANE233
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.6,'  ',F18.6,'   ',1F15.3)
/COM,-----------------------------------------------------------
/OUT
FINISH
*LIST,vm188,vrt

/COM,ANSYS MEDIA REL. 2023R1 (11/04/2022) REF. VERIF. MANUAL: REL. 2023R1
/VERIFY,VM318
/TITLE,VM318,SIMULATION OF NECKING OF A CIRCULAR BAR
/COM,
/COM, REFERENCE: SIMO, J.C., ALGORITHMS FOR STATIC AND DYNAMIC MULTIPLICATIVE
/COM, PLASTICITY THAT PRESERVE THE CLASSICAL RETURN MAPPING SCHEMES OF THE
/COM, INFINITESIMAL THEORY
/COM, COMPUTER METHODS IN APPLIED MECHANICS AND ENGINEERING. 1992, 99: 61-112
/COM,

/OUT,vm318_SCRATCH

/COM, 2D MODEL
/PREP7
R0=6.413                  ! RADIUS OF THE BAR
L=53.334                  ! LENGTH OF THE BAR
R1=R0*0.982
R2=R1+(R0-R1)/3
BK=164.21                 ! BULK MODULUS
SK=80.1938                ! SHEAR MODULUS
SY=0.45
SY8=0.715
DLT=16.93
H=0.12924
DELL=7.0                  ! DISPLACMENT
K,1
K,2,R1
K,3,R2,L/6
K,4,0,L/6
A,1,2,3,4

K,5,R0,L/2
K,6,,L/2
A,4,3,5,6
AGLUE,ALL

ET,1,182
KEYOPT,1,1,1              ! REDUCED INTEGRATION
KEYOPT,1,3,1              ! AXISYMMETRICAL
KEYOPT,1,6,1              ! U-P

LESIZE,3,,,6
LESIZE,2,,,8
LESIZE,5,,,4
MSHAPE,0,2D
MSHKEY,1
AMESH,ALL

! HYPER ELASTIC MATERIAL - NEO-HOOK
TB,HYPER,1,,,NEO
TBDATA,1,SK,2/BK

! PLASTIC MATERIAL WITH ISOTROPIC HARDENING
TB,NLISO,1,,,VOCE
TBDATA,1,SY,H,SY8-SY,DLT

NSEL,S,LOC,X,0
D,ALL,UX
NSEL,S,LOC,Y,0
D,ALL,UY
NSEL,S,LOC,Y,L/2
D,ALL,UY,DELL
ALLSEL

FINISH
/SOLU
TIME,1
NLGEOM,ON
NSUBST,50,50,50
OUTRES,ALL,ALL
SOLV
FINISH

/POST26
NSOL,2,2,U,X,
PLVAR,2,
PRVAR,2

/POST1
SET,LAST
*GET,U2_X,NODE,2,U,X
*DIM,LABEL,CHAR,1,1
*DIM,TARGET,,1,1
*DIM,ANSYS,,1,1
*DIM,RATIO,,1,1
LABEL(1)='U_X'
ANSYS(1)=U2_X
TARGET(1)=-3.740
RATIO(1)=ABS(ANSYS(1)/TARGET(1))
SAVE, TABLE_1

FINISH
/CLEAR, NOSTART
/COM,
/COM, 3D MODEL
/PREP7
R0=6.413                  ! RADIUS OF THE BAR
L=53.334                  ! LENGTH OF THE BAR
R1=R0*0.982
R2=R1+(R0-R1)/3
BK=164.21                 ! BULK MODULUS
SK=80.1938                ! SHEAR MODULUS
SY=0.45
SY8=0.715
DLT=16.93
H=0.12924
DELL=7.0                  ! DISPLACMENT

CONE,R1,R2,0,L/6,90
CONE,R2,R0,L/6,L/2,90

VGLUE,ALL

ET,1,185
KEYOPT,1,2,1              ! REDUCED INTEGRATION
KEYOPT,1,6,1              ! U-P

LESIZE,6,,,6
LESIZE,8,,,8
LESIZE,19,,,4
MSHAPE,0,3D
MSHKEY,1
VMESH,ALL

! HYPER ELASTIC MATERIAL - NEO-HOOK
TB,HYPER,1,,,NEO
TBDATA,1,SK,2/BK

! PLASTIC MATERIAL WITH ISOTROPIC HARDENING
TB,NLISO,1,,,VOCE
TBDATA,1,SY,H,SY8-SY,DLT

NSEL,S,LOC,X,0
D,ALL,UX
NSEL,S,LOC,Y,0
D,ALL,UY
NSEL,S,LOC,Z,0
D,ALL,UZ
NSEL,S,LOC,Z,L/2
D,ALL,UZ,DELL
ALLSEL

FINISH
/SOLU
TIME,1
NLGEOM,ON
NSUBST,50,50,50
OUTRES,ALL,ALL
SOLV
FINISH

/POST26
NSOL,2,2,U,X,
PLVAR,2,
PRVAR,2

/POST1
SET,LAST
*GET,U2_X,NODE,2,U,X
*DIM,LABEL,CHAR,1,1
*DIM,TARGET,,1,1
*DIM,ANSYS,,2,1
*DIM,RATIO,,2,1
LABEL(1)='U_X'
ANSYS(2)=U2_X
TARGET(1)=-3.740
RATIO(2)=ABS(ANSYS(2)/TARGET(1))
SAVE, TABLE_2
FINISH
/CLEAR, NOSTART

RESUME, TABLE_1
/COM,
/OUT,vm318,vrt
/COM,-------------------- vm318 RESULTS COMPARISON -------------------------
/COM,
/COM,               |  TARGET  |  Mechanical APDL  |   RATIO
/COM, 2D MODEL
*VWRITE,LABEL(1,1),TARGET(1),ANSYS(1),RATIO(1)
(1X,A10,'    ',F9.3,'     ',F9.3, '        ',F9.4)
/NOPR
RESUME, TABLE_2
/COM, 3D MODEL
*VWRITE,LABEL(1,1),TARGET(1),ANSYS(2),RATIO(2)
(1X,A10,'    ',F9.3,'     ',F9.3, '        ',F9.4)
/COM,-----------------------------------------------------------------------
/OUT
FINISH
*LIST,vm318,vrt

/COM,ANSYS MEDIA REL. 2023R1 (11/04/2022) REF. VERIF. MANUAL: REL. 2023R1
/VERIFY,VM148
/PREP7
/TITLE, VM148, BENDING OF A PARABOLIC BEAM 
C***    STR. OF MATL., TIMOSHENKO, PART 1, 3RD ED., PAGE 210
C***    USING 3-D SOLID95
ANTYPE,STATIC
ET,1,SOLID95                   ! 20 NODE SOLID ELEMENT
MP,EX,1,30E6
MP,EY,1,30E6
MP,EZ,1,30E6
MP,GXY,1,1.5E8
MP,GYZ,1,1.5E8
MP,GXZ,1,1.5E8
MP,NUXY,1,0
MP,NUYZ,1,0
MP,NUXZ,1,0
N,1,.05,SQRT(.05/4)            ! NODE CLOSE TO TIP 
*DO,I,2,9
 N,I,(I-1)/2,SQRT((I-1)/8)     ! NEXT EIGHT NODES
*ENDDO
N,11
N,19,4
FILL                           ! NODES ALONG THE AXIS
NSYMM,Y,20,1,9                 ! REFLECT NODES IN Y DIRECTION
NGEN,3,30,1,29,1,,,-.1         ! GENERATE NODES ALONG THICKNESS
E,1,3,23,21,61,63,83,81        
EMORE,2,13,22,11,62,73,82,71
EMORE,31,33,53,51
EGEN,4,2,1
F,11,FY,-500,,71,60            ! APPLY END LOAD AT TIP NODES
NSEL,S,LOC,X,4
D,ALL,ALL                      ! FIX NODES AT SUPPORTED END
NSEL,ALL
OUTPR,,1
SAVE
FINISH
/SOLU    
/OUT,SCRATCH
SOLVE
FINISH
*CREATE,RES3D,MAC              ! CREATE MACRO TO RETRIEVE RESULTS
/POST1
/OUT,
*GET,UY,NODE,11,U,Y
*DIM,LABEL,CHAR,1,2
*DIM,VALUE,,1,3
LABEL(1,1) = 'Y DELF '
LABEL(1,2) = ' in'
*VFILL,VALUE(1,1),DATA,-.01067
*VFILL,VALUE(1,2),DATA,UY
*VFILL,VALUE(1,3),DATA,ABS(UY/.01067)
FINISH
*END
RES3D                                   !EXECUTE MACRO TO RETRIEVE RESULTS
SAVE,TABLE_1

/CLEAR, NOSTART                         !CLEAR DATABASE FOR SECOND SOLUTION
/TITLE, VM148, BENDING OF A PARABOLIC BEAM 
C***    USING 3-D SOLID186
/PREP7
RESUME                                  ! RESUME DATABASE
ET,1,SOLID186                           ! ANALYZE AGAIN USING 3-D SOLID186 
FINISH                          
/SOLU				
/OUT,SCRATCH
SOLVE				
/OUT,
FINISH  			
RES3D                                   !EXECUTE MACRO TO RETRIEVE RESULTS
SAVE,TABLE_2

/NOPR
RESUME,TABLE_1
/GOPR
/COM
/OUT,vm148,vrt
/COM,------------------- VM148 RESULTS COMPARISON --------------
/COM,
/COM,                 |     TARGET   |     Mechanical APDL   |   RATIO
/COM,
/COM, SOLID95
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F12.5,'  ',F14.5,'   ',1F15.3)
/NOPR
RESUME,TABLE_2
/GOPR
/COM,
/COM, SOLID186
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F12.5,'  ',F14.5,'   ',1F15.3)
/COM,-----------------------------------------------------------
/OUT
FINISH
*LIST,vm148,vrt

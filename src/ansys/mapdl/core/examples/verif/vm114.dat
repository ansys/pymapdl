/COM,ANSYS MEDIA REL. 2023R1 (11/04/2022) REF. VERIF. MANUAL: REL. 2023R1
/VERIFY,VM114
JPGPRF,500,100,1             ! MACRO TO SET PREFS FOR JPEG PLOTS
/SHOW,JPEG
/PREP7
/TITLE, VM114, TEMPERATURE RESPONSE TO A LINEARLY RISING SURFACE TEMPERATURE
C***    CONDUCTION HEAT TRANSFER, SCHNEIDER, 2ND. PRINTING, PAGE 274, ART 11-2
ANTYPE,TRANS
ET,1,LINK33         ! HEAT CONDUCTING BAR
R,1,1               ! UNIT AREA ASSUMED
MP,KXX,1,10
MP,DENS,1,500
MP,C,1,0.2
K,1
K,2,.3              ! LENGTH OF MODEL FROM SURFACE
L,1,2
LESIZE,1,,,8,3      ! NON-UNIFORM MESH WITH 8 DIVISIONS
LMESH,1
FINISH
/SOLU    
AUTOTS,ON
OUTPR,,LAST 
OUTRES,,ALL
TIME,(2/60)         ! TIME PERIOD OF 2 MIN. CONVERTED TO HR.
NSUBST,20,20,1
DK,1,TEMP,120       ! TEMPERATURE SPECIFICATION AT KEY POINT 1
KBC,0
TINTP,,,,0.5,0.5,0.2,,,
/OUT,vm114_SCRATCH
SOLVE
/OUT
FINISH
/POST26
NSOL,2,1,TEMP       ! TEMPERATURE HISTORY AT NODES NEAR SURFACE
NSOL,3,3,TEMP
NSOL,4,4,TEMP
NSOL,5,5,TEMP
NSOL,6,6,TEMP
PRVAR,2,3,4,5,6     ! PRINT NODAL TEMPERATURE VARIATION WITH TIME
/GRID,1
/AXLAB,Y,TEMP
PLVAR,2,4,5,6,3     ! DISPLAY NODAL TEMPERATURE HISTORIES
FINISH
/POST1
SET,1
NSORT,TEMP          ! SORT NODES BY DESCENDING TEMPERATURE VALUES
NLIST               ! LIST NODES TO VERIFY X-COORDINATE LOCATION
PRNSOL,TEMP         ! PRINT NODAL TEMPERATURE DISTRIBUTION
*GET,TN1,NODE,1,TEMP
*GET,TN3,NODE,3,TEMP
*GET,TN4,NODE,4,TEMP
*GET,TN5,NODE,5,TEMP
*GET,TN6,NODE,6,TEMP
*GET,TN2,NODE,2,TEMP
*DIM,LABEL,CHAR,6,2
*DIM,VALUE,,6,3
LABEL(1,1) = 'T,F (NOD','T,F (NOD','T,F (NOD','T,F (NOD','T,F (NOD','T,F (NOD'
LABEL(1,2) = 'E 1)    ','E 3)    ','E 4)    ','E 5)    ','E 6)    ','E 2)    '
*VFILL,VALUE(1,1),DATA,120,79.32,46.62,23.44,9.51,0
*VFILL,VALUE(1,2),DATA,TN1,TN3,TN4,TN5,TN6,TN2
*VFILL,VALUE(1,3),DATA,ABS(TN1/120),ABS(TN3/79.32),ABS(TN4/46.62),ABS(TN5/23.44)
*VFILL,VALUE(5,3),DATA,ABS(TN6/9.51),0 
/COM
/OUT,vm114,vrt
/COM,------------------- VM114 RESULTS COMPARISON -------------
/COM,
/COM,                 |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.2,'  ',F12.2,'   ',1F15.3)
/COM,----------------------------------------------------------
/OUT
FINISH
*LIST,vm114,vrt


/COM,ANSYS MEDIA REL. 2023R1 (11/04/2022) REF. VERIF. MANUAL: REL. 2023R1
/VERIFY,VM61
/PREP7
MP,PRXY,,0.3
/TITLE, VM61, LONGITUDINAL VIBRATION OF A FREE-FREE ROD
C***VIBRATION THEORY AND APPLICATIONS, THOMSON, 2ND PRINTING, PAGE 269, EX 8.3-1
ANTYPE,MODAL                     ! MODE-FREQUENCY ANALYSIS
ET,1,BEAM188
SECT,1,BEAM,ASEC
SECD,1,1,,1,,1
MODOPT,LANB,3                    ! SELECT THE BLOCK LANCZOS EIGENSOLVER
MP,EX,1,3E7
MP,DENS,1,73E-5
K,1 
K,2,800
L,1,2
ESIZE,,11
LMESH,1
OUTPR,BASIC,1
D,ALL,UY,,,,,ROTZ,UZ,ROTY,ROTX   ! ALLOW UX DOF'S ONLY
FINISH
/out,scratch
/SOLU    
SOLVE
FINISH
/out
/SOLU
*GET,FREQ1,MODE,1,FREQ
*GET,FREQ2,MODE,2,FREQ
*GET,FREQ3,MODE,3,FREQ
*DIM,LABEL,CHAR,3,2
*DIM,VALUE,,3,3
LABEL(1,1) = '      f1','      f2','      f3'
LABEL(1,2) = ',  (Hz) ',',  (Hz) ',',  (Hz) '
*VFILL,VALUE(1,1),DATA,0,126.70,253.40
*VFILL,VALUE(1,2),DATA,FREQ1,FREQ2,FREQ3
*VFILL,VALUE(1,3),DATA,0,ABS(FREQ2/126.70 ),ABS(FREQ3/253.40 )
/COM
/OUT,vm61,vrt
/COM,------------------- VM61 RESULTS COMPARISON --------------
/COM,
/COM,                 |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.2,'  ',F14.2,'   ',1F15.3)
/COM,----------------------------------------------------------

/OUT
FINISH
*LIST,vm61,vrt

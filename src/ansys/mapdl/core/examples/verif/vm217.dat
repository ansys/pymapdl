/COM,ANSYS MEDIA REL. 150 (11/8/2013) REF. VERIF. MANUAL: REL. 150
/VERIFY,VM217
/TITLE,VM217, PORTAL FRAME UNDER SYMMETRIC LOADING
! N. J. HOFF, THE ANALYSIS OF STRUCTURES, PG 115
JPGPRF,500,100,1             ! MACRO TO SET PREFS FOR JPEG PLOTS
/SHOW,JPEG
W=-500
A=400
EX=30E6
IO=20300
MROT=((W*A*A*A)/(EX*IO))*(1/27) !CALCULATE MAX ROT TARGET VALUE
BNDM=(W*A*A)*(19/54)		!CALCULATE MAX BEND MOMENT
/GRA,POWER
/GST,ON
/TRIAD,OFF
/ESHAPE,1
/PREP7
ET,1,BEAM188
ET,2,BEAM189
SECTYPE,1,BEAM,I
SECDATA,16.655,16.655,36.74,1.68,1.68,.945
SECPLOT,1
C = 1.49535
SECTYPE,2,BEAM,I
SECDATA,C*16.655,C*16.655,C*36.74,C*1.68,C*1.68,C*.945
SECPLOT,2
MP,EX,1,30E6
MP,NUXY,1,0.3
MP,EX,2,30E6
MP,NUXY,2,0.3
A = 400
COLUMDIV = 4
SPANDIV = 16
K,1
K,2,,A
K,3,2*A
K,4,2*A,A
L,2,1
L,3,4
L,4,2
LSEL,,,,1
LATT,,,,,3
LSEL,,,,2
LATT,,,,,1
LSEL,,,,3
LATT,,,,,1
ALLSEL
LESIZE,1,,,COLUMDIV
LESIZE,2,,,COLUMDIV
LESIZE,3,,,SPANDIV
TYPE,1
SECNUM,1
REAL,1
LMESH,1,2
TYPE,2
SECNUM,2
REAL,2
LMESH,3
ALLSEL
DK,1,ALL
DK,3,ALL
LSEL,,,,3
ESLL
SFBEAM,ALL,1,PRESS,-500,-500
ALLSEL
/VIEW,1,1,1,1
/ANG,1
EPLOT
FINISH

/SOLUTION
SOLVE
FINISH
/POST1
ETABLE,SMIS2,SMISC,2
ETABLE,SMIS15,SMISC,15
ETABLE,SMIS5,SMISC,5
ETABLE,SMIS18,SMISC,18
PRNSOL,DOF
PRETAB,SMIS2,SMIS15,SMIS5,SMIS18
/VIEW,1,,,1
PLLS,SMIS2,SMIS15
PLDISP,0
ESEL,S,ELEM,,16
ETAB,B_S,SMISC,15
ESEL,ALL
*GET,A,NODE,48,ROT,Z,	!GET Mechanical APDL VALUE FOR MAX ROT
*GET,B,ELEM,16,ETAB,B_S	!GET Mechanical APDL VALUE FOR MAX BEN MOMENT
/FORMAT,10,E,16

*DIM,LABEL,CHAR,2
*DIM,VALUE,,2,8
LABEL(1) = 'MAX.ROT','BEND.MOM.'
*VFILL,VALUE(1,1),DATA,ABS(MROT),ABS(BNDM)
*VFILL,VALUE(1,2),DATA,ABS(A),ABS(B)
*VFILL,VALUE(1,3),DATA,ABS(A / MROT),ABS(B / BNDM)

/COM
/OUT,vm217,vrt
/COM,------------------- VM217 RESULTS COMPARISON ---------------
/COM,
/COM,                 |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
*VWRITE,LABEL(1),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,'      ',E10.3,'     ',E14.3,'      ',1F15.3)
/COM,
/COM,-----------------------------------------------------------
/OUT
FINISH
*LIST,vm217,vrt

/COM,ANSYS MEDIA REL. 2023R1 (11/04/2022) REF. VERIF. MANUAL: REL. 2023R1
/VERIFY,VM190
JPGPRF,500,100,1             ! MACRO TO SET PREFS FOR JPEG PLOTS
/SHOW,JPEG
/out,scratch
/COM, CREATING CDB FILE TO STORE MESH DATA
/COM,
_GEOMGEN = 0            ! SET TO 1 TO WRITE CDB FILE
*IF,_GEOMGEN,EQ,1,THEN
/PREP7
smrt,off
/TITLE, VM190, FERROMAGNETIC INDUCTOR
/COM,   CHAPMAN, "ELECTRIC MACHINERY FUNDAMENTALS", MCGRAW-HILL,
/COM,   1985, EXAMPLE 1-1, PG. 14
ET,1,SOLID98,10               ! 10-NODE TETRAHEDRAL, MAG OPTION
ET,2,INFIN47                  ! INFINITE ELEMENT
ET,3,SOURC36                  ! CURRENT ELEMENT
EMUNIT,MKS                    ! MKS UNITS
MP,MURX,1,1                   ! RELATIVE PERMEABILITY OF AIR
MP,MURX,2,2500                ! RELATIVE PERMEABILITY OF IRON
R,1,1,200,.02,.25             ! COIL DIMENSIONS AND CURRENT
LOCAL,11,0,-.325              ! SHIFT ORIGIN TO CENTER OF MODEL
WPCSYS,,11                    ! WORKING PLANE FOR SOLID MODELLING
N,1,.125,0,0                  ! CREATE NODES TO LOCATE COIL
N,2,.235,0,0
N,3,.235,.235,0
TYPE,3
E,2,3,1                       ! DEFINE COIL 
BLOCK,.05,.20,0,.05,0,.45     ! CREATE SOLID MODEL OF IRON
BLOCK,.20,.50,0,.05,.30,.45   
BLOCK,.50,.60,0,.05,0,.45
VGLUE,ALL   
BLOCK,0,.65,0,.10,0,.50       ! CREATE SOLID MODEL OF AIR
VOVLAP,ALL                    ! OVERLAP AIR AND IRON
ASEL,S,AREA,,8,10   
ASEL,A,AREA,,4  
MSHK,0                        ! FREE MESH 
MSHA,1,3D                     ! USING TETS 
MSHA,1,2D                     ! USING TRIS 
ESIZE,.10
TYPE,2
AMESH,ALL                     ! MESH EXTERIOR BOUNDARY WITH INFIN47
TYPE,1
MAT,2
VMESH,4,6                     ! MESH IRON
MAT,1
VMESH,2                       ! MESH AIR
NSEL,S,LOC,Z,0
D,ALL,MAG,0                   ! SET FLUX-NORMAL SYMMETRY CONDITION
NSEL,ALL
ALLSEL
cdwrite,db,vm190,cdb
FINISH
*ELSE
/PREP7
cdread,db,vm190,cdb
*ENDIF
FINISH
/SOLU
ANTYPE,STATIC                 ! STATIC MAGNETIC FIELD ANALYSIS
MAGSOLV,4
FINISH
/out,scratch
/POST1
PATH,IRON,7,,48               ! DEFINE PATH WITH NAME = "IRON"
PPATH,1,,-.2,0,0             ! DEFINE PATHS POINTS BY 
PPATH,2,,-.2,0,.20
PPATH,3,,-.2,0,.375
PPATH,4,,.025,0,.375
PPATH,5,,.225,0,.375
PPATH,6,,.225,0,.20
PPATH,7,,.225,0,0
*CREATE,MAC                     ! CREATE MACRO FOR MMF CALCULATION
PDEF,HX,H,X                     ! INTERPOLATE H FIELD TO PATH
PDEF,HY,H,Y
PDEF,HZ,H,Z
PVECT,TANG,TX,TY,TZ             ! INTERPOLATE UNIT TANGENTS
PDOT,D,HX,HY,HZ,TX,TY,TZ        ! PERFORM DOT PRODUCT
PCALC,INTG,MMF,D,S              ! INTEGRATE OVER PATH
*GET,MMF,PATH,,LAST,MMF         ! GET MMF
MMF=MMF*2                       ! MULTIPLY BY 2 FOR SYMMETRY
*STATUS,MMF
*END
ESEL,S,MAT,,2                   ! SELECT IRON ELEMENTS
*USE,MAC                        ! USE MACRO TO CALCULATE MMF
/VIEW,,1,-3,1
/VUP,1,Z
/TRIAD,OFF
/PBC,PATH,1                     ! SHOW PATH ON DISPLAY
/AUTO
WPSTYL,,,,,,,OFF
NSLE,S                          ! SELECT NODES ATTACHED TO IRON
/COM                            *** THE FOLLOWING ANNOTATION COMMANDS ARE ***
/COM                            *** TYPICALLY GENERATED INTERACTIVELY ***
/ANUM,1,12,-.28056,.71310       ! ANNOTATION NUMBER, TYPE, AND HOT SPOT
/LSYMBOL,-.282,.511,269,4,1.0 ! ANNOTATION SYMBOL DEFINITION - ARROW
/LINE,-.282,.511,-.279,.915     ! ANNOTATION LINE DEFINITION
/ANUM,2,4,-.21690,.91150
/LINE,-.282,.911,-.151,.911     
/ANUM,3,1.14734,.93021
/TLABEL,-.133,.930,CONTOUR PATH FOR  ! ANNOTATION LOCATION AND TEXT
/ANUM,4,1,.91097E-01,.87406
/TLABEL,-.136,.874,LINE INTEGRAL
/ANNOT,ON
/TITLE, VM190: MAGNETIC FLUX DENSITY
PLNSOL,B,SUM                    ! DISPLAY FLUX DENSITY IN IRON
FINISH
*DIM,LABEL,CHAR,1,2
*DIM,VALUE,,1,3
LABEL(1,1) = 'MMF DRP '
LABEL(1,2) = 'A-t'
*VFILL,VALUE(1,1),DATA,200
*VFILL,VALUE(1,2),DATA,MMF
*VFILL,VALUE(1,3),DATA,ABS(MMF/200),
/COM
/OUT,vm190,vrt
/COM,------------------- VM190 RESULTS COMPARISON --------------
/COM,
/COM,                 |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.2,'  ',F15.2,'   ',1F15.3)
/COM,-----------------------------------------------------------
/OUT
FINISH
/DELETE,MAC
/DELETE,magsolv,out
*LIST,vm190,vrt

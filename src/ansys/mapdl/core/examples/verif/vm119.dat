/COM,ANSYS MEDIA REL. 150 (11/8/2013) REF. VERIF. MANUAL: REL. 150
/VERIFY,VM119
/PREP7
/TITLE, VM119, CENTERLINE TEMP. OF AN ELECTRICAL WIRE
C***      HEAT, MASS AND MOMENTUM TRANS., ROHSENOW AND CHOI, 2ND. PR., PAGE 106,
C***      USING PLANE223 ELEMENTS (PLANE ELEMENTS)
!
ANTYPE,STATIC                 ! THERMAL (ELECTRICAL) ANALYSIS
ET,1,PLANE223,110,,1          ! AXISYMMETRIC ELEMENTS
MP,KXX,1,13                   ! CONDUCTIVITY
MP,RSVX,1,8.983782E-8         ! RESISTIVITY
N,1
N,6,(.375/12)
FILL
NGEN,2,10,1,6,1,,1
E,11,1,2,12
EGEN,5,1,1
CP,1,TEMP,1,11                ! COUPLING TO ENSURE AXIAL SYMMETRY
CPSGEN,6,1,1
CP,7,VOLT,1,2,3,4,5,6		! DEFINE GROUND ELECTRODE
CP,8,VOLT,11,12,13,14,15,16	! DEFINE CURRENT LOAD ELECTRODE
D,1,VOLT,0				! APPLY ZERO POTENTIAL TO GROUND ELECTRODE
F,11,AMPS,-1000*3.415		! APPLY TOTAL CURRENT (X CONVERSION FACTOR) TO THE MASTER NODE
NSEL,S,LOC,X,(0.375/12)
SF,ALL,CONV,5,70
NSEL,ALL
FINISH
/SOLU    
OUTPR,ALL,1                   ! USE TIME STEP OPTIMIZATION
OUTPR,VENG,NONE
KBC,1                         ! STEP BOUNDARY CONDITIONS
SOLVE
FINISH
/POST1
*GET,TEMP,NODE,6,TEMP         ! GET TEMPERATURE AT SURFACE NODE
PI=2*ASIN(1)
AREA=2*PI*(0.375/12)          ! COMPUTE AREA OF OUTER FACE (360 DEG)
HRATE=AREA*5.0*(TEMP-70)      ! TOTAL HEAT DISSIPATION RATE
PRNSOL,TEMP                   ! PRINT NODAL TEMPERATURES
*GET,TCL,NODE,1,TEMP
*DIM,LABEL,CHAR,3,2
*DIM,VALUE,,3,3
LABEL(1,1) = 'T CL,   ','T S,    ','q,   BTU'
LABEL(1,2) = 'DEGREE F','DEGREE F','/hr     '
*VFILL,VALUE(1,1),DATA,419.9,417.9,341.5
*VFILL,VALUE(1,2),DATA,TCL,TEMP,HRATE
*VFILL,VALUE(1,3),DATA,ABS(TCL/419.9) ,ABS( TEMP/417.9 ),ABS (HRATE/341.5)
SAVE,TABLE1
FINISH                       
!
/CLEAR, NOSTART ! CLEAR DATABASE FOR SOLID69 MODEL

/PREP7
/TITLE, VM119, CENTERLINE TEMP. OF AN ELECTRICAL WIRE
C***      USING SOLID226 ELEMENTS (SOLID ELEMENTS)
ANTYPE,STATIC                 ! THERMAL (ELECTRICAL) ANALYSIS
ET,1,SOLID226,110
MP,KXX,1,13                   ! CONDUCTIVITY
MP,RSVX,1,8.983782E-8         ! RESISTIVITY
CSYS,1                        ! CYLINDRICAL COORDINATE SYSTEM
N,1,1E-10,-5                  ! USE NON-ZERO RADIUS SINCE NODE IS NOT AT THETA=0
N,6,(.375/12),-5
FILL
N,11,1E-10,5
N,16,.03125,5
FILL
NGEN,2,20,1,16,1,,,-1
E,1,2,12,12,21,22,32,32
E,2,3,13,12,22,23,33,32
EGEN,4,1,2
CP,1,TEMP,1,21                ! COUPLING TO ENSURE AXIAL SYMMETRY
CP,2,TEMP,2,12,22,32          ! COUPLING TO ENSURE CIRCUMFERENTIAL SYMMETRY
CPSGEN,5,1,2
NSEL,S,LOC,Z,0
CP,6,VOLT,ALL			! DEFINE GROUND ELECTRODE
NSEL,ALL
NSEL,S,LOC,Z,-1
CP,7,VOLT,ALL			! DEFINE CURRENT LOAD ELECTRODE
NSEL,ALL
D,1,VOLT,0				! APPLY ZERO POTENTIAL TO GROUND ELECTRODE
F,21,AMPS,-1000*3.415/36	! APPLY TOTAL CURRENT CORRESPONDING TO THE 10 DEGREE SECTOR
					! (X CONVERSION FACTOR) TO THE MASTER NODE
NSEL,S,LOC,X,(0.375/12)
SF,ALL,CONV,5,70
NSEL,ALL
FINISH
/SOLU    
OUTPR,ALL,1
OUTPR,VENG,NONE
SOLVE
FINISH
/POST1
*GET,TEMP,NODE,6,TEMP         ! GET TEMPERATURE AT SURFACE NODE
PI=2*ASIN(1)
LENG=2*(0.375/12)*SIN(PI/36)  ! LENGTH ALONG 10 DEG ON OUTER FACE
AREA=LENG*36                  ! COMPUTE AREA OF OUTER FACE (360 DEG)
HRATE=AREA*5.0*(TEMP-70)      ! TOTAL HEAT DISSIPATION RATE
PRNSOL,TEMP                   ! PRINT NODAL TEMPERATURES
*status,parm                       ! SHOW PARAMETER STATUS
*DIM,LABEL,CHAR,3,2
*DIM,VALUE,,3,3
*GET,TCL,NODE,1,TEMP
LABEL(1,1) = 'T CL,   ','T S,    ','q,   BTU'
LABEL(1,2) = 'DEGREE F','DEGREE F','/hr     '
*VFILL,VALUE(1,1),DATA,419.9,417.9,341.5
*VFILL,VALUE(1,2),DATA,TCL,TEMP,HRATE
*VFILL,VALUE(1,3),DATA,ABS(TCL/419.9) ,ABS( TEMP/417.9 ),ABS (HRATE/341.5)
SAVE,TABLE2
!
RESUME,TABLE1
/COM
/OUT,vm119,vrt
/COM,------------------- VM119 RESULTS COMPARISON -------------
/COM,
/COM,                 |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
/COM,
/COM,PLANE223 RESULTS:
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.1,'  ',F12.1,'   ',1F15.3)
/COM,----------------------------------------------------------
/NOPR
RESUME,TABLE2
/GOPR
/COM,
/COM,SOLID226 RESULTS:
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.1,'  ',F12.1,'   ',1F15.3)
/COM,----------------------------------------------------------
/OUT
FINISH
*LIST,vm119,vrt

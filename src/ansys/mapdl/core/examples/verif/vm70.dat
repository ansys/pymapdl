/COM,ANSYS MEDIA REL. 2023R1 (11/04/2022) REF. VERIF. MANUAL: REL. 2023R1
/VERIFY,VM70
/PREP7
MP,PRXY,,0.3
/TITLE, VM70, SEISMIC RESPONSE OF A BEAM STRUCTURE
C***           INTRODUCTION TO STRUCT. DYNAMICS, BIGGS, PAGE 262, ART. 6.4
ET,1,BEAM189
HEIGHT=14
SECT,1,BEAM,ASEC
SECD,273.9726,(1000/3),,(1000/3),,1E-6  ! A = 273.9726, I = (1000/3), H = 14
MP,EX,1,30E6
MP,DENS,1,73E-5
K,1 
K,2,240 
L,1,2
ESIZE,,8 
LMESH,1
NSEL,S,LOC,X,0 
D,ALL,UY
NSEL,S,LOC,X,240 
D,ALL,UX,,,,,UY
NSEL,ALL
D,ALL,ROTY,,,,,ROTX,UZ
FINISH
/out,scratch
/SOLU    
ANTYPE,MODAL                 ! MODE-FREQUENCY ANALYSIS
MODOPT,LANB,3
MXPAND,3,,,YES               ! EXPAND FIRST MODE SHAPE, CALCULATE ELEMENT STRESSES
OUTPR,BASIC,1
SOLVE
/out
*GET,FREQ,MODE,1,FREQ
FINISH
/out,scratch
/SOLU    
ANTYPE,SPECTR                ! SPECTRUM ANALYSIS
SPOPT,SPRS                   ! SINGLE POINT SPECTRUM
SED,,1                       ! GLOBAL Y-AXIS AS SPECTRUM DIRECTION
SVTYP,3                      ! SEISMIC DISPLACEMENT SPECTRUM
FREQ,.1,10                   ! FREQUENCY POINTS FOR SV VS. FREQ TABLE
SV,,.44,.44                  ! SPECTRUM VALUES ASSOCIATED WITH FREQUENCY POINTS
SOLVE
/out
*GET,MCOEF,MODE,1,MCOEF      ! GET MODE COEFFICIENT FOR FIRST FREQUENCY
FINISH
/POST1
SET,1,1,MCOEF                ! SCALE VALUES OF FIRST LOAD STEP
PRNSOL,DOF                   ! PRINT NODAL SOLUTION
PRRSOL,F                     ! PRINT REACTION SOLUTION
ETABLE,MOMENT_Z,SMISC,3
*GET,MZ,ELEM,5,ETAB,MOMENT_Z
STRSS=(-1*MZ*(HEIGHT/2))/(1000/3)
*GET,DEF,NODE,10,U,Y
*DIM,LABEL,CHAR,3,2
*DIM,VALUE,,3,3
LABEL(1,1) = 'f,      ','DEF.    ','SIG MAX,' 
LABEL(1,2) = '      Hz','      in','     psi'
*VFILL,VALUE(1,1),DATA,6.0979,.56,20158
*VFILL,VALUE(1,2),DATA,FREQ,DEF,ABS(STRSS)
*VFILL,VALUE(1,3),DATA,ABS(FREQ/6.0979) ,ABS(DEF/.56), ABS(STRSS/20158)
/COM
/OUT,vm70,vrt
/COM,------------------- VM70 RESULTS COMPARISON --------------
/COM,
/COM,                   |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F12.5,'  ',F16.5,'   ',1F15.3)
/COM,----------------------------------------------------------

/OUT
FINISH
*LIST,vm70,vrt

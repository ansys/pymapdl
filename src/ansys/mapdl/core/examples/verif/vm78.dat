/COM,ANSYS MEDIA REL. 2023R1 (11/04/2022) REF. VERIF. MANUAL: REL. 2023R1
/VERIFY,VM78
/PREP7
/TITLE, VM78, TRANSVERSE SHEAR STRESSES IN A CANTILEVER BEAM
C***          THEORY OF ELASTICITY, TIMESHENKO, PG. 35, ARTICLE 20 
ANTYPE,STATIC
ET,1,SHELL281		     ! 8-NODE LAYERED SHELL; STRESS & STRAIN PRINTOUT
KEYOPT,1,8,2		     ! STORE RESULTS FOR ALL LAYERS
SECTYPE,1,SHELL
SECDATA,0.5,1,0,5,LAYER1
SECDATA,0.5,1,0,5,LAYER2
SECDATA,0.5,1,0,5,LAYER3
SECDATA,0.5,1,0,5,LAYER4
MP,EX,1,30E6
MP,NUXY,1,0
/COM  --- INPUT  FAILURE STRESSES FOR MATERIAL #1 ---
/COM  --- COMPRESSION VALUES ARE LEFT TO DEFAULT  ---
N,1
N,3,,1
FILL
NGEN,11,3,1,3,1,1
E,1,7,9,3,4,8,6,2
EGEN,5,6,-1
NSEL,S,LOC,X                 ! SELECT NODES AT FIXED END AND CONSTRAIN
D,ALL,ALL
NSEL,S,LOC,X,10
CP,1,UZ,ALL                  ! COUPLE FREE END NODES
NSEL,R,LOC,Y
F,ALL,FZ,10000               ! APPLY END FORCE
NSEL,ALL
OUTPR,,1
FINISH
/SOLU    
/OUT,SCRATCH
SOLVE
FINISH
/POST1
/OUT,
ETABLE,SXZ,S,XZ		     ! STRESS ALONG XZ
ETABLE,ILSXZ,SMISC,68        ! SXZ INTERLAMINAR SHEAR STRESS 
ETABLE,ILMX,SMISC,60	     ! INTERLAMINAR SHEAR STRESS VECTOR SUM
*GET,SIGXZ1,ELEM,4,ETAB,SXZ   
*GET,SIGXZ2,ELEM,1,ETAB,ILSXZ
*GET,SIGXZ3,ELEM,1,ETAB,ILMX
FC,1,TEMP,,
FC,1,S,XTEN,25000
FC,1,S,XCMP,-25000,
FC,1,S,YTEN,3000
FC,1,S,YCMP,-3000,
FC,1,S,ZTEN,5000
FC,1,S,ZCMP,-5000,
FC,1,S,XY,500
FC,1,S,YZ,500,
FC,1,S,XZ,500,
FC,1,S,XYCP,
FC,1,S,YZCP,
FC,1,S,XZCP,
ESEL,S,ELEM,,1
NSLE,S
LAYER,FCMAX
*GET,FC3,NODE,9,S,TWSI,
ALLSEL,ALL
*DIM,LABEL,CHAR,4,2
*DIM,VALUE,,4,3
LABEL(1,1) = 'SIGXZ,ps','SIGXZ,ps','SIGXZ,ps','FC3MAX ('
LABEL(1,2) = 'i(Z=H/2)','i(Z=H/4)','i(Z= 0 )','FCMX)	'
*VFILL,VALUE(1,1),DATA,0,5625,7500,225
*VFILL,VALUE(1,2),DATA,SIGXZ1,SIGXZ2,SIGXZ3,FC3
*VFILL,VALUE(1,3),DATA,0,ABS(SIGXZ2/5625),ABS(SIGXZ3/7500),ABS(FC3/225)
/COM
/OUT,vm78,vrt
/COM,------------------- VM78 RESULTS COMPARISON ---------------

/COM,
/COM,                 |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.1,'  ',F14.1,'   ',1F15.3)
/COM,-----------------------------------------------------------
/OUT
FINISH
*LIST,vm78,vrt

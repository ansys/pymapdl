/COM,ANSYS MEDIA REL. 2023R1 (11/04/2022) REF. VERIF. MANUAL: REL. 2023R1
/VERIFY,VM76
/PREP7  
JPGPRF,500,100,1             ! MACRO TO SET PREFS FOR JPEG PLOTS
/SHOW,JPEG
/TITLE, VM76, HARMONIC RESPONSE OF A GUITAR STRING
/COM, REFERENCE: BLEVINS, FORMULAS FOR NAT. FREQ. AND MODE SHAPE, TABLE 7-1.
ANTYPE,STATIC       ! STATIC ANALYSIS, PRESTRESS
ET,1,LINK180        ! TWO-DIMENSIONAL SPAR
SECTYPE,1,LINK
SECDATA,50671E-12   ! CROSS-SECTIONAL AREA OF STRING
MP,EX,1,190E9       ! MATERIAL, STAINLESS STEEL
MP,DENS,1,7920
N,1                 ! DEFINE NODES
N,31,.71
FILL
E,1,2               ! DEFINE ELEMENTS
EGEN,30,1,1
D,1,ALL             ! BOUNDARY CONDITIONS AND LOADING
D,2,UY,,,31
D,ALL,UZ
F,31,FX,84
FINISH
/OUT,SCRATCH
/SOLU
RESCONTROL,LINEAR,LAST,LAST    
OUTPR,BASIC,1
SOLVE
FINISH
/SOLU    
ANTYPE,STATIC,RESTART,,,PERTURB
PERTURB,MODAL,,,PARKEEP
SOLVE,ELFORM

MODOPT,LANB,6       ! LANB EXTRACTION METHOD, 6 FREQ.
DDEL,2,UY,30
SOLVE
/OUT,
*GET,FREQ,MODE,1,FREQ
FINISH
PARSAV,ALL
/DELETE,,rstp
/OUT,SCRATCH
/SOLU    
ANTYPE,STATIC,RESTART,,,PERTURB
PERTURB,HARMONIC,,,PARKEEP
SOLVE,ELFORM

PARRES,CHANGE
HROPT,FULL
DDEL,2,UY,30
HROUT,OFF           ! AMPLITUDE, PHASE ANGLE PRINTOUT
F,8,FY,-1           ! FORCE AT X=.1657, NEAR QUARTER POINT
KBC,1               ! STEP CHANGE FORCE
HARFRQ,,2000        ! OBTAIN FREQUENCY EVERY EIGHT HERTZ
NSUBST,250
OUTPR,,NONE
OUTRES,,1
SOLVE
FINISH
/OUT,
/POST26             ! TIME-HISTORY POSTPROCESSOR
FILE,,rstp          
NSOL,2,16,U,Y,DISP  ! RETRIEVE STRING MIDPOINT DISPLACEMENT RESPONSE
PRVAR,2
/AXLAB,Y,AMPL
PLCPLX,0            ! DISPLAY AMPLITUDE OF COMPLEX VARIABLE (DEFAULT)
PLVAR,2
*DIM,LABEL,CHAR,1,2
*DIM,VALUE,,1,3
LABEL(1,1) = '      f,'
LABEL(1,2) = ' Hz     '
*VFILL,VALUE(1,1),DATA,322.2
*VFILL,VALUE(1,2),DATA,FREQ
*VFILL,VALUE(1,3),DATA,ABS(FREQ/322.2 )
/COM
/OUT,vm76,vrt
/COM,------------------- VM76 RESULTS COMPARISON --------------
/COM,
/COM,                 |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.1,'  ',F14.1,'   ',1F15.3)
/COM,----------------------------------------------------------
/COM,
/COM,-----------------------------------------------------------
/COM,NOTE: THERE ARE VERIFIED RESULTS IN VM76  NOT CONTAINED IN
/COM,THIS TABLE
/COM,-----------------------------------------------------------
/OUT
FINISH
*LIST,vm76,vrt

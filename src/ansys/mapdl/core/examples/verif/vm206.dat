/COM,ANSYS MEDIA REL. 150 (11/8/2013) REF. VERIF. MANUAL: REL. 150
/VERIFY,VM206
/PREP7
SMRT,OFF
/TITLE,VM206,STRANDED COIL MODEL, WITH VOLTAGE EXCITATION
/COM,   REF: BOAST "ELECTRIC AND MAGNETIC FIELDS", PG. 247, EQN. 12.18
ET,1,53,,,1         ! AIR
ET,2,53,2,,1        ! VOLTAGE FORCED COIL
ET,3,110,,,1        ! FAR-FIELD

EMUNIT,MKS
MP,MURX,1,1
MP,MURX,2,1
MP,RSVX,2,3.00E-8   ! RESISTIVITY OF COIL

S=.02		    ! COIL WIDTH AND HEIGHT
N=500		    ! NUMBER OF TURNS
R=3*S/2		    ! COIL MIDSPAN RADIUS

RECTNG,S,2*S,0,S/2
PCIRC,0,6*S,0,90
PCIRC,0,12*S,0,90
AOVLAP,ALL
ASEL,S,AREA,,1
AATT,2,1,2
ASUM
*GET,A,AREA,,AREA        ! AREA OF 1/2 COIL CROSS-SECTION
ASEL,S,AREA,,5
AATT,1,1,1
ASEL,S,AREA,,4
AATT,1,1,3
ASEL,ALL
CSYS,1
LSEL,S,LOC,X,9*S
LESIZE,ALL,,,1  
ESIZE,,8
AMESH,4   

*GET,A,AREA,,AREA        ! AREA OF 1/2 COIL CROSS-SECTION
R,1,2*A,500,,1,1	 ! COIL CONSTANTS
ASEL,S,AREA,,1
LSLA,S  
LESIZE,ALL,,,5  
LSEL,ALL
ASEL,ALL
CSYS,0
KSEL,S,LOC,X,0
KSEL,R,LOC,Y,0
KESIZE,ALL,S/5
AMESH,ALL

NSEL,ALL

N1=NODE(S,0,0)           ! GET A NODE ON THE COIL
N2=NODE(0,0,0)           ! GET NODE AT ORIGIN

ESEL,S,MAT,,2            ! GET COIL ELEMENTS
NSLE,S
CP,1,CURR,ALL            ! COUPLE CURR DOF IN COIL
*GET,ELM,ELEM,,NUM,MIN   ! GET AN ELEMENT NUMBER IN THE COIL REGION
NSEL,ALL
ESEL,ALL
CSYS,1
NSEL,S,LOC,X,12*S
SF,ALL,INF
NSEL,S,LOC,X,0
D,ALL,AZ,0
NSEL,ALL
FINISH

/SOLU
ANTYPE,STATIC
ESEL,S,MAT,,2
BFE,ALL,VLTG,,12         ! 12 VOLT LOAD
ESEL,ALL
SOLVE
FINISH

/POST1
SET,LAST
ESEL,S,MAT,,2            ! SELECT COIL ELEMENTS
ETABLE,RES,NMISC,8       ! STORE ELEMENT RESISTANCE
ETABLE,IND,NMISC,9       ! STORE ELEMENT INDUCTANCE
SSUM
*GET,CRES,SSUM,,ITEM,RES ! GET COIL RESISTANCE
*GET,CIND,SSUM,,ITEM,IND ! GET COIL INDUCTANCE
 CRES=2*CRES             ! COIL RESISTANCE
 CIND=2*CIND             ! COIL INDUCTANCE
ICUR=12/CRES             ! CALCULATE COIL CURRENT
*GET,NCUR,NODE,N1,CURR   ! GET SOLUTION CURRENT
ESEL,ALL
FINISH
PI=4*ATAN(1)
W=2*PI*60
IMAG=12/SQRT(CRES**2+W**2*CIND**2)
ANG=-ATAN(W*CIND/CRES)
REALCURR=IMAG*COS(ANG)              ! TARGET REAL CURRENT
IMAGCURR=IMAG*SIN(ANG)              ! TARGET IMAGINARY CURRENT
/SOLU
ESEL,ALL
ANTYPE,HARM
HARFRQ,60
SOLVE
FINISH

/POST1
SET,1
*GET,CURREAL,NODE,N1,CURR        ! COMPARE TO REALCURR
SET,1,1,,1
*GET,CURIMAG,NODE,N1,CURR        ! COMPARE TO IMAGCURR
*DIM,LABEL,CHAR,5,2
*DIM,VALUE,,5,3
LABEL(1,1) = 'INDUCTAN','RESISTAN','COIL CUR','REAL SOL','IMAG SOL'
LABEL(1,2) = 'CE,HENRY','CE, OHM ','RENT    ','UTION   ','UTION   '
*VFILL,VALUE(1,1),DATA,.01274,3.534,3.395,1.192,-1.621
*VFILL,VALUE(1,2),DATA,CIND,CRES,ICUR,CURREAL,CURIMAG
V1 = ABS(CIND/.01274) 
V2 = ABS(CRES/3.534) 
V3 = ABS(ICUR/3.395) 
V4 = ABS(CURREAL/1.192) 
V5 = ABS(CURIMAG/1.621) 
*VFILL,VALUE(1,3),DATA,V1,V2,V3,V4,V5
/COM
/OUT,vm206,vrt
/COM,------------------- VM206 RESULTS COMPARISON -------------
/COM,
/COM,                 |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
/COM,PLANE53
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.5,'  ',F14.5,'   ',1F15.3)
/OUT
FINISH
!

/CLEAR,NOSTART
/NOPR
/PREP7
SMRT,OFF
ET,1,233,,,1        ! AIR
ET,2,233,2,,1       ! STRANDED COIL
ET,3,110,,,1        ! FAR-FIELD

EMUNIT,MKS
MP,MURX,1,1
MP,MURX,2,1
MP,RSVX,2,3.00E-8   ! RESISTIVITY OF COIL

S=.02		    ! COIL WIDTH AND HEIGHT
SC=S**2             ! COIL CROSS-SECTIONAL AREA
N=500		    ! NUMBER OF TURNS
RMID=3*S/2	    ! COIL MIDSPAN RADIUS
R=3.534             ! COIL RESISTANCE


R,1,1.0,SC,N,RMID,1,R
RMORE,2

RECTNG,S,2*S,0,S/2
PCIRC,0,6*S,0,90
PCIRC,0,12*S,0,90
AOVLAP,ALL
ASEL,S,AREA,,1
AATT,2,1,2
ASUM
*GET,A,AREA,,AREA        ! AREA OF 1/2 COIL CROSS-SECTION
ASEL,S,AREA,,5
AATT,1,2,1
ASEL,S,AREA,,4
AATT,1,3,3
ASEL,ALL
CSYS,1
LSEL,S,LOC,X,9*S
LESIZE,ALL,,,1  
ESIZE,,8
AMESH,4   


ASEL,S,AREA,,1
LSLA,S  
LESIZE,ALL,,,5  
LSEL,ALL
ASEL,ALL
CSYS,0
KSEL,S,LOC,X,0
KSEL,R,LOC,Y,0
KESIZE,ALL,S/5
AMESH,ALL

NSEL,ALL

N1=NODE(S,0,0)           ! GET A NODE ON THE COIL

ESEL,S,MAT,,2            ! GET COIL ELEMENTS
NSLE,S
CP,1,VOLT,ALL            ! COUPLE VOLT DOF IN COIL
CP,2,EMF,ALL             ! COUPLE EMF DOF IN COIL
*GET,ELM,ELEM,,NUM,MIN   ! GET AN ELEMENT NUMBER IN THE COIL REGION
NSEL,ALL
ESEL,ALL
CSYS,1
NSEL,S,LOC,X,12*S
SF,ALL,INF
NSEL,S,LOC,X,0
D,ALL,AZ,0
NSEL,ALL
FINISH

/SOLU
ANTYPE,STATIC
D,N1,VOLT,12             ! 12 VOLT LOAD
SOLVE
FINISH
*GET,ICOIL,NODE,N1,RF,AMPS        ! GET COIL CURRENT
/COM,
/COM, 

FINISH

/SOLU
ANTYPE,HARM
HARFRQ,60
SOLVE
FINISH

/COM,
/COM, 
/POST1
SET,1
*GET,CURREAL,NODE,N1,RF,AMPS 
SET,1,1,,1
*GET,CURIMAG,NODE,N1,RF,AMPS   

*DIM,LABEL,CHAR,4,2
*DIM,VALUE,,3,3
LABEL(1,1) = 'COIL CUR','REAL SOL','IMAG SOL'
LABEL(1,2) = 'RENT    ','UTION   ','UTION   '   
*VFILL,VALUE(1,1),DATA,3.395,1.192,-1.621
*VFILL,VALUE(1,2),DATA,ICOIL,CURREAL,CURIMAG
V1 = ABS(ICOIL/3.395)
V2 = ABS(CURREAL/1.192) 
V3 = ABS(CURIMAG/1.621) 
*VFILL,VALUE(1,3),DATA,V1,V2,V3
/COM
/OUT,vm206,vrt,,APPEND
/COM,PLANE233
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.5,'  ',F14.5,'   ',1F15.3)
/COM,----------------------------------------------------------
/OUT
FINISH
*LIST,vm206,vrt


/COM,ANSYS MEDIA REL. 2023R1 (11/04/2022) REF. VERIF. MANUAL: REL. 2023R1
/VERIFY,VM206
/TITLE,VM206,STRANDED COIL MODEL, WITH VOLTAGE EXCITATION
/COM,   REF: BOAST "ELECTRIC AND MAGNETIC FIELDS", PG. 247, EQN. 12.18
/COM,
/COM,
/OUT,SCRATCH
/COM, CREATING CDB FILE TO STORE MESH DATA
/COM,

_GEOMGEN = 0            ! SET TO 1 TO WRITE CDB FILE

*IF,_GEOMGEN,EQ,1,THEN

/PREP7
SMRT,OFF
ET,1,PLANE233,,,1        ! AIR
ET,2,PLANE233,2,,1       ! STRANDED COIL
ET,3,INFIN110,,,1        ! FAR-FIELD

EMUNIT,MKS
MP,MURX,1,1
MP,MURX,2,1
MP,RSVX,2,3.00E-8       ! RESISTIVITY OF COIL

R,1,1.0                 ! THICKNESS

S=.02		            ! COIL WIDTH AND HEIGHT
SC=S**2                 ! COIL CROSS-SECTIONAL AREA
N=500		            ! NUMBER OF TURNS
RMID=3*S/2	            ! COIL MIDSPAN RADIUS
R=3.534                 ! COIL RESISTANCE

R,2,1.0,SC,N,RMID,1,R
RMORE,2

R,3,1.0                 ! THICKNESS

RECTNG,S,2*S,0,S/2
PCIRC,0,6*S,0,90
PCIRC,0,12*S,0,90
AOVLAP,ALL
ASEL,S,AREA,,1
AATT,2,2,2
ASUM
*GET,A,AREA,,AREA        ! AREA OF 1/2 COIL CROSS-SECTION
ASEL,S,AREA,,5
AATT,1,1,1
ASEL,S,AREA,,4
AATT,1,3,3
ASEL,ALL
CSYS,1
LSEL,S,LOC,X,9*S
LESIZE,ALL,,,1  
ESIZE,,8
AMESH,4   


ASEL,S,AREA,,1
LSLA,S  
LESIZE,ALL,,,5  
LSEL,ALL
ASEL,ALL
CSYS,0
KSEL,S,LOC,X,0
KSEL,R,LOC,Y,0
KESIZE,ALL,S/5
AMESH,ALL

NSEL,ALL

N1=NODE(S,0,0)           ! GET A NODE ON THE COIL

ESEL,S,MAT,,2            ! GET COIL ELEMENTS
NSLE,S
CP,1,VOLT,ALL            ! COUPLE VOLT DOF IN COIL
CP,2,EMF,ALL             ! COUPLE EMF DOF IN COIL
*GET,ELM,ELEM,,NUM,MIN   ! GET AN ELEMENT NUMBER IN THE COIL REGION
NSEL,ALL
ESEL,ALL
CSYS,1
NSEL,S,LOC,X,12*S
SF,ALL,INF
NSEL,S,LOC,X,0
D,ALL,AZ,0
NSEL,ALL
ALLSEL
cdwrite,db,vm206,cdb
FINISH

*ELSE
/PREP7
cdread,db,vm206,cdb
*ENDIF
FINISH

/SOLU
ANTYPE,STATIC
D,N1,VOLT,12             ! 12 VOLT LOAD
SOLVE
FINISH
/OUT,
*GET,ICOIL,NODE,N1,RF,AMPS        ! GET COIL CURRENT
FINISH

/OUT,SCRATCH
/SOLU
ANTYPE,HARM
HARFRQ,60
SOLVE
FINISH

/POST1
SET,1
*GET,CURREAL,NODE,N1,RF,AMPS 
SET,1,1,,1
*GET,CURIMAG,NODE,N1,RF,AMPS   

*DIM,LABEL,CHAR,4,2
*DIM,VALUE,,3,3
LABEL(1,1) = 'COIL CUR','REAL SOL','IMAG SOL'
LABEL(1,2) = 'RENT    ','UTION   ','UTION   '   
*VFILL,VALUE(1,1),DATA,3.395,1.192,-1.621
*VFILL,VALUE(1,2),DATA,ICOIL,CURREAL,CURIMAG
V1 = ABS(ICOIL/3.395)
V2 = ABS(CURREAL/1.192) 
V3 = ABS(CURIMAG/1.621) 
*VFILL,VALUE(1,3),DATA,V1,V2,V3
/COM
/OUT,vm206,vrt,,
/COM,PLANE233
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.5,'  ',F14.5,'   ',1F15.3)
/COM,----------------------------------------------------------
/OUT
FINISH
*LIST,vm206,vrt


/COM,ANSYS MEDIA REL. 150 (11/8/2013) REF. VERIF. MANUAL: REL. 150
/VERIFY,VM189 
/TITLE,VM189,STRESS RELAXATION OF A CHLOROPRENE RUBBER
/COM,
/COM, REFERENCE:
/COM, HUSNU DAL, MICHAEL KALISKE, "BERGSTROM-BOYCE MODEL FOR
/COM, NONLINEAR FINITE RUBBER VISCOELASTICITY: THEORITICAL
/COM, ASPECTS AND ALGORITHMIC TREATMENT FOR THE FE METHOD  
/COM,

/PREP7
ET,1,SOLID185                    ! 3D 8 NODE ELEMENT
KEYOPT,1,6,1

TB,BB,1,1,38,ISO                 ! DEFINE BERGSTROM-BOYCE MODEL
TBTEMP,0
TBDATA,1,0.6,8,0.96,8,7
TBDATA,6,-1,4

N,1,0,0,0                        ! NODES AND ELEMENTS
N,2,1,0,0
N,3,1,1,0
N,4,0,1,0
N,11,0,0,1
N,12,1,0,1
N,13,1,1,1
N,14,0,1,1
E,1,2,3,4,11,12,13,14
ALLSEL

NSEL,S,LOC,X,0                    ! BOUNDARY CONDITIONS
D,ALL,UX,0
NSEL,S,LOC,Y,0
D,ALL,UY,0
NSEL,S,LOC,Z,0
D,ALL,UZ,0
ALLSEL
FINISH


/SOLU
ANTYPE,0
NLGEOM,ON
DELTIM,2
OUTRES,ALL,LAST
TIME,100
NSEL,S,LOC,Z,1
D,ALL,UZ,-0.26                   ! TRUE STRAIN -0.3,Ln(1-0.26/1)
ALLSEL
LSWRITE,1

TIME,220
ALLSEL
LSWRITE,2                        ! RELAXATION 120S

TIME,320
NSEL,S,LOC,Z,1
D,ALL,UZ,-0.45                   ! TRUE STRAIN -0.6
ALLSEL
LSWRITE,3

TIME,440
ALLSEL
LSWRITE,4                        !RELAXATION 120S

TIME,540
NSEL,S,LOC,Z,1
D,ALL,UZ,-0.55                   ! TRUE STRAIN -0.8
ALLSEL
LSWRITE,5

TIME,640
NSEL,S,LOC,Z,1
D,ALL,UZ,-0.45                   ! TRUE STRAIN -0.6
ALLSEL
LSWRITE,6

TIME,760
ALLSEL
LSWRITE,7                        !RELAXATION 120S


TIME,860
NSEL,S,LOC,Z,1
D,ALL,UZ,-0.26                   ! TRUE STRAIN -0.3
ALLSEL
LSWRITE,8

TIME,980                         ! RELAXATION 120S
ALLSEL
LSWRITE,9

TIME,1080
NSEL,S,LOC,Z,1
D,ALL,UZ,0
ALLSEL
LSWRITE,10

ALLSEL
/OUT,SCRATCH
LSSOLVE,1,10,1
FINISH

/POST1
/OUT,
SET,FIRST
*GET,SZ1,NODE,NODE(1,1,1),S,Z
TS1=-0.65                         !TARGET 
R1=SZ1/TS1
SET,3
*GET,SZ3,NODE,NODE(1,1,1),S,Z
TS3=-1.16                         !TARGET 
R2=SZ3/TS3
SET,5
*GET,SZ5,NODE,NODE(1,1,1),S,Z
TS5=-1.58                         !TARGET 
R3=SZ5/TS5
SET,6
*GET,SZ6,NODE,NODE(1,1,1),S,Z
TS6=-0.84                          !TARGET 
R4=SZ6/TS6
SET,8
*GET,SZ8,NODE,NODE(1,1,1),S,Z
TS8=-0.40                          !TARGET 
R5=SZ8/TS8

*DIM,LABEL,CHAR,5,1
*DIM,VALUE,,5,3
LABEL(1,1)='SET1','SET3','SET5','SET6','SET8'
*VFILL,VALUE(1,1),DATA,SZ1,SZ3,SZ5,SZ6,SZ8
*VFILL,VALUE(1,2),DATA,TS1,TS3,TS5,TS6,TS8
*VFILL,VALUE(1,3),DATA,R1,R2,R3,R4,R5
/COM,
/OUT,vm189,vrt
/COM, ------------------VM189 RESULTS COMPARISON --------------------
/COM,
/COM,              |  Mechanical APDL  |   TARGET    |  RATIO
/COM,
*VWRITE,LABEL(1,1),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,'  ',F11.3,'   ',1F14.3,'  ',1F14.3)
/COM, ---------------------------------------------------------------
/OUT,
*LIST,vm189,vrt
FINISH




/COM,ANSYS MEDIA REL. 150 (11/8/2013) REF. VERIF. MANUAL: REL. 150
/VERIFY,VM88
/PREP7
/TITLE, VM88, RESPONSE OF AN ECCENTRIC WEIGHT EXCITER
! VIBRATION THEORY AND APPLICATIONS, THOMSON, 2ND PRINTING, PAGE 60, EX 3.3-1
ANTYPE,HARMIC              ! HARMONIC RESPONSE ANALYSIS
HROPT,FULL                 ! FULL HARMONIC RESPONSE
HROUT,OFF                  ! PRINT COMPLEX DISP. AS AMPLITUDES AND PHASE ANGLES
ET,1,COMBIN40
R,1,30,.11754533,.02590673 ! K = 30, C = .11754533, M = .02590673
N,1
N,2
E,2,1
FINISH
/SOLU    
OUTPR,BASIC,1
HARFRQ,,5.415947           ! FREQUENCY RANGE FROM 0 TO 5.415947 HZ.
D,1,UX
F,2,FX,2.4
SOLVE
HARFRQ,,541.5947           ! FREQUENCY RANGE FROM 0 TO 541.5947 HZ.
F,2,FX,24000
SOLVE
FINISH
/POST26
FILE,,rst
NSOL,2,2,U,X,2UX
PRVAR,2
*GET,A1,VARI,2,ITIME,5.4159
*GET,A2,VARI,2,RTIME,541.59
*DIM,LABEL,CHAR,2,2
*DIM,VALUE,,2,3
LABEL(1,1) = 'AMP f=fn','AMP f=10'
LABEL(1,2) = ', in    ','0fn, in '
*VFILL,VALUE(1,1),DATA,.6,.08
*VFILL,VALUE(1,2),DATA,ABS(A1),ABS(A2)
*VFILL,VALUE(1,3),DATA,ABS(A1/.6),ABS(A2/.08)
/COM
/OUT,vm88,vrt
/COM,------------------- VM88 RESULTS COMPARISON ---------------

/COM,
/COM,                 |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.4,'  ',F14.4,'   ',1F15.3)
/COM,-----------------------------------------------------------
/COM,
/COM,-----------------------------------------------------------
/COM,NOTE: THERE ARE VERIFIED RESULTS IN VM88  NOT CONTAINED IN
/COM,THIS TABLE
/COM,-----------------------------------------------------------
/OUT
FINISH
*LIST,vm88,vrt

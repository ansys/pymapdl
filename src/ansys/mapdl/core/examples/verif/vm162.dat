/COM,ANSYS MEDIA REL. 2023R1 (11/04/2022) REF. VERIF. MANUAL: REL. 2023R1
/VERIFY,VM162
/PREP7
/TITLE, VM162, CIRCULAR COOLING FIN OF RECTANGULAR PROFILE
C***     CONDUCTION HEAT TRANSFER, SCHNEIDER, 2ND. PRINTING, PAGE 82, ART. 4-10
ANTYPE,STATIC
ET,1,SOLID90
MP,KXX,1,15
CSYS,1
N,1,.04167
N,9,.0625
FILL
NGEN,3,10,1,9,1,,2.5
NGEN,3,30,1,29,1,,,.002604
E,1,3,23,21,61,63,83,81
EMORE,2,13,22,11,62,73,82,71
EMORE,31,33,53,51
EGEN,4,2,1
OUTPR,,1
D,1,TEMP,100,,81,10
NSEL,S,LOC,Z
SF,ALL,CONV,100,0.0
NSEL,S,LOC,Z,0.005208
SF,ALL,CONV,100,0.0
NSEL,S,LOC,X,0.0625
SF,ALL,CONV,100,0.0
NSEL,ALL
FINISH
/SOLU    
/OUT,SCRATHC
SOLVE
FINISH
/POST1
SET,LAST
/OUT,
*GET,T,NODE,29,TEMP
FSUM,HEAT
*GET,Q1,FSUM,0,ITEM,HEAT
*SET,Q,ABS((Q1*360/5))
*DIM,LABEL,CHAR,2,2
*DIM,VALUE,,2,3
LABEL(1,1) = 'T2 ','q '
LABEL(1,2) = 'F','BTU/hr'
*VFILL,VALUE(1,1),DATA,53.22,102.05
*VFILL,VALUE(1,2),DATA,T,Q
*VFILL,VALUE(1,3),DATA,ABS(T/53.22),ABS(Q/102.05)
/COM
/OUT,vm162,vrt
/COM,------------------- VM162 RESULTS COMPARISON -------------
/COM,
/COM,                 |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.2,'  ',F13.2,'   ',1F15.3)
/COM,----------------------------------------------------------
/OUT
FINISH
*LIST,vm162,vrt

/COM,ANSYS MEDIA REL. 150 (11/8/2013) REF. VERIF. MANUAL: REL. 150
/VERIFY,VM207
/PREP7 
JPGPRF,500,100,1             ! MACRO TO SET PREFS FOR JPEG PLOTS
/SHOW,JPEG
SMRT,OFF
/TITLE,VM207,  STRANDED COIL MODEL, CIRCUIT-FED OPTION
/COM,   REF: BOAST "ELECTRIC AND MAGNETIC FIELDS", PG. 247, EQN. 12.18
ET,1,53,,,1         ! AIR
ET,2,53,3,,1        ! CIRCUIT-COUPLED STRANDED COIL
ET,3,110,,,1        ! FAR-FIELD
ET,4,124,0          ! EXTERNAL RESISTOR
ET,5,124,4,4        ! INDEPENDENT VOLTAGE SOURCE, PIECEWISE LINEAR LOAD
ET,6,124,5          ! STRANDED COIL (TO FEA DOMAIN)
R,2,2               ! 2 OHM RESISTOR
R,3,1               ! SYMMETRY FACTOR FOR COIL
EMUNIT,MKS
MP,MURX,1,1
MP,MURX,2,1
MP,RSVX,2,3.04878E-8  ! RESISTIVITY OF COIL
S=.02
N=500
R=3*S/2
N,1                   ! CREATE NODES FOR CIRCUIT ELEMENTS
*REPEAT,4,1
RECTNG,S,2*S,0,S/2
PCIRC,0,6*S,0,90
PCIRC,0,12*S,0,90
AOVLAP,ALL
ASEL,S,AREA,,1
AATT,2,1,2
ASUM
*GET,A,AREA,,AREA        ! AREA OF 1/2 COIL CROSS-SECTION
ASEL,S,AREA,,5
AATT,1,1,1
ASEL,S,AREA,,4
AATT,1,1,3
ASEL,ALL
CSYS,1
LSEL,S,LOC,X,9*S
LESIZE,ALL,,,1  
ESIZE,,8
AMESH,4   
*GET,A,AREA,,AREA        ! AREA OF 1/2 COIL CROSS-SECTION
R,1,2*A,500,,1,.9
ASEL,S,AREA,,1
LSLA,S  
LESIZE,ALL,,,5  
LSEL,ALL
ASEL,ALL
CSYS,0
KSEL,S,LOC,X,0
KSEL,R,LOC,Y,0
KESIZE,ALL,S/5
AMESH,ALL
N1=NODE(S,0,0)           ! GET A NODE ON THE COIL
TYPE,5                   ! VOLTAGE SOURCE
R,4,0,12,.01,12,.010001,0 ! PIECEWISE LINEAR LOAD
REAL,4
E,2,1,4                  ! VOLTAGE SOURCE ELEMENT
TYPE,4                   ! RESISTOR
REAL,2
E,2,3                    ! EXTERNAL RESISTOR ELEMENT
TYPE,6                   ! STRANDED COIL (TO FEA DOMAIN)
REAL,3
E,3,1,N1                 ! STRANDED COIL "ELEMENT"
ESEL,S,MAT,,2            ! GET COIL ELEMENTS
NSLE,S
CP,1,CURR,ALL            ! COUPLE CURR DOF IN COIL
CP,2,EMF,ALL             ! COUPLE EMF DOF IN COIL 
*GET,ELM,ELEM,,NUM,MIN   ! GET AN ELEMENT NUMBER IN THE COIL REGION
NSEL,ALL
ESEL,ALL
FINISH
/SOLU
ANTYPE,STATIC
TIME,1E-9
CSYS,1
NSEL,S,LOC,X,12*S
SF,ALL,INF               ! APPLY INFINITE SURFACE FLAG
CSYS,0
NSEL,S,LOC,X,0
D,ALL,AZ,0
NSEL,ALL
D,1,VOLT,0               ! GROUND
SOLVE
FINISH
/POST1
SET,LAST
ESEL,S,MAT,,2            ! SELECT COIL ELEMENTS
ETABLE,RES,NMISC,8       ! STORE ELEMENT RESISTANCE
ETABLE,IND,NMISC,9       ! STORE ELEMENT INDUCTANCE
SSUM
*GET,CRES,SSUM,,ITEM,RES ! GET COIL RESISTANCE
*GET,CIND,SSUM,,ITEM,IND ! GET COIL INDUCTANCE
 CRES=2*CRES
 CIND=2*CIND
ESEL,ALL
FINISH
/SOLU
ANTYPE,TRANS
OUTRES,ALL,ALL           ! STORE EVERY SUBSTEP
DELTIM,.0004
TIME,.01
SOLVE
FINISH
/POST26
NSOL,2,N1,CURR           ! GET CURRENT IN COIL
RES=CRES+2
I01=(12/RES)*(1-(EXP(-RES*.01/CIND)))
PRVAR,2  
*GET,ICUR,VARI,2,RTIME,.01                
/AXLAB,X,TIME
/AXLAB,Y,CURRENT IN COIL (AMPS)
/SHOW
PLVAR,2                  ! PLOT COIL CURRENT VS TIME.
*DIM,LABEL,CHAR,3,2
*DIM,VALUE,,3,3
LABEL(1,1) = 'INDUCTAN','RESISTAN','CURRENT,'
LABEL(1,2) = 'CE,HENRY','CE, OHM ',' AMPS   '
*VFILL,VALUE(1,1),DATA,.01274,3.9908,1.9849
*VFILL,VALUE(1,2),DATA,CIND,CRES,ICUR
*VFILL,VALUE(1,3),DATA,ABS(CIND/.01274),ABS(CRES/3.9908),ABS(ICUR/1.9849)
/COM
/OUT,vm207,vrt
/COM,------------------- VM207 RESULTS COMPARISON -------------
/COM,
/COM,                 |   TARGET   |   Mechanical APDL   |   RATIO
/COM,PLANE53
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.5,'  ',F14.5,'   ',1F15.3)
/OUT
FINISH

/CLEAR,NOSTART
/NOPR
/PREP7
JPGPRF,500,100,1             ! MACRO TO SET PREFS FOR JPEG PLOTS
/SHOW,JPEG
SMRT,OFF
/TITLE,VM207,  STRANDED COIL MODEL, CIRCUIT-FED OPTION
ET,1,233,,,1        ! AIR
ET,2,233,2,,1       ! STRANDED COIL
ET,3,110,,,1        ! FAR-FIELD
ET,4,124,0          ! EXTERNAL RESISTOR
ET,5,124,4,4        ! INDEPENDENT VOLTAGE SOURCE, PIECEWISE LINEAR LOAD
R,2,2               ! 2 OHM RESISTOR
R,4,0,12,.01,12,.010001,0 ! PIECEWISE LINEAR LOAD

EMUNIT,MKS
MP,MURX,1,1
MP,MURX,2,1

S=.02		    ! COIL WIDTH AND HEIGHT
SC=S**2             ! COIL CROSS-SECTIONAL AREA
N=500		    ! NUMBER OF TURNS
RMID=3*S/2	    ! BROOKS COIL MIDSPAN RADIUS
R=3.991             ! COIL RESISTANCE

N,1                 ! CREATE NODES FOR CIRCUIT ELEMENTS
*REPEAT,3,1

R,3,1.0,SC,N,RMID,1,R
RMORE,2

RECTNG,S,2*S,0,S/2
PCIRC,0,6*S,0,90
PCIRC,0,12*S,0,90
AOVLAP,ALL
ASEL,S,AREA,,1
AATT,2,3,2
ASUM
*GET,A,AREA,,AREA        ! AREA OF 1/2 COIL CROSS-SECTION
ASEL,S,AREA,,5
AATT,1,1,1
ASEL,S,AREA,,4
AATT,1,1,3
ASEL,ALL
CSYS,1
LSEL,S,LOC,X,9*S
LESIZE,ALL,,,1  
ESIZE,,8
AMESH,4   

ASEL,S,AREA,,1
LSLA,S  
LESIZE,ALL,,,5  
LSEL,ALL
ASEL,ALL
CSYS,0
KSEL,S,LOC,X,0
KSEL,R,LOC,Y,0
KESIZE,ALL,S/5
AMESH,ALL

NSEL,ALL

N1=NODE(S,0,0)           ! GET A NODE ON THE COIL

TYPE,5                   ! VOLTAGE SOURCE
REAL,4
E,2,1,3                  ! VOLTAGE SOURCE ELEMENT
TYPE,4                   ! RESISTOR
REAL,2
E,2,N1                   ! EXTERNAL RESISTOR ELEMENT, DIRECTLY CONNECTED TO THE COIL

ESEL,S,MAT,,2            ! GET COIL ELEMENTS
NSLE,S
CP,1,VOLT,ALL            ! COUPLE VOLT DOF IN COIL
CP,2,EMF,ALL             ! COUPLE EMF DOF IN COIL
*GET,ELM,ELEM,,NUM,MIN   ! GET AN ELEMENT NUMBER IN THE COIL REGION
NSEL,ALL
ESEL,ALL
CSYS,1
NSEL,S,LOC,X,12*S
SF,ALL,INF
CSYS,0
NSEL,S,LOC,X,0
D,ALL,AZ,0
NSEL,ALL
FINISH

/SOLU
ANTYPE,STATIC
TIME,1E-9
D,1,VOLT,0               ! GROUND
SOLVE
FINISH

/POST1
*GET,IC,NODE,1,RF,AMPS   ! GET COIL CURRENT
ETABLE,_SENE,SENE        ! SUM UP MAGNETIC ENERGY STORED
SSUM
*GET,W,SSUM,,ITEM,_SENE
W=2*W                    ! SYMMETRY FACTOR
LCOIL=2*W/IC**2          ! CALCULATE INDUCTANCE VIA MAGNETIC ENERGY
FINISH

/SOLU
ANTYPE,TRANS
OUTRES,ALL,ALL           ! STORE EVERY SUBSTEP
DELTIM,.0004
TIME,.01
SOLVE
FINISH

/POST26
RFOR,2,1,AMPS           ! GET CURRENT IN COIL
/AXLAB,X,TIME
/AXLAB,Y,CURRENT IN COIL (AMPS)
/YRANGE,0,2,.2
/SHOW
PLVAR,2                  ! PLOT COIL CURRENT VS TIME.
PRVAR,2                  ! PRINT COIL CURRENT VS TIME.
*DIM,LABEL,CHAR,2,2
*DIM,VALUE,,2,3
LABEL(1,1) = 'INDUCTAN','CURRENT,'
LABEL(1,2) = 'CE,HENRY',' AMPS   '
*VFILL,VALUE(1,1),DATA,.01274,1.9849
*VFILL,VALUE(1,2),DATA,LCOIL,IC
*VFILL,VALUE(1,3),DATA,ABS(LCOIL/.01274),ABS(IC/1.9849)
/COM
/OUT,vm207,vrt,,APPEND            
/COM,PLANE233
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.5,'  ',F14.5,'   ',1F15.3)
/COM,----------------------------------------------------------
/OUT
FINISH
*LIST,vm207,vrt

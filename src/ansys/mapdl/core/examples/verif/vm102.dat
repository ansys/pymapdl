/COM,ANSYS MEDIA REL. 2023R1 (11/04/2022) REF. VERIF. MANUAL: REL. 2023R1
/VERIFY,VM102
/PREP7
/TITLE, VM102, CYLINDER WITH TEMPERATURE DEPENDENT CONDUCTIVITY
C***    CONDUCTION HEAT TRANSFER, SCHNEIDER, 2ND. PRINTING, PAGE 166, ART. 7-9
ANTYPE,STATIC
ET,1,PLANE55,,,1                   ! AXISYMMETRIC OPTION
MP,KXX,1,50                        ! CONSTANT CONDUCTIVITY
N,1,(1/24)
N,6,(1/12)
FILL
NGEN,2,10,1,6,1,,.01
E,1,2,12,11
EGEN,5,1,1
OUTPR,,1
KBC,1                              ! STEP BOUNDARY CONDITIONS
D,1,TEMP,100,,11,10
D,6,TEMP,,,16,10
FINISH
/SOLU 
/OUT,vm102_SCRATCH   
SOLVE
/OUT
FINISH
/POST1
SET,1
NSEL,S,LOC,Y
PRNSOL,TEMP                        ! RADIAL TEMPERATURES FOR CONSTANT K
*GET,TN2,NODE,2,TEMP
*GET,TN3,NODE,3,TEMP
*GET,TN4,NODE,4,TEMP
*GET,TN5,NODE,5,TEMP
*DIM,LABEL_1,CHAR,4,2
*DIM,VALUE_1,,4,3
LABEL_1(1,1) = '   NODE ','   NODE ','   NODE ','   NODE '
LABEL_1(1,2) = '2       ','3       ','4       ','5       '
*VFILL,VALUE_1(1,1),DATA,73.8,51.5,32.2,15.3
*VFILL,VALUE_1(1,2),DATA,TN2,TN3,TN4,TN5
*VFILL,VALUE_1(1,3),DATA,ABS(TN2/73.8),ABS(TN3/51.5),ABS(TN4/32.2),ABS(TN5/15.3)
NSEL,ALL
FINISH

/PREP7
MP,KXX,1,50,0.5                    ! TEMPERATURE-DEPENDENT CONDUCTIVITY
FINISH
/SOLU
/OUT,vm102_SCRATCH,,,APPEND  
SOLVE
/OUT
FINISH
/POST1
SET,1
NSEL,S,LOC,Y
PRNSOL,TEMP                        ! RADIAL TEMPERATURES FOR K(T)
*GET,TN2,NODE,2,TEMP
*GET,TN3,NODE,3,TEMP
*GET,TN4,NODE,4,TEMP
*GET,TN5,NODE,5,TEMP
*DIM,LABEL_2,CHAR,4,2
*DIM,VALUE_2,,4,3
LABEL_2(1,1) = '   NODE ','   NODE ','   NODE ','   NODE '
LABEL_2(1,2) = '2       ','3       ','4       ','5       '
*VFILL,VALUE_2(1,1),DATA,79.2,59.6,40.2,20.8
*VFILL,VALUE_2(1,2),DATA,TN2,TN3,TN4,TN5
*VFILL,VALUE_2(1,3),DATA,ABS(TN2/79.2),ABS(TN3/59.6),ABS(TN4/40.2),ABS(TN5/20.8)
/COM
/OUT,vm102,vrt
/COM,------------------- VM102 RESULTS COMPARISON --------------
/COM,
/COM,                 |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
/COM,T, F (K=CONSTANT);FIRST LOAD STEP:
/COM,
*VWRITE,LABEL_1(1,1),LABEL_1(1,2),VALUE_1(1,1),VALUE_1(1,2),VALUE_1(1,3)
(1X,A8,A8,'   ',F10.1,'  ',F10.1,'   ',1F5.2)
/COM,
/COM,T, F (K=K(T);SECOND LOAD STEP:
/COM,
*VWRITE,LABEL_2(1,1),LABEL_2(1,2),VALUE_2(1,1),VALUE_2(1,2),VALUE_2(1,3)
(1X,A8,A8,'   ',F10.1,'  ',F10.1,'   ',1F5.2)
/COM,----------------------------------------------------------
/OUT
FINISH
*LIST,vm102,vrt

/COM,ANSYS MEDIA REL. 2023R1 (11/04/2022) REF. VERIF. MANUAL: REL. 2023R1
/VERIFY,vm26
/FILNAM,vm26
/PREP7
/TITLE, VM26, LARGE DEFLECTION OF A CANTILEVERED PLATE
/COM   REF: BATHE AND DVORKIN, " A FORMULATION OF GENERAL SHELL ELEMENTS... "
/COM                             IJNME, VOL 22, NO. 3 (1986) PAGE 720
/COM   USING SHELL181 ELEMENTS
/OUT,vm26_SCRATCH
/NOPR
ANTYPE,STATIC           ! STATIC ANALYSIS
NLGEOM,ON               ! LARGE DEFLECTION OPTION
ET,1,SHELL181,,,2
SECTYPE,1,SHELL
SECDATA,1,1,0,5         ! PLATE THICKNESS = 1
MP,EX,1,1800            ! MATERIAL PROPERTIES
MP,NUXY,1,0
N,1                     ! DEFINE MODEL
N,2,12
N,3,6
N,4,12,1
N,5,0,1
N,6,6,1
E,1,3,6,5
E,3,2,4,6
NSEL,S,LOC,X
D,ALL,ALL               ! FIXED END B.C.'S
NSEL,S,LOC,X,12
CP,1,ROTY,ALL           ! COUPLE ROTATIONS AT FREE END
TORQ=7.854              ! DEFINE HALF TOTAL LOAD
F,2,MY,TORQ
NSEL,ALL                ! RESELECT ALL NODES 
FINISH
/SOLU
DMPOPT,RNNN,YES    
AUTOTS,ON               ! USE AUTOMATIC LOAD STEPPING
NSUBST,10,100,10        ! START WITH MAX OF 10 SUBSTEPS FOR EACH LOAD STEP
CNVTOL,F,1,1.0E-2		! FORCE CONVERGENCE
CNVTOL,U,1,1.0E-2		! DISPLACEMENT CONVERGENCE
LNSRCH,ON               ! USE LINE SEARCH METHOD
OUTPR,BASIC,LAST        ! BASIC PRINTOUT IN THE LAST SUBSTEP
OUTRES,ALL,ALL          ! WRITE SOLUTION TO THE RESULTS FILE FOR EACH SUBSTEP
SOLVE
FINISH

/CLEAR,NOSTART          ! CLEAR THE DATA BASE
/SOLU      
ANTYPE,,REST            ! RESTART ANALYSIS
F,2,MY,TORQ*2           ! APPLY FULL LOAD
SOLVE
FINISH
/POST1
RSYS,SOLU               ! CHOOSE "AS-GENERATED" COORDINATE SYSTEM
SET,2                   ! USE LOAD STEP 2 (FROM RESTART ANALYSIS)
SHELL,TOP               ! CHOOSE TOP SURFACE OF SHELL FOR STRESS PRINTOUT
PRNSOL,S,COMP           ! PRINT NODAL STRESSES AND DISPLACEMENTS
PRNSOL,DOF
*GET,UX_N4,NODE,4,U,X
*GET,UZ_N4,NODE,4,U,Z
*GET,ROTY_N4,NODE,4,ROT,Y
*GET,STRSS_N1,NODE,1,S,X
*DIM,LABEL,CHAR,4,2
*DIM,VALUE,,4,3
LABEL(1,1) = 'UX,NODE ','UZ,NODE ','ROTY,NOD','STS_X,N_'
LABEL(1,2) = '4 (mm)  ','4 (mm)  ','E 4(rad)','1 N/mm^2'
*VFILL,VALUE(1,1),DATA,-2.9,-6.5,1.26,94.25
*VFILL,VALUE(1,2),DATA,UX_N4,UZ_N4,ROTY_N4,STRSS_N1
*VFILL,VALUE(1,3),DATA,ABS(UX_N4/2.9),ABS(UZ_N4/6.5),ABS(ROTY_N4/1.26),ABS(STRSS_N1/94.25)
SAVE,TABLE_1
FINISH

/CLEAR,NOSTART
/TITLE, VM26, LARGE DEFLECTION OF A CANTILEVERED PLATE
/COM          USING SHELL281 ELEMENTS
/PREP7
/NOPR
ANTYPE,STATIC           ! STATIC ANALYSIS
NLGEOM,ON               ! LARGE DEFLECTION OPTION
ET,1,SHELL281
SECTYPE,1,SHELL
SECDATA,1,1,0,5		! PLATE THICKNESS = 1
MP,EX,1,1800            ! MATERIAL PROPERTIES
MP,NUXY,1,0
N,1                     ! DEFINE MODEL
N,2,12
N,3,3
N,4,6
N,5,9
N,6,12,1
N,7,12,0.5
N,8,0,1
N,9,9,1
N,10,6,1
N,11,3,1
N,12,0,0.5
N,13,6,0.5
E,1,4,10,8,3,13,11,12
E,4,2,6,10,5,7,9,13
NSEL,S,LOC,X
D,ALL,ALL               ! FIXED END B.C.'S
NSEL,S,LOC,X,12
CP,1,ROTY,ALL           ! COUPLE ROTATIONS AT FREE END
TORQ=7.854              ! DEFINE HALF TOTAL LOAD
F,2,MY,TORQ
NSEL,ALL                ! RESELECT ALL NODES
FINISH
/SOLU    
AUTOTS,ON               ! USE AUTOMATIC LOAD STEPPING
NSUBST,10,100,10        ! START WITH MAX OF 10 SUBSTEPS FOR EACH LOAD STEP
CNVTOL,F,1,1.0E-2		! FORCE CONVERGENCE
CNVTOL,U,1,1.0E-2		! DISPLACEMENT CONVERGENCE
LNSRCH,ON               ! USE LINE SEARCH METHOD
OUTPR,BASIC,LAST        ! BASIC PRINTOUT IN THE LAST SUBSTEP
OUTRES,ALL,ALL          ! WRITE SOLUTION TO THE RESULTS FILE FOR EACH SUBSTEP
SOLVE
FINISH
/SOLU
ANTYPE,,REST            ! RESTART ANALYSIS
F,2,MY,TORQ*2           ! APPLY FULL LOAD
SOLVE
FINISH
/POST1
RSYS,SOLU               ! CHOOSE "AS-GENERATED" COORDINATE SYSTEM
SET,2                   ! USE LOAD STEP 2 (FROM RESTART ANALYSIS)
SHELL,TOP               ! CHOOSE TOP SURFACE OF SHELL FOR STRESS PRINTOUT
PRNSOL,S,COMP           ! PRINT NODAL STRESSES AND DISPLACEMENTS
PRNSOL,DOF
*GET,UX_N4,NODE,6,U,X
*GET,UZ_N4,NODE,6,U,Z
*GET,ROTY_N4,NODE,6,ROT,Y
! SHELL281 can produce uneven bending stresses due to curvature effects.
! The following method is adopted to linearize the bending stresses
*GET,STRSS_N1_TOP,NODE,1,S,X
SHELL,BOT
*GET,STRSS_N1_BOT,NODE,1,S,X
STRSS_N1 = 0.5*(STRSS_N1_TOP - STRSS_N1_BOT)
*DIM,LABEL,CHAR,4,2
*DIM,VALUE,,4,3
LABEL(1,1) = 'UX,NODE ','UZ,NODE ','ROTY,NOD','STS_X,N_'
LABEL(1,2) = '4 (mm)  ','4 (mm)  ','E 4(rad)','1 N/mm^2'
*VFILL,VALUE(1,1),DATA,-2.9,-6.5,1.26,94.25
*VFILL,VALUE(1,2),DATA,UX_N4,UZ_N4,ROTY_N4,STRSS_N1
*VFILL,VALUE(1,3),DATA,ABS(UX_N4/2.9),ABS(UZ_N4/6.5),ABS(ROTY_N4/1.26),ABS(STRSS_N1/94.25)
SAVE,TABLE_2
RESUME,TABLE_1
/COM
/COM
/OUT,vm26,vrt
/COM,------------------- VM26 RESULTS COMPARISON ---------------------
/COM,
/COM,                 |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
/COM,SHELL181:
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.2,'  ',F14.2,'   ',1F15.2)
/NOPR
RESUME,TABLE_2
/GOPR
/COM,
/COM,SHELL281:
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.2,'  ',F14.2,'   ',1F15.2)
/COM,-----------------------------------------------------------------
/OUT
FINISH
/DELETE,TABLE_1
/DELETE,TABLE_2
FINISH
*LIST,vm26,vrt

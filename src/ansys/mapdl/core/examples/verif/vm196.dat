/COM,ANSYS MEDIA REL. 2023R1 (11/04/2022) REF. VERIF. MANUAL: REL. 2023R1
/VERIFY,VM196
/PREP7
MP,PRXY,,0.3
SMRT,OFF
/TITLE, VM196, COUNTER-BALANCED LOADS ON A BLOCK
C***   ANY BASIC MECHANICS BOOK
C***   USING 3-D SOLID45 
ET,1,SOLID45       
MP,EX,1,70E9          ! ALUMINUM 
MP,DENS,1,2712
MP,PRXY,1,0.3
WPOFFS,,,300          ! AXIS OF ROTATION 300 M
BLOCK,-1,1,-1,1,0,3   ! 3 M HIGH BY 2 M SQUARE
ESIZE,1
VMESH,ALL
DK,1,ALL              ! CONSTRAIN 6 DOF SUCH THAT NO ROTATIONS OCCUR
DK,4,UX
DK,6,UY
DK,7,UZ
SAVE                  ! SAVE DATABASE FOR SECOND SOLUTION
FINISH
*CREATE,SOLV3D,MAC    ! CREATE MACRO TO SOLVE AND RETRIEVE RESULTS
/SOLU
ANTYPE,STATIC
IRLF,1                ! INERTIA RELIEF CALCULATIONS
FK,5,FX,-2000
FK,5,FY,3000
OUTPR,RSOL,1          ! PRINT REACTION SOLUTION
SOLVE
IRLIST                ! LIST INERTIA RELIEF LOADS AND ACCELERATIONS
*GET,MX,ELEM,,MMOR,X
*GET,MY,ELEM,,MMOR,Y
*GET,MZ,ELEM,,MMOR,Z
*DIM,LABEL,CHAR,3,2
*DIM,VALUE,,3,3
LABEL(1,1) = 'MOM X ','MOM Y','MOM Z'
LABEL(1,2) = 'N-m','N-m','N-m'
*VFILL,VALUE(1,1),DATA,-909000,-606000,-5000
*VFILL,VALUE(1,2),DATA,MX,MY,MZ
*VFILL,VALUE(1,3),DATA,ABS(MX/909000),ABS(MY/606000),ABS(MZ/5000)
FINISH
*END

SOLV3D                ! EXECUTE MACRO TO SOLVE AND RETRIEVE RESULTS
SAVE,TABLE_1

/CLEAR, NOSTART       ! CLEAR DATABASE FOR 2ND SOLUTION
/TITLE, VM196, COUNTER-BALANCED LOADS ON A BLOCK
C***   USING 3-D SOLID185 
/PREP7
SMRT,OFF
RESUME                ! RESUME DATABASE
ET,1,SOLID185         ! ANALYZE AGAIN USING 3-D SOLID185 
FINISH

SOLV3D                ! EXECUTE MACRO TO SOLVE AND RETRIEVE RESULTS
SAVE,TABLE_2
FINISH
/CLEAR,NOSTART

/COM,
/COM,  USING SOLID185 AND SUBSTRUCTURE MODELING
/COM,

/OUT,SCRATCH
/FILNAM,GEN1
/PREP7
ET,1,SOLID185       
MP,EX,1,70E9          ! ALUMINUM 
MP,DENS,1,2712
MP,PRXY,1,0.3
WPOFFS,,,300          ! AXIS OF ROTATION 300 M
BLOCK,-1,1,-1,0,0,3   ! 3 M HIGH BY 2 M SQUARE, half of block modeled about Y
ESIZE,1
VMESH,ALL
FINISH

/SOLUTION
ANTYPE,SUBSTRUCTURE   ! SUBSTRUCTURE ANALYSIS
SEOPT,GEN1,2          ! GENERATE K AND M MATRICES
CMSOPT,FIX,10         ! USING FIXED INTERFACE METHOD
M,NODE(-1,-1,300),ALL
M,NODE(-1,0,300),ALL
M,NODE(-1,-1,303),ALL
M,NODE(-1,0,303),ALL
M,NODE(1,0,300),ALL
M,NODE(1,-1,300),ALL
M,NODE(1,0,303),ALL
M,NODE(1,-1,303),ALL
SOLVE
SAVE
FINISH
/CLEAR,NOSTART

/FILNAM,GEN2
/PREP7
ET,1,SOLID185       
MP,EX,1,70E9          ! ALUMINUM 
MP,DENS,1,2712
MP,PRXY,1,0.3
WPOFFS,,,300          ! AXIS OF ROTATION 300 M
BLOCK,-1,1,0,1,0,3   ! 3 M HIGH BY 2 M SQUARE, half of block modeled about Y
ESIZE,1
NUMSTR,NODE,101
NUMSTR,ELEM,101
VMESH,ALL
FINISH

/SOLUTION
ANTYPE,SUBSTRUCTURE   ! SUBSTRUCTURE ANALYSIS
SEOPT,GEN2,2          ! GENERATE K AND M MATRICES
CMSOPT,FIX,10         ! USING FIXED INTERFACE METHOD
M,NODE(-1,1,300),ALL
M,NODE(-1,0,300),ALL
M,NODE(-1,1,303),ALL
M,NODE(-1,0,303),ALL
M,NODE(1,0,300),ALL
M,NODE(1,1,300),ALL
M,NODE(1,0,303),ALL
M,NODE(1,1,303),ALL
SOLVE
SAVE
FINISH
/CLEAR,NOSTART

/FILNAM,USE
/PREP7
ET,1,50
SE,GEN1
SE,GEN2
CPINTF,ALL
FINISH

/SOLU
NSEL,S,LOC,X,-1
NSEL,R,LOC,Y,1
NSEL,R,LOC,Z,300
D,ALL,ALL,0
NSEL,ALL

NSEL,S,LOC,X,1
NSEL,R,LOC,Y,-1
NSEL,R,LOC,Z,300
D,ALL,UX,0
NSEL,ALL

NSEL,S,LOC,X,1
NSEL,R,LOC,Y,-1
NSEL,R,LOC,Z,303
D,ALL,UY,0
NSEL,ALL

NSEL,S,LOC,X,1
NSEL,R,LOC,Y,1
NSEL,R,LOC,Z,303
D,ALL,UZ,0
NSEL,ALL
FINISH

/SOLU
ANTYPE,STATIC
IRLF,1                ! INERTIA RELIEF CALCULATIONS
NSEL,S,LOC,X,-1
NSEL,R,LOC,Y,-1
NSEL,R,LOC,Z,303
F,ALL,FX,-2000
F,ALL,FY,3000
NSEL,ALL
SOLVE
/OUT,
IRLIST                ! LIST INERTIA RELIEF LOADS AND ACCELERATIONS
FINISH
/OUT,SCRATCH
*GET,MX,ELEM,,MMOR,X
*GET,MY,ELEM,,MMOR,Y
*GET,MZ,ELEM,,MMOR,Z
*DIM,LABEL,CHAR,3,2
*DIM,VALUE,,3,3
LABEL(1,1) = 'MOM X ','MOM Y','MOM Z'
LABEL(1,2) = 'N-m','N-m','N-m'
*VFILL,VALUE(1,1),DATA,-909000,-606000,-5000
*VFILL,VALUE(1,2),DATA,MX,MY,MZ
*VFILL,VALUE(1,3),DATA,ABS(MX/909000),ABS(MY/606000),ABS(MZ/5000)
SAVE,TABLE_3
/NOPR
RESUME,TABLE_1
/GOPR
/COM
/OUT,vm196,vrt
/COM,------------------- VM196 RESULTS COMPARISON --------------
/COM,
/COM,                 |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
/COM, SOLID45
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.0,'  ',F15.0,'   ',1F15.3)
/NOPR
RESUME,TABLE_2
/GOPR
/COM,
/COM, SOLID185
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.0,'  ',F15.0,'   ',1F15.3)
/NOPR
RESUME,TABLE_3
/GOPR
/COM,
/COM, SOLID185 WITH SUBSTRUCTURE MODELING
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.0,'  ',F15.0,'   ',1F15.3)
/COM,-----------------------------------------------------------
/COM,
/COM,-----------------------------------------------------------
/COM,NOTE: THERE ARE VERIFIED RESULTS IN VM196  NOT CONTAINED IN
/COM,THIS TABLE
/COM,-----------------------------------------------------------
/OUT
*LIST,vm196,vrt
FINISH

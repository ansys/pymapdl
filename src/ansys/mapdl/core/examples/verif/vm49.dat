/COM,ANSYS MEDIA REL. 2023R1 (11/04/2022) REF. VERIF. MANUAL: REL. 2023R1
/VERIFY,VM49   
/PREP7 
SMRT,OFF 
/TITLE, VM49, ELECTROSTATIC FIELD ANALYSIS OF QUADPOLE WIRES IN OPEN AIR
C*** ANY BASIC STATIC AND DYNAMIC ELECTRICITY BOOK
ANTYPE,STATIC
ET,1,PLANE121                ! 2-D 8-NODE ELECTROSTATIC ELEMENT             
ET,2,INFIN110,1              ! 2-D 4-NODE INFINITE ELEMENT WITH VOLT DOF
EMUNIT,MKS                   ! MKS UNIT 
MP,PERX,1,1                  ! ELECTRICAL PERMITTIVITY 
CSYS,1                       ! CYLINDRICAL COORDINATE SYSTEM
PCIRC,25.4/1000,0,90         ! QUARTER CIRCULAR AREA 
PCIRC,50.8/1000,0,90
PCIRC,470/1000,0,90
AOVLAP,1,2,3                 ! OVERLAP AREAS 
KPSCALE,7,8,1,2              ! SCALE KEYPOINTS 7 & 8 TO DOUBLE
L,7,6
L,6,9
L,8,9
AL,7,5,6,8
LSEL,S,LINE,,1,4,1           ! SELECT LINES 
LSEL,A,LINE,,6,7
LSEL,A,LINE,,10,11
LESIZE,ALL,,,10              ! DIVIDE THE SELECTED LINES INTO TEN 
!                               DIVISION  
LSEL,ALL
LSEL,S,LINE,,12,13
LESIZE,ALL,,,30,10
LSEL,ALL
LSEL,S,LINE,,5,8,3
LESIZE,ALL,,,1
LSEL,ALL
TYPE,2                       ! USE ELEMENT TYPE 2
MSHK,1                    ! MAPPED AREA MESH 
MSHA,0,2D                 ! USING QUADS
ESIZE,,1                     ! CREATE 1 ELEMENT PER LINE DIVISION
ASEL,S,AREA,,2
AMESH,ALL                    ! MESH THE AREA 2
ASEL,ALL
ESIZE,,10                    ! CREATE 10 ELEMENTS PER LINE DIVISION
TYPE,1                       ! USE ELEMENT TYPE 1 
ASEL,S,AREA,,1,4,3           ! SELECT AREAS
ASEL,A,AREA,,5
AMESH,ALL
NSEL,S,LOC,X,25.4/1000       ! SELECT NODES 
NSEL,R,LOC,Y,0
F,ALL,CHRG,.5E-6             ! APPLY CHARGE  AS POINT LOAD
NSEL,S,LOC,X,25.4/1000
NSEL,R,LOC,Y,90  
F,ALL,CHRG,-.5E-6          
NSEL,ALL
NSEL,S,LOC,X,940/1000        
SF,ALL,INF                   ! FLAG THE EXTERIOR FACE OF INFIN110 AT  
!                               INFINITE DISTANCE 
NSEL,ALL
FINISH
/SOLU
OUTRES,ALL,ALL
OUTPR,,NONE
SOLVE
FINISH
/POST1
/COM    SELECT THE NODES AT ANGLES FROM 0 TO 90 DEGREE WITH 10 
/COM     DIVISION ON SURFACE OF RADIUS 470 MM AND RETRIEVE THE 
/COM      ELECTRIC POTENTIAL, V
DSYS,1
*DIM,ANG,,11,2
*VFILL,ANG(1,1),RAMP,0,9
*DO,J,1,11
   NSEL,S,LOC,X,470/1000
   NSEL,R,LOC,Y,ANG(J,1)
   *GET,NOD,NODE,,NUM,MAX
   *GET,ANG(J,2),NODE,NOD,VOLT
   NSEL,ALL
*ENDDO
*DIM,VLT,,11
*VFUN,VLT(1),COPY,ANG(1,2)
*DIM,VALUE,,11,2
*VFILL,VALUE(1,1),DATA,105.05,99.9,84.98,61.74,32.46,0,-32.46,-61.74,-84.98
*VFILL,VALUE(10,1),DATA,-99.98,-105.05
*VFILL,VALUE(1,2),DATA,ABS(VLT(1,1)/105.05 ),ABS(VLT(2,1)/99.9),ABS(VLT(3,1)/84.98)
*VFILL,VALUE(4,2),DATA,ABS(VLT(4,1)/61.74),ABS(VLT(5,1)/32.46),0
*VFILL,VALUE(7,2),DATA,ABS(VLT(7,1)/32.46),ABS(VLT(8,1)/61.74),ABS(VLT(9,1)/84.98)
*VFILL,VALUE(10,2),DATA,ABS(VLT(10,1)/99.98),ABS(VLT(11,1)/105.05 )
*DIM,LABEL,CHAR,11,2
*DO,I,1,11,1
LABEL(I,1) = 'V(VOLT) '
LABEL(I,2) = 'AT ANGLE'
*ENDDO
! WRITE DESIRED ANGLE AND POTENTIAL VALUES
/COM
/OUT,vm49,vrt
/COM,------------------- VM49 RESULTS COMPARISON ---------------
/COM,
/COM,                        |TARGET | Mechanical APDL  | RATIO
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),ANG(1,1),VALUE(1,1),VLT(1),VALUE(1,2)
(1X,A8,A8,': ',F4.1,'  ',F7.2,'  ',F12.2,'  ',1F15.3)
/COM,-----------------------------------------------------------
/OUT
FINISH
*LIST,vm49,vrt


/COM,ANSYS MEDIA REL. 150 (11/8/2013) REF. VERIF. MANUAL: REL. 150
/VERIFY,VM201
JPGPRF,500,100,1               ! MACRO TO SET PREFS FOR JPEG PLOTS
/SHOW,JPEG
R = 200                        ! RADIUS OF CYLINDER (mm)
/PREP7
smrt,off
/TITLE, VM201, RUBBER CYLINDER PRESSED BETWEEN TWO PLATES
/COM    REF: T. SUSSMAN, K.J. BATHE, "A FE FORMULATION FOR NONLINEAR ..."
/COM         COMPUTERS & STRUCTURES, VOL. 26, NOS. 1/2, 1987
ET,1,PLANE182, , ,2            ! 2-D PLANE-STRAIN 4-NODE STRUCTURAL SOLID
KEYOPT,1,6,1
ET,2,CONTA175                  ! 2-D 1-NODE NODE-TO-SURFACE CONTACT ELEMENT
R,2, , ,-2000                  ! SET SURFACE STIFFNESS
ET,3,TARGE169                  ! 2-D TARGET ELEMENT
C10 = 0.293
C01 = 0.177
NU1 = 0.49967
DD  = (1-2*NU1)/(C10+C01)
TB,HYPER,1,1,2,MOONEY
TBDATA,1,C10,C01,DD
CSYS,1                         ! SWITCH TO CYLINDRICAL C.S.
K,1                            ! DEFINE KEYPOINTS
K,2,R,-90
K,3,R
K,4,(0.5*R),-90
K,5,(0.6*R),-45
K,6,(0.5*R)
K,7,R,-45
L,2,7
L,7,3
CSYS,0                         ! SWITCH TO CARTESIAN C.S.
A,2,7,5,4
A,7,3,6,5
A,4,5,6,1
ESIZE,,4                       ! SET ELEMENT DIVISION SIZE
AMESH,ALL                      ! MESH ALL AREAS
SAVE                           ! SAVE MODEL FOR MORE ANALYSIS
N,1001,(-2*R),-R               ! TARGET SURFACE NODES
N,1002,(2*R) ,-R
NSEL,S,NODE,,1001,1002
TYPE,3
REAL,2
TSHAP,LINE                     ! SET TARGET SHAPE TO LINE
E,1002,1001                    ! GENERATE RIGID TARGET
D,ALL,ALL,0                    ! FIX TARGET
NSEL,S,LOC,X                   ! SELECT LEFT EDGE
D,ALL,UX                       ! CONSTRAIN LEFT EDGE IN UX
NSEL,S,LOC,Y                   ! SELECT TOP EDGE
CP,1,UY,ALL                    ! COUPLE TOP EDGE IN UY
*GET,NCEN,NODE,,NUM,MIN        ! GET MINIMUM NODE NUMBER FROM SELECTED SET
NSEL,ALL
CSYS,1                         ! SWITCH TO CYLINDRICAL C.S.
ESEL,S,TYPE,,1
NSLE
NSEL,R,LOC,X,R
TYPE,2
REAL,2
ESURF                          ! DEFINE CONTACT ELEMENTS
ALLSEL,ALL
CSYS,0
SAVE,CONT2D                    ! SAVE 2D CONTACT MODEL FOR SECOND ANALYSIS
FINISH

*CREATE,SOLVIT,MAC             ! MACRO TO SOLVE MODEL
 /SOLU
 ANTYPE,STATIC
 CNVTOL,F,,,,-1
 NLGEOM,ON                     ! INCLUDE LARGE DEFORMATION EFFECTS
 NSUBST,6                      ! SPECIFY NUMBER OF SUBSTEPS IN LOAD STEP
 OUTRES,,1                     ! WRITE SOLUTION FOR EVERY SUBSTEP
 D,NCEN,UY,-100                ! APPLY DISPLACEMENT UY = -100 TO COUPLED NODES
 SOLVE
 FINISH
*END
SOLVIT                         ! USE MACRO SOLVIT

*CREATE,PLOTS,MAC              ! MACRO FOR POST-PROCESSING
 /POST1
 /DSCALE,1,1
 PLDISP,1                      ! PLOT DISPLACED SHAPE
 FINISH
 /POST26
 /AXLAB,Y,FORCE
 /AXLAB,X,DISPLACEMENT
 NSOL,2,NCEN,U,Y
 RFORCE,3,NCEN,F,Y
 PROD,2,2,,,,,,-2
 PROD,3,3,,,,,,-2
 XVAR,2
 PLVAR,3                       ! PLOT DISPLACEMENT VS FORCE
 PRVAR,2,3                     ! PRINT DISPLACEMENT, FORCE
 *GET,F1,VARI,3,RTIME,.5
 *GET,F2,VARI,3,RTIME,1
 *DIM,LABEL,CHAR,2,2
 *DIM,VALUE,,2,3
 LABEL(1,1) = 'F (N) @ ','F (N) @ '
 LABEL(1,2) = '.1','.2'
 *VFILL,VALUE(1,1),DATA,250,1400
 *VFILL,VALUE(1,2),DATA,F1,F2
 *VFILL,VALUE(1,3),DATA,ABS(F1/250),ABS(F2/1400)
 FINISH
*END
PLOTS                          ! USE MACRO PLOTS
SAVE,TABLE_1

RESUME
/PREP7
SMRT,OFF
ET,5,SOLID185                  ! 3-D 8-NODE STRUCTURAL SOLID
KEYOPT,5,6,1
ET,6,CONTA175                  ! 3-D 1-NODE NODE-TO-SURFACE CONTACT ELEMENT
R,6, , ,-2000,-0.1,
ET,7,TARGE170                  ! 3-D TARGET ELEMENT
ET,8,MESH200,6                 ! 2-D 4-NODED QUAD
R,8,0.05
ALLSEL
TYPE,5
ESIZE,,1
VEXT,ALL,,,,,1
N,1001,,-R                     ! CREATE TARGET PLANE OF NODES
N,1002,2*R,-R
N,1003,2*R,-R,8*R
N,1004,,-R,8*R
TYPE,8
REAL,8
E,1002,1001,1004,1003
NSEL,S,NODE,,1001,1004
TYPE,7
REAL,6
ESURF                          ! GENERATE TARGET ELEMENTS
D,ALL,ALL,0
CSYS,1                         ! SWITCH TO CYLINDRICAL C.S.
ESEL,S,TYPE,,5
NSLE
NSEL,R,LOC,X,R
ESEL,S,TYPE,,5,7
TYPE,6
REAL,6
ESURF
CSYS,0                         ! SWITCH TO CARTESIAN C.S.
NSEL,ALL
D,ALL,UZ                       ! CONSTRAIN ALL NODES IN Z (PLANE STRAIN)
NSEL,S,LOC,X
D,ALL,UX
NSEL,S,LOC,Y
CP,1,UY,ALL                    ! COUPLE TOP NODES IN Y
*GET,NCEN,NODE,,NUM,MIN
ESEL,S,TYPE,,5,7
NSLE
SAVE,CONT3D                    ! SAVE 3D CONTACT MODEL FOR SECOND ANALYSIS
FINISH
/OUT,SCRATCH
SOLVIT                         ! USE MACRO TO OBTAIN SOLUTION
PLOTS                          ! USE MACRO TO POSTPROCESS
SAVE,TABLE_2

RESUME,CONT2D                  ! RESUME CONT175 -2D MODEL
/OUT,
/PREP7
R,2                            ! CONTACT STIFFNESS IS NOT REQUIRED
KEYOPT,2,2,3                   ! LAGRANGE MULTIPLIER ON CONTACT NORMAL AND PENALTY ON TANGENT
FINISH
/OUT,SCRATCH
SOLVIT                         ! USE MACRO TO OBTAIN SOLUTION
PLOTS                          ! USE MACRO TO POSTPROCESS
SAVE,TABLE_3

RESUME,CONT3D                  ! RESUME CONT175 -3D MODEL
/OUT,
/PREP7
R,6                            ! CONTACT STIFFNESS IS NOT REQUIRED
KEYOPT,6,2,4                   ! PURE LAGRANGE MULTIPLIER ON CONTACT NORMAL AND TANGENT
FINISH
/OUT,SCRATCH
SOLVIT                         ! USE MACRO TO OBTAIN SOLUTION
PLOTS                          ! USE MACRO TO POSTPROCESS
SAVE,TABLE_4

RESUME,TABLE_1
/OUT,
/COM
/OUT,vm201,vrt
/COM,------------------- VM201 RESULTS COMPARISON --------------
/COM,
/COM,                 |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
/COM,RESULTS USING PLANE182 AND 2D-CONTA175:
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.2,'  ',F14.2,'   ',1F15.3)
/NOPR
RESUME,TABLE_2
/GOPR
/COM,
/COM,RESULTS USING SOLID185 AND 3D-CONTA175:
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.2,'  ',F14.2,'   ',1F15.3)
/NOPR
RESUME,TABLE_3
/GOPR
/COM,
/COM,RESULTS USING PLANE182 AND 2D-CONTA175 WITH K(2)=3:
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.2,'  ',F14.2,'   ',1F15.3)
/NOPR
RESUME,TABLE_4
/GOPR
/COM,
/COM,RESULTS USING SOLID185 AND 3D-CONTA175 WITH K(2)=4:
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.2,'  ',F14.2,'   ',1F15.3)
/COM,-----------------------------------------------------------
/COM,
/OUT
FINISH
*LIST,vm201,vrt
/DELETE,PLOTS,MAC
/DELETE,SOLVIT,MAC
/DELETE,TABLE_1
/DELETE,TABLE_2
/DELETE,TABLE_3
/DELETE,TABLE_4

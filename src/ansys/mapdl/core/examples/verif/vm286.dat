/COM,ANSYS MEDIA REL. 2023R1 (11/04/2022) REF. VERIF. MANUAL: REL. 2023R1
/VERIFY,VM286
/TITLE,VM286,WEAR OF BLOCK UNDER UNIFORM COMPRESSION
/COM, 2D MODEL WITH CONTACT ELEMENT 172
/PREP7 
LEN=10
! ELEMENTS TYPES
ET,1,PLANE182
ET,2,TARGE169
ET,3,CONTA172
KEYOPT,3,2,0            ! AUGMENTED LAGRANGIAN CONTACT FORMULATION
KEYOPT,3,4,2            ! NODAL DETECTION-NORMAL TO TARGET
KEYOPT,3,10,1           ! USE CONSTANT CONTACT STIFFNESS

!MATERIAL PROPETIES
MP,EX,1,100E3           ! YOUNG'S MODULUS
MP,NUXY,1,0.3           ! POISSON'S RATIO

! WEAR PROPERTIES
KARCD=0.0001            ! WEAR COEFFICIENT
TB,WEAR,2,,,ARCD        ! WEAR MATERIAL MODEL
TBFIELD,TIME,0
TBDATA,1,0.00000,1,1,1  ! NO WEAR IN FIRST LOAD STEP  
TBFIELD,TIME,1
TBDATA,1,0.00000,1,1,1 
TBFIELD,TIME,1.0001
TBDATA,1,KARCD,1,1,1    ! START WEAR IN 2ND LOAD STEP
TBFIELD,TIME,10
TBDATA,1,KARCD,1,1,1 

! GEOMETRY AND MESH
RECTNG,,LEN,,LEN
LESIZE,ALL,,,2
TYPE,1
REAL,1
MAT,1
AMESH,1

! GENERATE CONTACT ELEMENTS
TYPE,3
REAL,2
MAT,2
NSEL,S,LOC,Y
ESURF
ALLSEL,ALL,

! GENERATE TARGET ELEMENTS
TYPE,2
REAL,2
MAT,2
N,1001,-5*LEN
N,1002,5*LEN
E,1002,1001
TSHAP,PILOT
E,1001
NSEL,S,NODE,,1001
CM,PILOTNODE,NODES
NSEL,ALL

! APPLY BC
NSEL,S,LOC,X
NSEL,R,LOC,Y,LEN
CM,UXFIX,NODES
D,UXFIX,UX,0
NSEL,ALL
NSEL,S,LOC,Y,LEN
CM,UYFIX,NODES
D,UYFIX,UY,-0.01*LEN
ALLSEL,ALL,ALL
FINISH

/SOLUTION
ANTYPE,STATIC           ! STATIC ANALYSIS
NLGEOM,ON               ! NON-LINEAR ANALYSIS
OUTRES,ALL,ALL
AUTOTS,ON               ! AUTO TIME STEPPING
! LOAD STEP 1, PUSH DOWN THE BLOCK ON THE RIGID PLATE
TIME,1                  ! END TIME
DELTIM,1,1E-3,1         ! TIME INCREMENT
/OUT,SCRATCH
SOLVE                   ! 1ST LOAD STEP

! LOAD STEP 2, START WEAR BY SLIDING THE RIGID PLATE
D,PILOTNODE,UX,1
TIME,2                  ! END TIME
DELTIM,0.01,1E-4,0.01   ! TIME INCREMENT
SOLVE                   ! 2ND LOAD STEP
FINISH

/POST1
! CALCULATE THE REFERENCE SOLUTION
EX_MODULI=100e3
LEN=10
U_APPLIED=0.01*LEN
TIME=1
DTIME=0.01
NSUBTOT=TIME/DTIME +1
KWEAR=1e-4
*DIM,WEAR,ARRAY,NSUBTOT
*VFILL,WEAR,RAMP,0,0
*DIM,STRAIN_,ARRAY,NSUBTOT
*VFILL,STRAIN_,RAMP,0,0
*DIM,STRESS_,ARRAY,NSUBTOT
*VFILL,STRESS_,RAMP,0,0
*DO,I,1,NSUBTOT
STRAIN_(I)=((U_APPLIED-WEAR(I))/LEN)+ 0.5*((U_APPLIED-WEAR(I))/LEN)**2
STRESS_(I)=EX_MODULI*STRAIN_(I)
*IF,I,LT,NSUBTOT,THEN
WEAR(I+1)=WEAR(I)+(KWEAR*STRESS_(I)*DTIME)
*ENDIF
*ENDDO
*DIM,LABEL1,CHAR,8
LABEL1(1)='WEAR-Y'
*DIM,LABEL2,CHAR,8
LABEL2(1)='CON-PRES'
! POSTPROCESS MAPDL RESULTS
SET,2
ESEL,S,TYPE,,3
EL1_=ELNEXT(0)
*GET,ANSWEAR,ELEM,EL1_,NMISC,88
*GET,ANSPRES,ELEM,EL1_,SMISC,5
/OUT,vm286,vrt
/COM,----------------- VM286 RESULTS COMPARISON -------------------
/COM,
/COM,
/COM,2D    WEAR    |  TARGET   |  Mechanical APDL  |   RATIO
/COM,
*VWRITE,LABEL1(1),WEAR(NSUBTOT),ANSWEAR,ANSWEAR/WEAR(NSUBTOT)
(2X,A12,6X,,F6.4,6X,,F6.4,12X,,F5.3)
/COM,
/COM,
/COM,
/COM,2D    PRES      |  TARGET   |  Mechanical APDL  |   RATIO
/COM,
*VWRITE,LABEL2(1),STRESS_(NSUBTOT),ANSPRES,ANSPRES/STRESS_(NSUBTOT)
(2X,A12,4X,,F12.4,4X,,F12.3,8X,,F5.3)
/COM,
/COM,
/COM,----------------- VM286 RESULTS COMPARISON -------------------
FINISH


/CLEAR,NOSTART

/OUT,
/COM, 3D MODEL WITH  CONTACT ELEMENT 174
/PREP7 
LEN=10
! ELEMENTS TYPES
ET,1,SOLID185
ET,2,TARGE170
ET,3,CONTA174
KEYOPT,3,2,3            ! PURE LAGRANGIAN CONTACT FORMULATION
KEYOPT,3,4,2            ! NODAL DETECTION-NORMAL TO TARGET
KEYOPT,3,10,1           ! USE CONSTANT CONTACT STIFFNESS

!MATERIAL PROPETIES
MP,EX,1,100E3           ! ELASTIC PROP
MP,NUXY,1,0.3

! WEAR PROPERTIES
KARCD=0.0001            ! WEAR COEFFICIENT
TB,WEAR,2,,,ARCD        ! WEAR MATERIAL MODEL
TBFIELD,TIME,0
TBDATA,1,0.00000,1,1,1  ! NO WEAR IN FIRST LOAD STEP  
TBFIELD,TIME,1
TBDATA,1,0.00000,1,1,1 
TBFIELD,TIME,1.0001
TBDATA,1,KARCD,1,1,1    ! START WEAR IN 2ND LOAD STEP
TBFIELD,TIME,10
TBDATA,1,KARCD,1,1,1 

! GEOMETRY AND MESH
BLOCK,,LEN,,LEN,,LEN
LESIZE,ALL,,,2
TYPE,1
REAL,1
MAT,1
VMESH,1
! GENERATE CONTACT ELEMENTS

TYPE,3
REAL,2
MAT,2
NSEL,S,LOC,Z
ESURF
ALLSEL,ALL,ALL

! GENERATE TARGET ELEMENTS
TYPE,2
REAL,2
MAT,2
N,1001,-5*LEN,-5*LEN
N,1002,5*LEN,-5*LEN
N,1003,5*LEN,5*LEN
N,1004,-5*LEN,5*LEN
E,1001,1002,1003,1004
TSHAP,PILOT
E,1001
NSEL,S,NODE,,1001
CM,PILOTNODE,NODES
NSEL,ALL

! APPLY BC
NSEL,S,LOC,X
NSEL,R,LOC,Y
NSEL,R,LOC,Z,LEN
CM,ALLFIX,NODES
D,ALLFIX,UX,0
D,ALLFIX,UY,0

NSEL,ALL
NSEL,S,LOC,Z,LEN
NSEL,R,LOC,X,LEN
NSEL,R,LOC,Y
CM,UYFIX,NODES
D,UYFIX,UY,0

NSEL,ALL
NSEL,S,LOC,Z,LEN
NSEL,R,LOC,Y,LEN
NSEL,R,LOC,X
CM,UXFIX,NODES
D,UXFIX,UX,0

NSEL,S,LOC,Z,LEN
CM,BCNODES,NODES
D,BCNODES,UZ,-0.01*LEN
ALLSEL,ALL,ALL
FINISH

/SOLU
ANTYPE,STATIC           ! STATIC ANALYSIS
NLGEOM,ON               ! NON-LINEAR ANALYSIS
OUTRES,ALL,ALL
AUTOTS,ON               ! AUTO TIME STEPPING
! LOAD STEP 1, PUSH DOWN THE BLOCK ON THE RIGID PLATE
TIME,1                  ! END TIME
DELTIM,1,1E-3,1         ! TIME INCREMENT
/OUT,SCRATCH
SOLVE                   ! PERFORM 1ST LOAD STEP

! LOAD STEP 2, START WEAR BY SLIDING THE RIGID PLATE
D,PILOTNODE,UX,1
TIME,2                  ! END TIME
DELTIM,0.01,1E-4,0.01   ! TIME INCREMENT
SOLVE                   ! PERFORM 2ND LOAD STEP
FINISH

/POST1
! CALCULATE THE REFERENCE SOLUTION
EX_MODULI=100e3
LEN=10
U_APPLIED=0.01*LEN
TIME=1
DTIME=0.01
NSUBTOT=TIME/DTIME +1
KWEAR=1e-4
*DIM,WEAR,ARRAY,NSUBTOT
*VFILL,WEAR,RAMP,0,0
*DIM,STRAIN_,ARRAY,NSUBTOT
*VFILL,STRAIN_,RAMP,0,0
*DIM,STRESS_,ARRAY,NSUBTOT
*VFILL,STRESS_,RAMP,0,0
*DO,I,1,NSUBTOT
STRAIN_(I)=((U_APPLIED-WEAR(I))/LEN)+ 0.5*((U_APPLIED-WEAR(I))/LEN)**2
STRESS_(I)=EX_MODULI*STRAIN_(I)
*IF,I,LT,NSUBTOT,THEN
WEAR(I+1)=WEAR(I)+ (KWEAR*STRESS_(I)*DTIME)
*ENDIF
*ENDDO
*DIM,LABEL1,CHAR,8
LABEL1(1)='WEAR-Z'
*DIM,LABEL2,CHAR,8
LABEL2(1)='CON-PRES'
! POSTPROCESS ANSYS RESULTS
SET,2
ESEL,S,TYPE,,3
EL1_=ELNEXT(0)
*GET,ANSWEAR,ELEM,EL1_,NMISC,181
*GET,ANSPRES,ELEM,EL1_,SMISC,13

/OUT,vm286,vrt,,append
/COM,----------------- VM286 RESULTS COMPARISON -------------------
/COM,
/COM,
/COM,3D    WEAR    |  TARGET   |  Mechanical APDL  |   RATIO
/COM,
*VWRITE,LABEL1(1),WEAR(NSUBTOT),ANSWEAR,ANSWEAR/WEAR(NSUBTOT)
(2X,A12,6X,,F6.4,6X,,F6.4,12X,,F5.3)
/COM,
/COM,
/COM,
/COM,
/COM,3D    PRES      |  TARGET   |  Mechanical APDL  |   RATIO
/COM,
*VWRITE,LABEL2(1),STRESS_(NSUBTOT),ANSPRES,ANSPRES/STRESS_(NSUBTOT)
(2X,A12,4X,,F12.4,4X,,F12.3,8X,,F5.3)
/COM,
/COM,
/COM,----------------- VM286 RESULTS COMPARISON -------------------
/OUT
*LIST,vm286,vrt
FINI
/EXIT,NOSAVE








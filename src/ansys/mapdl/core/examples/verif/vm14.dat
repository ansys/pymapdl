/COM,ANSYS MEDIA REL. 2023R1 (11/04/2022) REF. VERIF. MANUAL: REL. 2023R1
/VERIFY,VM14
/PREP7
/TITLE, VM14, LARGE DEFLECTION ECCENTRIC COMPRESSION OF SLENDER COLUMN
C***      STR. OF MATL., TIMOSHENKO, PART 1, 3RD ED., PG. 263, PROB. 1
ET,1,BEAM188,,,3
SECTYPE,1,BEAM,CHAN
SECDATA,2.26,2.26,8,0.39,0.39,0.22
SECOFFSET,USER,,0.6465
MP,EX,1,30E6
MP,PRXY,1,0.3
N,1
N,5,,60
FILL
E,1,2
EGEN,4,1,1
D,1,ALL
F,5,FY,-4000
DSYM,SYMM,Z
FINISH
/SOLU   
NLGEOM,ON               ! ACTIVATE LARGE DEFLECTIONS
CNVTOL,F,,1E-4
CNVTOL,M,,1E-4
OUTPR,,LAST
/OUT,SCRATCH
SOLVE
FINISH
/POST1
/OUT,
END_NODE = NODE (0,120/2,0)
*GET,DEF,NODE,END_NODE,U,X
*GET,STS_TENS,SECR,1,S,X,MAX
*GET,STS_COMP,SECR,1,S,X,MIN
*DIM,LABEL,CHAR,3,2
*DIM,VALUE,,3,3
LABEL(1,1) = 'DEFLECTI','STRSS_TE','STRSS_CO'
LABEL(1,2) = 'ON (in) ','NS (psi)','MP (psi)'
*VFILL,VALUE(1,1),DATA,.1086,1803.63,-2394.53
*VFILL,VALUE(1,2),DATA,ABS(DEF),STS_TENS,STS_COMP
*VFILL,VALUE(1,3),DATA, ABS(DEF/.1086 ),ABS(STS_TENS/1803.63),ABS(STS_COMP/2394.53)
/COM
/OUT,vm14,vrt
/COM,------------------- VM14 RESULTS COMPARISON ---------------------
/COM,
/COM,                 |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.4,'  ',F12.4,'   ',1F15.3)
/COM,-----------------------------------------------------------------
/OUT
FINISH
*LIST,vm14,vrt

/COM,ANSYS MEDIA REL. 2023R1 (11/04/2022) REF. VERIF. MANUAL: REL. 2023R1
/VERIFY,VM39
/PREP7    
JPGPRF,500,100,1             ! MACRO TO SET PREFS FOR JPEG PLOTS
/SHOW,JPEG
/TITLE, VM39, BENDING OF A CIRCULAR PLATE WITH A CENTER HOLE
C***          STR. OF MATLS., TIMOSHENKO, PART 2, 3RD ED., PAGE 111, EQNS. (E,F)
C***          USING SHELL63 ELEMENTS
ANTYPE,STATIC
ET,1,SHELL63
R,1,.25                     ! DEFINE PLATE THICKNESS = .25
MP,EX,1,30.E6
MP,NUXY,1,.3
CSYS,1                      ! DEFINE CYLINDRICAL C.S.
N,1,10                      ! BEGIN NODE DEFINITION
N,7,30
FILL,,,,,,,,3               ! USE 3:1 SPACING RATIO FOR FILLING IN NODES 
NGEN,2,10,1,7,1,,10
NROTAT,1,17,1
E,1,2,12,11                 ! DEFINE FIRST ELEMENT
EGEN,6,1,1                  ! GENERATE NEXT 5 ELEMENTS 
D,1,ALL,,,11,10             ! CONSTRAIN INNER EDGE IN ALL D.O.F.
D,2,UY,,,7,,ROTX,ROTZ       ! CONSTRAIN LOWER EDGE AGAINST ROTATIONS IN X & Z
D,12,UY,,,17,,ROTX,ROTZ     ! CONSTRAIN UPPER EDGE AGAINST ROTATIONS IN X & Z
F,7,MY,-26.18,,17,10        ! APPLY MOMENT LOAD AT OUTER EDGE
OUTPR,BASIC,1
FINISH
*CREATE,SOLVIT,MAC
/SOLU    
/OUT,SCRATCH
SOLVE
FINISH
/POST1
/WINDOW,1,TOP               ! SET UP WINDOW 1 FOR DISPLACEMENT CONTOUR DISPLAY
/PLOPTS,MINM,OFF	    ! TURN OFF MN AND MX DUE TO INSTABILITY
PLNSOL,U,Z                  ! DISPLAY PERPENDICULAR DISPLACEMENTS AS CONTOURS
/WINDOW,1,OFF               ! TURN OFF WINDOW 1
/NOERASE                    ! TURN OFF AUTOMATIC ERASE BETWEEN DISPLAYS
/WINDOW,2,BOT               ! SET UP WINDOW 2 FOR EDGE DISPLACEMENT DISPLAY
/VIEW,2,,-1                 ! CHANGE VIEW FOR WINDOW 2
PLDISP,1                    ! DISPLAY UNDISPLACED & DISPLACED SHAPES
/OUT,
SHELL,TOP
ESEL,,,,1                   ! SELECT INNER ELEMENT(ELEM #1) 
ETABLE,MOMX,SMISC,4         ! RETRIEVE MOMENT(X) AND SX AT TOP
ETABLE,SIGX,S,X
PRETAB,GRP1                 ! PRINT STORED VALUES
*GET,M1,ETAB,1,ELEM,1
*GET,P1,ETAB,2,ELEM,1
ESEL,,,,6                   ! SELECT OUTER ELEMENT(ELEM#6)
ETABLE,REFL
PRETAB,GRP1                 ! PRINT STORED VALUES 
*GET,M2,ETAB,1,ELEM,6
*GET,P2,ETAB,2,ELEM,6
ESEL,ALL                        
RSYS,1
PRNSOL,S,COMP               ! PRINT NODAL STRESSES
NSEL,S,LOC,X,30             ! SELECT NODES AT R=A     
PRNSOL,DOF                  ! PRINT DISPLACEMENTS
*GET,DEF,NODE,7,U,Z
*GET,ROT,NODE,7,ROT,Y
*DIM,LABEL,CHAR,2,2
*DIM,VALUE,,2,3
LABEL(1,1) = 'DEFLECTI','MX_SLOPE'
LABEL(1,2) = 'ON (in) ','(rad)   '
*VFILL,VALUE(1,1),DATA,.049064,-.0045089
*VFILL,VALUE(1,2),DATA,DEF,ROT
*VFILL,VALUE(1,3),DATA,ABS(DEF/.049064 ) ,ABS( ROT/.0045089)
*DIM,LABEL2,CHAR,2,2
*DIM,VALUE2,,2,3
LABEL2(1,1) = 'MOMENT ','PRESSURE'
LABEL2(1,2) = 'in-lb/in',' psi  '
*VFILL,VALUE2(1,1),DATA,-13.783,-1323.2
*VFILL,VALUE2(1,2),DATA,M1,P1
*VFILL,VALUE2(1,3),DATA,ABS(M1/13.783),ABS(P1/1323.2)
*DIM,VALUE3,,2,3
*VFILL,VALUE3(1,1),DATA,-10.127,-972.22
*VFILL,VALUE3(1,2),DATA,M2,P2
*VFILL,VALUE3(1,3),DATA,ABS(M2/10.127),ABS(P2/972.22)
FINISH
*END
SOLVIT
SAVE,TABLE_1
/CLEAR, NOSTART                ! CLEAR DATABASE FOR SECOND SOLUTION
/TITLE, VM39, BENDING OF A CIRCULAR PLATE WITH A CENTER HOLE
C***          USING SHELL181 ELEMENTS
/PREP7    
ANTYPE,STATIC
ET,1,SHELL181, , ,2
SECT,1,SHELL
SECD,.25,1                  ! DEFINE PLATE THICKNESS = .25
MP,EX,1,30.E6
MP,NUXY,1,.3
CSYS,1                      ! DEFINE CYLINDRICAL C.S.
N,1,10                      ! BEGIN NODE DEFINITION
N,7,30
FILL,,,,,,,,3               ! USE 3:1 SPACING RATIO FOR FILLING IN NODES 
NGEN,2,10,1,7,1,,10
NROTAT,1,17,1
E,1,2,12,11                 ! DEFINE FIRST ELEMENT
EGEN,6,1,1                  ! GENERATE NEXT 5 ELEMENTS 
D,1,ALL,,,11,10             ! CONSTRAIN INNER EDGE IN ALL D.O.F.
D,2,UY,,,7,,ROTX,ROTZ       ! CONSTRAIN LOWER EDGE AGAINST ROTATIONS IN X & Z
D,12,UY,,,17,,ROTX,ROTZ     ! CONSTRAIN UPPER EDGE AGAINST ROTATIONS IN X & Z
F,7,MY,-26.18,,17,10        ! APPLY MOMENT LOAD AT OUTER EDGE
OUTPR,NSOL,1
OUTPR,RSOL,1
FINISH
SOLVIT
SAVE,TABLE_2
/NOPR
RESUME,TABLE_1
/COM
/OUT,vm39,vrt
/COM,------------------- VM39 RESULTS COMPARISON --------------
/COM,
/COM,                 |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
/COM,RESULTS USING SHELL63:
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.7,'  ',F14.7,'   ',1F15.3)
/COM,
/COM,X=10.81 in
/COM,
*VWRITE,LABEL2(1,1),LABEL2(1,2),VALUE2(1,1),VALUE2(1,2),VALUE2(1,3)
(1X,A8,A8,'   ',F10.3,'  ',F14.3,'   ',1F15.3)
/COM,
/COM,X=27.1 in
/COM,
*VWRITE,LABEL2(1,1),LABEL2(1,2),VALUE3(1,1),VALUE3(1,2),VALUE3(1,3)
(1X,A8,A8,'   ',F10.3,'  ',F14.3,'   ',1F15.3)
/NOPR
RESUME,TABLE_2
/GOPR
/COM,
/COM,RESULTS USING SHELL181:
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.7,'  ',F14.7,'   ',1F15.3)
/COM,
/COM,X=10.81 in
/COM,
*VWRITE,LABEL2(1,1),LABEL2(1,2),VALUE2(1,1),VALUE2(1,2),VALUE2(1,3)
(1X,A8,A8,'   ',F10.3,'  ',F14.3,'   ',1F15.3)
/COM,
/COM,X=27.1 in
/COM,
*VWRITE,LABEL2(1,1),LABEL2(1,2),VALUE3(1,1),VALUE3(1,2),VALUE3(1,3)
(1X,A8,A8,'   ',F10.3,'  ',F14.3,'   ',1F15.3)
/COM,----------------------------------------------------------
/OUT
FINISH
*LIST,vm39,vrt

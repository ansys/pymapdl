/COM,ANSYS MEDIA REL. 2023R1 (11/04/2022) REF. VERIF. MANUAL: REL. 2023R1
/VERIFY,VM101
/PREP7
JPGPRF,500,100,1             ! MACRO TO SET PREFS FOR JPEG PLOTS
/SHOW,JPEG
/TITLE, VM101, TEMPERATURE DISTRIBUTION IN A SHORT SOLID CYLINDER
C***    CONDUCTION HEAT TRANSFER, SCHNEIDER, 2ND. PRINTING, PAGE 134, FIG. 6-7
ANTYPE,STATIC
ET,1,SOLID70
MP,KXX,1,1
CSYS,1
N,1,1E-10,-5               ! ZERO RADIUS WITH NON-ZERO THETA IS NOT PERMITTED
N,5,.5,-5
FILL
N,6,1E-10,5                ! ZERO RADIUS WITH NON-ZERO THETA IS NOT PERMITTED
N,10,.5,5
FILL
NGEN,5,10,1,10,1,,,.125
E,1,2,7,7,11,12,17,17
E,2,3,8,7,12,13,18,17
EGEN,3,1,2
EGEN,4,10,1,4
OUTPR,,1
D,1,TEMP,,,10               ! APPLY TEMPERATURES
D,15,TEMP,,,40,5
D,41,TEMP,40,,50
NUMMRG,NODE                 ! MERGE COINCIDENT NODE NUMBERS
FINISH
/SOLU
/OUT,vm101_SCRATCH    
SOLVE
/OUT
FINISH
/POST1
/VIEW,,,-1
!/DEVICE,VECTOR,ON
PLNSOL,TEMP
CSYS,1
NSEL,S,LOC,X,0
PRNSOL,TEMP                 ! TEMPERATURES ALONG AXIS (R=0)
NSEL,S,LOC,X,0.25
NSEL,R,LOC,Y,-5
PRNSOL,TEMP                 ! TEMPERATURES ALONG R=0.25 FT
ALLSEL
*GET,TN11,NODE,11,TEMP
*GET,TN21,NODE,21,TEMP
*GET,TN31,NODE,31,TEMP
*GET,TN13,NODE,13,TEMP
*GET,TN23,NODE,23,TEMP
*GET,TN33,NODE,33,TEMP
*DIM,LABEL_1,CHAR,3,2
*DIM,VALUE_1,,3,3
LABEL_1(1,1) = '   NODE ','   NODE ','   NODE '
LABEL_1(1,2) = '11      ','21      ','31      '
*VFILL,VALUE_1(1,1),DATA,6.8,15.6,26.8
*VFILL,VALUE_1(1,2),DATA,TN11,TN21,TN31
*VFILL,VALUE_1(1,3),DATA,ABS(TN11/6.8),ABS(TN21/15.6),ABS(TN31/26.8)
*DIM,LABEL_2,CHAR,3,2
*DIM,VALUE_2,,3,3
LABEL_2(1,1) = '   NODE ','   NODE ','   NODE '
LABEL_2(1,2) = '13      ','23      ','33      '
*VFILL,VALUE_2(1,1),DATA,5.2,12.8,24
*VFILL,VALUE_2(1,2),DATA,TN13,TN23,TN33
*VFILL,VALUE_2(1,3),DATA,ABS(TN13/5.2),ABS(TN23/12.8),ABS(TN33/24)
/COM
/OUT,vm101,vrt
/COM,------------------- VM101 RESULTS COMPARISON --------------
/COM,
/COM,                 |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
/COM,T, F (CENTERLINE):
/COM,
*VWRITE,LABEL_1(1,1),LABEL_1(1,2),VALUE_1(1,1),VALUE_1(1,2),VALUE_1(1,3)
(1X,A8,A8,'   ',F10.1,'  ',F10.1,'   ',1F5.2)
/COM,
/COM,T, F (MID-RADIUS):
/COM,
*VWRITE,LABEL_2(1,1),LABEL_2(1,2),VALUE_2(1,1),VALUE_2(1,2),VALUE_2(1,3)
(1X,A8,A8,'   ',F10.1,'  ',F10.1,'   ',1F5.2)
/COM,----------------------------------------------------------
/OUT 
FINISH
*LIST,vm101,vrt


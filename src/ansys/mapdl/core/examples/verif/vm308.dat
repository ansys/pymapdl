/COM,ANSYS MEDIA REL. 2023R1 (11/04/2022) REF. VERIF. MANUAL: REL. 2023R1
/VERIFY,VM308
/TITLE,VM308,STEADY HEAT CONDUCTION IN FUNCTIONALLY GRADED HOLLOW CYLINDER AND SPHERE
/COM,
/COM, REF: YOSHIHIRO OBATA AND NAOTAKE NODA (1994),
/COM,      STEADY THERMAL STRESSES IN A HOLLOW CIRCULAR 
/COM,      CYLINDER AND A HOLLOW SPHERE OF A FUNCTIONALLY 
/COM,      GRADIENT MATERIAL, JOURNAL OF THERMAL STRESSES,
/COM,      17:3, 471-487.
/COM,

R1=0.1  ! RADIUS OF INNER SURFACE
R2=0.12 ! RADIUS OF OUTER SURFACE

K1=7    ! CONDUCTIVITY ON INNER SURFACE
K2=2    ! CONDUCTIVITY ON OUTER SURFACE

T1=100  ! TEMPERATURE ON INNER SURFACE
T2=25   ! TEMPERATURE ON OUTER SURFACE

/COM, PARAMETER FOR CREATING THE CDB FILES TO STORE GEOMETRY AND MESH DATA
_GEOMGEN = 0   ! SET TO 1 TO WRITE CDB FILES IF ANY INPUT FILE MODIFICATIONS
/OUT,vm308_SCRATCH

/PREP7
*IF,_GEOMGEN,EQ,1,THEN
ET,1,292     ! 2-D 4-NODE THERMAL SOLID
KEYOPT,1,3,1 ! AXISYMMETRIC BEHAVIOR

ET,2,293,1   ! 2-D 6-NODE THERMAL SOLID
KEYOPT,2,3,1 ! AXISYMMETRIC BEHAVIOR

RECTNG,R1,R2,0,R2-R1  ! CREATE THE GEOMETRY FOR THE HOLLOW CYLINDER
LSEL,S,LINE,,1,3,2
LESIZE,ALL,,,8
LSEL,S,LINE,,2,4,2
LESIZE,ALL,,,1
LSEL,ALL
TYPE,1
MAT,1
AMESH,1

PCIRC,R1,R2,0,10  ! CREATE THE GEOMETRY FOR THE HOLLOW SPHERE
ESIZE,0.15*(R2-R1)
TYPE,2
MAT,2
AMESH,2

TB,THERM,1,2,,COND       ! MATERIAL PROPERTIES FOR THE HOLLOW CYLINDER
TBFIELD,XCOR,R1
TBDATA,1,K1,K1,K1
TBFIELD,XCOR,R2
TBDATA,1,K2,K2,K2

TB,THERM,2,2,,COND       ! MATERIAL PROPERTIES FOR THE HOLLOW SPHERE
TBFIELD,UF09,R1
TBDATA,1,K1,K1,K1
TBFIELD,UF09,R2
TBDATA,1,K2,K2,K2

*GET,NN,NODE,0,COUNT
INIS,SET,NODE,1
INIS,SET,DTYP,UF09
CSYS,1
*DO,I,1,NN,1
    INISTATE,DEFINE,I,,,,NX(I) ! SET UF09 EQUAL TO THE RADIUS
*ENDDO
CSYS,0

LSEL,S,LINE,,4,7,3   ! APPLY TEMPERATURE BOUNDARY CONDITIONS
NSLL,S,1
D,ALL,TEMP,T1
LSEL,S,LINE,,2,5,3
NSLL,S,1
D,ALL,TEMP,T2
ALLSE
CDWRITE,db,vm308,cdb
*ELSE
 CDREAD,db,vm308,cdb
*ENDIF

FINISH

/SOLU
ALLSE
ANTYPE,STAT
NSUB,1
SOLVE
FINISH

/OUT
/POST1
SET,LAST
! PRINT AND PLOT THE RESULTS IN COMPANION WITH THE ANALYTICAL SOLUTION
/NOPR
/SHOW,JPEG
/ERASE
/WIND,1,LTOP $ /WIND,2,RTOP $ /WIND,3,LBOT $ /WIND,4,RBOT
/WIND,2,OFF $ /WIND,3,OFF $ /WIND,4,OFF
ESEL,S,TYPE,,1
NSLE,S
EPLOT

PATH,PATH1,2,7,10
PPATH,1,,R1,0,0
PPATH,2,,R2,0,0
PDEF,CYL-NUM,TEMP
PAGET,TAB1,TABLE
PAGET,LAB1,LABEL
ALLSE
LN1=LOG(R1/K1)
LN2=LOG(R2/K2)
C=(T1-T2)/(LN1-LN2)
B=(T2*LN1-T1*LN2)/(LN1-LN2)
*DO,I,1,11,1
    R=TAB1(I,1)
    K=K1+(R-R1)/(R2-R1)*(K2-K1)
    TAB1(I,6)=C*LOG(R/K)+B    ! ANALYTICAL SOLUTION FOR CYLINDER
    TAB1(I,7)=TAB1(I,5)/TAB1(I,6)
*ENDDO
LAB1(6)='CYL-EXA'
PAPUT,TAB1,TABLE
PAPUT,LAB1,LABEL

/WIND,1,OFF
/WIND,2,ON
/NOERASE
PLPATH,CYL-NUM,CYL-EXA

/WIND,2,OFF
/WIND,3,ON
/NOERASE
ESEL,S,TYPE,,2
NSLE,S
EPLOT

PATH,PATH2,2,7,10
PPATH,1,,R1,0,0
PPATH,2,,R2,0,0
PDEF,SPH-NUM,TEMP
PAGET,TAB2,TABLE
PAGET,LAB2,LABEL
ALLSE
D=(K1*R2-K2*R1)/(K2-K1)
LN1=D/R1+LOG(R1/K1)
LN2=D/R2+LOG(R2/K2)
C=(T1-T2)/(LN1-LN2)
B=(T2*LN1-T1*LN2)/(LN1-LN2)
*DO,I,1,11,1
    R=TAB2(I,1)
    K=K1+(R-R1)/(R2-R1)*(K2-K1)
    TAB2(I,6)=C*(D/R+LOG(R/K))+B  ! ANALYTICAL SOLUTION FOR SPHERE
    TAB2(I,7)=TAB2(I,5)/TAB2(I,6)
*ENDDO
LAB2(6)='SPH-EXA'
PAPUT,TAB2,TABLE
PAPUT,LAB2,LABEL

/WIND,3,OFF
/WIND,4,ON
/NOERASE
PLPATH,SPH-NUM,SPH-EXA
/SHOW,CLOSE

/COM
/OUT,vm308,vrt
/COM,------------------- VM308 RESULTS COMPARISON --------------
/COM,
/COM, HOLLOW CYLINDER:
/COM,
/COM,   RADIUS    |    TARGET    |  MECHANICAL APDL  |    RATIO
*VWRITE,TAB1(1,1),TAB1(1,6),TAB1(1,5),TAB1(1,7)
(2X,F7.3,5X,'|',3X,F7.3,4X,'|',6X,F7.3,6X,'|',3X,F7.4)
/COM,
/COM, HOLLOW SPHERE:
/COM,
/COM,   RADIUS    |    TARGET    |  MECHANICAL APDL  |    RATIO
*VWRITE,TAB2(1,1),TAB2(1,6),TAB2(1,5),TAB2(1,7)
(2X,F7.3,5X,'|',3X,F7.3,4X,'|',6X,F7.3,6X,'|',3X,F7.4)
/COM,------------------------------------------------------------
/COM,
/OUT
FINISH
*LIST,vm308,vrt

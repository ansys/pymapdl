/COM,ANSYS MEDIA REL. 2023R1 (11/04/2022) REF. VERIF. MANUAL: REL. 2023R1
/VERIFY,VM215
/PREP7
/TITLE,VM215, CONDUCTING SPHERE
ET,1,157              
R,1,0.2               ! THICKNESS = 0.2
MP,RSVX,,7            ! DEFINE ELECTRICAL RESISTIVITIES PROPERTY 
MP,KXX,,3             ! DEFINE THERMAL CONDUCTIVITIES PROPERTY
CSYS,2                ! SPHERICAL COORDINATE SYSTEM
N,1,10                ! 10 UNIT RADIUS SPHERE
N,21,10,,80           ! 10 DEGREE HOLE AT THE TOP
FILL,,, ,,, ,,0.1     ! SHIFT ELEMENTS TOWARD HOLE
NGEN,2,30,1,21,1,,3   ! ANALYSE A 3 DEGREE SECTOR
E,1,2,32,31           ! DEFINE ELEMENT
EGEN,20,1,-1          ! USE 20 ELEMENTS
CP,1,VOLT,1,31
CP,2,TEMP,1,31
CP,3,VOLT,21,51
CP,4,TEMP,21,51
FINISH
/SOLU
OUTPR,,1
D,21,ALL              ! SET ALL VOLTAGES AND TEMPERATURES AT THE HOLE TO ZERO
D,1,VOLT,100          ! SET VOLTAGE AT THE EQUATOR TO 100
/OUT,SCRATCH
SOLVE
FINISH
/POST1
/OUT,
PRRSOL,AMPS           ! PRINTS THE CONSTRAINED NODE REACTION AT CURRENT FLOW
PRRSOL,HEAT           ! PRINTS THE CONSTRAINED NODE REACTION AT HEAT FLOW
NSEL,S,NODE,,21,51,30
FSUM
*GET,I21,FSUM,0,ITEM,AMPS
*GET,H21,FSUM,0,ITEM,HEAT
*DIM,LABEL,CHAR,2,2
*DIM,VALUE,,2,3
LABEL(1,1) = 'I AMPS ','Q WATT'
LABEL(1,2) = 'NODE 21','NODE 21'
*VFILL,VALUE(1,1),DATA,0.0614,6.14058
*VFILL,VALUE(1,2),DATA,I21,H21
*VFILL,VALUE(1,3),DATA,(I21/0.0614),(H21/6.14058)
/COM
/OUT,vm215,vrt
/COM,------------------- VM215 RESULTS COMPARISON --------------------------
/COM,
/COM,      SHELL157    |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A9,A8,'     ',F7.5,'     ',F14.5,'     ',1F15.3)
/COM,------------------------------------------------------------------------
/OUT
FINISH
*LIST,vm215,vrt

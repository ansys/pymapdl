/COM,ANSYS MEDIA REL. 2023R1 (11/04/2022) REF. VERIF. MANUAL: REL. 2023R1
/VERIFY,VM167
/PREP7
JPGPRF,500,100,1             ! MACRO TO SET PREFS FOR JPEG PLOTS
/SHOW,JPEG
/TITLE, VM167, TRANSIENT EDDY CURRENTS IN A SEMI-INFINITE SOLID
/COM,   HOLMAN, J.P., 'HEAT TRANSFER', 4TH EDITION, MCGRAW HILL,
/COM,   INC., 1976, PAGE 104, EQN. 4-14 (ANALOGOUS FIELD SOLUTION)
ET,1,PLANE13                         ! 2-D COUPLED-FIELD SOLID, AZ DOF
EMUNIT,MKS                           ! MKS UNITS
MP,MURX,1,1                          ! RELATIVE PERMEABILITY - AIR
MP,MURX,2,1                          ! RELATIVE PERMEABILITY - ALUMINUM
MP,RSVX,2,4E-7                       ! RESISTIVITY - ALUMINUM 
N,1
N,41,20
FILL,1,41,,,,,,20
NGEN,2,50,1,41,1,,.4
MAT,2
E,1,2,52,51
EGEN,40,1,-1
FINISH
/SOLU    
ANTYPE,TRANS                         ! TRANSIENT ANALYSIS
NSEL,S,LOC,X,0
D,ALL,AZ,2                           ! APPLY STEP POTENTIAL
NSEL,INVE
IC,ALL,AZ,0                          ! INITIAL CONDITION ON REMAINING POTENTIALS
NSEL,S,LOC,X,20
D,ALL,AZ,0                           ! SET FAR-FIELD POTENTIAL TO ZERO
NSEL,ALL
KBC,1                                ! STEPPED BOUNDARY CONDITIONS
DELTIM,.0002,.0002,.005              ! DEFINE TIME STEP SIZES
AUTOTS,ON                            ! AUTO TIME-STEPPING
TIME,.15                             ! TIME AT END OF 1ST LOAD STEP
OUTRES,ALL,ALL
SOLVE
DELTIM,,,,ON                         ! CARRY OVER TIME STEP USED PREVIOUSLY
TIME,.24                             ! TIME AT END OF 2ND LOAD STEP
SOLVE
FINISH
/POST26
NSOL,2,4,A,Z                         ! STORE NODE 4 VECTOR POTENTIAL
NSOL,3,6,A,Z                         ! STORE NODE 6 VECTOR POTENTIAL
NSOL,4,8,A,Z                         ! STORE NODE 8 VECTOR POTENTIAL
DERIV,5,2                            ! CALCULATE DA/DT
DERIV,6,3
DERIV,7,4
ADD,5,5,,,JEDDY_4,,,-.25E7           ! FIND EDDY CURRENT, -(DA/DT)/(RESISTIVITY)
ADD,6,6,,,JEDDY_6,,,-.25E7
ADD,7,7,,,JEDDY_8,,,-.25E7
SOLU,8,DTIM                          ! STORE TIME STEP SIZE
SOLU,9,RESE                          ! STORE RESPONSE EIGENVALUE
SOLU,10,NCMIT                        ! STORE NO. OF CUMULATIVE ITERATIONS
STORE,MERGE                          ! MERGE DATA WITH PREVIOUSLY STORED DATA
/AXLAB,Y,A
/GROPT,AXNSC,2.0
PLVAR,2,3,4                          ! DISPLAY VECTOR POTENTIAL VS. TIME
/AXLAB,Y,EDDY
PLVAR,5,6,7                          ! DISPLAY EDDY CURRENT DENSITY VS. TIME
PRVAR,2,3,4,5,6,7                    ! PRINT VALUES
*GET,V1,VARI,2,RTIME,.15
*GET,V2,VARI,3,RTIME,.15
*GET,V3,VARI,4,RTIME,.15
*GET,E1,VARI,5,RTIME,.15
*GET,E2,VARI,6,RTIME,.15
*GET,E3,VARI,7,RTIME,.15
PRVAR,8,9,10                         ! PRINT SOLUTION SUMMARY DATA
FINISH
/POST1
NSEL,S,NODE,,4,8,2
ESLN,S,0
SET,,,,,.15                          ! SELECT ITERATION AT TIME=.15
PRITER
PRNSOL,B,COMP                        ! PRINT NODAL FLUX DENSITY
*GET,F1,NODE,4,B,Y
*GET,F2,NODE,6,B,Y
*GET,F3,NODE,8,B,Y
*STATUS,PARM
*DIM,LABEL,CHAR,3,2
*DIM,VALUE,,3,3
LABEL(1,1) = '@ X=','@ X=','@ X='
LABEL(1,2) = '.2517','.4574','.6914'
*VFILL,VALUE(1,1),DATA,.831,.282,.05
*VFILL,VALUE(1,2),DATA,V1,V2,V3
*VFILL,VALUE(1,3),DATA,ABS(V1/.831),ABS(V2/.282),ABS(V3/.05)
/COM
/OUT,vm167,vrt
/COM,------------------- VM167 RESULTS COMPARISON ---------------
/COM,
/COM,   	                   |   TARGET   |   Mechanical APDL   |   RATIO
/COM,PLANE13
/COM,
/COM,VECTOR POTENTIALS (WB/M)
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F14.3,'  ',F18.3,'   ',1F17.3)
/NOPR
*VFILL,VALUE(1,1),DATA,3.707,1.749,.422
*VFILL,VALUE(1,2),DATA,F1,F2,F3
*VFILL,VALUE(1,3),DATA,ABS(F1/3.707),ABS(F2/1.749),ABS(F3/.422)
/COM,
/COM,FLUX DENSITY (T)
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F14.3,'  ',F18.3,'   ',1F17.3)
/NOPR
*VFILL,VALUE(1,1),DATA,-.777E7,-.663E7,-.243E7
*VFILL,VALUE(1,2),DATA,E1,E2,E3
*VFILL,VALUE(1,3),DATA,ABS(E1/(.777E7)),ABS(E2/(.663E7)),ABS(E3/(.243E7))
/COM,
/COM,EDDY CURRENT DENSITY (A/M/M)
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F14.0,'  ',F18.0,'   ',1F17.3)
/COM,-----------------------------------------------------------
/OUT
FINISH

/CLEAR,NOSTART
/NOPR
/PREP7
JPGPRF,500,100,1             ! MACRO TO SET PREFS FOR JPEG PLOTS
/SHOW,JPEG
/TITLE, VM167, TRANSIENT EDDY CURRENTS IN A SEMI-INFINITE SOLID
ET,1,PLANE233,1,2                    ! 2-D LF EMAG SOLID (AZ,VOLT DOF)
MP,MURX,1,1                          ! RELATIVE PERMEABILITY - AIR
MP,MURX,2,1                          ! RELATIVE PERMEABILITY - ALUMINUM
MP,RSVX,2,4E-7                       ! RESISTIVITY - ALUMINUM
MP,RSVX,1,1.E9                       ! RESISTIVITY - AIR 
N,1
N,41,20
FILL,1,41,,,,,,20
NGEN,2,50,1,41,1,,.4
MAT,2
E,1,2,52,51
EGEN,40,1,-1
FINISH
/SOLU    
D,ALL,VOLT,0
ANTYPE,TRANS                         ! TRANSIENT ANALYSIS
NSEL,S,LOC,X,0
D,ALL,AZ,2                           ! APPLY STEP POTENTIAL
NSEL,INVE
IC,ALL,AZ,0                          ! INITIAL CONDITION ON REMAINING POTENTIALS
NSEL,S,LOC,X,20
D,ALL,AZ,0                           ! SET FAR-FIELD POTENTIAL TO ZERO
NSEL,ALL
KBC,1                                ! STEPPED BOUNDARY CONDITIONS
DELTIM,.0002,.0002,.005              ! DEFINE TIME STEP SIZES
AUTOTS,ON                            ! AUTO TIME-STEPPING
TIME,.15                             ! TIME AT END OF 1ST LOAD STEP
OUTRES,ALL,ALL
SOLVE
DELTIM,,,,ON                         ! CARRY OVER TIME STEP USED PREVIOUSLY
TIME,.24                             ! TIME AT END OF 2ND LOAD STEP
SOLVE
FINISH
/POST26
NSOL,2,4,A,Z                         ! STORE NODE 4 VECTOR POTENTIAL
NSOL,3,6,A,Z                         ! STORE NODE 6 VECTOR POTENTIAL
NSOL,4,8,A,Z                         ! STORE NODE 8 VECTOR POTENTIAL
/AXLAB,Y,A
/GROPT,AXNSC,2.0
PLVAR,2,3,4                          ! DISPLAY VECTOR POTENTIAL VS. TIME
*GET,V1,VARI,2,RTIME,.15
*GET,V2,VARI,3,RTIME,.15
*GET,V3,VARI,4,RTIME,.15
FINISH
/POST1
NSEL,S,NODE,,4,8,2
ESLN,S,0
SET,,,,,.15                          ! SELECT ITERATION AT TIME=.15
PRITER
PRNSOL,B,COMP                        ! PRINT NODAL FLUX DENSITY
*GET,F1,NODE,4,B,Y
*GET,F2,NODE,6,B,Y
*GET,F3,NODE,8,B,Y
*STATUS,PARM
*DIM,LABEL,CHAR,3,2
*DIM,VALUE,,3,3
LABEL(1,1) = '@ X=','@ X=','@ X='
LABEL(1,2) = '.2517','.4574','.6914'
/NOPR
*VFILL,VALUE(1,1),DATA,.831,.282,.05
*VFILL,VALUE(1,2),DATA,V1,V2,V3
*VFILL,VALUE(1,3),DATA,ABS(V1/.831),ABS(V2/.282),ABS(V3/.05)
/COM
/OUT,vm167,vrt,,APPEND
/COM,PLANE233
/COM,
/COM,VECTOR POTENTIALS (WB/M)
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F14.3,'  ',F18.3,'   ',1F17.3)
/COM,
/OUT
/NOPR
*VFILL,VALUE(1,1),DATA,3.707,1.749,.422
*VFILL,VALUE(1,2),DATA,F1,F2,F3
*VFILL,VALUE(1,3),DATA,ABS(F1/3.707),ABS(F2/1.749),ABS(F3/.422)
/COM,FLUX DENSITY (T)
/COM,
/OUT,vm167,vrt,,APPEND
/COM,FLUX DENSITY (T)
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F14.3,'  ',F18.3,'   ',1F17.3)
/COM,
/OUT
FINISH
*LIST,vm167,vrt

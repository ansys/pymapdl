/COM,ANSYS MEDIA REL. 150 (11/8/2013) REF. VERIF. MANUAL: REL. 150
/VERIFY,VM273
/TITLE,VM273, SHAPE MEMORY ALLOY WITH THERMAL EFFECT UNDER UNIAXIAL LOAD
/COM,   REF: FERDINANDO AURICHIO, LORENZA PETRINI
/COM,   "IMPROVEMENTS AND ALGORITHMICAL CONSIDERATIONS ON A RECENT
/COM,   THREE-DIMENSIONAL MODEL DESCRBING STRESS-INDUCED SOLID
/COM,   PHASE TRANSFORMATIONS"
/COM,   INT. J. NUMER. METH. ENGNG. 55 (2002) 1255-1284
/COM,
/COM,   TWO CASES ARE COMPUTED: (1) BODY TEMPERATURE T=285.15K
/COM,   (2) BODY TEMPERATURE T=253.15K
/COM,
/COM,   CASE ONE: WITH BODY TEMPERATURE T=285.15K
/PREP7
ET,1,SOLID185                        !* 3D 8-NODE STRUCTURAL SOLID ELEMENT

/COM, DEFINING SMA MATERIAL PROPERTIES
MP,EX,1,70E3     !MPA, [AUSTENITE MODULUS]
MP,PRXY,1,0.33

C1=500 		!MPA [HARDENING PARAMETER]
C2=253.15	!K   [REF TEMP]
C3=45		!MPA  [ELASTIC LIMIT]
C4=7.5		!MPA [TEMPERATURE SCALING PARAMETER]
C5=0.03		![MAX TRANSFORMATION STRAIN]
C6=70E3		!MPA, [MARTENSITE MODULUS]
C7=0        ! M = 0, SYMMETRICAL BEHAVIOR

TB,SMA,1,,7,MEFF
TBDATA,1,C1,C2,C3,C4,C5,C6,C7

BLOCK,0.00,10.00,0.00,10.00,0.00,10.00
ESIZE,10
TYPE,1
MAT,1
VMESH,1

NSEL,S,LOC,X
D,ALL,UX
NSEL,S,LOC,Y
D,ALL,UY
NSEL,S,LOC,Z
D,ALL,UZ
NSEL,ALL
BFUNIF,TEMP,285.15
FINISH
/SOLU
NROPT,UNSYM
OUTRES,ALL,ALL
NSUBST,50,50,50
TIME,1
NSEL,S,LOC,Y,10
D,ALL,UY,0.35               ! TENSION LOADING
ALLSEL
/OUT,SCRATCH
SOLVE

TIME,2
NSEL,S,LOC,Y,10
D,ALL,UY,0.00               ! UNLOADING
ALLSEL
SOLVE

TIME,3
NSEL,S,LOC,Y,10
D,ALL,UY,-0.35              ! COMPRESSION LOADING
ALLSEL
SOLVE

TIME,4
NSEL,S,LOC,Y,10
D,ALL,UY,0.00               ! UNLOADING
ALLSEL
SOLVE
FINISH

/POST26
ESOL,2,1,NODE(10,10,0),S,Y           !* Y STRESS AT NODE(10,10,0)
ESOL,3,1,NODE(10,10,0),EPEL,Y        !* ELASTIC STRAIN AT NODE(10,10,0) 
ESOL,4,1,NODE(10,10,0),EPPL,Y        !* PLASTIC STRAIN AT NODE(10,10,0)
ADD,5,3,4                    !* TOTAL STRAIN AT NODE(10,10,0)
PROD,6,5, , ,STRAIN, , ,100  !* PERCENT TOTAL STRAIN
XVAR,6
/AXLAB,X,Strain[%]
/AXLAB,Y,Stress [MPa]
/YRANGE,-800,800                !* SET Y-RANGE
/XRANGE,-4,4                    !* SET X-RANGE
PLVAR,2                         !* PLOT TOTAL STRAIN VS Y STRESS
PRVAR,3,4,2
FINISH
/POST1
/OUT,
SET, , , , ,0.16
*GET,S_SAS,NODE,NODE(10,10,0),S,Y
SET, , , , ,0.84
*GET,S_FAS,NODE,NODE(10,10,0),S,Y
SET, , , , ,1.20
*GET,S_SSA,NODE,NODE(10,10,0),S,Y
SET, , , , ,1.90
*GET,S_FSA,NODE,NODE(10,10,0),S,Y
R1 = S_SAS/345
R2 = S_FAS/367
R3 = S_SSA/258
R4 = S_FSA/236
*DIM,LABEL,CHAR,4,2
*DIM,VALUE,,4,3
LABEL(1,1) = 'S','S','S','S'
LABEL(1,2) = '-SAS','-FAS','-SSA','-FSA'
*VFILL,VALUE(1,1),DATA,345,367,258,236
*VFILL,VALUE(1,2),DATA,S_SAS,S_FAS,S_SSA,S_FSA
*VFILL,VALUE(1,3),DATA,R1,R2,R3,R4
SAVE, TABLE_1
FINISH

/CLEAR,NOSTART   ! *CLEAR DATABASE FOR SECOND SOLUTION
/COM,    CASE TWO: WITH BODY TEMPERATURE T=253.15K
/PREP7
ET,1,SOLID185                        !* 3D 8-NODE STRUCTURAL SOLID ELEMENT

/COM, DEFINING SMA MATERIAL PROPERTIES
MP,EX,1,70E3     !MPA, [AUSTENITE MODULUS]
MP,PRXY,1,0.33

C1=500 		!MPA [HARDENING PARAMETER]
C2=253.15	!K   [REF TEMP]
C3=45		!MPA  [ELASTIC LIMIT]
C4=7.5		!MPA
C5=0.03		!     [MAX TRANSFORMATION STRAIN]
C6=70E3		!MPA, [MARTENSITE MODULUS]
C7=0        ! M = 0

TB,SMA,1,,7,MEFF
TBDATA,1,C1,C2,C3,C4,C5,C6,C7

BLOCK,0.00,10.00,0.00,10.00,0.00,10.00
ESIZE,10
TYPE,1
MAT,1
VMESH,1

NSEL,S,LOC,X
D,ALL,UX
NSEL,S,LOC,Y
D,ALL,UY
NSEL,S,LOC,Z
D,ALL,UZ
NSEL,ALL
BFUNIF,TEMP,253.15
FINISH
/SOLU
NROPT,UNSYM
OUTRES,ALL,ALL
NSUBST,50,50,50

TIME,1
NSEL,S,LOC,Y,10
D,ALL,UY,0.35               ! TENSION LOADING
ALLSEL
/OUT,SCRATCH
SOLVE

TIME,2
NSEL,S,LOC,Y,10
D,ALL,UY,0.00               ! UNLOADING
ALLSEL
SOLVE

TIME,3
NSEL,S,LOC,Y,10
D,ALL,UY,-0.35              ! COMPRESSION LOADING
ALLSEL
SOLVE

TIME,4
NSEL,S,LOC,Y,10
D,ALL,UY,0.00               ! UNLOADING
ALLSEL
SOLVE
FINISH

/POST26
ESOL,2,1,NODE(10,10,0),S,Y           !* Y STRESS AT NODE(10,10,0)
ESOL,3,1,NODE(10,10,0),EPEL,Y        !* ELASTIC STRAIN AT NODE(10,10,0) 
ESOL,4,1,NODE(10,10,0),EPPL,Y        !* PLASTIC STRAIN AT NODE(10,10,0)
ADD,5,3,4                            !* TOTAL STRAIN AT NODE(10,10,0)
PROD,6,5, , ,STRAIN, , ,100          !* PERCENT TOTAL STRAIN
XVAR,6
/AXLAB,X,Strain[%]
/AXLAB,Y,Stress [MPa]
/YRANGE,-800,800                !* SET Y-RANGE
/XRANGE,-4,4                    !* SET X-RANGE
PLVAR,2                         !* PLOT TOTAL STRAIN VS YSTRESS
PRVAR,3,4,2
FINISH
/POST1
/OUT,
SET, , , , ,0.04
*GET,S_SAS,NODE,NODE(10,10,0),S,Y
SET, , , , ,0.72
*GET,S_FAS,NODE,NODE(10,10,0),S,Y
SET, , , , ,1.32
*GET,S_SSA,NODE,NODE(10,10,0),S,Y
R1 = S_SAS/54.6
R2 = S_FAS/74.1
R3 = S_SSA/(-37.1)
*DIM,LABEL,CHAR,3,2
*DIM,VALUE,,3,3
LABEL(1,1) = 'S','S','S'
LABEL(1,2) = '-SAS','-FAS','-SSA'
*VFILL,VALUE(1,1),DATA,54.6,74.1,-37.1
*VFILL,VALUE(1,2),DATA,S_SAS,S_FAS,S_SSA
*VFILL,VALUE(1,3),DATA,R1,R2,R3
SAVE, TABLE_2
RESUME,TABLE_1
FINISH
/COM
/OUT,vm273,vrt
/COM,------------------- VM273 RESULTS COMPARISON --------------
/COM,
/COM,               |  TARGET   |   Mechanical APDL  |  RATIO
/COM,
/COM,RESULTS WITH BODY TEMPERATURE T=285.15K
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A1,A8,'   ',F10.3,'  ',1F14.3,'   ',1F15.3)
/NOPR
RESUME,TABLE_2
/GOPR
/COM,RESULTS WITH BODY TEMPERATURE T=253.15K
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A1,A8,'   ',F10.3,'  ',1F14.3,'   ',1F15.3)

/COM,-----------------------------------------------------------
/OUT
FINISH
*LIST,vm273,vrt

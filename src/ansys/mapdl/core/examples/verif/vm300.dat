/COM,ANSYS MEDIA REL. 2023R1 (11/04/2022) REF. VERIF. MANUAL: REL. 2023R1
/VERIFY,VM300
/TITLE,VM300,TRANSMISSION LOSS IN COUPLED-ROOM
/COM,
/COM, REFERENCE: A.BILLON,C.FOY,"MODELING THE SOUND TRANSMISSION BETWEEN
/COM,            ROOMS COUPLED THROUGH WALLS BY USING A DIFFUSION MODEL"
/COM,            MARCH 2008.
/COM,

/PREP7
/NOPR
! ROOM DIMENSIONS
LZ=5
V=LZ*LZ*LZ
S=6*LZ*LZ
S12=LZ*LZ       ! PARTITION WALL AREA
S2=5*LZ*LZ

! MATERIAL PROPERTIES
C0=343
RHO=1.21
PREF=2E-5
MFP=4*V/S
D=MFP*C0/3.
ALPHA=0.1       ! ABSORPTION COEFFICIENT
R=30            ! TRANSMISSION LOSS
TAU=1.E-3       ! TRANSMISSION COEFFICIENT TAU=10^(-R/10)

! DEFINE MATERIAL
MP,DENS,1,RHO
MP,SONC,1,C0

TB,AFDM,1,,,ROOM
TBDATA,1,D
TBLIST

H=0.5
WALL=H
N=NINT(LZ/H)

! GENERATE GEOMETRY
*DIM,A,ARRAY,4
*DIM,B,ARRAY,4
*DIM,C,ARRAY,3
A(1)=0.
A(2)=2.
A(3)=2.5
A(4)=LZ
B(1)=0.
B(2)=2
B(3)=2.5
B(4)=LZ
C(1)=0.
C(2)=2
C(3)=LZ

*DO,I,1,3
  *DO,J,1,3
    *DO,K,1,2
      BLOCK,A(I),A(I+1),B(J),B(J+1),C(K),C(K+1)
    *ENDDO
  *ENDDO
*ENDDO
VGLUE,ALL

! GENERATE MESH
ET,1,220,,4
TYPE,1
MAT,1
ESIZE,H
VMESH,ALL
ALLS

ASEL,S,LOC,Z,LZ
ESIZE,,1
TYPE,1
VEXT,ALL,,,0,0,WALL
ASEL,S,LOC,Z,LZ+WALL
ESIZE,,N
TYPE,1
VEXT,ALL,,,0,0,LZ
ALLS
NUMMRG,ALL,ALL

NSEL,S,LOC,Z,LZ,LZ+WALL
ESLN,S,1
CM,EWALL,ELEM
ALLS

CMSEL,U,EWALL
NSLE,S,ALL

! APPLY ABSORPTION ON ROOM WALLS, EXCEPT THE COUPLED WALL

NSEL,S,LOC,X,A(1)
NSEL,A,LOC,X,A(4)
NSEL,A,LOC,Y,B(1)
NSEL,A,LOC,Y,B(4)
NSEL,A,LOC,Z,C(1)
NSEL,A,LOC,Z,LZ+WALL+LZ
SF,ALL,ATTN,ALPHA
ALLS

! APPLY ABSORPTION AND TRANSMISSION LOSS ON ROOM WALL

CMSEL,S,EWALL
NSLE,S,ALL

NSEL,S,LOC,Z,LZ
SF,ALL,ATTN,ALPHA,R
NSEL,S,LOC,Z,LZ+WALL
SF,ALL,ATTN,ALPHA,R        ! TRANSMISSION LOSS = 30DB
ALLS

! APPLY SOUND SOURCE

NSEL,S,LOC,X,A(2)
NSEL,R,LOC,Y,B(2)
NSEL,R,LOC,Z,C(2)
BF,ALL,MASS,1.E-2
ALLS
FINI

/SOLVE
/OUT,SCRATCH
ANTY,STATIC
SOLVE
FINI

*CREATE,SPLPOST,MAC
*GET,ND,NODE,,COUNT
NN=0
TOTED=0
*DO,K,1,ND
  NN=NDNEXT(NN)
  TOTED=TOTED+ENKE(NN)
*ENDDO
AVG_ED=TOTED/ND
*END

/POST1
SET,LAST
NSEL,S,LOC,X,2.5
NSEL,R,LOC,Y,2.5
NSEL,R,LOC,Z,0,5

SPLPOST
ED1=AVG_ED
SPL1=10*(LOG10((ED1*RHO*C0*C0)/(PREF*PREF)))

ALLS
NSEL,S,LOC,X,2.5
NSEL,R,LOC,Y,2.5
NSEL,R,LOC,Z,5.5,10.5

SPLPOST
ED2=AVG_ED
SPL2=10*(LOG10((ED2*RHO*C0*C0)/(PREF*PREF)))
SPL_DIFF_APDL = SPL1 - SPL2

A20=ALPHA*S2
A22=A20+TAU*S12
SPL_DIFF_ANL=R-10*(LOG10(S12/A22))

RATIO = SPL_DIFF_APDL/SPL_DIFF_ANL

*DIM,VALUE,ARRAY,1,3
*VFILL,VALUE(1,1),DATA,SPL_DIFF_ANL
*VFILL,VALUE(1,2),DATA,SPL_DIFF_APDL
*VFILL,VALUE(1,3),DATA,RATIO

/OUT,vm300,vrt
/COM,
/COM, --------------- RESULT COMPARISON ----------------
/COM,
/COM,    |  TARGET  |  MECHANICAL APDL  | RATIO
/COM,
*VWRITE,VALUE(1,1),VALUE(1,2),VALUE(1,3)
(5X,F8.3,'   ',F8.3,'   ',F8.3)
/COM,
/OUT,
*LIST,vm300,vrt
FINISH

/COM,ANSYS MEDIA REL. 2023R1 (11/04/2022) REF. VERIF. MANUAL: REL. 2023R1
/VERIFY,vm293
/TITLE,vm293,EVALUATION OF STRESS INTENSITY FACTOR USING UMM METHOD
/COM,
/COM, REFERENCE:
/COM, J.R.RICE,"MATHEMATICAL ANALYSIS IN THE MECHANICS OF FRACTURE",
/COM, CHAPTER 3, 1968,PG:222
/COM,
JPGPRF,500,100,1
/OUT,SCRATCH

/COM, CREATING CDB FILE TO STORE MESH DATA
/COM,

_GEOMGEN = 0            ! SET TO 1 TO WRITE CDB FILE

*IF,_GEOMGEN,EQ,1,THEN

/PREP7

ET,1,SOLID285           ! 3D 4 NODE TETRAHEDRAL STRUCTURAL SOLID WITH NODAL PRESSURE
MP,EX,1,200E3           ! YOUNG'S MODULUS IN MPA
MP,NUXY,1,0.3           ! POISSON'S RATIO

/COM, ONLY 1/8TH OF THE MODE IS ANALYZED DUE TO SYMMETRY

W = 20                  ! WIDTH OF THE BLOCK IN MM
A = 5                   ! LENGTH OF THE CRACK IN MM
H = 100                 ! HEIGHT OF THE BLOCK IN MM
L = 20                  ! LENGTH OF THE BLOCK IN MM

EL=0.5                  !ELEMENT SIZE

CYL4,5,0,6*EL,90
CYL4,5,0,6*EL,90,,180

WPROTA,,,-90            ! ROTATE THE WORKING PLAN
CSWPLA,11               ! DEFINE LOCAL COORDINATE SYSTEM 11

CYL4,5,0,6*EL,90
CYL4,5,0,6*EL,90,,180

CSYS,0
WPROTA,,,90
WPROTA,,-90
CSWPLA,12               ! DEFINE LOCAL COORDINATE SYSTEM 12

CYL4,0,0,A,-90,A+6*EL
CYL4,0,0,A,-90,A-6*EL

ALLSEL
AGLUE,ALL               ! CREATE NEW AREAS BY GLUEING AREAS
ALLSEL

VDRAG,1,,,,,,25         ! GENERATING VOLUME BY DRAGGING AREA ALONG A PATH
VDRAG,7,,,,,,26

ALLSEL
VGLUE,ALL               ! CREATING NEW VOLUMES BY GLUING VOLUMES
ALLSEL

ALLSEL
AOVLAP,ALL              ! OVERLAP AREAS
ALLSEL

LSEL,S,LINE,,11
LESIZE,ALL,,,16

CYL4,0,0,A-6*EL,-90,,,7*EL               ! CREATE CYLINDRICAL VOLUME IN WP 12
CYL4,0,0,A-6*EL,-90,A,,7*EL              ! CREATE CYLINDRICAL VOLUME IN WP 12
CYL4,0,0,A,-90,A+6*EL,,7*EL              ! CREATE CYLINDRICAL VOLUME IN WP 12

CYLIND,,A-6*EL,7*EL,H,0,-90              ! CREATE CYLINDRICAL VOLUME CENTERED ABOUT ORIGIN
CYLIND,A-6*EL,A,7*EL,H,0,-90             ! CREATE CYLINDRICAL VOLUME CENTERED ABOUT ORIGIN
CYLIND,A,A+6*EL,7*EL,H,0,-90             ! CREATE CYLINDRICAL VOLUME CENTERED ABOUT ORIGIN

BLOCK,0,W,0,-L,0,H                       ! CREATE RECTANGULAR BLOCK

VSBV,9,ALL,,DELETE,KEEP                  ! SUBTRACT VOLUMES FROM VOLUMES
VSBV,5,1,,DELETE,KEEP
VSBV,4,3,,DELETE,KEEP

ALLSEL
VGLUE,ALL                                ! GLUE ALL VOLUMES
ALLSEL

VSEL,S,VOLU,,1,4,3
TYPE,1
ESIZE,EL
VMESH,ALL                                ! MESH THE SELECTED VOLUME

VSEL,S,VOLU,,12,13,1
VSEL,A,VOLU,,2
TYPE,1
ESIZE,EL
VMESH,ALL

VSEL,S,VOLU,,11
TYPE,1
ESIZE,3
VMESH,ALL                                ! MESH THE SELECTED VOLUME

VSEL,S,VOLU,,14,15,1
VSEL,A,VOLU,,16
TYPE,1
ESIZE,6
VMESH,ALL                                ! MESH THE SELECTED VOLUME

ALLSEL

CSYS,0                                   ! GLOBAL CARTESIAN COORDINATE SYSTEM

NSEL,S,LOC,X,0
D,ALL,UX,0                               ! CONSTRAINING UX DOF
ALLSEL

NSEL,S,LOC,Z,0
D,ALL,UZ,0                               ! CONSTRAINING UZ DOF
ALLSEL

ASEL,S,AREA,,5,58,53
NSLA,S,1
D,ALL,UY,0                               ! CONSTRAINING UY DOF
ALLSEL

CSYS,0
NSEL,S,LOC,Y,H
SF,ALL,PRES,-100                         ! SURFACE PRESSURE LOADING
ALLSEL

ADELE,14,19,5,1

LSEL,S,LINE,,11
NSLL,S,1
CM,CRACK1,NODE                           ! DEFINING COMPONENT FOR CRACK NODE

ALLSEL
cdwrite,db,vm293,cdb
FINISH

*ELSE
/PREP7
cdread,db,vm293,cdb
*ENDIF
FINISH

/SOLU
ANTYPE,STATIC                           ! PERFORM STATIC SOLVE
ERESX,NO                                ! COPY INTEGRATION POINT RESULTS TO NODE

CINT,NEW,1                              ! NEW CRACK ID
CINT,TYPE,SIFS,2                        ! CALCULATE STRESS INTENSITY FACTOR
CINT,CTNC,CRACK1,921                    ! DEFINE CRACK TIP NODE AND CRACK EXTENSION DIRECTION
CINT,NCON,5                             ! NUMBER OF COUNTOURS
CINT,SYMM,ON                            ! SYMMETRIC ABOUT CRACK PLANE
CINT,UMM,ON                             ! ACTIVATE UNSTRUCTURED MESH METHOD
CINT,NORM,,2                            ! DEFINE AXIS OF COORDINATE SYSTEM
OUTRES,CINT,10
ALLSEL,ALL

NSUBST,1,100,1
OUTRES,ALL,ALL
SOLVE
FINI

/POST1

SET,LAST,LAST
CMSEL,S,CRACK1,NODE
*GET,NNUM,NODE,0,COUNT
*GET,NMIN,NODE,0,NUM,MIN

/OUT,vm293,vrt
/COM,-----------------VM293 RESULTS COMPARISON------------------------
/COM,
/COM,  |  CRACKTIP NODE  |   TARGET    |      MECHANICAL APDL     |  RATIO

*DO,I,1,NNUM
NCUR=NMIN
*GET,K2,CINT,1,CTIP,NCUR,,2,,K1
*GET,K3,CINT,1,CTIP,NCUR,,3,,K1
*GET,K4,CINT,1,CTIP,NCUR,,4,,K1

KK=ABS(K2+K3+K4)/3.0
KE=252.3                                ! REFERENCE SOLUTION
/COM,
/COM,
*VWRITE,NCUR,  KE, KK, (KK/KE)
(1X,'      ',F9.0,'     ', F10.2, '       ', F14.2, '         ', F10.3)

*GET,NMIN,NODE,NCUR,NXTH
*ENDDO
/COM, ----------------------------------------------------------------------
/OUT
*LIST,vm293,vrt
FINISH
/EXIT,NOSAVE

! Copyright (C) 2024 ANSYS, Inc. and/or its affiliates.
! SPDX-License-Identifier: MIT
! 
!
! Permission is hereby granted, free of charge, to any person obtaining a copy
! of this software and associated documentation files (the "Software"), to deal
! in the Software without restriction, including without limitation the rights
! to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
! copies of the Software, and to permit persons to whom the Software is
! furnished to do so, subject to the following conditions:
!
! The above copyright notice and this permission notice shall be included in all
! copies or substantial portions of the Software.
!
! THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
! IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
! FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
! AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
! LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
! OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
! SOFTWARE.

/COM,ANSYS MEDIA REL. 2023R2 (05/12/2023) REF. VERIF. MANUAL: REL. 2023R2
/VERIFY,vmr027-cr4c-183
/TITLE,vmr027-cr4c-183,2D PLANE STRESS-SHEAR LOADING SECONDARY CREEP
/COM,THE COMPARISON IS MADE GRAPHICALLY AND QUANTITATIVELY WITH THE
/COM,RESULTS OF THE TEST4(C) FROM THE NAFEMS R0027 REPORT.
/SHOW,JPEG
JPGPRF,500,100,1

/PREP7
TAU=100.0
L=100
C*** PARAMETRIC INPUT FOR CREEP CONSTANT !(TIME HARDENING)***
*SET,C1,3.125E-14
*SET,C2,5
*SET,C3,0
*SET,C4,0
C*** TIME PARAMETER
*SET,HOUR,200
C*** ELASTIC CONSTANT
MP,EX,1,200E3
MP,NUXY,1,0.3
TUNIF,HOT
TOFF,1E-10
TB,CREEP,1,,,2
TBDATA,1,C1,C2,C3,C4
N,1,0,0,0
N,2,100,0
N,3,100,100
N,4,0,100
N,5,50,0
N,6,100,50
N,7,50,100
N,8,0,50
ET,1,183
KEYOPT,1,3,0
E,1,2,3,4,5,6,7,8
NSEL,S,LOC,X,
NSEL,R,LOC,Y,
D,ALL,UX,
D,ALL,UY,
NSEL,ALL
NSEL,S,LOC,X,
NSEL,R,LOC,Y,100
D,ALL,UX,
D,ALL,UY
NSEL,ALL
FINISH

/SOLU
F,1,FX,-L*TAU/6
F,5,FX,-L*TAU*2/3
F,2,FX,-L*TAU/6
F,2,FY,L*TAU/6
F,6,FY,L*TAU*2/3
F,3,FY,L*TAU/6
F,3,FX,L*TAU/6
F,7,FX,L*TAU*2/3
F,4,FX,L*TAU/6
F,4,FY,-L*TAU/6
F,8,FY,-L*TAU*2/3
F,1,FY,-L*TAU/6
RATE, ON
DELT,1.0E-8,1.0E-9,1.0E-8
TIME, 1.0E-8
/OUTPUT,SCRATCH
OUTRES,ALL,LAST
SOLVE
/OUT
RATE, ON
DELT,0.10,0.0999,0.10
TIME, 0.10
/OUTPUT,SCRATCH
OUTRES,ALL,LAST
SOLVE
/OUT
RATE, ON
DELT,1.0,0.999,1.0
TIME, 1.0
/OUTPUT,SCRATCH
OUTRES,ALL,LAST
SOLVE
/OUT
RATE, ON
DELT,5.0,4.999,5.0
TIME, 5.0
/OUTPUT,SCRATCH
OUTRES,ALL,LAST
SOLVE
/OUT
RATE, ON
DELT,10.0,9.999,10.0
TIME, 10.0
/OUTPUT,SCRATCH
OUTRES,ALL,LAST
SOLVE
/OUT
RATE, ON, ON
DELT,10,1,100
AUTOS,OFF
TIME,1000
/OUTPUT,SCRATCH
OUTRES,ALL,ALL
SOLVE
/OUT
FINISH

/POST26
/GROPT,VIEW,0
/GTHK,CURVE,1
/GROPT,FILL,OFF
/GRID,1
/GTHK,GRID,1
/GROPT,CGRID,1
/AXLAB,X,TIME
/AXLAB,Y,CREEP STRAIN
/GTHK,AXIS,1
/GRTYP,0
/GROPT,DIG1,5
/GROPT,DIG2,3
/GROPT,DIVX,5
/GROPT,DIVY,9
/XRANGE,0,1000
/YRANGE,0,9
ESOL,2,1,,EPCR,XY
PLVAR,2
PRVAR,2
FINISH

/POST1
SET,1,1
*GET,SH1,NODE,3,EPCR,XY
R1=1.000
SET,2,1
*GET,SH2,NODE,3,EPCR,XY
R2=SH2/0.000844
SET,3,1
*GET,SH3,NODE,3,EPCR,XY
R3=SH3/0.0084375
SET,4,1
*GET,SH4,NODE,3,EPCR,XY
R4=SH4/0.042188
SET,,,,,,,5
*GET,SH5,NODE,3,EPCR,XY
R5=SH5/0.084375
SET,,,,,,,9
*GET,SH6,NODE,3,EPCR,XY
R6=SH6/0.421875
SET,,,,,,,14
*GET,SH7,NODE,3,EPCR,XY
R7=SH7/0.84375
SET,,,,,,,54
*GET,SH8,NODE,3,EPCR,XY
R8=SH8/4.21875
SET,,,,,,,104
*GET,SH9,NODE,3,EPCR,XY
R9=SH9/8.4375
*DIM,VALUE,,9,3
*DIM,LABEL,CHAR,13
LABEL(1) = '0','0.1', '1.0', '5.0', '10.0', '50.0','100.0','500.0','1000.0'
*VFILL,VALUE(1,1),DATA,0.0, 0.000844, 0.0084375,0.042188,0.084375,0.421875,0.84375,4.21875,8.4375
*VFILL,VALUE(1,2),DATA,SH1,SH2,SH3,SH4,SH5,SH6,SH7,SH8,SH9
*VFILL,VALUE(1,3),DATA,R1,R2,R3,R4,R5,R6,R7,R8,R9
/COM
/COM,--------- vmr027-cr4c-183 RESULTS COMPARISON-----------
/COM,
/COM,|   TIME   | TARGET |   Mechanical APDL  |   RATIO
/COM,
*VWRITE,LABEL(1), VALUE(1,1), VALUE(1,2), VALUE(1,3)
(1X,A8,'   ',F8.5,'   ',F12.5,'   ',1F12.3)
FINISH

/POST26
*DIM,LABEL1,CHAR,2
*DIM,VALUE1,,2,3
LABEL1(1) = ' ECR2X ',' ECR2Y '
*VFILL,VALUE1(1,1),DATA,SH9
*VFILL,VALUE1(1,2),DATA,R9
*DIM,LABEL2,CHAR,2
LABEL2(1) = 'vmr027-','cr4c-183'

/OUT,vmr027-cr4c-183,vrt
/COM
/COM,------------------- vmr027-cr4c RESULTS COMPARISON ---------------------
/COM,
/COM,            |   Mechanical APDL   |   RATIO  |         INPUT         |
/COM,
/COM, PLANE183
*VWRITE,LABEL1(1),VALUE1(1,1),VALUE1(1,2),LABEL2(1),LABEL2(2)
(1X,A8,'      ',F12.3,'      ',F12.4,'     ',A7,A8)
/COM,
/COM,----------------------------------------------------------------
/OUT
FINISH
*LIST,vmr027-cr4c-183,vrt

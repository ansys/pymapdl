/COM,ANSYS MEDIA REL. 2023R1 (11/04/2022) REF. VERIF. MANUAL: REL. 2023R1
/VERIFY,VM290
/TITLE,VM290,SPHERICAL CAVITY IN AN INFINITE MEDIUM USING INFINITE ELEMENTS
/COM,
/COM, REFERENCE: J.M.M.C.MARQUES AND D.R.J.OWNEN,"INFINITE ELEMENTS IN
/COM,            QUASI-STATIC MATERIALLY NONLINEAR PROBLEMS",COMPUTERS
/COM,            AND STRUCTURES, VOL,18,NO.4,PP.739-751,1984
/COM,
/OUT,SCRATCH

/COM, CREATING CDB FILE TO STORE MESH DATA
/COM,

_GEOMGEN = 0            ! SET TO 1 TO WRITE CDB FILE

*IF,_GEOMGEN,EQ,1,THEN

/PREP7
RM=1.0                  ! RADIUS IN M
T=3.0                   ! OUTER RADIUS IN M
P=750.0                 ! INTERNAL PRESSURE IN PA
Exx=1e+07               ! YOUNG'S MODULUS
NUxy=0.33               ! POISSON'S RATIO
Gx=Exx/(2*(1+NUxy))     ! RIGIDITY MODULUS
ET,1,SOLID186           ! 3D 20 NODE STRUCTURAL SOLID

MP,EX,1,Exx             ! DEFINE MATERIAL MODEL
MP,NUXY,1,NUxy

K,1
K,2,,RM+T
PCIRC,RM,RM+T,0,90          ! CREATE CIRCULAR AREA
VROTAT,ALL,,,,,,1,2,-90,,   ! CREATE CYLINDRICAL VOLUMES

LESIZE,ALL,,,4
LESIZE,2,,,2,0.5
LESIZE,4,,,2,0.5
LESIZE,7,,,2,0.5
VSWEEP,ALL                  ! MESH VOLUMES WITH SOLID186 ELEMENTS

*GET,NMAX,NODE,0,NUM,MAXD
NPOLE=NMAX+1
N,NPOLE

CSYS,2                      ! SPHERICAL COORDINATE SYSTEM
ASEL,S,,,2
NSLA,S,1
EINFIN,,NPOLE               ! GENERATE STRUCTURAL INFINITE ELEMENTS
ALLSEL

ASEL,S,,,3
NSLA,S,1
NROTAT,ALL,ALL
SF,ALL,PRES,P               ! APPLYING PRESSURE
ALLSEL

CSYS,0
NSEL,S,LOC,Z,0
DSYM,SYMM,Z                 ! SYMMETRIC CONSTRAINTS,UZ
ALLSEL,ALL,ALL
NSEL,S,LOC,Y,0
DSYM,SYMM,Y                 ! SYMMETRIC CONSTRAINTS,UY
ALLSEL,ALL,ALL
NSEL,S,LOC,X,0
DSYM,SYMM,X                 ! SYMMETRIC CONSTRAINTS,UX
ALLSEL,ALL,ALL

cdwrite,db,vm290,cdb
FINISH

*ELSE
/PREP7
cdread,db,vm290,cdb
*ENDIF
FINISH

/SOLUTION
ANTYPE,STATIC               ! STATIC SOLVE
OUTRES,ALL,ALL
ALLSEL,ALL,ALL
OUTRES,LOCI,ALL
TIME,1.0
SOLVE
FINISH

/POST1
SET,LAST

*DIM,VALUE,,5,3
*DIM,VALUE1,,4,5

VALUE(1,2)=1.0,1.5,2.0,4.0,8.0                  ! LOCATIONS AT WHICH RADIAL DISPLACEMENTS ARE COMPUTED

RSYS,2
*GET,UX10,NODE,54 ,U,X                          ! MAPDL SPHERICAL UX AT R = 1.0
*GET,UX15,NODE,146,U,X                          ! MAPDL SPHERICAL UX AT R = 1.5
*GET,UX20,NODE,145,U,X                          ! MAPDL SPHERICAL UX AT R = 2.0
*GET,UX40,NODE,100,U,X                          ! MAPDL SPHERICAL UX AT R = 4.0
*GET,UX80,NODE,210,U,X                          ! MAPDL SPHERICAL UX AT R = 8.0
RSYS,0

VALUE(1,1)=1e+06*%UX10%,1e+06*%UX15%,1e+06*%UX20%,1e+06*%UX40%,1e+06*%UX80%   ! MULTIPLYING BY A FACTOR OF 1E+06 TO COMPARE WITH REFERENCE
VALUE(1,3)=49.22,21.68,12.34,3.12,0.77                                        ! FROM REFERENCE, TABLE 8

*DIM,RATIO1,,5

*DO,I,1,5
RATIO1(I,1)=VALUE(I,1)/VALUE(I,3)
*ENDDO

/OUT,vm290,vrt
/COM,--------------VM290 RESULTS COMPARISON--------------
/COM,
/COM,  LOCATION  |  TARGET  |  Mechanical APDL  |  RATIO
/COM,
/COM, RADIAL DISPLACEMENT
/COM,
*VWRITE,VALUE(1,2),VALUE(1,3),VALUE(1,1),RATIO1(1,1)
(F7.3,2XF10.3,5XF10.3,5XF10.3)
/COM,
/COM,-------------------------------------------------------
/OUT,
*LIST,vm290,vrt
FINISH
/EXIT,NOSAVE

/COM,ANSYS MEDIA REL. 2023R1 (11/04/2022) REF. VERIF. MANUAL: REL. 2023R1
/VERIFY,VM288
/PREP7
/TITLE,VM288,ELECTRICAL INPUT ADMITTANCE ON A PIEZOELECTRIC TRANDUCER
/COM,        USING ENFORCED MOTION APPROACH
/COM,   
/COM,   REFERENCE:
/COM    KAGAWA AND YAMABUCHI, FINITE ELEMENT SIMULATION OF A COMPOSITE
/COM    PIEZOELECTRIC ULTRASONIC TRANSDUCER, IEEE TRANS. SONICS AND
/COM    ULTRASONICS, VOL. SU-26, NO.2, MARCH 1979
/COM
ET,1,SOLID186,,1                 ! 3-D STRUCTURAL SOLID
MP,DENS,3,7730                   ! NEPEC DENSITY
MP,EX,2,7.03E10                  ! ALUMINUM MODULUS OF ELASTICITY
MP,NUXY,2,.345                   ! ALUMINUM POISSON RATIO
MP,DENS,2,2690                   ! ALUMINUM DENSITY
MP,EX,4,10E9                     ! ADHESIVE MODULUS OF ELASTICITY
MP,DENS,4,1700                   ! ADHESIVE DENSITY
MP,NUXY,4,.38                    ! ADHESIVE POISSON RATIO

ET,2,SOLID226,1001               ! 3-D COUPLED-FIELD SOLID
TB,PIEZ,3                        ! DEFINE PIEZO. TABLE FOR NEPEC
TBDATA,3,-6.10                   ! PIEZO MATRIX CONSTANTS
TBDATA,6,-6.10
TBDATA,9,15.70
MP,PERX,3,993.55                 ! PERMITTIVITY
TB,ANEL,3                        ! DEFINE STRUCTURAL TABLE FOR NEPEC
TBDATA,1,12.80E10,6.8E10,6.6E10  ! INPUT [C] MATRIX FOR NEPEC
TBDATA,7,12.8E10,6.6E10
TBDATA,12,11.0E10
TBDATA,16,2.1E10
TBDATA,19,2.1E10
TBDATA,21,2.1E10

/COM                             ** DEFINE GEOMETRIC PARAMETERS **
R=27.5E-3                        ! DISK RADIUS
HA=15.275E-3                     ! ALUMINUM 1/2 HEIGHT LOCATION
HN=5E-3                          ! NEPEC 1/2 HEIGHT
HB=5.275E-3                      ! ADHESIVE MATERIAL HEIGHT
RDIV=5                           ! NO. ELEMENTS ALONG RADIUS
HADV=3                           ! NO. ELEMENTS ALONG ALUMINUM HEIGHT
HNDV=2                           ! NO. ELEMENTS ALONG NEPEC HEIGHT
HBDV=1                           ! NO. ELEMENTS ALONG ADHESIVE HEIGHT
ZRO=1E-5                         ! DEFINE ZERO FOR KEYPOINT LOCATION
CSYS,1
K,1,ZRO,-5                       ! DEFINE KEYPOINTS FOR MESH, WEDGE ELEMENTS
K,2,R,-5                         ! ARE NOT ALLOWED BY MESH MODULE SO KEYPOINTS
K,3,R,-5,HN                      ! ARE DEFINED NEAR ZERO AND LATER MERGED
K,4,ZRO,-5,HN
K,5,R,-5,HB
K,6,ZRO,-5,HB
K,7,R,-5,HA
K,8,ZRO,-5,HA
KGEN,2,1,8,1,,10
L,2,3                            ! DEFINE LINE SEGMENTS
LESIZE,1,,,HNDV
L,3,5
LESIZE,2,,,HBDV
L,5,7
LESIZE,3,,,HADV
L,2,10
LESIZE,4,,,1
V,11,3,4,12,10,2,1,9             ! CREATE NEPEC VOLUME
VATT,3,,2                        ! ASSIGN MATERIAL ATTRIBUTES
V,13,5,6,14,11,3,4,12            ! CREATE ADHESIVE VOLUME
VSEL,S,VOLU,,2
VATT,4,,1                        ! ASSIGN MATERIAL ATTRIBUTES
V,15,7,8,16,13,5,6,14            ! CREATE ALUMINUM VOLUME
VSEL,S,VOLU,,3
VATT,2,,1                        ! ASSIGN MATERIAL ATTRIBUTES
VSEL,ALL
MSHK,1                           ! MAPPED VOLUME MESH 
MSHA,0,3D                        ! USING HEX  
ESIZE,,RDIV
SHPP,OFF                         ! TURN OFF SHAPE CHECKING TO ALLOW
!                                  FOR WEDGE SHAPE MESH OF SOLIDS
TYPE,1
/OUT,SCRATCH
VMESH,ALL                        ! MESH ALL VOLUMES
NUMMRG,NODE                      ! MERGE NODES TO CREATE WEDGE ELMENTS
!                                  AT AXIS
NSEL,S,LOC,Y,-5
DSYM,SYMM,Y,1                    ! SYMMETRY B.C. AT THETA=-5 DEG.
NSEL,S,LOC,Y,5
DSYM,SYMM,Y,1                    ! SYMMETRY B.C. AT THETA=5 DEG.
NSEL,S,LOC,X,0,.001
DSYM,SYMM,X,1                    ! SYMMETRY B.C. AT X=0
NSEL,S,LOC,Z,0
DSYM,SYMM,Z,1                    ! SYMMETRY B.C. AT Z=0
NSEL,S,LOC,Z,HN                  ! SELECT NODES AT TOP ELECTRODE
CP,1,VOLT,ALL                    ! COUPLE VOLT DOF ON ELECTRODE
*GET,N1,NODE,,NUM,MIN            ! GET NODE ON ELECTRODE
NSEL,S,LOC,Z,0                   ! SELECT NODES AT SYMMETRY PLANE
D,ALL,VOLT,0.0                   ! SET VOLT TO ZERO AT SYMMETRY PLANE
NSEL,ALL
FINISH

/SOLUTION
ANTYPE,MODAL                     ! PERFORM MODAL SOLVE
MODCONT,,ON                      ! TURN ON ENFORCED MOTION CALCULATION
MODOPT,LANB,3,,                  ! BLOCK-LANCZOS EIGENSOLVER
MXPAND,,,,YES,                   ! EXPAND MODES AND COMPUTE ELEMENT RESULTS
D,N1,VOLT,1                      ! ENFORCED MOTION ID TO APPLY VOLT IN MSUP HARMONIC
SOLVE
FINISH

/POST1
/OUT,
SET,LIST                         ! LIST MODAL FREQUENCIES AND ENFORCED STATIC MODE
FINISH

/OUT,SCRATCH
/SOLUTION
ANTYPE,HARMIC                    ! PERFORM HARMONIC ANALYSIS
HROPT,MSUP                       ! USE MODE-SUPERPOSITION METHOD
OUTRES,ALL,ALL                   ! STORE EVERY SUBSTEP
HARFRQ,5000,35000                ! SOLVE FOR FREQ=5KHZ AND 35KHZ
NSUBST,20                        ! SPECIFY NUMBER OF SUBSTEPS
KBC,1                            ! STEP BOUNDARY CONDITIONS  
DVAL,1,U,-0.5                    ! APPLY ENFORCED MOTION TO TOP ELECTRODE
SOLVE
HARFRQ,39000,45000               ! SOLVE FOR FREQ=39KHZ AND 45KHZ
SOLVE
HARFRQ,46000,54000               ! SOLVE FOR FREQ=46KHZ AND 54KHZ
SOLVE
FINISH

/SOLUTION
EXPASS,ON                        ! EXPANSION PASS
NUMEXP,ALL,,,                    ! EXPAND SOLUTION FOR ALL SUBSTEPS,COMPUTE ELEMENT RESULTS
SOLVE
FINISH

/POST26
RFORCE,2,N1,CHRG                 ! STORE CHARGE ON ELECTRODE
PI2=(3.14159*2.)
PROD,3,2,1,,MHOS,,,PI2           ! CALCULATE ADMITTANCE (10 DEG. SLICE)
PROD,4,3,,,MMHO,,,36000          ! CALCULATE TOTAL ADMITTANCE (MMHOS)
/OUT,
PRVAR,4                          ! PRINT ELECTRICAL ADMITTANCE VS. FREQUENCY
/OUT,SCRATCH
*GET,F1,VARI,4,RTIME,20000
*GET,F2,VARI,4,RTIME,35000
*GET,F3,VARI,4,RTIME,40000
*GET,F4,VARI,4,RTIME,45000
*GET,F5,VARI,4,RTIME,50000
*GET,F6,VARI,4,RTIME,54000
*STATUS,PARM
*DIM,LABEL,CHAR,6,2
*DIM,VALUE,,6,3
LABEL(1,1) = 'Y MMHOS ','Y MMHOS','Y MMHOS ','Y MMHOS ','Y MMHOS ','Y MMHOS '
LABEL(1,2) = '@20 kHz','@35 kHz','@40 kHz','@45 kHz','@50 kHz','@54 kHz'
*VFILL,VALUE(1,1),DATA,.41,.9,1.80,-1.1,.39,.65
*VFILL,VALUE(1,2),DATA,F1,F2,F3,F4,F5,F6
*VFILL,VALUE(1,3),DATA,ABS(F1/.41),ABS(F2/0.9),ABS(F3/1.80),ABS(F4/(-1.1)),ABS(F5/.39),ABS(F6/0.65)
/COM
/OUT,vm288,vrt
/COM, ----------------------------VM288 RESULT COMPARISON-------------------------------------
/COM,
/COM,                   |   TARGET  |   MECHANICAL APDL |   RATIO
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.2,'  ',F14.2,'   ',1F15.3)
/COM,
/COM, ------------------------------------------------------------------------------------------
/OUT
*LIST,vm288,vrt
/out,scratch
FINISH
/EXIT,NOSAVE


/COM,ANSYS MEDIA REL. 2023R1 (11/04/2022) REF. VERIF. MANUAL: REL. 2023R1
/VERIFY,VM198
/TITLE,VM198,LARGE STRAIN IN-PLANE TORSION TEST
/COM,
/COM, REFERENCE: J.C.NAGTEGAAL, J.E. DEJONG, "SOME COMPUTATIONAL
/COM,            ASPECTS OF ELASTIC-PLASTIC STRAIN ANALYSIS",
/COM,            INTERNATIONAL JOURNAL OF NUMERICAL METHODS IN
/COM,            ENGINEERING, VOL.17,1981,PP.15-41.
*DIM,A,CHAR,2              ! DEFINE AND DIMENSION THE CHARACTER PARAMETER A
A(1)='UX','UY'             ! SET CHARACTER STRINGS AS VALUES OF CHARACTER
!                             PARAMETER A
/PREP7
EX=7200
ET=40
EP=EX*ET/(EX-ET)
ET,1,PLANE182,,,2      ! 2-D 4 NODE PLANE ELEMENT,PLANE STRAIN
MP,EX,1,7200           ! YOUNG'S MODULUS
MP,NUXY,1,0.33         ! POISSON'S RATIO
TB,PLAS,1,,,BISO       ! BILINEAR ISOTROPIC HARDENING
TBDATA,1,10                ! YIELD STRESS
TBDATA,2,EP                ! PLASTIC TANGENT MODULUS
SAVE                       ! SAVE DATABASE FOR LATER USE
CSYS,1
N,1,10
N,11,20
FILL
NGEN,2,100,1,11,1,,3
E,1,2,102,101
EGEN,10,1,1,1
!         APPLY BOUNDARY CONDITIONS
NSEL,S,LOC,X,20
D,ALL,ALL                  ! CLAMP OUTER SURFACE
NSEL,ALL
!         ROTATE APPROPRIATE NODES AND APPLY COUPLING
LOCAL,11,0,,,,3
NROTAT,101,111
CSYS,0
CP,1,A(1),2,102            ! USE A(1) VALUE FOR DOF LABEL
CP,2,A(2),2,102            ! USE A(2) VALUE FOR DOF LABEL
CPSGEN,9,1,1,2,1
FINISH
/OUT,SCRATCH
/SOLUTION
!     CREATE SOLUTION OPTIONS AND LOADING MACRO FOR MULTIPLE USE
*CREATE,SOLD,MAC
   ANTYPE,STATIC
   NLGEOM,ON                  ! LARGE STRAIN OPTION ACTIVATED
   NEQIT,100                  ! 100 EQUILIBRIUM ITERATIONS ALLOWED
   CUTC,PLSLIMIT,0.5          ! RESET MAXIMUM PLASTIC INCREMENTAL STRAIN
   CUTC,NPOINT,20
   NSUBS,1000,100000,100
   D,ARG1,ARG2,,,ARG3,ARG4,ARG5
   KBC,0
   CNVTOL,U,-1
   TINTP,,,,0.5,0.5,0.2,,,
   SOLVE
   OUTRES,ESOL,1              ! STORE RESULTS FOR EVERY SUBSTEP
   NSTP = 10                  ! NO. OF LOAD STEPS USED
   T1   = 60/NSTP             ! ROTATION PER LOAD STEP
   T2   = 3.1415927/180       ! PARAMETER FOR FURTHER CALCULATIONS
   T33  = T1*T2               ! DEGREES TO RADIANS CONVERSION
   *DO,I,1,NSTP               ! USE DO LOOP FOR LOADING
       T3   = (I*T33)         ! CURRENT ANGLE
       T4   = (10*SIN(T3))    ! UY DISPLACEMENT
       T5   = (10*COS(T3))
       T5   = (T5-10)         ! UX DISPLACEMENT
       D,ARG1,ARG2,T5,,ARG3,ARG4
       D,ARG1,ARG5,T4,,ARG3,ARG4
       KBC,0
       SOLVE
   *ENDDO
*END
SOLD,1,A(1),101,100,A(2)      ! USE A(1) AND A(2) AS ARG2 AND ARG5 VALUES FOR
FINISH                        ! DOF LABELS
/OUT
!         CREATE POST PROCESSING MACRO FOR MULTIPLE USE
*CREATE,POSP,MAC
   !         OBTAIN MAXIMUM SHEAR STRESS USING POST26
   /POST26
   ESOL,2,1,1,S,1
   ESOL,3,1,1,S,3
   ADD,4,2,3,,SHEAR,,,-1/2,1/2         ! COMPUTE MAX. SHEAR USING
   *GET,P1,VARI,4,EXTREM,VLAST
   *DIM,LABEL,CHAR,1,2
   *DIM,VALUE,,1,3
   LABEL(1,1) = 'PRS MAX '
   LABEL(1,2) = 'psi'
   *VFILL,VALUE(1,1),DATA,-48
   *VFILL,VALUE(1,2),DATA,P1
   *VFILL,VALUE(1,3),DATA,ABS(P1/48)
   FINISH
*END
POSP
SAVE,TABLE_1
FINISH
/CLEAR, NOSTART
!         ANALYZE THE SAME PROBLEM WITH ELEMENT SOLID185
/PREP7
RESUME
/OUT,SCRATCH,,,APPEND
ET,1,SOLID185              ! 3D 8 NODE SOLID ELEMENT
CSYS,1
N,1,10
N,11,20
FILL
NGEN,2,100,1,11,1,,3
NGEN,2,200,1,111,1,,,1
E,1,2,102,101,201,202,302,301
EGEN,10,1,1,1
!          APPLY BOUNDARY CONDITIONS
D,ALL,UZ
NSEL,S,LOC,X,20
D,ALL,ALL                  ! CLAMP OUTER SURFACE
NSEL,ALL
!          ROTATE APPROPRIATE NODES AND APPLY COUPLING
LOCAL,11,0,,,,3
NROTAT,101,111
NROTAT,301,311
CSYS,0
CP,1,A(1),2,102,202,302
CP,2,A(2),2,102,202,302
CPSGEN,9,1,1,2,1
FINISH
/SOLUTION
SOLD,1,A(1),301,100,A(2)
FINISH
/OUT
POSP                       ! POSTPROCESS RESULTS USING POSP MACRO
SAVE,TABLE_2
FINISH
/CLEAR,NOSTART
!         ANALYZE THE SAME PROBLEM WITH ELEMENT PLANE183
/PREP7
RESUME
/OUT,SCRATCH,,,APPEND
ET,1,PLANE183,,,2
N,1,10
N,21,20
FILL
CSYS,1
NGEN,3,30,1,21,1,,1.5
E,1,3,63,61,2,33,62,31
EGEN,10,2,1,1,1
NSLE
NSEL,INVE
NDELE,ALL
!         APPLY BOUNDARY CONDITIONS
NSEL,S,LOC,X,20
D,ALL,ALL
!         ROTATE APPROPRIATE NODES AND APPLY COUPLING
NSEL,S,NODE,,61,81
LOCAL,11,0,,,,3
NROTAT,ALL
NSEL,S,NODE,,31,51,2
LOCAL,12,0,,,,1.5
NROTAT,ALL
NSEL,ALL
CSYS,0
CP,1,A(1),3,33,63
CP,2,A(2),3,33,63
CPSGEN,9,2,1,2,1
CP,21,A(1),2,62
CP,22,A(2),2,62
CPSGEN,10,2,21,22,1
FINISH

/SOLU
!     CREATE SOLUTION OPTIONS AND LOADING MACRO FOR MULTIPLE USE
*CREATE,SOLD1,MAC
   ANTYPE,STATIC
   NLGEOM,ON                  ! LARGE STRAIN OPTION ACTIVATED
   NEQIT,100                  ! 100 EQUILIBRIUM ITERATIONS ALLOWED
   CUTC,PLSLIMIT,0.5          ! RESET MAXIMUM PLASTIC INCREMENTAL STRAIN
   CUTC,NPOINT,20
   NSUBS,5000,100000,500
   KBC,0
   CNVTOL,U,-1
   TINTP,,,,0.5,0.5,0.2,,,
   D,ARG1,ARG2,,,ARG3,ARG4,ARG5
   SOLVE
   OUTRES,ESOL,1              ! STORE RESULTS FOR EVERY SUBSTEP
   NSTP = 10                  ! NO. OF LOAD STEPS USED
   T1   = 60/NSTP             ! ROTATION PER LOAD STEP
   T2   = 3.1415927/180       ! PARAMETER FOR FURTHER CALCULATIONS
   T33  = T1*T2               ! DEGREES TO RADIANS CONVERSION
   *DO,I,1,NSTP               ! USE DO LOOP FOR LOADING
       T3   = (I*T33)         ! CURRENT ANGLE
       T4   = (10*SIN(T3))    ! UY DISPLACEMENT
       T5   = (10*COS(T3))
       T5   = (T5-10)         ! UX DISPLACEMENT
       D,ARG1,ARG2,T5,,ARG3,ARG4
       D,ARG1,ARG5,T4,,ARG3,ARG4
       KBC,0
       SOLVE
   *ENDDO
*END
SOLD1,1,A(1),61,30,A(2)      ! USE A(1) AND A(2) AS ARG2 AND ARG5 VALUES FOR
FINISH

/OUT
POSP                       ! POSTPROCESS RESULTS USING POSP MACRO
SAVE,TABLE_3
FINISH

PARSAV,ALL
/CLEAR,NOSTART
PARRES,CHANGE
!         ANALYZE THE SAME PROBLEM WITH ELEMENT PLANE182 AND WITH TORSION OPTION
/PREP7
PI=ACOS(-1)
EX=7200
ET=40
EP=EX*ET/(EX-ET)
MP,EX,1,7200
MP,NUXY,1,0.33
TB,PLAS,1,,,BISO           ! BILINEAR ISOTROPIC HARDENING
TBDATA,1,10                ! YIELD STRESS
TBDATA,2,EP                ! PLASTIC TANGENT MODULUS
ET,1,PLANE182,,,6          ! 2-D 4 NODE AXISYMMETRIC ELEMENT WITH TORSION
TYPE,1
MAT,1
N,1,10
N,41,20
FILL
NGEN,2,100,1,41,1,,0.25
E,1,2,102,101
EGEN,40,1,1,1              ! MODEL IS CREATED ON THE XOY PLANE AND THE HOOP
                           ! DIRECTION IS THE NEGATIVE GLOBAL Z DIRECTION
ALLSEL,ALL
!
SAVE                       ! SAVE DATABASE FOR LATER USE
!         APPLY BOUNDARY CONDITIONS
NSEL,S,LOC,X,20
D,ALL,UX,0
D,ALL,ROTY,0               ! CLAMP OUTER SURFACE
ALLS
NSEL,S,LOC,X,10
D,ALL,UX,0
D,ALL,ROTY,PI/3            ! ROTATE INNER SURFACE INTO THE XOY PLANE BY 60 DEG.
ALLS
D,ALL,UY,0                 ! PLANE STRAIN PROBLEM ALONG GLOBAL Y-AXIS
ALLS
FINISH

*CREATE,SOLDV,MAC
   !     CREATE SOLUTION MACRO FOR MULTIPLE USE
   /SOLUTION
   NLGEOM,ON           ! LARGE DEFLECTION TURNED ON
   OUTRES,ALL,ALL
   NSUB,1e6,1e6,10     ! NUMBER OF SUBSTEPS
   ERESX,NO        ! COPY THE INTEGRATION POINT RESULTS TO NODE
   SOLVE
   FINISH
*END
/OUT,SCRATCH,,,APPEND
SOLDV

*CREATE,POSPL,MAC
   !         CREATE POST PROCESSING MACRO FOR MULTIPLE USE
   !         OBTAIN MAXIMUM SHEAR STRESS USING POST26
   /POST26
   ESOL,2,1,1,S,1
   ESOL,3,1,1,S,3
   ADD,4,2,3,,SHEAR,,,-1/2,1/2         ! COMPUTE MAX. SHEAR USING
   *GET,P1,VARI,4,EXTREM,VLAST
   *DIM,LABEL,CHAR,1,2
   *DIM,VALUE,,1,3
   LABEL(1,1) = 'PRS MAX '
   LABEL(1,2) = 'psi'
   *VFILL,VALUE(1,1),DATA,-48
   *VFILL,VALUE(1,2),DATA,P1
   *VFILL,VALUE(1,3),DATA,ABS(P1/48)
   FINISH
*END
/OUT,
POSPL
! GENERATING PLOT OF SHEAR STRESS VS DEGREES
   /POST26
   ESOL,2,1,1,S,1
   ESOL,3,1,1,S,3
   NSOL,5,1,ROT,Y,
   ADD,4,2,3,,SHEAR,,,-1/2,1/2         ! COMPUTE MAX. SHEAR USING
   PROD,6,5,,,DEGREES,,,57.2958
   /GRID,1
   /XRANGE,0,60
   /YRANGE,-60,0
   /AXLAB,X,ROTATION(DEGREES)
   /AXLAB,Y,SHEAR STRESS(PSI)
   XVAR,6
   JPGPRF,500,100,1             ! MACRO TO SET PREFS FOR JPEG PLOTS
   /SHOW,JPEG
   PLVAR,4
   /SHOW,CLOSE
   FINISH
SAVE,TABLE_4
FINISH
/CLEAR,NOSTART
!         ANALYZE THE SAME PROBLEM WITH MAP2DTO3D - PLANE182 TO SOLID185
!         CREATE MACRO TO PERFORM MAP2DTO3D FROM LAST SUBSTEP
!         OF 2D ANALYSIS FOR MULTIPLE USE
*CREATE,MAPTO,MAC
   /SOLU
   MAP2DTO3D,START         ! START THE MAPPING ROUTINE
   EEXTRUDE,AXIS,60        ! EXTRUDE 2-D MODEL TO 3-D MODEL WITH
                           ! 60 DIVISIONS ALONG THE HOOP DIRECTION
   MAP2DTO3D,FINI          ! MAP THE BOUNDARY CONDITIONS AND LOADS
   MAP2DTO3D,SOLVE         ! REBALANCE THE RESULTS
   FINISH
*END
/OUT,SCRATCH,,,APPEND
MAPTO
/OUT,
*CREATE,MAPOS,MAC
   !         CREATE POST PROCESSING MACRO FOR MULTIPLE USE
   !         OBTAIN MAXIMUM SHEAR STRESS USING POST1
   /POST1
   PARSAV,ALL
   SET,LAST
   PARRES,CHANGE
   ALLS
   /SHOW
   PLESOL,S,XZ
   *GET,P1,PLNSOL,0,MIN
   /SHOW,CLOSE
   *DIM,LABEL,CHAR,1,2
   *DIM,VALUE,,1,3
   LABEL(1,1) = 'PRS MAX '
   LABEL(1,2) = 'psi'
   *VFILL,VALUE(1,1),DATA,-48
   *VFILL,VALUE(1,2),DATA,P1
   *VFILL,VALUE(1,3),DATA,ABS(P1/48)
   FINISH
*END
MAPOS
SAVE,TABLE_5

PARSAV,ALL
/CLEAR,NOSTART
PARRES,CHANGE
!         ANALYZE THE SAME PROBLEM WITH PLANE183 WITH TORSION OPTION
/PREP7
RESUME
ET,100,PLANE183,,,6        ! 2-D 8 NODE AXISYMMETRIC ELEMENT WITH TORSION
EMODIF,ALL,TYPE,100
EMID,ADD,ALL
ALLSEL,ALL
PI=ACOS(-1)
NSEL,S,LOC,X,20
D,ALL,UX,0
D,ALL,ROTY,0
ALLSEL,ALL
NSEL,S,LOC,X,10
D,ALL,UX,0
D,ALL,ROTY,PI/3
ALLSEL,ALL
D,ALL,UY,0
ALLSEL,ALL
FINISH
/OUT,SCRATCH,,,APPEND
SOLDV
/OUT,
POSPL
SAVE,TABLE_6

PARSAV,ALL
/CLEAR,NOSTART
PARRES,CHANGE
!         ANALYZE THE SAME PROBLEM WITH MAP2DTO3D - PLANE183 TO SOLID186
/OUT,SCRATCH,,,APPEND
MAPTO
/OUT,
MAPOS
SAVE,TABLE_7

/CLEAR,NOSTART

RESUME,TABLE_1
/COM
/OUT,vm198,vrt
/COM,------------------- VM198 RESULTS COMPARISON --------------
/COM,
/COM,                 |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
/COM,RESULTS USING PLANE182
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.1,'  ',F15.1,'   ',1F15.3)
/NOPR
RESUME,TABLE_2
/GOPR
/COM,
/COM,RESULTS USING SOLID185
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.1,'  ',F15.1,'   ',1F15.3)
/NOPR
RESUME,TABLE_3
/GOPR
/COM,
/COM,RESULTS USING PLANE183
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.1,'  ',F15.1,'   ',1F15.3)
/COM,
/NOPR
RESUME,TABLE_4
/GOPR,
/COM,
/COM,RESULTS USING AXISYMMETRIC PLANE182 WITH TORSION
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.1,'  ',F15.1,'   ',1F15.3)
/NOPR
RESUME,TABLE_5
/GOPR
/COM,
/COM,RESULTS USING SOLID185 EXTRUDED FROM PLANE182 WITH TORSION USING MAP2DTO3D
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.1,'  ',F15.1,'   ',1F15.3)
/NOPR
RESUME,TABLE_6
/GOPR
/COM,
/COM,RESULTS USING AXISYMMETRIC PLANE183 WITH TORSION
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.1,'  ',F15.1,'   ',1F15.3)
/NOPR
RESUME,TABLE_7
/GOPR
/COM,
/COM,RESULTS USING SOLID186 EXTRUDED FROM PLANE183 WITH TORSION USING MAP2DTO3D
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.1,'  ',F15.1,'   ',1F15.3)
/COM,-----------------------------------------------------------
/OUT
FINISH
/NOPR
/DELETE,POSP,MAC
/DELETE,POSPL,MAC
/DELETE,MAPOS,MAC
/DELETE,MAPTO,MAC
/DELETE,SOLD,MAC
/DELETE,SOLD1,MAC
/DELETE,SOLDV,MAC
/DELETE,TABLE_1
/DELETE,TABLE_2
/DELETE,TABLE_3
/DELETE,TABLE_4
/DELETE,TABLE_5
/DELETE,TABLE_6
/DELETE,TABLE_7
*LIST,vm198,vrt

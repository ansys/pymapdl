/COM,ANSYS MEDIA REL. 2023R1 (11/04/2022) REF. VERIF. MANUAL: REL. 2023R1
/VERIFY,VM152
/PREP7
JPGPRF,500,100,1             ! MACRO TO SET PREFS FOR JPEG PLOTS
/SHOW,JPEG
/TITLE, VM152, NONAXISYM. VIBR. OF A STRETCHED CIRCULAR MEMBRANE (HARMONIC ELS)
C***          VIBRATION PROBS. IN ENGR., TIMOSHENKO, 3RD. ED., ART. 69, PAGE 438
ET,1,SHELL61
R,1,.00005              ! PLATE THICKNESS
MP,EX,1,30E6
MP,ALPX,1,1E-5
MP,DENS,1,.00073
MP,NUXY,1,0
K,1                     ! DEFINE GEOMETRY
K,2,3
L,1,2
LESIZE,1,,,9
LMESH,1
NSEL,S,LOC,X,0
D,ALL,UX
NSEL,S,LOC,X,3
D,ALL,UX,,,,,UY
NSEL,ALL
D,ALL,UZ
TREF,0                  ! REFERENCE (STRESS-FREE) TEMPERATURE
BFUNIF,TEMP,-(20/3)     ! COOL MEMBRANE TO INVOKE PRESOLESS
FINISH
/SOLU    
ANTYPE,STATIC
PSTRES,ON               ! STATIC PRESTRESS ANALYSIS
OUTPR,BASIC,ALL
/OUT,SCRATCH
SOLVE
FINISH
/SOLU    
ANTYPE,MODAL
PSTRES,ON               ! PRESTRESSED MODAL ANALYSIS
MXPAND,3
MODOPT,LANB,3
MODE,0                  ! ZERO HARMONIC MODE
SOLVE
FINISH
/POST1
/WINDOW,1,LTOP
/OUT,
SET,1,1                 ! FOR WINDOW 1
PLDISP,1
/NOERASE
*GET,F11,MODE,0,FREQ
/WINDOW,1,OFF
/WINDOW,4,RBOT
SET,1,2                 ! FOR 4TH WINDOW 
PLDISP,1
*GET,F12,MODE,2,FREQ
/WINDOW,4,OFF
FINISH
/SOLU    
MODE,1                  ! FIRST HARMONIC MODE
/OUT,SCRATCH
SOLVE
/OUT,
FINISH
/POST1
/WINDOW,2,RTOP
SET,1,1
PLDISP,1
*GET,F21,MODE,0,FREQ
/WINDOW,2,OFF
FINISH
/SOLU    
MODE,2                  ! SECOND HARMONIC MODE
/OUT,SCRATCH
SOLVE
/OUT,
FINISH
/POST1
/WINDOW,3,LBOT
SET,1,1
PLDISP,1
*GET,F31,MODE,0,FREQ
*status,parm
*DIM,LABEL,CHAR,4,2
*DIM,VALUE,,4,3
LABEL(1,1) = 'F(0,1) ','F(1,1) ','F(2,1) ','F(0,2) '
LABEL(1,2) = 'Hz ','Hz ','Hz ','Hz '
*VFILL,VALUE(1,1),DATA,211.1,336.5,450.9,484.7
*VFILL,VALUE(1,2),DATA,F11,F21,F31,F12
*VFILL,VALUE(1,3),DATA,ABS(F11/211.1),ABS(F21/336.5),ABS(F31/450.9),ABS(F12/484.7)
/COM
/OUT,vm152,vrt
/COM,------------------- VM152 RESULTS COMPARISON -------------
/COM,
/COM,                 |    TARGET   |    Mechanical APDL   |    RATIO
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F11.1,'  ',F13.1,'   ',1F16.3)
/COM,----------------------------------------------------------
/OUT
FINISH
*LIST,vm152,vrt

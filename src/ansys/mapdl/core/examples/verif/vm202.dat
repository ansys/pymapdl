/COM,ANSYS MEDIA REL. 150 (11/8/2013) REF. VERIF. MANUAL: REL. 150
/VERIFY,VM202
JPGPRF,500,100,1             ! MACRO TO SET PREFS FOR JPEG PLOTS
/SHOW,JPEG
/PREP7
/TITLE, VM202, TRANSVERSE VIBRATIONS OF A SHEAR BEAM
/COM, REF:  R. BLEVINS, FORMULAS FOR NATURAL FREQUENCY AND MODE SHAPE, 1979.
ANTYPE,MODAL
ET,1,SHELL28
R,1,0.1                 ! DEFINE REAL CONSTANT SET 1; THICKNESS OF SHELL
MP,EX,  1,200E9
MP,NUXY,1,0.27
MP,DENS,1,7860.0 
N,1                     ! DEFINE NODES
N,2,10
N,3,10,10
N,4,  ,10
NGEN,9,4,1,4,1,,,3.75
E,1,2,6,5               ! DEFINE ELEMENTS
EGEN,3,1,-1
E,4,1,5,8
EGEN,8,4,-4
D,ALL,UZ                ! DEFINE BOUNDARY CONDITIONS
D,1,UX,,,4,1,UY
NSEL,S,LOC,Z,3.75       ! SELECT NODES AT COMMON Z LOCATION
CP,1,UX,ALL             ! COUPLE NODES IN X AND Y DOF
CP,2,UY,ALL
NSEL,S,LOC,Z,7.5
CP,3,UX,ALL
CP,4,UY,ALL
NSEL,S,LOC,Z,11.25
CP,5,UX,ALL
CP,6,UY,ALL
NSEL,S,LOC,Z,15
CP,7,UX,ALL
CP,8,UY,ALL
NSEL,S,LOC,Z,18.75
CP,9,UX,ALL
CP,10,UY,ALL
NSEL,S,LOC,Z,22.5
CP,11,UX,ALL
CP,12,UY,ALL
NSEL,S,LOC,Z,26.25
CP,13,UX,ALL
CP,14,UY,ALL
NSEL,S,LOC,Z,30
CP,15,UX,ALL
CP,16,UY,ALL
NSEL,ALL
FINISH
/out,scratch
/SOLU
MXPAND,4                ! EXPAND FIRST TWO REPEATED MODES
MODOPT,LANB,8
SOLVE
*GET,F1,MODE,1,FREQ
*GET,F2,MODE,3,FREQ
FINISH
/out
/POST1
SET,1,1
/VUP,,Z
/VIEW,,,1
PLDISP,1                ! DISPLAY DISPLACEMENTS (FIRST MODE)
SET,1,3
PLDISP,1                ! DISPLAY DISPLACEMENTS (SECOND MODE)
*status,parm
*DIM,VALUE,,2,3
*DIM,LABEL,CHAR,2,2
LABEL(1,1) = 'F1 ','F2 '
LABEL(1,2) = 'Hz ','Hz '
*VFILL,VALUE(1,1),DATA,17.375,52.176
*VFILL,VALUE(1,2),DATA,F1,F2
*VFILL,VALUE(1,3),DATA,ABS(F1/17.375),ABS(F2/52.176)
/COM
/OUT,vm202,vrt
/COM,------------------- VM202 RESULTS COMPARISON -------------
/COM,
/COM,                  |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.3,'  ',F14.3,'   ',1F18.3)
/COM,----------------------------------------------------------
/OUT
FINISH
*LIST,vm202,vrt

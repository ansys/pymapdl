/COM,ANSYS MEDIA REL. 2023R1 (11/04/2022) REF. VERIF. MANUAL: REL. 2023R1
/VERIFY,VM50
/PREP7
/TITLE, VM50, FUNDAMENTAL FREQUENCY OF A SIMPLY SUPPORTED BEAM
C*** VIBRATION THEORY AND APPLICATIONS, THOMSON, 2ND PRINTING, PAGE 18, EX 1.5-1
ET,1,BEAM189         ! THREE DIMENSIONAL ELASTIC BEAM
MP,EX,1,30E6         ! DEFINE MATERIAL PROPERTIES
MP,DENS,1,728E-6
MP,GXY,1,30E6/2.6
SECT,1,BEAM,ASEC
SECD,4,(4/3),1e-6,(4/3),,1e-6
K,1                  ! BEGIN DEFINING KEYPOINTS
K,2,80
L,1,2                ! DEFINE LINE WITH
LESIZE,ALL,,,4       !  4 DIVISIONS
LMESH,1              ! MESH LINE
ALLSEL,ALL
D,ALL,UZ
D,ALL,ROTY
D,ALL,ROTX
FINISH
/out,scratch
/SOLU    
ANTYPE,MODAL
MODOPT,LANB,3,,,
MXPAND,3             ! EXPAND FIRST MODE
OUTPR,ALL,1
DK,ALL,UX            ! CONSTRAIN ENDS OF BEAM IN DISP. X DOF
DK,ALL,UY            ! CONSTRAIN ENDS OF BEAM IN DISP. Y DOF
SOLVE
FINISH
/out
/POST26
*GET,FREQ,MODE,1,FREQ
*DIM,LABEL,CHAR,1,2
*DIM,VALUE,,1,3
LABEL(1,1) = '       f'
LABEL(1,2) = ', (Hz)  '
*VFILL,VALUE(1,1),DATA,28.766 
*VFILL,VALUE(1,2),DATA,FREQ
*VFILL,VALUE(1,3),DATA,ABS(FREQ/28.766) 
/COM
/OUT,vm50,vrt
/COM,------------------- VM50 RESULTS COMPARISON ---------------
/COM,
/COM,                 |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.3,'  ',F14.3,'   ',1F15.3)
/COM,-----------------------------------------------------------
/OUT
FINISH
*LIST,vm50,vrt


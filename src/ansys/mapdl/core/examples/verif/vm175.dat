/COM,ANSYS MEDIA REL. 2023R1 (11/04/2022) REF. VERIF. MANUAL: REL. 2023R1
/VERIFY,VM175
JPGPRF,500,100,1             ! MACRO TO SET PREFS FOR JPEG PLOTS
/SHOW,JPEG
/PREP7
SMRT,OFF
/TITLE, VM175, NATURAL FREQUENCY OF A PIEZOELECTRIC TRANSDUCER
/COM,         COMPUTATION OF THE VIBRATIONAL MODES FOR PIEZOELECTRIC
/COM,         ARRAY TRANSDUCERS USING A MIXED FINITE ELEMENT
/COM,         PERTURBATION METHOD, BOUCHER, D., LAGIER, M., MAERFELD,
/COM,         C., IEEE TRANS. SONICS AND ULTRASONICS, VOL. SU-28,
/COM,         NO. 5, SEPTEMBER 1981., PG. 318-330
/COM,
ET,1,SOLID5,3                      ! 3-D COUPLED-FIELD SOLID, PIEZO OPTION 
MP,DENS,3,7500                     ! DENSITY
MP,PERX,3,804.6                    ! PERMITTIVITY (X AND Y DIRECTION)
MP,PERZ,3,659.7                    ! PERMITTIVITY (Z DIRECTION)
TB,PIEZ,3                          ! DEFINE PIEZ. TABLE
TBDATA,16,10.5                     ! E61 PIEZOELECTRIC CONSTANT
TBDATA,14,10.5                     ! E52 PIEZOELECTRIC CONSTANT
TBDATA,3,-4.1                      ! E13 PIEZOELECTRIC CONSTANT
TBDATA,6,-4.1                      ! E23 PIEZOELECTRIC CONSTANT
TBDATA,9,14.1                      ! E33 PIEZOELECTRIC CONSTANT
TB,ANEL,3                          ! DEFINE STRUCTURAL TABLE
TBDATA,1,13.2E10,7.1E10,7.3E10     ! INPUT [C] MATRIX
TBDATA,7,13.2E10,7.3E10
TBDATA,12,11.5E10
TBDATA,16,3.0E10
TBDATA,19,2.6E10
TBDATA,21,2.6E10

L=10E-3                            ! SET LENGTH
W=10E-3                            ! SET WIDTH
H=20E-3                            ! SET HEIGHT
K,1
K,2,L
K,3,L,W
K,4,,W
KGEN,2,1,4,1,,,H
L,1,5
LESIZE,1,,,4
ESIZE,,2
MSHK,1                    ! MAPPED VOLUME MESH 
MSHA,0,3D                 ! USING HEX 
V,1,2,3,4,5,6,7,8
MAT,3
VMESH,1
NSEL,S,LOC,X,0
DSYM,SYMM,X                        ! SYMMETRY BOUNDARY CONSTRAINTS
NSEL,S,LOC,Y,0
DSYM,SYMM,Y
NSEL,ALL
FINISH
/out,scratch
/SOLU    
ANTYP,MODAL
MODOPT,LANB,10,50000,150000        ! BLOCK LANCZOS, EXTRACT 10 MODES
MXPAND,10                          ! EXPAND ALL MODES
/COM,                              ** RESONANCE FREQUENCY B.C.'S **
NSEL,S,LOC,Z,0
D,ALL,VOLT,0                       ! GROUND BOTTOM ELECTRODE
NSEL,S,LOC,Z,H
D,ALL,VOLT,0                       ! SHORT-CIRCUIT TOP ELECTRODE
NSEL,ALL
SOLVE
*GET,F1,MODE,2,FREQ
*GET,F2,MODE,5,FREQ
FINISH
/out
/POST1
/VIEW,1,1,1,1
/VUP,,Z
/TYPE,,4
*DO,I,1,10                       
SET,LS,I
PLDISP,1                           ! DISPLAY ANTI-RESONANCE MODE SHAPES
*ENDDO
FINISH
/out,scratch
/SOLU    
ANTYPE,MODAL
MODOPT,LANB,10,50000,150000        ! BLOCK LANCZOS, EXTRACT 10 MODES
MXPAND,10
/COM,                              ** ANTI-RESONANCE FREQUENCY B.C.'S **
NSEL,S,LOC,Z,H
DDELE,ALL,VOLT                     ! DELETE VOLT CONSTRAINTS ON TOP ELECTRODE
CP,1,VOLT,ALL                      ! COUPLE VOLT DOF ON TOP ELECTRODE
NSEL,ALL
SOLVE
*GET,F3,MODE,4,FREQ
*GET,F4,MODE,8,FREQ
FINISH
/out
/POST1
*DO,I,1,10
SET,LS,I
PLDISP,1                           ! DISPLAY RESONANCE MODE SHAPES
*ENDDO
*STATUS,PARM
*DIM,LABEL,CHAR,2,2
*DIM,VALUE,,2,3
LABEL(1,1) = 'F1 ','F2 '
LABEL(1,2) = 'Hz','Hz'
*VFILL,VALUE(1,1),DATA,66560,88010
*VFILL,VALUE(1,2),DATA,F1,F2
*VFILL,VALUE(1,3),DATA,ABS(F1/66560),ABS(F2/88010)
/COM
/OUT,vm175,vrt
/COM,------------------- VM175 RESULTS COMPARISON -------------
/COM
/COM,                 |   TARGET   |   Mechanical APDL   |   RATIO
/COM, SOLID5
/COM,
/COM, SHORT CIRCUIT
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.0,'  ',F14.0,'   ',1F15.3)
/COM
/NOPR
*VFILL,VALUE(1,1),DATA,81590,93410
*VFILL,VALUE(1,2),DATA,F3,F4
*VFILL,VALUE(1,3),DATA,ABS(F3/81590),ABS(F4/93410)
/COM, OPEN CIRCUIT
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.0,'  ',F14.0,'   ',1F15.3)
/COM,----------------------------------------------------------
/OUT
FINISH

/CLEAR,NOSTART
JPGPRF,500,100,1             ! MACRO TO SET PREFS FOR JPEG PLOTS
/SHOW,JPEG
/PREP7
SMRT,OFF
/TITLE, VM175, NATURAL FREQUENCY OF A PIEZOELECTRIC TRANSDUCER
ET,1,SOLID226,1001                 ! 3-D 20-NODE COUPLED-FIELD SOLID, PIEZO OPTION 
MP,DENS,3,7500                     ! DENSITY
MP,PERX,3,804.6                    ! PERMITTIVITY (X AND Y DIRECTION)
MP,PERZ,3,659.7                    ! PERMITTIVITY (Z DIRECTION)
TB,PIEZ,3                          ! DEFINE PIEZ. TABLE
TBDATA,16,10.5                     ! E61 PIEZOELECTRIC CONSTANT
TBDATA,14,10.5                     ! E52 PIEZOELECTRIC CONSTANT
TBDATA,3,-4.1                      ! E13 PIEZOELECTRIC CONSTANT
TBDATA,6,-4.1                      ! E23 PIEZOELECTRIC CONSTANT
TBDATA,9,14.1                      ! E33 PIEZOELECTRIC CONSTANT
TB,ANEL,3                          ! DEFINE STRUCTURAL TABLE
TBDATA,1,13.2E10,7.1E10,7.3E10     ! INPUT [C] MATRIX
TBDATA,7,13.2E10,7.3E10
TBDATA,12,11.5E10
TBDATA,16,3.0E10
TBDATA,19,2.6E10
TBDATA,21,2.6E10

L=10E-3                            ! SET LENGTH
W=10E-3                            ! SET WIDTH
H=20E-3                            ! SET HEIGHT
K,1
K,2,L
K,3,L,W
K,4,,W
KGEN,2,1,4,1,,,H
L,1,5
LESIZE,1,,,4
ESIZE,,2
MSHK,1                    ! MAPPED VOLUME MESH 
MSHA,0,3D                 ! USING HEX   
V,1,2,3,4,5,6,7,8
MAT,3
VMESH,1
NSEL,S,LOC,X,0
DSYM,SYMM,X                        ! SYMMETRY BOUNDARY CONSTRAINTS
NSEL,S,LOC,Y,0
DSYM,SYMM,Y
NSEL,ALL
FINISH
/out,scratch
/SOLU    
ANTYP,MODAL
MODOPT,LANB,10,50000,150000        ! BLOCK LANCZOS, EXTRACT 10 MODES
MXPAND,10                          ! EXPAND ALL MODES
/COM,                              ** RESONANCE FREQUENCY B.C.'S **
NSEL,S,LOC,Z,0
D,ALL,VOLT,0                       ! GROUND BOTTOM ELECTRODE
NSEL,S,LOC,Z,H
D,ALL,VOLT,0                       ! SHORT-CIRCUIT TOP ELECTRODE
NSEL,ALL
SOLVE
*GET,F1,MODE,2,FREQ
*GET,F2,MODE,5,FREQ
FINISH
/out
/POST1
/VIEW,1,1,1,1
/VUP,,Z
/TYPE,,4
*DO,I,1,10                       
SET,LS,I
PLDISP,1                           ! DISPLAY ANTI-RESONANCE MODE SHAPES
*ENDDO
FINISH
/out,scratch
/SOLU    
ANTYPE,MODAL
MODOPT,LANB,10,50000,150000        ! BLOCK LANCZOS, EXTRACT 10 MODES
MXPAND,10
/COM,                              ** ANTI-RESONANCE FREQUENCY B.C.'S **
NSEL,S,LOC,Z,H
DDELE,ALL,VOLT                     ! DELETE VOLT CONSTRAINTS ON TOP ELECTRODE
CP,1,VOLT,ALL                      ! COUPLE VOLT DOF ON TOP ELECTRODE
NSEL,ALL
SOLVE
*GET,F3,MODE,4,FREQ
*GET,F4,MODE,8,FREQ
FINISH
/out
/POST1
*DO,I,1,10
SET,LS,I
PLDISP,1                           ! DISPLAY RESONANCE MODE SHAPES
*ENDDO
*STATUS,PARM
*DIM,LABEL,CHAR,2,2
*DIM,VALUE,,2,3
LABEL(1,1) = 'F1 ','F2 '
LABEL(1,2) = 'Hz','Hz'
*VFILL,VALUE(1,1),DATA,66560,88010
*VFILL,VALUE(1,2),DATA,F1,F2
*VFILL,VALUE(1,3),DATA,ABS(F1/66560),ABS(F2/88010)
/COM
/OUT,vm175,vrt,,APPEND 
/COM, SOLID226
/COM,
/COM, SHORT CIRCUIT
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.0,'  ',F14.0,'   ',1F15.3)
/COM,
/NOPR
*VFILL,VALUE(1,1),DATA,81590,93410
*VFILL,VALUE(1,2),DATA,F3,F4
*VFILL,VALUE(1,3),DATA,ABS(F3/81590),ABS(F4/93410)
/COM, OPEN CIRCUIT
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.0,'  ',F14.0,'   ',1F15.3)
/COM,----------------------------------------------------------
/OUT
FINISH
*LIST,vm175,vrt


/COM,ANSYS MEDIA REL. 150 (11/8/2013) REF. VERIF. MANUAL: REL. 150
/VERIFY,vme6
JPGPRF,500,100,1             ! MACRO TO SET PREFS FOR JPEG PLOTS
/SHOW,JPEG
/TITLE,VME6, Projectile with Air Resistance
/COM, REF: Marion and Thornton, Pages 60-63
G=386.4                 	! DEFINE ACCELERATION DUE TO GRAVITY
VX=100.0			! DEFINE INTIAL VELOCITY IN X DIRECTION
VY=500.0			! DEFINE INTIAL VELOCITY IN Y DIRECTION
/PREP7				! ENTER PREPROCESSOR
BLOCK,0,1,0,1,0,1       	! CREATE THE PROJECTILE 
ET,1,164			! SET ELEMENT TYPE TO EXPLICIT 3D SOLID
MP,EX,1,1.0			! DEFINE YOUNG'S MODULUS
MP,DENS,1,1.0           	! DEFINE DENSITY
MP,NUXY,1,0.3           	! DEFINE POISSON'S RATIO
ESIZE,,1                	
TYPE,1
REAL,1
VMESH,ALL			! MESH THE PROJECTILE
NSEL,S,LOC,X,0
NSEL,R,LOC,Y,0
NSEL,R,LOC,Z,0
CM,NODE0,NODE 			! CREATE NODAL COMPONENT OF NODE AT ORIGIN
*GET,NODENUM,NODE,0,NUM,MAX 	! STORE THE NODE NUMBER
ALLSEL
CM,NODES,NODE			! CREATE NODAL COMPONENT OF ALL NODES
ALLSEL
EDDAMP,ALL,0,1.0                ! DEFINE ALPHA DAMPING
FINISH
/SOLUTION			! ENTER SOLUTION PROCESSOR
EDRST,100			! SPECIFY OUTPUT INTERVAL
EDHTIME,100			! SPECIFY TIME-HISTORY OUTPUT INTERVAL
EDHIST,NODE0			! SPECIFY TIME-HISTORY OUTPUT FOR NODE0
EDCTS,,0.00025			! SPECIFY SCALE FACTOR FOR COMPUTED TIME STEP
*DIM,T,ARRAY,2			! DEFINE TIME ARRAY
*DIM,ACCELY,ARRAY,2		! DEFINE ACCELERATION ARRAY
T(1)=0.0,2.0            	! INTIALIZE TIME ARRAY
ACCELY(1)=G,G            	! INTIALIZE ACCELERATION ARRAY

!***EDLOAD,ADD,AY,,NODES,T,ACCELY   ! SPECIFY THE ACCELERATION DUE TO GRAVITY LOAD
edload,add,acly,,nodes,T,ACCELY     ! apply a -g to the base to simulate gravity

TIME,2.0                	! SPECIFY SOLUTION TIME
EDVEL,VELO,NODES,VX,VY		! SPECIFY INITIAL VELOCITY ON ALL LOADS
/COM  &COMPARE,NOCOMPARE
SOLVE				! PERFORM SOLUTION
/COM  &COMPARE,NORMAL
FINISH
/POST26				! ENTER TIME-HISTORY POSTPROCESSOR
FILE,vme6,his			! READ THE HISTORY FILE
NSOL,2,NODENUM,U,Y,dISPL-Y	! DEFINE VARIABLE 2 AS UY OF NODENUM
NSOL,3,NODENUM,U,X,dISPL-X	! DEFINE VARIABLE 3 AS UX OF NODENUM
*DIM,TIME,ARRAY,3000		! DEFINE TIME ARRAY
*DIM,YDISP,ARRAY,3000		! DEFINE Y-DISPLACEMENT ARRAY	
*DIM,XDISP,ARRAY,3000		! DEFINE X-DISPLACEMENT ARRAY	
VGET,TIME(1),1			! STORE TIME VECTOR IN THE ARRAY
VGET,YDISP(1),2			! STORE Y-DISPLACEMENT VECTOR IN THE ARRAY
VGET,XDISP(1),3			! STORE X-DISPLACEMENT VECTOR IN THE ARRAY

! SORT THROUGH THE ARRAY YDISP AND FIND THE TIME STEP INCREMENT
! AT WHICH THE Y-DIRECTION DEFLECTION WOULD REACH ZERO. THE
! PROJECTILE BEGINS AT Y = 0, AND THEN TRAVELS INTO POSITIVE
! Y-VALUES, THEN FALLS BACK TO Y = 0. THE TIME AT WHICH
! IT REACHES Y = 0 AGAIN IS THE TOTAL TRAVEL TIME OF THE PROJECTILE.
*DO,I,1,1000,1
  *IF,YDISP(I),LT,0,THEN
    *EXIT
  *ENDIF
*ENDDO

Z=(0.0-YDISP(I-1))/(YDISP(I)-YDISP(I-1))
TRAVELT=TIME(I-1)+Z*(TIME(I)-TIME(I-1))		! LINEAR INTERPOLATION OF TRAVEL TIME
TRAVELD=XDISP(I-1)+Z*(XDISP(I)-XDISP(I-1))	! LINEAR INTERPOLATION OF TRAVEL DISTANCE
CHECK=((VY+G)/G)*(1.0-EXP(-TRAVELT))		! COMPUTE THEORETICAL SOLUTION

*VWRITE,CHECK,TRAVELT,TRAVELD
('TRAVELT=',E10.4,' CHECK=',E10.4,' TRAVELD=',E10.4)
*DIM,LABEL,CHAR,2
*DIM,RES,,2,3
LABEL(1) = 'TRAV TM','DIST TRA'
*VFILL,RES(1,1),DATA,1.976,86.138
*VFILL,RES(1,2),DATA,TRAVELT,TRAVELD
*VFILL,RES(1,3),DATA,(RES(1,2)/RES(1,1)),(RES(2,2)/RES(2,1))
/OUT,vme6,vrt
/COM,
/COM,-------------------- VME6 DYNA RESULTS COMPARISION --------------------
/COM,
/COM,          |  TARGET  |   ANSYS   |  RATIO  |
/COM,
*VWRITE,LABEL(1),RES(1,1),RES(1,2),RES(1,3)
(1X,A8,'    ',F7.4,'      ',F7.4,'    ',F5.3)
/COM,
/COM,-----------------------------------------------------------------------
/OUT
*LIST,vme6,vrt
/GOPR
FINISH





/COM,ANSYS MEDIA REL. 2023R1 (11/04/2022) REF. VERIF. MANUAL: REL. 2023R1
/VERIFY,VM58
/PREP7
SMRT,OFF
/TITLE, VM58, CENTERLINE TEMP. OF A HEAT GENERATING WIRE
C*** HEAT, MASS AND MOMENTUM TRANS., ROHSENOW AND CHOI, 2ND. PR., P. 106,EX 6.5
ANTYPE,STATIC
ET,1,PLANE35              ! 2-D 6 NODE TRIANGULAR THERMAL SOLID
ET,2,SURF151              ! 2-D THERMAL SURFACE EFFECT ELEMENT
KEYOPT,2,8,2
MP,KXX,1,13
CSYS,1
K,1                       ! DEFINE MODEL
K,2,.03125,-15
K,3,.03125,15
L,2,3
CSYS
A,1,2,3
ESIZE,,4
AMESH,1
CSYS,1
NSEL,S,LOC,X,.03125
CP,1,TEMP,ALL             ! COUPLE NODAL TEMPERATURES ON EXTERIOR
TYPE,2                    ! SELECT SURF151 TYPE
ESURF                     ! GENERATE SURF151 ELEMENTS ON OUTER RADIUS
NSEL,ALL
ESEL,S,TYPE,,2
SFE,ALL,,CONV,,5
SFE,ALL,,CONV,2,70
ESEL,ALL
BFA,1,HGEN,111311.7	  ! HEAT GENERATION
FINISH
/SOLU
SOLVE
FINISH
/POST1
NSEL,S,LOC,X
PRNSOL,TEMP
NSEL,S,LOC,X,.03125
PRNSOL,TEMP
ESLN,S,1                  ! SELECT SURFACE ELEMENTS ON OUTER RADIUS
ETABLE,Q1,SMISC,2         ! HEAT RATE OVER EACH SURFACE ELEMENT
SSUM
*GET,HFLW,SSUM,,ITEM,Q1
HFLW=HFLW*12              ! CALCULATE TOTAL HEAT DISSIPATION RATE
*status,parm
ALLSEL
*GET,T_AT_CL,NODE,1,TEMP
*GET,T_AT_S,NODE,2,TEMP
*DIM,LABEL,CHAR,3,2
*DIM,VALUE,,3,3
LABEL(1,1) = 'T CL,   ','T S,    ','Q, (BTU/'
LABEL(1,2) = '(F)     ','(F)     ','HR)     '
*VFILL,VALUE(1,1),DATA,419.9,417.9,341.5
*VFILL,VALUE(1,2),DATA,T_AT_CL,T_AT_S,HFLW
*VFILL,VALUE(1,3),DATA,ABS(T_AT_CL/419.9),ABS(T_AT_S/417.9),ABS(HFLW/341.5)
/COM
/OUT,vm58,vrt
/COM,------------------- VM58 RESULTS COMPARISON --------------
/COM,
/COM,                 |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.1,'  ',F14.1,'   ',1F15.3)
/COM,----------------------------------------------------------
/OUT
FINISH
*LIST,vm58,vrt

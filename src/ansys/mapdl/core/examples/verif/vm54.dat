/COM,ANSYS MEDIA REL. 150 (11/8/2013) REF. VERIF. MANUAL: REL. 150
/VERIFY,VM54
/PREP7
/TITLE, VM54, VIBRATION OF A ROTATING CANTILEVER BLADE
C*** CARNEGIE,W., VIBRATIONS OF ROTATING CANTILEVER BLADING,
C*** JOURNAL OF MECHANICAL ENGINEERING SCIENCE,PG. 239,VOL.1,NO.3,1959.
ET,1,SHELL63,,,,,,,1     ! FOUR NODE SHELL, SUPPRESS STRESS PRINTOUT
R,1,3E-3                 ! THICKNESS OF SHELL
MP,EX,1,217E9            ! MATERIAL, STEEL
MP,NUXY,1,0.3
MP,DENS,1,7850
N,1,-.014,,.150          ! DEFINE NODES
N,9,-.014,,.478
FILL
NGEN,2,9,1,9,1,.028
E,1,2,11,10              ! DEFINE ELEMENTS
EGEN,8,1,-1
FINISH
/SOLU    
ANTYPE,STATIC            ! STATIC ANALYSIS, PRESTRESS
PSTRES,ON                ! PRESTRESS ANALYSIS
D,1,ALL,,,10,9           ! BOUNDARY CONDITIONS AND LOADING
OMEGA,314.159265         ! SPINNING LOAD
OUTPR,,1
/OUT,SCRATCH
SOLVE
FINISH
/SOLU    
ANTYPE,MODAL             ! MODAL ANALYSIS
MODOPT,LANB,5            ! LANB EXTRACTION METHOD, EXTRACT 5 MODES
PSTRES,ON                ! PRESTRESS ANALYSIS
SOLVE
/OUT,
*GET,FREQ,MODE,1,FREQ
*DIM,LABEL,CHAR,1,2
*DIM,VALUE,,1,3
LABEL(1,1) = '     f, '
LABEL(1,2) = 'Hz      '
*VFILL,VALUE(1,1),DATA,52.75 
*VFILL,VALUE(1,2),DATA,FREQ 
*VFILL,VALUE(1,3),DATA,ABS(FREQ /52.75)
FINISH
SAVE,TABLE_1
/CLEAR, NOSTART

/TITLE, VM54, VIBRATION OF A ROTATING CANTILEVER BLADE
C***          USING SOLSH190 ELEMENTS
/PREP7
SMRT,OFF
ET,1,SOLSH190,,,,,,,1    ! ANALYZE AGAIN USING 3-D SOLSH190, SUPPRESS STRESS PRINTOUT
THICK = 3E-3		 ! THCIKNESS = 3E-3
R,1,THICK
MP,EX,1,217E9            ! MATERIAL, STEEL
MP,NUXY,1,0.3
MP,DENS,1,7850
N,1,-.014,,.150          ! DEFINE NODES
N,9,-.014,,.478
FILL
NGEN,2,9,1,9,1,.028
NGEN,2,18,1,18,1,,THICK
E,1,2,11,10,19,20,29,28  ! DEFINE ELEMENTS
EGEN,8,1,-1
FINISH
/SOLU    
ANTYPE,STATIC            ! STATIC ANALYSIS, PRESTRESS
RESCONTROL,LINEAR,ALL,1  ! NEEDED FOR PERTURBATION ANALYSIS
D,1,ALL,,,10,9           ! BOUNDARY CONDITIONS AND LOADING
D,19,ALL,,,28,9		 !
OMEGA,314.159265         ! SPINNING LOAD
OUTPR,,1
/OUT,SCRATCH
SOLVE
FINISH

/SOLU
ANTYPE,STATIC,RESTART,,,PERTURB     ! RESTART FROM LAST LOAD STEP AND SUB STEP
PERTURB,MODAL,,,PARKEEP             ! PERFORM PERTURBED MODAL SOLVE
SOLVE,ELFORM                        ! REGENERATE ELEMENT MATRICES

OUTPR,ALL,NONE
MODOPT,LANB,5
MXPAND,5
SOLVE
/OUT,
*GET,FREQ,MODE,1,FREQ
*DIM,LABEL,CHAR,1,2
*DIM,VALUE,,1,3
LABEL(1,1) = '     f, '
LABEL(1,2) = 'Hz      '
*VFILL,VALUE(1,1),DATA,52.75 
*VFILL,VALUE(1,2),DATA,FREQ 
*VFILL,VALUE(1,3),DATA,ABS(FREQ /52.75)
FINISH
SAVE,TABLE_2
/CLEAR, NOSTART

/TITLE, VM54, VIBRATION OF A ROTATING CANTILEVER BLADE
C***          USING SHELL181 ELEMENTS
/PREP7
ET,1,SHELL181, , ,2,,,,1 ! SUPPRESS STRESS PRINTOUT
SECT,1,SHELL
SECD,3E-3,1              ! THICKNESS OF SHELL
MP,EX,1,217E9            ! MATERIAL, STEEL
MP,NUXY,1,0.3
MP,DENS,1,7850
CSYS,4
WPRO, ,-90
RECTNG,-0.014,0.014,-0.150,-0.478
LSEL,S,LINE,,1,3,2
LESIZE,ALL, , ,1
LSEL,INVE
LESIZE,ALL, , ,9
LSEL,ALL
AMESH,1
FINISH
/OUT,SCRATCH
/SOLU    
ANTYPE,STATIC            ! STATIC ANALYSIS, PRESTRESS
RESCONTROL,LINEAR,ALL,1  ! NEEDED FOR PERTURBATION ANALYSIS
NSEL,S,LOC,Y,-0.150
D,ALL,ALL
NSEL,ALL
OMEGA,314.159265         ! SPINNING LOAD
OUTPR,NSOL,1
OUTPR,RSOL,1
SOLVE
FINISH
/SOLU
ANTYPE,STATIC,RESTART,,,PERTURB     ! RESTART FROM LAST LOAD STEP AND SUB STEP
PERTURB,MODAL,,,PARKEEP             ! PERFORM PERTURBED MODAL SOLVE
SOLVE,ELFORM                        ! REGENERATE ELEMENT MATRICES

OUTPR,ALL,NONE
MODOPT,LANB,5
MXPAND,5
SOLVE
/OUT,
*GET,FREQ,MODE,1,FREQ
*DIM,LABEL,CHAR,1,2
*DIM,VALUE,,1,3
LABEL(1,1) = '     f, '
LABEL(1,2) = 'Hz      '
*VFILL,VALUE(1,1),DATA,52.75 
*VFILL,VALUE(1,2),DATA,FREQ 
*VFILL,VALUE(1,3),DATA,ABS(FREQ /52.75)
FINISH
SAVE,TABLE_3

/CLEAR, NOSTART
/TITLE, VM54, VIBRATION OF A ROTATING CANTILEVER BLADE
C***          USING SHELL281 ELEMENTS
/PREP7
ET,1,SHELL281, , ,,,,,1 ! SUPPRESS STRESS PRINTOUT
SECT,1,SHELL
SECD,3E-3,1              ! THICKNESS OF SHELL
MP,EX,1,217E9            ! MATERIAL, STEEL
MP,NUXY,1,0.3
MP,DENS,1,7850
CSYS,4
WPRO, ,-90
RECTNG,-0.014,0.014,-0.150,-0.478
LSEL,S,LINE,,1,3,2
LESIZE,ALL, , ,1
LSEL,INVE
LESIZE,ALL, , ,9
LSEL,ALL
AMESH,1
FINISH
/SOLU    
ANTYPE,STATIC            ! STATIC ANALYSIS, PRESTRESS
RESCONTROL,LINEAR,ALL,1  ! NEEDED FOR PERTURBATION ANALYSIS
NSEL,S,LOC,Y,-0.150
D,ALL,ALL
NSEL,ALL
OMEGA,314.159265         ! SPINNING LOAD
OUTPR,NSOL,1
OUTPR,RSOL,1
/OUT,SCRATCH
SOLVE
FINISH
/SOLU
ANTYPE,STATIC,RESTART,,,PERTURB     ! RESTART FROM LAST LOAD STEP AND SUB STEP
PERTURB,MODAL,,,PARKEEP             ! PERFORM PERTURBED MODAL SOLVE
SOLVE,ELFORM                        ! REGENERATE ELEMENT MATRICES

OUTPR,ALL,NONE
MODOPT,LANB,5
MXPAND,5
SOLVE
/OUT,
*GET,FREQ,MODE,1,FREQ
*DIM,LABEL,CHAR,1,2
*DIM,VALUE,,1,3
LABEL(1,1) = '     f, '
LABEL(1,2) = 'Hz      '
*VFILL,VALUE(1,1),DATA,52.75 
*VFILL,VALUE(1,2),DATA,FREQ 
*VFILL,VALUE(1,3),DATA,ABS(FREQ /52.75)
FINISH
SAVE,TABLE_4
FINISH
/CLEAR,NOSTART

RESUME,TABLE_1
/COM
/OUT,vm54,vrt
/COM,------------------- VM54 RESULTS COMPARISON ---------------
/COM,
/COM,                 |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
/COM, SHELL63
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.2,'  ',F14.2,'   ',1F15.3)
/NOPR
RESUME,TABLE_2
/COM,
/COM, SOLSH190 USING LINEAR PERTURBATION ANALYSIS
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.2,'  ',F14.2,'   ',1F15.3)
/NOPR
RESUME,TABLE_3
/COM,
/COM, SHELL181 USING LINEAR PERTURBATION ANALYSIS
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.2,'  ',F14.2,'   ',1F15.3)
/NOPR
RESUME,TABLE_4
/COM,
/COM, SHELL281 USING LINEAR PERTURBATION ANALYSIS
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.2,'  ',F14.2,'   ',1F15.3)
/COM,-----------------------------------------------------------
/OUT
FINISH
*LIST,vm54,vrt
/DELETE,TABLE_1
/DELETE,TABLE_2
/DELETE,TABLE_3
/DELETE,TABLE_4

/COM,ANSYS MEDIA REL. 150 (11/8/2013) REF. VERIF. MANUAL: REL. 150
/VERIFY,VM105
JPGPRF,500,100,1             ! MACRO TO SET PREFS FOR JPEG PLOTS
/SHOW,JPEG
/PREP7
/TITLE, VM105, HEAT GENERATING COIL WITH TEMP. DEPENDENT CONDUCTIVITY
C***    CONDUCTION HEAT TRANSFER, SCHNEIDER, 2ND. PRINTING, PAGE 193, ART. 8-8
ANTYPE,STATIC
ET,1,PLANE55                 ! THERMAL SOLID
MP,KXX,1,10,.075
CSYS,1
N,1,(1/48),-5                ! RADIAL SECTOR ONE-ELEMENT WIDE
N,10,(1/12),-5
FILL
NGEN,2,10,1,10,1,,10
E,1,2,12,11
EGEN,9,1,1
OUTPR,BASIC,1
KBC,1                        ! STEP LOAD
D,1,TEMP,0,,11,10            ! INNER WALL TEMPERATURE
D,10,TEMP,0,,20,10           ! OUTER WALL TEMPERATURE
BFE,ALL,HGEN,,1E6            ! APPLY HEAT GENERATION RATES
FINISH
/SOLU    
SOLVE
FINISH
/POST1
CSYS,1
NSEL,S,LOC,Y,-5              ! SELECT NODES ALONG RADIUS AT THETA=-5
PRNSOL,TEMP                  ! PRINT TEMPERATURE DISTRIBUTION
NSEL,S,NODE,,ALL
PATH,TPATH,2,,48             ! DEFINE PATH WITH NAME = "TPATH"
PPATH,1,1                    ! DEFINE PATH POINTS BY NODE
PPATH,2,10
PDEF,TEMP,TEMP
PLPATH,TEMP
*DIM,VALUE,,8,3
*VFILL,VALUE(1,1),DATA,23.3,35.9,42.2,44,42.2,37,28.6,16.5 
*DO,I,2,9,1
*GET,TN,NODE,I,TEMP
*VFILL,VALUE(I-1,2),DATA,TN
*VFILL,VALUE(I-1,3),DATA,ABS(VALUE(I-1,2)/VALUE(I-1,1))
*ENDDO
*DIM,LABEL,CHAR,8,2
LABEL(1,1) = 'T,F (AT ','T,F (AT ','T,F (AT ','T,F (AT ','T,F (AT ','T,F (AT '
LABEL(7,1) = 'T,F (AT ','T,F (AT '
LABEL(1,2) = 'NODE 2) ','NODE 3) ','NODE 4) ','NODE 5) ','NODE 6) ','NODE 7) '
LABEL(7,2) = 'NODE 8) ','NODE 9) '
/COM
/OUT,vm105,vrt
/COM,------------------- VM105 RESULTS COMPARISON --------------
/COM,
/COM,                 |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.1,'  ',F10.1,'   ',1F5.3)
/COM,-----------------------------------------------------------
/OUT
FINISH
*LIST,vm105,vrt



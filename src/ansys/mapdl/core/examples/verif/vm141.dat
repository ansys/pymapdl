/COM,ANSYS MEDIA REL. 2023R1 (11/04/2022) REF. VERIF. MANUAL: REL. 2023R1
/VERIFY,VM141
/SHOW
/PREP7
smrt,off
/DEVICE,VECTOR,ON
/TITLE, VM141, DIAMETRAL COMPRESSION OF A DISK
/COM,               THEORY OF ELASTICITY, TIMOSHENKO AND GOODIER, 2ND ED., PG 107
/COM,               PLANE STRESS ELEMENTS (PLANE82 AND PLANE183)
C***  USING PLANE183, PLANE82 ELEMENTS
ANTYPE,STATIC             ! STATIC ANALYSIS
ET,1,PLANE183,,,3,,,,1      ! THICKNESS INPUT, SUPPRESS SOLUTION PRINTOUT
ET,2,PLANE82,,,3,,,,1
ET,3,PLANE82,,,3,,2       ! NODAL STRESS PRINTOUT SELECTED
R,1,.2                    ! THICKNESS
MP,EX,1,30E6
MP,NUXY,1,0.3
CSYS,1                    ! CYLINDRICAL COORDINATES
K,1,1,90
K,2,.5,90
K,4,1
K,5,1,50
L,1,5
LESIZE,1,,,7,5
L,5,4
LESIZE,2,,,4,2
CSYS,0                    ! CARTESIAN COORDINATES
K,3
L,3,4
LESIZE,3,,,5
L,2,3
LESIZE,4,,,4,2
L,2,5
LESIZE,5,,,5
L,1,2
LESIZE,6,,,7,5
A,1,2,5,5
A,2,3,4,5
TYPE,2
MSHK,1                    ! MAPPED AREA MESH
MSHA,0,2D                 ! USING QUADS
AMESH,2                   ! QUADRILATERAL MESHING 
EPLOT
TYPE,1
MSHK,0                    ! FREE AREA MESH
MSHA,1,2D                 ! USING TRIS  
AMESH,1                   ! TRIANGLE MESHING 
EPLOT
NSEL,S,LOC,X,0
NSEL,R,LOC,Y,0
ESLN
TYPE,3
EMODIF,ALL                ! MODIFY ALL SELECTED ELEMENTS
SAVE,VM141,DB
NSEL,S,LOC,Y,0
DSYM,SYMM,Y               ! SYMMETRY ALONG X AXIS
NSEL,S,LOC,X,0
DSYM,SYMM,X               ! SYMMETRY ALONG Y AXIS
NSEL,ALL
ESEL,ALL
OUTPR,NSOL,NONE           ! NODAL DISPL. & REACTION FORCES PRINTOUT CONTROL
OUTPR,ESOL,ALL            ! ELEMENTAL  PRINTOUT CONTROL
FK,1,FY,-1000             ! APPLY HALF OF FORCE (SYMMETRY)
FINISH
*CREATE,SOLVIT,MAC
/SOLU
/OUT,SCRATCH
SOLVE
FINISH
/POST1
/OUT,
NSEL,S,LOC,X,0            ! SELECT ONLY THE NODES OF INTEREST
NSEL,A,LOC,X,0.1
NSEL,R,LOC,Y,0
PRNSOL,S,COMP             ! PRINT COMPONENT NODAL STRESSES
NSEL,R,LOC,X,0
NSEL,A,LOC,X,0.2
NSEL,R,LOC,Y,0
*GET,SNOD,NODE,,NUM,MIN   ! GET STARTING NODE FOR PATH (X=0)
*GET,FNOD,NODE,,NUM,MAX   ! GET END NODE FOR PATH (X=0.2)
NSEL,ALL
ESEL,ALL
PATH,STRESS1,2,,48         ! DEFINE PATH WITH NAME = "STRESS1"
PPATH,1,SNOD              ! DEFINE PATH POINTS BY NODE
PPATH,2,FNOD
PDEF,SY,S,Y               ! INTERPOLATE SY STRESS ON PATH
PRANGE,24                 ! PRINT EVERY 24TH POINT
PRPATH,SY                 ! PRINT SY STRESS ALONG THE PATH
*GET,S1,PATH,0,MIN,SY
NSEL,R,LOC,X,0
NSEL,A,LOC,X,0.1
NSEL,R,LOC,Y,0
*GET,FNOD,NODE,,NUM,MAX
PATH,STRESS2,2,,48        ! DEFINE PATH WITH NAME = "STRESS2"
PPATH,1,SNOD
PPATH,2,FNOD
PDEF,SY,S,Y               ! INTERPOLATE SY STRESS ON PATH
PRANGE,24                 ! PRINT EVERY 24TH POINT
PRPATH,SY                 ! PRINT SY STRESS ALONG THE PATH
*GET,S2,PATH,0,MAX,SY
*DIM,LABEL,CHAR,2,2
*DIM,VALUE,,2,3
LABEL(1,1) = 'P (psi)','P (psi)'
LABEL(1,2) = ' X=0 ',' X=.1'
*VFILL,VALUE(1,1),DATA,-9549,-9298
*VFILL,VALUE(1,2),DATA,S1,S2
*VFILL,VALUE(1,3),DATA,ABS(S1/9549),ABS(S2/9298)
FINISH
*END
SOLVIT
SAVE,TABLE_1
/CLEAR, NOSTART 
C***  USING SHELL281 ELEMENTS
/FILNAM,GEN
/PREP7
smrt,off
!              S.E. GENERATION PASS  (SHELL ELEMENTS ,SHELL281)
RESUME,VM141,DB
ANTYPE,SUBST              ! SUBSTRUCTURE GENERATION PASS
SEOPT,GEN
! NOTE: SINCE PLANE183 AND SHELL281 HAVE DIFFERENT NODE ORDER, DELETE OLD MESH
ACLEAR,1,2                ! DELETE NODES AND ELEMENTS
ETDELE,1,3                ! DELETE PREVIOUS ELEMENT TYPES
ET,1,SHELL281		  ! SHELL 281 ELEMENT 
ET,2,SHELL281
ET,3,SHELL281	
SECTYPE,1,SHELL
SECDATA,0.2,1,0,5        
/OUTPUT,SCRATCH
NUMCMP,NODE               ! COMPRESS NODE NUMBER TO ZERO
/OUTPUT
TYPE,2
MSHK,1                    ! MAPPED AREA MESH
MSHA,0,2D                 ! USING QUADS 
AMESH,2                   ! MESH AREA 2 WITH QUADRILATERALS
EPLOT
TYPE,1
MSHK,0                    ! FREE AREA MESH
MSHA,1,2D                 ! USING TRIS  
AMESH,1                   ! MESH AREA 1 WITH TRIANGLES
EPLOT
NSEL,S,LOC,X,0
NSEL,R,LOC,Y,0
ESLN
TYPE,3
EMODIF,ALL
NSEL,S,LOC,Y,0
DSYM,SYMM,Y               ! SYMMETRY ALONG X AXIS
NSEL,S,LOC,X,0
DSYM,SYMM,X               ! SYMMETRY ALONG Y AXIS
NSEL,ALL
ESEL,ALL
D,ALL,UZ,,,,,ROTX,ROTY    ! CONSTRAIN UNNEEDED DOF'S
NSEL,S,LOC,X
NSEL,R,LOC,Y,1
*GET,NDOF,NODE,,NUM,MAX   ! GET NODE NUMBER FOR MASTER DOF 
M,NDOF,UY                 ! SELECT MASTER DOF AT LOAD APPLICATION POINT
NSEL,ALL
FINISH
/SOLU
EQSLV,SPARSE
/OUT,SCRATCH
SOLVE
/OUT,
SAVE                      ! SAVE SUBSTRUCTURE DATA BASE FOR EXPANSION PASS
PARSAV,SCALAR,GEN,PARM
FINISH
/CLEAR, NOSTART 
/FILNAM,USE
PARRES,,GEN,PARM
/PREP7
smrt,off
/TITLE, VM141, DIAMETRAL COMPRESSION OF A DISK  (S.E. USE PASS)
ET,1,MATRIX50
SE,GEN
F,NDOF,FY,-1000
FINISH
/SOLU
/OUT,SCRATCH
SOLVE
/OUT,
FINISH
/CLEAR, NOSTART 
/FILNAM,GEN
RESUME
/SOLU
EXPASS,ON,YES             ! EXPANSION PASS WITH ELEMENT SOLUTION
SEEXP,GEN,USE
/TITLE, VM141, DIAMETRAL COMPRESSION OF A DISK  (S.E. EXPANSION PASS)
OUTPR,NSOL,NONE           ! DISPLACEMENT PRINTOUT CONTROL
OUTPR,ESOL,ALL
EXPSOL,1,1
/OUT,SCRATCH
SOLVE
/OUT,
FINISH
/POST1
NSEL,R,LOC,X,0
NSEL,A,LOC,X,0.2
NSEL,R,LOC,Y,0
*GET,SNOD,NODE,,NUM,MIN   ! GET STARTING NODE FOR PATH (X=0)
*GET,FNOD,NODE,,NUM,MAX   ! GET END NODE FOR PATH (X=0.2)
NSEL,ALL
ESEL,ALL
PATH,STRESS3,2,,48         ! DEFINE PATH WITH NAME = "STRESS3"
PPATH,1,SNOD
PPATH,2,FNOD
PDEF,SY,S,Y               ! INTERPOLATE SY STRESS ON PATH
PRANGE,24                 ! PRINT EVERY 24TH POINT
PRPATH,SY                 ! PRINT SY STRESS ALONG THE PATH
*GET,S1,PATH,0,MIN,SY
NSEL,R,LOC,X,0
NSEL,A,LOC,X,0.1
NSEL,R,LOC,Y,0
*GET,FNOD,NODE,,NUM,MAX
NSEL,ALL
ESEL,ALL
PATH,STRESS4,2,,48         ! DEFINE PATH WITH NAME = "STRESS4"
PPATH,1,SNOD
PPATH,2,FNOD
PDEF,SY,S,Y               ! INTERPOLATE SY STRESS ON PATH
PRANGE,24                 ! PRINT EVERY 24TH POINT
PRPATH,SY                 ! PRINT SY STRESS ALONG THE PATH
*GET,S2,PATH,0,LAST,SY
*DIM,LABEL,CHAR,2,2
*DIM,VALUE,,2,3
LABEL(1,1) = 'P (psi)','P (psi)'
LABEL(1,2) = ' X=0 ',' X=.1'
*VFILL,VALUE(1,1),DATA,-9549,-9298
*VFILL,VALUE(1,2),DATA,S1,S2
*VFILL,VALUE(1,3),DATA,ABS(S1/9549),ABS(S2/9298)
SAVE,TABLE_2
FINISH
/CLEAR, NOSTART 
/TITLE, VM141, DIAMETRAL COMPRESSION OF A DISK
C***  USING SHELL181 ELEMENTS
/PREP7
smrt,off
ET,1,SHELL181
SECTYPE,1,SHELL
SECDATA,0.2,1,0,5         ! THICKNESS
MP,EX,1,30E6              ! DEFINE MATERIAL PROPERTIES
MP,NUXY,1,0.3
PCIRC,,1,0,90             ! CREATE MODEL GEOMETRY
ESIZE,,10
AMESH,ALL                 ! MESH ALL AREAS
EPLOT
FINISH
/SOLU
NSEL,S,LOC,Y,0            ! APPLY BOUNDARY CONDITIONS
DSYM,SYMM,Y               ! SYMMETRY ALONG X AXIS
NSEL,S,LOC,X,0
DSYM,SYMM,X               ! SYMMETRY ALONG Y AXIS
NSEL,ALL
ESEL,ALL
F,NODE(0,1,0),FY,-1000    ! APPLY HALF OF FORCE (SYMMETRY)
D,ALL,UZ,,,,,ROTX,ROTY    ! CONSTRAIN UNNEEDED DOF'S
/OUT,SCRATCH
SOLVE
/OUT,
FINISH
/POST1
*GET,S1,NODE,12,S,Y
*GET,S2,NODE,22,S,Y
*DIM,LABEL,CHAR,2,2
*DIM,VALUE,,2,3
LABEL(1,1) = 'P (psi)','P (psi)'
LABEL(1,2) = ' X=0 ',' X=.1'
*VFILL,VALUE(1,1),DATA,-9549,-9298
*VFILL,VALUE(1,2),DATA,S1,S2
*VFILL,VALUE(1,3),DATA,ABS(S1/9549),ABS(S2/9298)
FINISH
SAVE,TABLE_3
/CLEAR,NOSTART
/TITLE, VM141, DIAMETRAL COMPRESSION OF A DISK
C***  USING SHELL281 ELEMENTS
/PREP7
smrt,off
/DEVICE,VECTOR,ON
ANTYPE,STATIC             ! STATIC ANALYSIS
ET,1,SHELL281
ET,2,SHELL281
ET,3,SHELL281
SECTYPE,1,SHELL
SECDATA,0.2,1,0,5
MP,EX,1,30E6
MP,NUXY,1,0.3
CSYS,1                    ! CYLINDRICAL COORDINATES
K,1,1,90
K,2,.5,90
K,4,1
K,5,1,50
L,1,5
LESIZE,1,,,7,5
L,5,4
LESIZE,2,,,4,2
CSYS,0                    ! CARTESIAN COORDINATES
K,3
L,3,4
LESIZE,3,,,5
L,2,3
LESIZE,4,,,4,2
L,2,5
LESIZE,5,,,5
L,1,2
LESIZE,6,,,7,5
A,1,2,5,5
A,2,3,4,5
TYPE,2
MSHK,1                    ! MAPPED AREA MESH
MSHA,0,2D                 ! USING QUADS
AMESH,2                   ! QUADRILATERAL MESHING (PLANE82)
EPLOT
TYPE,1
MSHK,0                    ! FREE AREA MESH
MSHA,1,2D                 ! USING TRIS  
AMESH,1                   ! TRIANGLE MESHING (PLANE183)
EPLOT
NSEL,S,LOC,X,0
NSEL,R,LOC,Y,0
ESLN
TYPE,3
EMODIF,ALL                ! MODIFY ALL SELECTED ELEMENTS
NSEL,S,LOC,Y,0
DSYM,SYMM,Y               ! SYMMETRY ALONG X AXIS
NSEL,S,LOC,X,0
DSYM,SYMM,X               ! SYMMETRY ALONG Y AXIS
NSEL,ALL
ESEL,ALL
OUTPR,NSOL,NONE           ! NODAL DISPL. & REACTION FORCES PRINTOUT CONTROL
FK,1,FY,-1000             ! APPLY HALF OF FORCE (SYMMETRY)
D,ALL,UZ,,,,,ROTX,ROTY    ! CONSTRAIN UNNEEDED DOF'S
FINISH
SOLVIT
SAVE,TABLE_4
/NOPR
RESUME,TABLE_1
/COM
/OUT,vm141,vrt
/COM,------------------- VM141 RESULTS COMPARISON ------------
/COM,
/COM,                      TARGET   |    Mechanical APDL   |    RATIO
/COM,
/COM, PLANE82 AND PLANE183
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F11.0,'  ',F13.0,'   ',1F16.3)
/NOPR
RESUME,TABLE_2
/GOPR
/COM,
/COM, SHELL281-SUBSTRUCTURE
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F11.0,'  ',F13.0,'   ',1F16.3)
/NOPR
RESUME,TABLE_3
/GOPR
/COM,
/COM, SHELL181
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F11.0,'  ',F13.0,'   ',1F16.3)
/NOPR
RESUME,TABLE_4
/GOPR
/COM,
/COM, SHELL281
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F11.0,'  ',F13.0,'   ',1F16.3)
/COM,---------------------------------------------------------
/OUT
FINISH
*LIST,vm141,vrt
/DELETE,TABLE_1
/DELETE,TABLE_2
/DELETE,TABLE_3
/DELETE,TABLE_4
/DELETE,GEN,PARM
/DELETE,GEN,db
/DELETE,GEN,emat
/DELETE,GEN,esav
/DELETE,GEN,rst
/DELETE,GEN,seld
/DELETE,GEN,sub
/DELETE,USE,dsub
/DELETE,USE,emat
/DELETE,USE,esav
/DELETE,USE,rst
/DELETE,USE,sord
/DELETE,VM141,DB
/DELETE,SOLVIT,MAC

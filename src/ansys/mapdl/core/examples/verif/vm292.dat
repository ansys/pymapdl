/COM,ANSYS MEDIA REL. 2023R1 (11/04/2022) REF. VERIF. MANUAL: REL. 2023R1
/VERIFY,VM292
/TITLE,VM292,INTERFERENCE FIT BETWEEN TWO CYLINDERS

/COM, REFERENCE: RICHARD G.BUDYNAS, J.KEITH NISBETT, SHIGLEY'S
/COM,            MECHANICAL ENGINEERING DESIGN, NINTH EDITION, 
/COM,            PG:116,2011
JPGPRF,500,100,1

/OUT,SCRATCH
/PREP7

IR1 = 1.250             ! INNER RADIUS OF INNER CYLINDER IN M
OR1 = 1.516             ! OUTER RADIUS OF INNER CYLINDER IN M
OR2 = 1.750             ! OUTER RADIUS OF OUTER CYLINDER IN M
WIDTH = 1.50            ! WIDTH OF THE CYLINDER IN M
GAP = 0.02              ! INTERFERENCE IN M

E = 206E9               ! YOUNG'S MODULUS IN N/M^2
NU = 0.0                ! POISSON'S RATIO

MP,EX,1,E
MP,NUXY,1,NU

ET,1,SOLID185           ! SOLID185 ELEMENTS


CYLINDER,IR1,OR1,0,WIDTH,0,90
CYLINDER,OR1,OR2,0,WIDTH,0,90

ESIZE,0.1

TYPE,1
MAT,1
VMESH,ALL               ! MESHING CYLINDERS BY USING LINE DIVISIONS
ALLSEL,ALL

SECTYPE,2,CONTACT,CYLINDER
SECDATA,0,0,0,0,0,2.00  ! GEOMETRY CORRECTION FOR CYLINDER

ET,2,CONTA174           ! CONTA174 ELEMENTS
ET,3,TARGE170           ! TARGE170 ELEMENTS

REAL,2
KEYOPT,2,2,0            ! AUGUMENT LAGRANGE ALGORITHM
KEYOPT,2,12,0           ! STANDARD CONTACT
KEYOPT,2,10,0           ! ITERATION BASED CONTACT STIFFNESS UPDATE
KEYOPT,2,4,3            ! PROJECTION BASED METHOD
KEYOPT,2,9,2            ! INCLUSION OF GEOMETRICAL PENETRATION WITH RAMPED EFFECTS
R,2
RMODIF,2,10,0.02        ! CONTACT SURFACE OFFSET

TYPE,2
REAL,2
SECNUM,2
ASEL,S,AREA,,10
NSLA,S,1
ESLN,S
ESURF                   ! FORMING CONTACT ELEMENTS
ALLSEL,ALL

TYPE,3
REAL,2
ASEL,S,AREA,,3
NSLA,S,1
ESLN,S
ESURF                   ! FORMING TARGET ELEMENTS
ALLSEL,ALL

CSYS,1
NSEL,S,LOC,X,1.2500,1.7500
NSEL,R,LOC,Y,0
D,ALL,UY,0              ! CONSTRAINING UY DOF
ALLSEL,ALL

NSEL,S,LOC,Y,90,90
D,ALL,UX,0              ! CONSTRAINING UX DOF
ALLSEL,ALL

NSEL,S,LOC,Z,0
NSEL,A,LOC,Z,WIDTH
D,ALL,UZ,0              ! CONSTRAINING UZ AT FRONT AND BACK FACES
ALLSEL,ALL
FINISH

/SOLUTION
ANTYPE,STATIC           ! PERFORM STATIC ANALYSIS
OUTRES,ALL,ALL
TIME,1.0
KBC,0                              
NSUBS,1,10,1
SOLVE
FINISH


/POST1
SET,LAST
/OUT,
/COM, 
/COM,  ANALYTICAL SOLUTION

IR12 = IR1*IR1
OR12 = OR1*OR1
OR22 = OR2*OR2
OR13 = OR1*OR1*OR1

TERM1 = ((OR22-OR12)*(OR12-IR12))/(OR22-IR12)
TERM2 = (E*GAP)/(2*OR13)
PRES  = TERM2*TERM1

/OUT,SCRATCH
/SHOW,JPEG
/VIEW,1,1,1,1
/GRAPHICS,POWER
ESEL,S,ENAME,,174
PLESOL,CONT,PRES
ESEL,ALL
VSEL,S,VOLUME,,2,,,1
NSEL,R,LOC,X,OR1
NSEL,R,LOC,Y,40,60
ESLN,S
ESEL,R,ENAME,,174
PLESOL,CONT,PRES
*GET,PRESMAX,PLNSOL,0,MAX
*GET,PRESMIN,PLNSOL,0,MIN
ALLSEL,ALL
/SHOW,CLOSE
/OUT,
*STAT,PRESMAX
*STAT,PRESMIN

/OUT,SCRATCH
*DIM,LABEL,CHAR,1,2
*DIM,VALUE,,1,3

LABEL(1,1) = 'Pressure'

*VFILL,VALUE(1,1),DATA,PRES             ! FROM ANALYTICAL SOLUTION
*VFILL,VALUE(1,2),DATA,PRESMAX          ! FROM MAPDL
*VFILL,VALUE(1,3),DATA,ABS(PRES/PRESMAX)! RATIO
/COM,
/COM,
/OUT,vm292,vrt
/COM,-----------------VM292 RESULTS COMPARISON------------------------
/COM,
/COM,                      |  TARGET   |  Mechanical APDL | RATIO
/COM,
/COM,
*VWRITE,LABEL(1,1),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A17,'   ',F15.3,'    ',F15.3,'    ',F5.3)
/COM,
/COM,
/COM,------------------------------------------------------------------
/OUT,
*LIST,vm292,vrt
FINISH
/EXIT,NOSAVE








/COM,ANSYS MEDIA REL. 2023R1 (11/04/2022) REF. VERIF. MANUAL: REL. 2023R1
/VERIFY,VM257
JPGPRF,500,100,1                     ! MACRO TO SET PREFS FOR JPEG PLOTS
/SHOW,JPEG
/TITLE,VM257,SWING COMPRISING TWO RIGID LINKS AND A BEAM WITH MIDSPAN MASS
/COM    O.A. BAUCHAU. G. DAMILANO AND N.J. THERON
/COM    NUMERICAL INTEGRATION OF NON-LINEAR ELASTIC MULTI-BODY SYSTEMS
/COM    INTERNATIONAL JOURNAL FOR NUMERICAL METHODS IN  ENGINEERING,
/COM    VOL. 38, 2727-2751 (1995)
C***  PERFORM ANALYSIS USING MPC184 RIGID LINKS
/PREP7
MP,EX,1,73E9               ! 73 GN/M^2
MP,NUXY,1,0.3
MP,DENS,1,2700             ! KG/M^3 
K,1,0,0.36,0
K,2,0,0,0
K,3,0.72,0,0
K,4,0.72+0.36,0.36
L,1,2
L,2,3
L,3,4
ET,1,BEAM188               ! 3D LINEAR FINITE STRAIN BEAM  
ET,2,MPC184                ! RIGID LINK
ET,3,MASS21                ! 3DMASS ELEMENT
KEYOPT,3,3,2               ! 3D MASS WITHOUT ROTARY INERTIA
R,3,0.5                    ! 0.5 KG
SECTYPE, 1, BEAM, RECT
SECDATA,1E-3,5E-3          ! 1 MM X 5 MM CROSS SECTION
TYPE,1
MAT,1
SECNUM,1
LESIZE,2, , ,4
LMESH,2                    ! DEFORMABLE BEAM
TYPE,2
MAT,2
SECNUM,2
LESIZE,1, , ,1             ! RIGID LINK
LMESH,1
TYPE,2
MAT,2
SECNUM,2
LESIZE,3, , ,1
LMESH,3                    ! RIGID LINK
NDMID = NODE(0.72/2,0,0)   ! NODE AT BEAM MIDSPAN 
NDB   = NODE(0.72/4,0,0)   ! NODE AT LOCATION B 
TYPE,3
REAL,3
E,NDMID                    ! MASS AT BEAM MIDSPAN
NSEL,S,LOC,Y,0.36
D,ALL,ALL
DDELE,ALL,ROTZ
ALLSEL,ALL
NDA = NODE(0,0,0)          ! NODE AT LOCATION A
NDE = NODE(0.72,0,0)       ! NODE AT LOCATION E
D, NDA, UZ, 0.0, ,, ,ROTX,ROTY
D, NDE, UZ, 0.0, ,, ,ROTX,ROTY
ALLSEL
FINISH
SAVE
C*** PERFORM SOLUTION USING HHT ALGORITHM WITH RESPONSE FREQ
/SOLU
*DIM,FXMID,TABLE,4,1,1
FXMID(1,0,1) = 0.0         !  TIME VALUES
FXMID(2,0,1) = 0.128
FXMID(3,0,1) = 0.256
FXMID(4,0,1) = 1.0  
FXMID(1,1,1) = 0.0         !  FX IMPULSE LOAD VALUES
FXMID(2,1,1) = 2.0
FXMID(3,1,1) = 0.0
FXMID(4,1,1) = 0.0
F,NDMID,FX,%FXMID%         ! APPLY TABULAR LOADS
ANTYPE,TRANS
TRNOPT,FULL,,,,,HHT
TINTP,0.3                  ! 30% DAMPING
EQSLV,PCG
NLGEOM,ON                  ! LARGE DEFLECTION
AUTOTS,ON                  ! AUTO TIME STEPPING
OUTRES,ALL,-100            ! SAVE ALL RESULTS
KBC,0                      ! RAMPED LOADING
TOT_TIME = 1.0
TIME,TOT_TIME
NSUBST,1000,10E5,1000
CNVTOL,F                   ! FORCE CONVERGENCE CHECK
CNVTOL,M, , , ,1E-3        ! MOMENT CONVERGENCE CHECK
CNVTOL,U                   ! DISPLACEMENT CONVERGENCE CHECK
CNVTOL,ROT                 ! ROTATION CONVERGENCE CHECK
/OUT,vm257_SCRATCH
SOLVE
/OUT
FINISH
/AUTO,1
/DIST,  1, 0
/REPLO  
/VIEW,1,,,1
/ANG,1
/POST1
/PLOPTS,INFO,0  
/PLOPTS,LEG1,0  
/PLOPTS,LEG2,0  
/PLOPTS,LEG3,0  
/PLOPTS,FRAME,0
/PLOPTS,MINM,0
/PLOPTS,TITLE,0  
/PLOPTS,FILE,0  
/PLOPTS,LOGO,0  
/PLOPTS,WINS,1  
/PLOPTS,WP,0
/PLOPTS,DATE,0  
/TRIAD,OFF  
*DO,JJ,0,10
SET, , , , ,JJ/10
*IF,JJ,EQ,10,THEN
/PLOPTS,TITLE,1 
/TITLE,SWING:MOTION AND DEFORMATION OF BEAM AND MOTION OF LINKS,AT 0.1 S
*ENDIF
PLNSOL,U,SUM
/NOERASE
*ENDDO
/ERASE
/POST26
NSOL,2,NDB,U,X
NSOL,3,NDB,U,Y
/TITLE,SWING:TIME HISTORY OF DISP COMPS OF POINT B IN THE I1&I2 DIRECTIONS
PLVAR,2,3                      ! COMPARE THIS PLOT WITH FIGURE 15. IN REFERENCE
!PRVAR,2,3
ESOL,4,2,3,F,X                 ! AXIAL FORCE AT POINT B
/TITLE,SWING:TIME HISTORY OF AXIAL FORCE IN THE BEAM, AT POINT B
PLVAR,4                        ! COMPARE THIS PLOT WITH FIGURE 16. IN REFERENCE 
*GET,FMAX,VARI,4,EXTREM,VMAX   ! MAX AXIAL FORCE AT POINT B CORRESPONDING TO EVENT-X IN REF.
*GET,TMAX,VARI,4,EXTREM,TMAX   ! TIME CORRESPONDING TO EVENT-X IN REFERENCE
FINISH
/POST1
SET, , , , ,TMAX               ! RETRIEVE RESULTS AT TMAX
*GET,UY_NDB,NODE,NDB,U,X       ! UX DISP AT POINT B CORRESPONDING TO EVENT-X IN REFERENCE
*GET,UX_NDB,NODE,NDB,U,Y       ! UY DISP AT POINT B CORRESPONDING TO EVENT-X IN REFERENCE
*DIM,LABEL,CHAR,4,2
*DIM,VALUE,,4,3
LABEL(1,1) = 'TIME','DISP','DISP','FORCE'
LABEL(1,2) = '   (sec)','UY (m)','UX (m)','FX (N)'
*VFILL,VALUE(1,1),DATA,0.641,0.28,0.075,112.7
*VFILL,VALUE(1,2),DATA,TMAX,UY_NDB,UX_NDB,FMAX
*VFILL,VALUE(1,3),DATA,ABS(TMAX/0.641),ABS(UY_NDB/0.28),ABS(UX_NDB/0.075),ABS(FMAX/112.7)
FINISH
SAVE,TABLE_1
/CLEAR,NOSTART
C***  PERFORM ANALYSIS USING TARGET RIGID LINKS
/PREP7
MP,EX,1,73E9               ! 73 GN/M^2
MP,NUXY,1,0.3
MP,DENS,1,2700             ! KG/M^3 
K,1,0,0.36,0
K,2,0,0,0
K,3,0.72,0,0
K,4,0.72+0.36,0.36
L,1,2
L,2,3
L,3,4
ET,1,BEAM188               ! 3D LINEAR FINITE STRAIN BEAM  
ET,11,TARGE170             ! TARGET ELEMENT
KEYOPT,11,2,1              ! TARGET MODELED AS A RIGID BODY
KEYOPT,11,4,111            ! DO NOT CONSTRAIN ROTATIONS
ET,3,MASS21                ! 3DMASS ELEMENT
KEYOPT,3,3,2               ! 3D MASS WITHOUT ROTARY INERTIA
R,3,0.5                    ! 0.5 KG
SECTYPE, 1, BEAM, RECT
SECDATA,1E-3,5E-3          ! 1MM X 5 MM CROSS SECTION
TYPE,1
MAT, 1
SECNUM,1
LESIZE,2, , ,4
LMESH,2                    ! DEFORMABLE BEAM
TYPE,11
REAL,11
LESIZE,1, , ,1
LMESH,1
TSHAP,PILO
TYPE,11
E,6
TYPE,11
REAL,12
LESIZE,3, , ,1
LMESH,3
TSHAP,PILO
TYPE,11
REAL,12
E,7
NDMID = NODE(0.72/2,0,0)   ! NODE AT BEAM MIDSPAN 
NDB   = NODE(0.72/4,0,0)   ! NODE AT LOCATION B 
TYPE,3
REAL,3
E,NDMID                    ! MASS AT BEAM MIDSPAN
NSEL,S,LOC,Y,0.36
D,ALL,ALL
DDELE,ALL,ROTZ
ALLSEL,ALL
NDA = NODE(0,0,0)          ! NODE AT LOCATION A
NDE = NODE(0.72,0,0)       ! NODE AT LOCATION E
D, NDA, UZ, 0.0, ,, ,ROTX,ROTY
D, NDE, UZ, 0.0, ,, ,ROTX,ROTY
FINISH
SAVE
C*** PERFORM SOLUTION USING HHT ALGORITHM WITH RESPONSE FREQ
/SOLU
*DIM,FXMID,TABLE,4,1,1
FXMID(1,0,1) = 0.0         !  TIME VALUES
FXMID(2,0,1) = 0.128
FXMID(3,0,1) = 0.256
FXMID(4,0,1) = 1.0  
FXMID(1,1,1) = 0.0         !  FX IMPULSE LOAD VALUES
FXMID(2,1,1) = 2.0
FXMID(3,1,1) = 0.0
FXMID(4,1,1) = 0.0
F,NDMID,FX,%FXMID%         ! APPLY TABULAR LOADS
ANTYPE,TRANS
TRNOPT,FULL,,,,,HHT
TINTP,0.3                  ! 30% DAMPING
NLGEOM,ON                  ! LARGE DEFLECTION
EQSLV,PCG
AUTOTS,ON                  ! AUTO TIME STEPPING
OUTRES,ALL,-100            ! SAVE ONLY 100 RESULTS
KBC,0                      ! RAMPED LOADING                      
TOT_TIME = 1.0
TIME,TOT_TIME
NSUBST,1000,1000,1000      
CNVTOL,U                   ! DISPLACEMENT CONVERGENCE CHECK
CNVTOL,ROT                 ! ROTATION CONVERGENCE CHECK
CNVTOL,F                   ! FORCE CONVERGENCE CHECK
CNVTOL,M, , , ,0.01        ! MOMENT CONVERGENCE CHECK
PRED,OFF
LNSRCH,OFF
/OUT,vm257_SCRATCH,,,APPEND
SOLVE
FINISH
/DIST,1,0
/AUTO,1
/REPLO
/VIEW,1,,,1
/ANG,1
/POST1
/PLOPTS,INFO,0  
/PLOPTS,LEG1,0  
/PLOPTS,LEG2,0  
/PLOPTS,LEG3,0  
/PLOPTS,FRAME,0
/PLOPTS,MINM,0
/PLOPTS,TITLE,0  
/PLOPTS,FILE,0  
/PLOPTS,LOGO,0  
/PLOPTS,WINS,1  
/PLOPTS,WP,0
/PLOPTS,DATE,0  
/TRIAD,OFF  
*DO,JJ,0,10
SET, , , , ,JJ/10
*IF,JJ,EQ,10,THEN
/PLOPTS,TITLE,1 
/TITLE,SWING:MOTION AND DEFORMATION OF BEAM AND MOTION OF LINKS,AT 0.1 S
*ENDIF
PLNSOL,U,SUM
/NOERASE
*ENDDO
/ERASE
/POST26
NSOL,2,NDB,U,X
NSOL,3,NDB,U,Y
/TITLE,SWING:TIME HISTORY OF DISP COMPS OF POINT B IN THE I1&I2 DIRECTIONS
PLVAR,2,3                      ! COMPARE THIS PLOT WITH FIGURE 15. IN REFERENCE
!PRVAR,2,3
ESOL,4,2,3,F,X                 ! AXIAL FORCE AT POINT B
/TITLE,SWING:TIME HISTORY OF AXIAL FORCE IN THE BEAM, AT POINT B
PLVAR,4                        ! COMPARE THIS PLOT WITH FIGURE 16. IN REFERENCE 
*GET,FMAX,VARI,4,EXTREM,VMAX   ! MAX AXIAL FORCE AT POINT B CORRESPONDING TO EVENT-X IN REF.
*GET,TMAX,VARI,4,EXTREM,TMAX   ! TIME CORRESPONDING TO EVENT-X IN REFERENCE
FINISH
/POST1
SET, , , , ,TMAX               ! RETRIEVE RESULTS AT TMAX
*GET,UY_NDB,NODE,NDB,U,X       ! UX DISP AT POINT B CORRESPONDING TO EVENT-X IN REFERENCE
*GET,UX_NDB,NODE,NDB,U,Y       ! UY DISP AT POINT B CORRESPONDING TO EVENT-X IN REFERENCE
*DIM,LABEL,CHAR,4,2
*DIM,VALUE,,4,3
LABEL(1,1) = 'TIME','DISP','DISP','FORCE'
LABEL(1,2) = '   (sec)','UY (m)','UX (m)','FX (N)'
*VFILL,VALUE(1,1),DATA,0.641,0.28,0.075,112.7
*VFILL,VALUE(1,2),DATA,TMAX,UY_NDB,UX_NDB,FMAX
*VFILL,VALUE(1,3),DATA,ABS(TMAX/0.641),ABS(UY_NDB/0.28),ABS(UX_NDB/0.075),ABS(FMAX/112.7)
FINISH
SAVE,TABLE_2
RESUME,TABLE_1
/COM
/OUT,vm257,vrt
/COM,------------------- VM257 RESULTS COMPARISON --------------
/COM,
/COM,                 |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
/COM,RESULTS USING MPC184 RIGID LINKS
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.4,'  ',F14.4,'   ',1F15.3)
/NOPR
RESUME,TABLE_2
/GOPR
/COM,
/COM,RESULTS USING TARGET RIGID LINKS
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.4,'  ',F14.4,'   ',1F15.3)
/COM,-----------------------------------------------------------
/COM,
/OUT
FINISH
*LIST,vm257,vrt
/OUT,vm257_SCRATCH,,,APPEND
/DELETE,TABLE_1
/DELETE,TABLE_2

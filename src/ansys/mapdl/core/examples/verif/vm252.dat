/COM,ANSYS MEDIA REL. 150 (11/8/2013) REF. VERIF. MANUAL: REL. 150
/VERIFY,VM252
JPGPRF,500,100,1
/SHOW,JPEG
/TITLE,VM252,GURSON BAR-NECKING BENCHMARK WITH APPLIED DISPLACEMENT
/COM, REFERENCE:
/COM, N. ARAVAS, "ON THE NUMERICAL INTEGRATION OF A CLASS OF PRESSURE
/COM, DEPENDENT PLASTICITY MODELS." INT. J. FOR NUMERICAL METHODS IN
/COM, ENGINEERING.  VOLUME. 24, PP. 1395-1416 (1987)
/COM, SECTION 5.3, FIGURE 10.
! DEFINED CONSTANTS
PI=3.141592654
MYSUBST=100                  ! NUMBER OF SUBSTEPS FOR SOLUTION
UAPP=0.7602                  ! APPLIED DISPLACEMENT
R0=1                         ! WIDTH OF ROD
DR0=0.005*R0                 ! NODE OFFSET TO CREATE NOTCH
L0=4.0*R0                    ! LENGTH OF MODEL
YOUNG=1000000                ! YOUNG'S MODULUS
NU=0.3                       ! POISSON RATIO
! GURSON COEFFICIENTS
Q1=1.5                       ! FIRST TVERGAARD CONSTANT
Q2=1                         ! SECOND TVERGAARD CONSTANT
Q3=Q1*Q1                     ! THIRD TVERGAARD CONSTANT
SIGMA_Y=YOUNG/300.0          ! YIELD STRESS
YIELD=1.0D0/SIGMA_Y/PI/R0/R0 ! YIELD STRENGTH
F_0=1E-8                     ! INITIAL POROSITY
F_N=0.04                     ! VOLUME FRACTION/ VOID NUCLEATION
S_N=0.1                      ! STANDARD DEV. OF MEAN STRAIN FOR NUCLEATION.
STRAIN_N=0.3                 ! MEAN STRAIN FOR NUCLEATIONS
POWER_N=0.1                  ! FOR ELASTIC MATERIAL DEFINITION
/PREP7
MP,EX,1,YOUNG                ! MATERIAL PROPERTIES
MP,NUXY,1,NU
TB,NLISO,1,1,2,5             ! ELASTIC MODEL
TBDATA,1,SIGMA_Y,POWER_N

TB,GURS,1,,5,BASE		! BASE DEFINED
TBDATA,1,SIGMA_Y,F_0,Q1,Q2,Q3

TB,GURS,1,,3,SNNU		! SNNU DEFINED
TBDATA,1,F_N,STRAIN_N,S_N

ET,1,PLANE182,,,1                 ! AXISYMMETRIC 2D
RECT,0,R0,0,L0/8             ! DEFINE GEOMETRY AND MESH
RECT,0,R0,L0/8,L0
AGLUE,ALL
LSEL,S,LOC,X,R0/2
LESIZE,ALL,,,10
LSEL,S,LOC,Y,L0/16
LESIZE,ALL,,,5
LSEL,S,LOC,Y,L0/8,L0
LESIZE,ALL,,,20,4
SAVE,MODEL
AMESH,ALL
MODMSH,DETACH
NMODIF,NODE(R0,0,0),R0-DR0,0,0    ! TO CREATE NOTCH AT BOTTOM OF ROD
NSEL,S,LOC,X,0                    ! BOUNDARY CONDITIONS
D,ALL,UX,0
NSEL,S,LOC,Y,0
D,ALL,UY,0
ALLS
NSEL,S,LOC,Y,L0
D,ALL,UY,UAPP
ALLS
FINISH
*CREATE,MACRO,MAC		  ! MACRO FOR SOLUTION AND POST PROCESSING
/SOLU                             ! NON-LINEAR SOLUTION
OUTRES,ALL,ALL
NLGEOM,ON
NROPT,UNSYM
SOLCONTROL,ON
CNVTOL,F,1.0,0.1
NSUBST,MYSUBST,MYSUBST,MYSUBST
ALLS
/OUTPUT,SCRATCH
SOLVE
/OUT
FINISH
/POST1                            ! REACTION FORCES AT TOP OF BAR
*DIM,X,ARRAY,MYSUBST
*DIM,Y,ARRAY,MYSUBST
*DO,J,1,MYSUBST
SET,1,J
*GET,DISPY,NODE,NODE(R0,L0,0),U,Y
X(J)=LOG(1+DISPY/L0)              ! AS DEFINED BY REFERENCE
TOTFORCE=0.0D0
NSEL,ALL
ESLN
NSLE
*GET,NNODE,NODE,,COUNT
*DO,K,1,NNODE
*IF,NY(K),EQ,4.0,THEN
*GET,NFOR,NODE,K,RF,FY
TOTFORCE=TOTFORCE+NFOR            ! TOTAL FORCE
*ENDIF
*ENDDO
Y(J)=TOTFORCE*YIELD               ! Y DATA TO BE PLOTTED
*ENDDO
MAXIMUM=0.01                      ! LOOP TO DETERMINE MAXIMUM IN VECTOR Y
*DO,KK,1,MYSUBST,1
*IF,Y(KK),LT,MAXIMUM,THEN
MAXIMUM=Y(KK-1)
*EXIT
*ELSE
MAXIMUM=Y(KK)
KK=KK+1
*ENDIF
*ENDDO
/AXLAB,Y,NORMALIZED LOAD
*VPLOT,X(1),Y(1)
*DIM,VALUE,ARRAY,1,3
*DIM,LABEL,CHAR,1,1
LABEL(1) = 'LOADING'
*VFILL,VALUE(1,1),DATA,1.25
*VFILL,VALUE(1,2),DATA,MAXIMUM
*VFILL,VALUE(1,3),DATA,1.25/MAXIMUM
*END
MACRO
SAVE,TABLE_1
RESUME,MODEL
/PREP7
ET,1,PLANE183,,,1
AMESH,ALL
MODMSH,DETACH
NMODIF,NODE(R0,0,0),R0-DR0,0,0    ! TO CREATE NOTCH AT BOTTOM OF ROD
NSEL,S,LOC,X,0                    ! BOUNDARY CONDITIONS
D,ALL,UX,0
NSEL,S,LOC,Y,0
D,ALL,UY,0
ALLS
NSEL,S,LOC,Y,L0
D,ALL,UY,UAPP
ALLS
FINISH
MACRO
SAVE,TABLE_2
RESUME,TABLE_1
/COM,
/OUT,vm252,vrt
/COM,--------------- VM252 RESULTS COMPARISON -------------------
/COM,
/COM,            |  TARGET  |  Mechanical APDL |  RATIO  |
/COM,
/COM,PLANE182 RESULTS COMPARISON
*VWRITE,LABEL(1),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,'   ',F8.4,'   ',F14.4,'   ',1F15.3)
/NOPR
RESUME,TABLE_2
/GOPR
/COM,
/COM,PLANE183 RESULTS COMPARISON
*VWRITE,LABEL(1),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,'   ',F8.4,'   ',F14.4,'   ',1F15.3)
/COM,------------------------------------------------------------
/OUT
FINISH
*LIST,vm252,vrt
/DELETE,TABLE_1
/DELETE,TABLE_2

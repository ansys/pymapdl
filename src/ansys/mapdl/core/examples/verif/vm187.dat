/COM,ANSYS MEDIA REL. 2023R1 (11/04/2022) REF. VERIF. MANUAL: REL. 2023R1
/VERIFY,VM187
JPGPRF,500,100,1             ! MACRO TO SET PREFS FOR JPEG PLOTS
/SHOW,JPEG
/PREP7
smrt,off
/TITLE, VM187, BENDING OF A CURVED BEAM
C***  FORMULAS FOR STRESS AND STRAIN, ROARK, 4TH ED.
/out,scratch
ANTYPE,STATIC
ET,1,SOLID5,2             ! USING SOLID5 HEXAHEDRONS, DISPLACEMENT DOF ONLY
MP,EX,,1E7
MP,NUXY,,.25
CSYS,1
K,1,4.12                  ! DEFINE KEYPOINTS
K,2,4.32
KGEN,2,1,2,1,,,.1
KGEN,2,1,4,1,,90
L,1,5
LESIZE,1,,,20
V,1,2,4,3,5,6,8,7         ! DEFINE VOLUME
ESIZE,,1
VMESH,1                   ! CREATE NODES AND ELEMENTS
NSEL,S,LOC,Y,0
D,ALL,ALL,0               ! BOUNDARY CONDITIONS AND LOADING
NSEL,ALL
FK,5,FY,.25               ! APPLY LOAD
*REPEAT,4,1
FINISH
/SOLU    
SOLVE
FINISH
*CREATE,MAC               ! CREATE A MACRO TO DO POSTPROCESSING
/POST1
CSYS,0
NSEL,S,LOC,X,0,.001,,1    ! SELECT NODES AT FREE END OF BEAM
PRNSOL,U,COMP
LCDEF,1,1          
LCFACT,1,(1/.08854)
LCASE,1
PRNSOL,U,COMP
FINISH
*END
*GET,U1,NODE,5,U,Y
*DIM,LABEL,CHAR,1,2
*DIM,VALUE,,1,3
LABEL(1,1) = 'DEFL '
LABEL(1,2) = 'in '
*VFILL,VALUE(1,1),DATA,.08854
*VFILL,VALUE(1,2),DATA,U1
*VFILL,VALUE(1,3),DATA,ABS(U1/.08854)
SAVE,TABLE_1
*USE,MAC                  ! EXECUTE POSTPROCESSING MACRO
/CLEAR, NOSTART 
/PREP7
smrt,off
/TITLE, VM187, BENDING OF A CURVED BEAM
ET,1,SOLID92              ! USING SOLID92 TETRAHEDRONS
MP,EX,,1E7
MP,NUXY,,.25
CSYS,1
K,1,4.12                  ! DEFINE KEYPOINTS
K,2,4.32
KGEN,2,1,2,1,,,.1
KGEN,2,1,4,1,,4.5
V,1,2,4,3,5,6,8,7         ! DEFINE VOLUMES
VGEN,20,1,1,1,,4.5,,4
ESIZE,,1
VMESH,ALL                 ! CREATE NODES AND ELEMENTS
NSEL,S,LOC,Y,0
D,ALL,ALL,0               ! BOUNDARY CONDITIONS AND LOADING
NSEL,S,LOC,Y,90
CP,1,UY,ALL               ! COUPLE UY DOF ON LOADED FACE
NSEL,R,LOC,X,4.32
NSEL,R,LOC,Z,0
F,ALL,FY,1                ! APPLY LOAD
NSEL,ALL
SAVE
FINISH
/SOLU    
SOLVE
FINISH
*USE,MAC                  ! EXECUTE POSTPROCESSING MACRO
NDE=NODE(0,4.32,0)
*GET,UY2,NODE,NDE,U,Y
*SET,U2,(UY2/(1/.08854))
*status,parm
*DIM,LABEL,CHAR,1,2
*DIM,VALUE,,1,3
LABEL(1,1) = 'DEFL '
LABEL(1,2) = 'in '
*VFILL,VALUE(1,1),DATA,.08854
*VFILL,VALUE(1,2),DATA,U2
*VFILL,VALUE(1,3),DATA,ABS(U2/.08854)
SAVE,TABLE_2
/PREP7
smrt,off
RESUME
ET,1,SOLID98,2            ! ANALYZE AGAIN USING SOLID98 TETRAHEDRONS
FINISH
/SOLU    
SOLVE
FINISH
*USE,MAC                  ! EXECUTE POSTPROCESSING MACRO
NDE=NODE(0,4.32,0)
*GET,UY3,NODE,NDE,U,Y
*SET,U3,(UY3/(1/.08854))
*status,parm
*DIM,LABEL,CHAR,1,2
*DIM,VALUE,,1,3
LABEL(1,1) = 'DEFL '
LABEL(1,2) = 'in '
*VFILL,VALUE(1,1),DATA,.08854
*VFILL,VALUE(1,2),DATA,U3
*VFILL,VALUE(1,3),DATA,ABS(U3/.08854)
SAVE,TABLE_3
/PREP7
smrt,off
RESUME
ET,1,SOLID187             ! ANALYZE AGAIN USING SOLID187 TETRAHEDRONS
FINISH
/SOLU    
SOLVE
FINISH
*USE,MAC                  ! EXECUTE POSTPROCESSING MACRO
NDE=NODE(0,4.32,0)
*GET,UY3,NODE,NDE,U,Y
*SET,U3,(UY3/(1/.08854))
*status,parm
*DIM,LABEL,CHAR,1,2
*DIM,VALUE,,1,3
LABEL(1,1) = 'DEFL '
LABEL(1,2) = 'in '
*VFILL,VALUE(1,1),DATA,.08854
*VFILL,VALUE(1,2),DATA,U3
*VFILL,VALUE(1,3),DATA,ABS(U3/.08854)
SAVE,TABLE_4
RESUME,TABLE_1
/COM
/OUT,vm187,vrt
/COM,------------------- VM187 RESULTS COMPARISON ------------
/COM,
/COM,                      TARGET   |    Mechanical APDL   |    RATIO
/COM, SOLID5
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F11.4,'  ',F15.4,'   ',1F16.3)
/NOPR
RESUME,TABLE_2
/GOPR
/COM,
/COM, SOLID92
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F11.4,'  ',F15.4,'   ',1F16.3)
/NOPR
RESUME,TABLE_3
/GOPR
/COM,
/COM, SOLID98
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F11.4,'  ',F15.4,'   ',1F16.3)
/NOPR
RESUME,TABLE_4
/GOPR
/COM,
/COM, SOLID187
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F11.4,'  ',F15.4,'   ',1F16.3)
/COM,---------------------------------------------------------
/OUT
FINISH
/DELETE,TABLE_1
/DELETE,TABLE_2
/DELETE,TABLE_3
/DELETE,TABLE_4
/DELETE,MAC
*LIST,vm187,vrt

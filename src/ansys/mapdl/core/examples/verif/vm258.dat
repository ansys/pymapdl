/COM,ANSYS MEDIA REL. 150 (11/8/2013) REF. VERIF. MANUAL: REL. 150
/VERIFY,VM258
/TITLE,VM258,SPIN-UP MANEUVER OF A FLEXIBLE BEAM
/COM,   J.C. SIMO AND L. VU-QUOC
/COM    ON THE DYNAMICS IN SPACE OF RODS UNDERGOING LARGE MOTIONS-
/COM,   -A GEOMETRICALLY EXACT APPROACH
/COM,   COMPUTER METHODS IN APPLIED MECHANICS AND ENGINEERING, VOL. 66,
/COM,   125-161 (1988.
/PREP7
ET,1,189	!3D QUADRATIC FINITE STRAIN BEAM
ET,2,184,16	!JOINT ELEMENT TYPE GENERAL
ET,3,184,6	!JOINT ELEMENT TYPE REVOLUTE
KEYOPT,3,4,1    !Z-AXIS REVOLUTE JOINT
N,1,0.0,0.0	!NODES FOR CANTILEVER BEAM
N,9,10.0,0.0
FILL,1,9
SECT,1,GENB,ELASTIC	        !NONLINEAR GENERAL BEAM SECTION (ELASTIC)
BSAX,0.0,0.0	!BEAM SECTION AXIAL STIFFNESS (AXIAL STRAIN/AXIAL FORCE)
BSAX,1.0,2.8E7
BSM1,0.0,0.0	!BEAM SECTION BENDING STIFFNESS FOR PLANE XZ(CURVATURE/BENDING MOMENT)
BSM1,1.0,1.4E4
BSM2,0.0,0.0	!BEAM SECTION BENDING STIFFNESS FOR PLANE XY(CURVATURE/BENDING MOMENT)
BSM2,1.0,1.4E4
BSTQ,0.0,0.0	!BEAM SECTION TORSION STIFFNESS (TWIST/TORQUE)
BSTQ,1.0,1.4E4
BSS1,0.0,0.0	!BEAM SECTION SHEAR STIFFNESS FOR PLANE XZ (SHEAR STRAIN/TRANSVERSE SHEAR FORCE)
BSS1,1.0,1.0E7
BSS2,0.0,0.0	!BEAM SECTION SHEAR STIFFNESS FOR PLANE XY (SHEAR STRAIN/ TRANSVERSE SHEAR FORCE)
BSS2,1.0,1.0E7
BSMD,1.2	!BEAM SECTION MASS DENSITY (MASS/LENGTH)
TYPE,1		!BEAM ELEMENT DEFINITION
SECNUM,1
EN,1,1,3,2
ENGEN,1,4,2,1
SECTYPE,2,JOIN,GENE,GENE	!GENERAL JOINT SECTION
SECJOIN,                        !LOCAL COORDINATE SYSTEM=GLOBAL CARTESIAN (DEFAULT)
TYPE,2		                !GENERAL JOINT ELEMENT DEFINITION
SECNUM,2
EN,201,1,9
SECTYPE,3,JOIN,REVO,REVO	!REVOLUTE JOINT SECTION
SECJOIN,                        !LOCAL COORDINATE SYSTEM=GLOBAL CARTESIAN(DEFAULT)
TYPE,3		                !REVOLUTE JOINT ELEMENT DEFINITION
SECNUM,3
EN,301, ,1                      !GROUNDED JOINT ELEMENT
C***DEFINE ROTATION ANGLE AS A FUNCTION OF TIME
*DIM,FUNC1,TABLE,3001,1,1
!FIRST TABULAR LOAD: (6/15)*(((TIME*TIME)/2)+((15/(2*PI))^2)*(COS((2*TIME*PI)/15)-1))
FUNC1(0,0,1) = 0.0
FUNC1(0,1,1) = 0.0
PI = ACOS(-1.0)
OM = (2.0*PI)/15.0
A  = 0.4
B  = 45.0/(2.0*PI*PI)
*DO,II,1,3001
 T1 = (II-1)*0.005
 FUNC1(II,0,1) = T1             !TIME
 T2 = A*((T1*T1)/2.0)
 T3 = B*(COS(OM*T1)-1.0)
 FUNC1(II,1,1) = T2 + T3        !VALUE
*END DO
!SECOND TABULAR LOAD: 6*TIME-45
*DIM,FUNC2,TABLE,2,1,1
FUNC2(0,0,1) = 0.0
FUNC2(0,1,1) = 0.0
FUNC2(1,0,1) = 15.0             !TIME VALUES
FUNC2(2,0,1) = 30.0
FUNC2(1,1,1) = 45               !ROTATION VALUES
FUNC2(2,1,1) = 135.0
FINISH
C*** PERFORM SOLUTION USING HHT ALGORITHM
/SOLU
ANTYPE,TRANS	                !TRANSIENT ANALYSIS
NLGEOM,ON	                !LARGE DEFLECTION
! LOAD STEP 1--ROTATION WITH ANGULAR ACCELERATION (SMALL CENTRIFUGAL FORCE)
DJ,301,ROTZ,%FUNC1%             !APPLY FIRST TABULAR LOAD
TRNOPT,FULL, , , , ,HHT	        !HHT TIME INTEGRATION SCHEME
TINTP,0.1		        !10% NUMERICAL DAMPING
TIME,10
AUTOTS,ON		        !AUTO TIME STEPPING
MIDTOL,ON,2		        !MIDSTEP RESIDUAL CHECK
DELTIM,0.005,0.002,0.02
OUTRES,ALL,2                    !WRITE RESULTS EVERY 2 SUBSTEPS
/OUT,SCRATCH
SOLVE
/OUT
TIME,15
! LOAD STEP 2--ROTATION WITH ANGULAR ACCELERATION (INTERMEDIATE CENTRIFUGAL FORCE)
DELTIM,0.005,0.005,0.01
OUTRES,ALL,2
/OUT,SCRATCH
SOLVE
/OUT
! LOAD STEP 3--STEADY MOTION AT CONSTANT ANGULAR VELOCITY
DJ,301,ROTZ,%FUNC2%             !APPLY SECOND TABULAR LOAD
TIME,18
OUTRES,ALL,2
/OUT,SCRATCH
SOLVE
/OUT
FINISH
! EXTRACT TIME POINTS FROM Mechanical APDL RESULTS CLOSEST TO THE REFERENCE TIME DATA
*DIM,TIME2,ARRAY,12
/POST1
*DO,K,1,12,1
 T = K*1.5
 SET,NEAR, , , ,T
 *GET,TIME2(K),ACTIVE,0,SET,TIME
*END DO
FINISH
/POST26
ESOL,2,201,9,SMISC,61,U1        !AXIAL DISPLACEMENT OF BEAM TIP
ESOL,3,201,9,SMISC,62,U2        !TRANSVERSE DISPLACEMENT OF BEAM TIP
NSOL,4,9,ROT,Z,ROTZ9            !ROTATION OF BEAM BASE
NSOL,5,1,ROT,Z,ROTZ1            !ROTATION OF BEAM TIP
! ROTATION OF BEAM TIP RELATIVE TO BASE--CONVERTED TO DEGREES
ADD,6,5,4,,ROTZ, , ,57.2957795131,-57.2957795131,0
/OUT
/TITLE,SPIN-UP OF A FLEXIBLE BEAM: ROTATION ANGLE VS. TIME
/AXLAB,Y,ROTATION ANGLE
PLVAR,5
/TITLE, TIME HISTORY OF AXIAL DISPLACEMENT OF BEAM TIP
/AXLAB,Y,AXIAL DISPLACEMENT
PLVAR,2                         !COMPARE THIS PLOT WITH FIGURE 6 IN REFERENCE
/TITLE,TIME HISTORY OF TRANSVERSE DISPLACEMENT OF BEAM TIP
/AXLAB,Y,TRANSVERSE DISPLACEMENT
PLVAR,3                         !COMPARE THIS PLOT WITH FIGURE 6 IN REFERENCE
/TITLE,TIME HISTORY OF RELATIVE ROTATION OF BEAM TIP
/AXLAB,Y,RELATIVE ROTATION
PLVAR,6                         !COMPARE THIS PLOT WITH FIGURE 6 IN REFERENCE

*GET,UXMIN,VARI,2,EXTREM,VMIN   ! MIN AXIAL DISPLACEMENT AT BEAM TIP
*GET,TXMIN,VARI,2,EXTREM,TMIN   ! TIME CORRESPONDING TO MIN AXIAL DISPLACEMENT

*GET,UYMIN,VARI,3,EXTREM,VMIN   ! MIN TRANSVERSE DISPLACEMENT AT BEAM TIP
*GET,TYMIN,VARI,3,EXTREM,TMIN   ! TIME CORRESPONDING TO MIN TRANSVERSE  DISPLACEMENT

*GET,ROTZMAX,VARI,6,EXTREM,VMAX ! MAX RELATIVE ROTATION
*GET,TZMAX,VARI,6,EXTREM,TMAX   ! TIME CORRESPONDING TO MAXIMUM RELATIVE ROTATION

*GET,UXMAX,VARI,2,EXTREM,VMAX   ! MAX STRETCH
*GET,TXMAX,VARI,2,EXTREM,TMAX   ! TIME CORRESPONDING TO MAX STRETCH

*DIM,LABEL1,CHAR,2,2
*DIM,VALUE1,,2,3
LABEL1(1,1) = 'TIME','TIP DISP'
LABEL1(1,2) = '   (sec)','UX '
*VFILL,VALUE1(1,1),DATA,6.7,-0.019
*VFILL,VALUE1(1,2),DATA,TXMIN,UXMIN
*VFILL,VALUE1(1,3),DATA,ABS(TXMIN/6.7),ABS(UXMIN/(-0.019))
FINISH
SAVE,TABLE_1
/POST26
*DIM,LABEL2,CHAR,2,2
*DIM,VALUE2,,2,3
LABEL2(1,1) = 'TIME','TIP DISP'
LABEL2(1,2) = '   (sec)','UY '
*VFILL,VALUE2(1,1),DATA,6.85,-0.575
*VFILL,VALUE2(1,2),DATA,TYMIN,UYMIN
*VFILL,VALUE2(1,3),DATA,ABS(TYMIN/6.85),ABS(UYMIN/(-0.575))
FINISH
SAVE,TABLE_2
/POST26
*DIM,LABEL3,CHAR,2,2
*DIM,VALUE3,,2,3
LABEL3(1,1) = 'TIME','REL'
LABEL3(1,2) = '   (sec)','ROTZ(deg)'
*VFILL,VALUE3(1,1),DATA,6.7,4.424
*VFILL,VALUE3(1,2),DATA,TZMAX,ROTZMAX
*VFILL,VALUE3(1,3),DATA,ABS(TZMAX/6.7),ABS(ROTZMAX/4.424)
FINISH
SAVE,TABLE_3
/POST26
*DIM,LABEL4,CHAR,2,2
*DIM,VALUE4,,2,3
LABEL4(1,1) = 'TIME','STRETCH'
LABEL4(1,2) = '   (sec)','UX'
*VFILL,VALUE4(1,1),DATA,16,5.14E-4
*VFILL,VALUE4(1,2),DATA,TXMAX,UXMAX
*VFILL,VALUE4(1,3),DATA,ABS(TXMAX/16),ABS(UXMAX/5.14E-4)
FINISH
SAVE,TABLE_4


RESUME,TABLE_1
/COM
/OUT,vm258,vrt
/COM,------------------- VM258 RESULTS COMPARISON --------------
/COM,
/COM,                 |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
/COM,PEAK AXIAL DISPLACEMENT
*VWRITE,LABEL1(1,1),LABEL1(1,2),VALUE1(1,1),VALUE1(1,2),VALUE1(1,3)
(1X,A8,A8,'   ',F10.4,'  ',F14.4,'   ',1F15.3)
/NOPR
RESUME,TABLE_2
/GOPR
/COM,
/COM,PEAK TRANSVERSE DISPLACEMENT
*VWRITE,LABEL2(1,1),LABEL2(1,2),VALUE2(1,1),VALUE2(1,2),VALUE2(1,3)
(1X,A8,A8,'   ',F10.4,'  ',F14.4,'   ',1F15.3)
/NOPR
RESUME,TABLE_3
/GOPR
/COM,
/COM,PEAK RELATIVE ROTATION
*VWRITE,LABEL3(1,1),LABEL3(1,2),VALUE3(1,1),VALUE3(1,2),VALUE3(1,3)
(1X,A8,A8,'   ',F10.4,'  ',F14.4,'   ',1F15.3)
/NOPR
RESUME,TABLE_4
/GOPR
/COM,
/COM,STEADY-STATE STRETCH
*VWRITE,LABEL4(1,1),LABEL4(1,2),VALUE4(1,1),VALUE4(1,2),VALUE4(1,3)
(1X,A8,A8,'   ',F10.4,'  ',F14.4,'   ',1F15.3)
/COM,-----------------------------------------------------------
/COM,
/OUT
FINISH
*LIST,vm258,vrt
/DELETE,TABLE_1
/DELETE,TABLE_2
/DELETE,TABLE_3
/DELETE,TABLE_4

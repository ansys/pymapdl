! Copyright (C) 2024 ANSYS, Inc. and/or its affiliates.
! SPDX-License-Identifier: MIT
! 
!
! Permission is hereby granted, free of charge, to any person obtaining a copy
! of this software and associated documentation files (the "Software"), to deal
! in the Software without restriction, including without limitation the rights
! to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
! copies of the Software, and to permit persons to whom the Software is
! furnished to do so, subject to the following conditions:
!
! The above copyright notice and this permission notice shall be included in all
! copies or substantial portions of the Software.
!
! THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
! IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
! FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
! AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
! LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
! OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
! SOFTWARE.

/COM,ANSYS MEDIA REL. 2023R2 (05/12/2023) REF. VERIF. MANUAL: REL. 2023R2
/VERIFY,vmr049-cr1b-182
/TITLE,vmr049-cr1b-182,CONSTANT-LOAD CREEP BENCHMARK
/COM, REFERENCE: TEST CR-1B FROM NAFEMS REPORT R0049.
/SHOW,JPEG
JPGPRF,500,100,1

/PREP7
C*** PARAMETRIC INPUT FOR CREEP CONSTANT !(TIME HARDENING)***
*SET,C1,1.5625E-14
*SET,C2,5
*SET,C3,-0.5
*SET,C4,0
C*** TIME PARAMETER
*SET,HOUR,200
C*** ELASTIC CONSTANT
MP,EX,1,200E3
MP,NUXY,1,0.3
TUNIF,HOT
TOFF,1E-10
TB,CREEP,1,,,2
TBDATA,1,C1,C2,C3,C4
N,1,0,0,0
N,2,100,0
N,3,100,50
N,4,0,50
N,5,100,100
N,6,0,100
ET,1,182
KEYOPT,1,1,1
KEYOPT,1,3,0
E,1,2,3,4
E,4,3,5,6
NSEL,S,LOC,X,
D,ALL,UX,
NSEL,ALL
NSEL,S,,,4
D,ALL,UY,
NSEL,ALL

/SOLU
NSEL,S,LOC,X,100
SF,ALL,PRES,-200
NSEL,ALL
RATE, OFF
DELT,1.0E-8,1.0E-9,1.0E-8
TIME, 1.0E-8
SOLVE
RATE, ON, ON
DELT,100,99,101
TIME,1000
OUTRES,ALL,ALL
/OUTPUT,SCRATCH
SOLVE
/OUT
FINISH

/POST26
ESOL,2,1,,EPCR,X
ESOL,3,1,,EPCR,Y
PRVAR,2,3
/AXLAB,X,TIME
/AXLAB,Y,CREEP STRAIN
PLVAR,2,3
FINISH

/POST1
SET,,,,,,,1
*GET,S1,NODE,3,EPCR,X
*GET,S1_Y,NODE,3,EPCR,Y
R1=1.00
R7=1.00
SET,,,,,,,3
*GET,S2,NODE,3,EPCR,X
*GET,S2_Y,NODE,3,EPCR,Y
R2=S2/0.854E-1
R8=S2_Y/(-0.427E-1)
SET,,,,,,,5
*GET,S3,NODE,3,EPCR,X
*GET,S3_Y,NODE,3,EPCR,Y
R3=S3/0.1396
R9=S3_Y/(-0.698E-1)
SET,,,,,,,7
*GET,S4,NODE,3,EPCR,X
*GET,S4_Y,NODE,3,EPCR,Y
R4=S4/0.183
R10=S4_Y/(-0.9133E-1)
SET,,,,,,,9
*GET,S5,NODE,3,EPCR,X
*GET,S5_Y,NODE,3,EPCR,Y
R5=S5/0.219
R11=S5_Y/(-0.1097)
SET,,,,,,,11
*GET,S6,NODE,3,EPCR,X
*GET,S6_Y,NODE,3,EPCR,Y
R6=S6/0.251
R12=S6_Y/(-0.125)
*DIM,VALUE,,6,4
*DIM,LABEL,CHAR,10
*DIM,TARGETX,CHAR,10
*DIM,TARGETY,CHAR,10
*DIM,RATIO,,6,4
LABEL(1) = '0','200','400','600','800','1000'
TARGETX(1) = '0.50E-6','0.0854','0.1396','0.183','0.219','0.251'
TARGETY(1) = '-0.25E-6','-0.0427','-0.0698','-0.09133','-0.1097','-0.125'
*VFILL,VALUE(1,1),DATA,S1,S2,S3,S4,S5,S6
*VFILL,VALUE(1,2),DATA,R1,R2,R3,R4,R5,R6
*VFILL,VALUE(1,3),DATA,S1_Y,S2_Y,S3_Y,S4_Y,S5_Y,S6_Y
*VFILL,VALUE(1,4),DATA,R7,R8,R9,R10,R11,R12
/COM
/COM
/COM,--------- vmr049-cr1b-182 RESULTS COMPARISON-----------
/COM,
/COM, vmr049-cr1b-182.grph RESULTS SHOULD MATCH R0049 NAFEMS MANUAL
/COM, GRAPH RESULTS ON PAGE 100,FIGURE 3.7(B). THE RESULTS
/COM, DISPLAYED ARE INCREMENTED FOR THIS PURPOSE
/COM,
/COM,-------- CREEP COMPONENT IN X DIRECTION ---------
/COM,
/COM,| TIME |   TARGET  |    Mechanical APDL   |    RATIO
/COM,
*VWRITE,LABEL(1),TARGETX(1), VALUE(1,1), VALUE(1,2)
(1X,A6,'   ',1X,A9,'   ',F14.5,'   ',F12.3,'   ')
/COM,
/COM,-------- CREEP COMPONENT IN Y DIRECTION ---------
/COM,
/COM,| TIME |   TARGET  |    Mechanical APDL   |    RATIO
/COM,
*VWRITE,LABEL(1),TARGETY(1), VALUE(1,3), VALUE(1,4)
(1X,A6,'   ',1X,A9,'   ',F14.5,'   ',F12.3,'   ')
FINISH

/POST26
/COM,ESOL,2,1,,EPCR,X
/COM,ESOL,3,1,,EPCR,Y
/COM,PRVAR,2,3
/COM,PLVAR,2,3
/COM,*GET,RES1X,VARI,2,RTIME,1000
/COM,*GET,RES1Y,VARI,3,RTIME,1000

*DIM,LABEL1,CHAR,2
*DIM,VALUE1,,2,3
LABEL1(1) = ' ECR2X ',' ECR2Y '
*VFILL,VALUE1(1,1),DATA,S6,S6_Y
*VFILL,VALUE1(1,2),DATA,R6,R12
*DIM,LABEL2,CHAR,2
LABEL2(1) = 'vmr049-','cr1b-182'

/OUT,vmr049-cr1b-182,vrt
/COM
/COM,------------------- vmr049-cr1b RESULTS COMPARISON ---------------------
/COM,
/COM,            |   Mechanical APDL   |   RATIO  |         INPUT         |
/COM,
/COM, PLANE182
*VWRITE,LABEL1(1),VALUE1(1,1),VALUE1(1,2),LABEL2(1),LABEL2(2)
(1X,A8,'      ',F14.4,'      ',F9.4,'     ',A7,A8)
*VWRITE,LABEL1(2),VALUE1(2,1),VALUE1(2,2),LABEL2(1),LABEL2(2)
(1X,A8,'      ',F14.4,'      ',F9.4,'     ',A7,A8)
/COM,
/COM,----------------------------------------------------------------
/OUT

FINISH
*LIST,vmr049-cr1b-182,vrt





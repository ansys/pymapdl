! Copyright (C) 2024 ANSYS, Inc. and/or its affiliates.
! SPDX-License-Identifier: MIT
! 
!
! Permission is hereby granted, free of charge, to any person obtaining a copy
! of this software and associated documentation files (the "Software"), to deal
! in the Software without restriction, including without limitation the rights
! to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
! copies of the Software, and to permit persons to whom the Software is
! furnished to do so, subject to the following conditions:
!
! The above copyright notice and this permission notice shall be included in all
! copies or substantial portions of the Software.
!
! THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
! IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
! FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
! AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
! LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
! OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
! SOFTWARE.

/COM,ANSYS MEDIA REL. 2023R2 (05/12/2023) REF. VERIF. MANUAL: REL. 2023R2
/VERIFY,vmr027-cr10b-181
/TITLE,vmr027-cr10b-181,2D PLANE STRESS-BIAXIAL DISPLACEMENT PRIMARY CREEP
/COM, REFERENCE: TEST 10(B) FROM NAFEMS R0027.
/SHOW,JPEG
JPGPRF,500,100,1

/PREP7
C*** PARAMETRIC INPUT FOR CREEP CONSTANTS (TIME HARDENING)!***
*SET,C1,1.5625E-14
*SET,C2,5.0
*SET,C3,-0.5
*SET,C4,0
C***  TIME PARAMETER
*SET,HOUR,1000
C***   ELASTIC CONSTANT
MP,EX,1,200E3
MP,NUXY,1,0.3
TUNIF,HOT
TOFF,OFFS
TB,CREEP,1,,,6
TBDATA,1,C1,C2,C3,C4
C*** PARAMETRIC INPUT FOR CREEP CONSTANTS (STRAIN HARDENING)!***
*SET,C1,1.5625E-14
*SET,C2,5
*SET,C3,-0.5
MP,EX,2,200E3
MP,NUXY,2,0.3
TB,CREEP,2,,,7
TBDATA,1,C1,C2,C3
SAVE
N,1,0,0,0
N,2,100,0
N,3,100,100
N,4,0,100
N,5,50,0
N,6,100,50
N,7,50,100
N,8,0,50
N,9,50,50
ET,1,181,,,
R,1,1,1,1,1,
E,1,5,9,8
E,5,2,6,9
E,9,6,3,7
E,8,9,7,4
NSEL,ALL
NSEL,S,LOC,X
D,ALL,UX
NSEL,S,LOC,Y
D,ALL,UY
NSEL,ALL
NSEL,S,LOC,X,100
D,ALL,UX,0.1
NSEL,ALL
NSEL,S,LOC,Y,100
D,ALL,UY,-0.1
NSEL,ALL
D,9,UZ,0
D,9,ROTY,0
D,5,ROTX,0
FINISH
*DO,I,1,2

/PREP7
*IF,I,EQ,1,THEN
/COM,
/COM, TIME HARDENING CASE
/COM,
MAT,1
*ELSEIF,I,EQ,2,THEN
/COM,
/COM, STRAIN HARDENING CASE
/COM,
MPCHG,2,ALL
*ENDIF

/SOLU
RATE, OFF
DELT,1.0E-8,1.0E-9,1.0E-8
TIME, 1.0E-8
OUTRES,ALL,LAST
/OUTPUT,SCRATCH
SOLVE
/OUT
RATE, ON
DELT,0.10,0.099,0.10
TIME, 0.10
OUTRES,ALL,LAST
/OUTPUT,SCRATCH
SOLVE
/OUT
RATE, ON
DELT,1.0,0.99,1.0
TIME, 1.0
OUTRES,ALL,LAST
/OUTPUT,SCRATCH
SOLVE
/OUT
RATE, ON
DELT,5.0,4.99,5.0
TIME, 5.0
OUTRES,ALL,LAST
/OUTPUT,SCRATCH
SOLVE
/OUT
RATE, ON
DELT,10.0,9.99,10.0
TIME, 10.0
OUTRES,ALL,LAST
/OUTPUT,SCRATCH
SOLVE
/OUT
RATE, ON, ON
DELT,10,1,100
AUTOS,OFF
TIME,1000
OUTRES,ALL,ALL
/OUTPUT,SCRATCH
SOLVE
/OUT
FINISH

/POST26
*IF,I,EQ,1,THEN
ESOL,2,1,,S,X,TIMEHARD
PRVAR,2
/AXLAB,X,TIME
/AXLAB,Y,STRESS
/YRANGE,0,160
/COLOR,CURVE,RED,1
PLVAR,2
/NOERASE

/POST1
SET,1,1
*GET,SH1,NODE,3,S,X
R1=SH1/153.85
SET,,,,,,,9
*GET,SH6,NODE,3,S,X
R6=SH6/26.69
SET,,,,,,,14
*GET,SH7,NODE,3,S,X
R7=SH7/24.45
SET,,,,,,,54
*GET,SH8,NODE,3,S,X
R8=SH8/20.00
SET,,,,,,,104
*GET,SH9,NODE,3,S,X
R9=SH9/18.34
*DIM,VALUE,,5,3
*DIM,LABEL,CHAR,10
LABEL(1) = '0','50.0','100.0','500.0','1000.0'
*VFILL,VALUE(1,1),DATA,153.85,26.69,24.45,20.00,18.34
*VFILL,VALUE(1,2),DATA,SH1,SH6,SH7,SH8,SH9
*VFILL,VALUE(1,3),DATA,R1,R6,R7,R8,R9
/COM
/COM
/COM,----------- vmr027-cr10b-181 TIME HARDENING RESULTS COMPARISON ------------
/COM,
/COM,|   TIME   | TARGET |   Mechanical APDL  |   RATIO
/COM,
*VWRITE,LABEL(1), VALUE(1,1), VALUE(1,2), VALUE(1,3)
(1X,A8,'   ',F8.3,'   ',F13.3,'   ',1F13.3,'   ')
FINISH

*ELSEIF,I,EQ,2,THEN
/POST26
ESOL,3,1,,S,X,STRAINHA
PRVAR,3
/AXLAB,X,TIME
/AXLAB,Y,STRESS
/YRANGE,0,160
/COLOR,CURVE,BLUE,1
PLVAR,3

/POST1
SET,1,1
*GET,S1,NODE,3,S,X
R11=S1/153.85
SET,,,,,,,9
*GET,S6,NODE,3,S,X
R66=S6/35.96
SET,,,,,,,14
*GET,S7,NODE,3,S,X
R77=S7/33.46
SET,,,,,,,54
*GET,S8,NODE,3,S,X
R88=S8/28.06
SET,,,,,,,104
*GET,S9,NODE,3,S,X
R99=S9/26.10
*DIM,VALUE2,,5,6
LABEL(1) = '0','50.0','100.0','500.0','1000.0'
*VFILL,VALUE2(1,1),DATA,153.85,35.96,33.46,28.06,26.10
*VFILL,VALUE2(1,2),DATA,S1,S6,S7,S8,S9
*VFILL,VALUE2(1,3),DATA,R11,R66,R77,R88,R99
/COM
/COM
/COM,------------ vmr027-cr10b-181 STRAIN HARDENING RESULTS COMPARISON -------------
/COM,
/COM,|   TIME   | TARGET |   Mechanical APDL  |   RATIO  |
/COM,
*VWRITE,LABEL(1), VALUE2(1,1), VALUE2(1,2), VALUE2(1,3)
(1X,A8,'   ',F8.3,'   ',F13.3,'   ',1F13.3,'   ')
FINISH
*ENDIF
*ENDDO
FINISH

/POST26
*DIM,LABEL1,CHAR,2
*DIM,VALUE1,,2,3
LABEL1(1) = ' S6 ',' S7 '
*VFILL,VALUE1(1,1),DATA,SH9,S9
*VFILL,VALUE1(1,2),DATA,R9,R99
*DIM,LABEL2,CHAR,3
LABEL2(1) = 'vmr027-','cr10b-18','1'

/OUT,vmr027-cr10b-181,vrt
/COM
/COM,------------------- vmr027-cr10b RESULTS COMPARISON ---------------------
/COM,
/COM,            |   Mechanical APDL   |   RATIO  |         INPUT         |
/COM,
/COM, SHELL181
*VWRITE,LABEL1(1),VALUE1(1,1),VALUE1(1,2),LABEL2(1),LABEL2(2),LABEL2(3)
(1X,A8,'      ',F13.3,'      ',F9.4,'     ',A7,A8,A8)
*VWRITE,LABEL1(2),VALUE1(2,1),VALUE1(2,2),LABEL2(1),LABEL2(2),LABEL2(3)
(1X,A8,'      ',F13.3,'      ',F9.4,'     ',A7,A8,A8)
/COM,
/COM,----------------------------------------------------------------
/OUT

FINISH
*LIST,vmr027-cr10b-181,vrt

/COM,ANSYS MEDIA REL. 2023R1 (11/04/2022) REF. VERIF. MANUAL: REL. 2023R1
/VERIFY,VM268
JPGPRF,500,100,1
/SHOW,JPEG
/TITLE,VM268,MULLINS EFFECT ON A RUBBER TUBE MODEL SUBJECTED TO TENSION LOADING 
/COM, REFERENCE: "R.W.OGDEN, ET AL., A PSEUDO-ELASTIC MODEL FOR THE MULLINS EFFECT IN FILLED RUBBER  
/COM,             ROYAL SOCIETY OF LONDON PROCEEDINGS SERIES A, VOL.455,1999,PG:2861-2877
/COM,
/PREP7 
ET,1,PLANE182             ! ELEMENT TYPE 182     
KEYOPT,1,3,1
RECTNG,0,0.5,0,1
ESIZE,0.25
AMESH,1
MUA=8                	  ! INITIAL SHEAR MODULUS OF THE MATERIAL
RR=2.104		  ! DAMAGE VARIABLE PARAMETER
MM=30.45		  ! DAMAGE VARIABLE PARAMETER
BB=0.2			  ! DAMAGE VARIABLE PARAMETER
TB,HYPER,1,,,NEO     	  ! NEO-HOOKEAN OPTION
TBDATA,1,MUA
TB,CDM,1,,3,PSE2      	  ! MODIFIED OGDEN ROXBURGH MULLINS EFFECT
TBDATA,1,RR,MM,BB         ! DEFINE R,M,AND B

! CONSTRAINTS
NSEL,S,LOC,Y,0.
D,ALL,UY,0.      
NSEL,S,LOC,Y,0
NSEL,R,LOC,X,0
D,ALL,ALL,0.       
ALLSEL
FINISH

/SOLU
ANTYPE,STATIC
NLGEOM,ON
*SET,N,6                  ! LOADING STEP
*DIM, UYY,ARRAY,N
*SET,UYY(1),0.5,1.0,2.0,1.0,0.5,0
*DO,I,1,N
  TIME,I
  OUTRES,ALL,LAST
  NSUBST,20,1000,5
  KBC,0
  NSEL,S,LOC,Y,1
  D,ALL,UY,UYY(I)
  ALLSEL
  SOLVE
*ENDDO
FINISH

/OUT,SCRATCH
/POST1
*DIM,SS,ARRAY,N
*DIM,SS_REF,ARRAY,N
*DIM,WM,ARRAY,N
*DO,I,1,N
   SET,I
   ETABLE,SY,S,Y   
  *GET,SS(I),ELEM,1,ETAB,SY
   ETABLE,WMM,CDM,LM
  *GET,WM(I),ELEM,1,ETAB,WMM

!THEORETICAL RESULTS FROM PAPER
   L=UYY(I)+1
   WM0=MUA*(L**2+2/L-3)/2
   DA=(WM(I)-WM0)/(MM+BB*WM(I))
   DM=1-2/SQRT(3.1416)*(DA-DA**3/3+DA**5/10)/RR
   SS_REF(I)=DM*MUA*(L**2-1/L)
*ENDDO

*DIM,LABEL,CHAR,1,N
*DIM,VALUE,,N,3
LABEL(1,1) = '1.5'
LABEL(1,2) = '2.0'
LABEL(1,3) = '3.0'
LABEL(1,4) = '2.0'
LABEL(1,5) = '1.5'
LABEL(1,6) = '1.0'
/COM,
*VFILL,VALUE(1,1),DATA,SS_REF(1)            						
*VFILL,VALUE(1,2),DATA,SS(1)
*VFILL,VALUE(1,3),DATA,ABS(SS(1)/SS_REF(1))
*VFILL,VALUE(2,1),DATA,SS_REF(2)             						
*VFILL,VALUE(2,2),DATA,SS(2)
*VFILL,VALUE(2,3),DATA,ABS(SS(2)/SS_REF(2))
*VFILL,VALUE(3,1),DATA,SS_REF(3)             						
*VFILL,VALUE(3,2),DATA,SS(3)
*VFILL,VALUE(3,3),DATA,ABS(SS(3)/SS_REF(3))
*VFILL,VALUE(4,1),DATA,SS_REF(4)             					
*VFILL,VALUE(4,2),DATA,SS(4)
*VFILL,VALUE(4,3),DATA,ABS(SS(4)/SS_REF(4))
*VFILL,VALUE(5,1),DATA,SS_REF(5)             						
*VFILL,VALUE(5,2),DATA,SS(5)
*VFILL,VALUE(5,3),DATA,ABS(SS(5)/SS_REF(5))
*VFILL,VALUE(6,1),DATA,SS_REF(6)             						
*VFILL,VALUE(6,2),DATA,SS(6)
*VFILL,VALUE(6,3),DATA,1.0
/COM
/OUT,vm268,vrt
/COM,------------ vm268 RESULTS COMPARISON --------------
/COM,
/COM,STRETCH      |  TARGET   |  Mechanical APDL  |  RATIO
/COM,
*VWRITE,LABEL(1,1),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,'   ',F10.4,'  ',F14.4,'   ',F15.3)
*VWRITE,LABEL(1,2),VALUE(2,1),VALUE(2,2),VALUE(2,3)
(1X,A8,'   ',F10.4,'  ',F14.4,'   ',F15.3)
*VWRITE,LABEL(1,3),VALUE(3,1),VALUE(3,2),VALUE(3,3)
(1X,A8,'   ',F10.4,'  ',F14.4,'   ',F15.3)
*VWRITE,LABEL(1,4),VALUE(4,1),VALUE(4,2),VALUE(4,3)
(1X,A8,'   ',F10.4,'  ',F14.4,'   ',F15.3)
*VWRITE,LABEL(1,5),VALUE(5,1),VALUE(5,2),VALUE(5,3)
(1X,A8,'   ',F10.4,'  ',F14.4,'   ',F15.3)
*VWRITE,LABEL(1,6),VALUE(6,1),VALUE(6,2),VALUE(6,3)
(1X,A8,'   ',F10.4,'  ',F14.4,'   ',F15.3)
/COM--------------------------------------------------------
/OUT
FINISH
*LIST,vm268,vrt


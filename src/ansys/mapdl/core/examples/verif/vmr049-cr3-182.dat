! Copyright (C) 2024 ANSYS, Inc. and/or its affiliates.
! SPDX-License-Identifier: MIT
! 
!
! Permission is hereby granted, free of charge, to any person obtaining a copy
! of this software and associated documentation files (the "Software"), to deal
! in the Software without restriction, including without limitation the rights
! to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
! copies of the Software, and to permit persons to whom the Software is
! furnished to do so, subject to the following conditions:
!
! The above copyright notice and this permission notice shall be included in all
! copies or substantial portions of the Software.
!
! THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
! IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
! FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
! AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
! LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
! OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
! SOFTWARE.

/COM,ANSYS MEDIA REL. 2023R2 (05/12/2023) REF. VERIF. MANUAL: REL. 2023R2
/VERIFY,vmr049-cr3-182
/TITLE,vmr049-cr3-182,VARIABLE-LOAD UNIAXIAL CREEP BENCHMARK
/COM,THE COMPARISON IS MADE GRAPHICALLY WITH THE RESULTS OF THE TEST CR-3A
/COM,FROM NAFEMS REPORT R0049.
/SHOW,JPEG
JPGPRF,500,100,1

/PREP7
C*** PARAMETRIC INPUT FOR CREEP CONSTANT !(TIME HARDENING)***
*SET,C1,1.5625E-14
*SET,C2,5
*SET,C3,-0.5
*SET,C4,0
C*** TIME PARAMETER
*SET,HOUR,200
C*** ELASTIC CONSTANT
MP,EX,1,200E3
MP,NUXY,1,0.3
TUNIF,HOT
TOFF,1E-10
TB,CREEP,1,,,2
TBDATA,1,C1,C2,C3,C4
C*** PARAMETRIC INPUT FOR CREEP CONSTANT !(STRAIN HARDENING)***
*SET,C1,1.5625E-14
*SET,C2,5
*SET,C3,-0.5
*SET,C4,0
C*** ELASTIC CONSTANT
MP,EX,2,200E3
MP,NUXY,2,0.3
TB,CREEP,2,,,7
TBDATA,1,C1,C2,C3,C4
N,1,0,0,0
N,2,100,0
N,3,100,50
N,4,0,50
N,5,100,100
N,6,0,100
ET,1,182
KEYOPT,1,1,1
KEYOPT,1,3,0
E,1,2,3,4
E,4,3,5,6
NSEL,S,LOC,X,
D,ALL,UX,
NSEL,ALL
NSEL,S,,,4
D,ALL,UY,
NSEL,ALL
*DO,I,1,2
*STATUS,I

/PREP7
*IF,I,EQ,1,THEN
MAT,1
*ELSEIF,I,EQ,2,THEN
MPCHG,2,ALL
*ENDIF
FINISH

/SOLU
NSEL,S,LOC,X,100
SF,ALL,PRES,-200
NSEL,ALL
RATE, OFF
DELT,1.0E-8,1.0E-9,1.0E-8
TIME, 1.0E-8
/OUTPUT,SCRATCH
OUTRES,ALL,LAST
SOLVE
/OUT
RATE, ON, ON
DELT,1E-2,1E-2,1
TIME,100
/OUTPUT,SCRATCH
OUTRES,ALL,ALL
SOLVE
/OUT

/SOLU
SFDELE,ALL,ALL
NSEL,S,LOC,X,100
SF,ALL,PRES,-250
NSEL,ALL
RATE, OFF
DELT,1.0E-8,1.0E-9,1.0E-8
TIME,100.00000001
/OUTPUT,SCRATCH
OUTRES,ALL,LAST
SOLVE
/OUT
RATE, ON, ON
DELT,1,1,10
TIME,200
/OUTPUT,SCRATCH
OUTRES,ALL,ALL
SOLVE
/OUT
FINISH

*IF,I,EQ,1,THEN

/POST1
SET,,,,,,,1
*GET,S1,NODE,3,EPCR,X
R1=1.000
SET,,,,,,,62
*GET,S2,NODE,3,EPCR,X
R2=S2/0.068
SET,,,,,,,112
*GET,S3,NODE,3,EPCR,X
R3=S3/0.097
SET,,,,,,,123
*GET,S4,NODE,3,EPCR,X
R4=S4/0.166
SET,,,,,,,128
*GET,S5,NODE,3,EPCR,X
R5=S5/0.221
*DIM,VALUE,,5,3
*DIM,VALUE2,,5,3
*DIM,LABEL,CHAR,10
*DIM,TARGET,CHAR,10
LABEL(1) = '0','50','100','150','200'
TARGET(1) = '0.000','0.068','0.097','0.166','0.221'
*VFILL,VALUE(1,1),DATA,S1,S2,S3,S4,S5
*VFILL,VALUE2(1,1),DATA,R1,R2,R3,R4,R5

/POST26
/GROPT,VIEW,0
/GTHK,CURVE,1
/GROPT,FILL,OFF
/GRID,1
/GTHK,GRID,1
/GROPT,CGRID,1
/GTHK,AXIS,1
/GRTYP,0
/GROPT,DIG1,5
/GROPT,DIG2,3
/GROPT,DIVX,4
/GROPT,DIVY,7
/XRANGE,0,200
/YRANGE,0,0.35
ESOL,2,1,,EPCR,X,TIMEHARDENING
PRVAR,2
/COLOR,CURVE,GREE,1
/AXLAB,X,TIME
/AXLAB,Y,CREEP STRAIN(XX)
PLVAR,2

*ELSEIF,I,EQ,2,THEN
/POST1
SET,,,,,,,1
*GET,S1_2,NODE,3,EPCR,X
R6=1.000
SET,,,,,,,62
*GET,S2_2,NODE,3,EPCR,X
R7=S2_2/0.070
SET,,,,,,,112
*GET,S3_2,NODE,3,EPCR,X
R8=S3_2/0.099
SET,,,,,,,123
*GET,S4_2,NODE,3,EPCR,X
R9=S4_2/0.236
SET,,,,,,,128
*GET,S5_2,NODE,3,EPCR,X
R10=S5_2/0.315
*DIM,VALUE3,,5,3
*DIM,VALUE4,,5,3
*DIM,LABEL2,CHAR,10
*DIM,TARGET2,CHAR,10
*VFILL,VALUE3(1,1),DATA,S1_2,S2_2,S3_2,S4_2,S5_2
*VFILL,VALUE4(1,1),DATA,R6,R7,R8,R9,R10
LABEL2(1) = '0','50','100','150','200'
TARGET2(1) = '0.000','0.070','0.099','0.236','0.315'

/POST26
/NOERASE
/AXLAB,X,
/AXLAB,Y,
/GROPT,DIVX,4
/GROPT,DIVY,7
/XRANGE,0,200
/YRANGE,0,0.35
ESOL,3,1,,EPCR,X,STRAINHARDENING
/COLOR,CURVE,BMAG,1
/AXLAB,X,TIME
/AXLAB,Y,CREEP STRAIN(XX)
PLVAR,3
PRVAR,3
*ENDIF
*ENDDO
/COM
/COM
/COM
/COM,--------- vmr049-cr3-182 TIME HARDENING COMPARISON-----------
/COM
/COM  COMPARE NUMERICAL VALUES LISTED
/COM  BELOW WITH GRAPH OF FIGURE 3.12A ON
/COM  PAGE 105 AS NO NUMERICAL RESULTS ARE
/COM  PRESENTED.
/COM
/COM,| TIME |  TARGET |  Mechanical APDL  |  RATIO |
/COM,
*VWRITE,LABEL(1),TARGET(1),VALUE(1,1),VALUE2(1,1)
(1X,A6,'   ',1X,A6,'   ',F14.3,'   ',1F12.3,'   ')
/COM
/COM
/COM,--------- vmr049-cr3-182 STRAIN HARDENING COMPARISON-----------
/COM,
/COM,| TIME |  TARGET |  Mechanical APDL  |  RATIO |
/COM,
*VWRITE,LABEL2(1),TARGET2(1),VALUE3(1,1),VALUE4(1,1)
(1X,A6,'   ',1X,A6,'   ',F14.3,'   ',1F12.3,'   ')
FINISH


/POST26
*DIM,LABEL3,CHAR,2
*DIM,VALUE5,,2,3
LABEL3(1) = ' S2 ',' S7 '
*VFILL,VALUE5(1,1),DATA,S5,S5_2
*VFILL,VALUE5(1,2),DATA,R5,R10
*DIM,LABEL4,CHAR,2
LABEL4(1) = 'vmr049-','cr3-182'

/OUT,vmr049-cr3-182,vrt
/COM
/COM,------------------- vmr049-cr3 RESULTS COMPARISON ---------------------
/COM,
/COM,            |   Mechanical APDL   |   RATIO  |         INPUT         |
/COM,
/COM, PLANE182
*VWRITE,LABEL3(1),VALUE5(1,1),VALUE5(1,2),LABEL4(1),LABEL4(2)
(1X,A8,'      ',F14.4,'      ',F9.4,'     ',A7,A8)
*VWRITE,LABEL3(2),VALUE5(2,1),VALUE5(2,2),LABEL4(1),LABEL4(2)
(1X,A8,'      ',F14.4,'      ',F9.4,'     ',A7,A8)
/COM,
/COM,----------------------------------------------------------------
/OUT

FINISH
*LIST,vmr049-cr3-182,vrt










/COM,ANSYS MEDIA REL. 2023R1 (11/04/2022) REF. VERIF. MANUAL: REL. 2023R1
/VERIFY,VM136
/PREP7                    
MP,PRXY,,0.3
JPGPRF,500,100,1             ! MACRO TO SET PREFS FOR JPEG PLOTS
/SHOW,JPEG
/TITLE, VM136, LARGE DEFLECTIONS OF A BUCKLED BAR (THE ELASTICA)
C***    THEORY OF ELASTIC STABILITY, TIMOSHENKO AND GERE, 2ND ED., PAGE 78
ANTYPE,STATIC
NLGEOM,ON                 ! ACTIVATE LARGE DEFLECTION PROCESS
ET,1,BEAM188
KEYOPT,1,3,3
SECT,1,BEAM,RECT
SECD,0.5,0.5
MP,EX,1,3E7
N,1
N,11,,100
FILL
E,1,2
EGEN,10,1,1
D,ALL,UZ,,,,,ROTX,ROTY
FINISH
/SOLU    
NEQIT,150                 ! PERFORM MAXIMUM 150 EQUILIBRIUM ITERATIONS
OUTPR,BASIC,LAST          ! PRINT BASIC SOLUTION AT THE END OF EACH LOAD STEP
D,1,ALL
FCR=-38.553               ! SET CRITICAL LOAD PARAMETER
PI=3.14159265359
F,11,FY,FCR*1.015         ! VERTICAL LOAD
F,11,FX,.5                ! SMALL HORIZONTAL LOAD
/OUT,SCRATCH
SOLVE                     ! LOAD STEP 1
FDEL,11,FX                ! REMOVE PERTURBING HORIZONTAL LOAD
F,11,FY,FCR*1.063
SOLVE                     ! LOAD STEP 2
F,11,FY,FCR*1.152
SOLVE                     ! LOAD STEP 3
F,11,FY,FCR*1.293
SOLVE                     ! LOAD STEP 4
F,11,FY,FCR*1.518
SOLVE                     ! LOAD STEP 5
F,11,FY,FCR*1.884
SOLVE                     ! LOAD STEP 6
FINISH
/POST1
/USER
/FOCUS,,50,50             ! USER FOCUS TO CENTER ALL DISPLAYS
/DIST,,55                 ! SELECT DISTANCE FOR MAGNIFICATION
/DSCALE,,1                ! SCALE TRUE TO GEOMETRY
/OUT,
SET,1,0  
PLDISP,1
/NOERASE                  ! OVERLAY DISPLAYS ON SAME FRAME
SET,2,0  
PLDISP
SET,3,0  
PLDISP
*GET,UX3,NODE,11,U,X
*GET,UY3,NODE,11,U,Y
*GET,ROT3A,NODE,11,ROT,Z
SET,4,0  
PLDISP
*GET,UX4,NODE,11,U,X
*GET,UY4,NODE,11,U,Y
*GET,ROT4A,NODE,11,ROT,Z
SET,5,0  
PLDISP
*GET,UX5,NODE,11,U,X
*GET,UY5,NODE,11,U,Y
*GET,ROT5A,NODE,11,ROT,Z
SET,6,0  
PLDISP
*GET,UX6,NODE,11,U,X
*GET,UY6,NODE,11,U,Y
*GET,ROT6A,NODE,11,ROT,Z
*status,parm
*SET,ROT6,(180*ROT6A/PI)
*SET,ROT5,(180*ROT5A/PI)
*SET,ROT4,(180*ROT4A/PI)
*SET,ROT3,(180*ROT3A/PI)
*DIM,LABEL,CHAR,3,2
*DIM,VALUE,,3,3
LABEL(1,1) = 'ROT Z ','UX','UY'
LABEL(1,2) = ' DEG',' in',' in'
*VFILL,VALUE(1,1),DATA,-60,59.3,-25.9
*VFILL,VALUE(1,2),DATA,ROT3,UX3,UY3
*VFILL,VALUE(1,3),DATA,ABS(ROT3/60),ABS(UX3/59.3),ABS(UY3/25.9)
/COM
/OUT,vm136,vrt
/COM,------------------- VM136 RESULTS COMPARISON -------------
/COM,
/COM,    F=44.413 lb  |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.1,'  ',F12.1,'   ',1F15.3)
/COM,----------------------------------------------------------
/OUT
/NOPR
*VFILL,VALUE(1,1),DATA,-80,71.9,-44
*VFILL,VALUE(1,2),DATA,ROT4,UX4,UY4
*VFILL,VALUE(1,3),DATA,ABS(ROT4/80),ABS(UX4/71.9),ABS(UY4/44)
/GOPR
/COM
/OUT,vm136,vrt,,APPEND
/COM,------------------- VM136 RESULTS COMPARISON -------------
/COM,
/COM,    F=49.849 lb  |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.1,'  ',F12.1,'   ',1F15.3)
/COM,----------------------------------------------------------
/OUT
/NOPR
*VFILL,VALUE(1,1),DATA,-100,79.2,-65.1
*VFILL,VALUE(1,2),DATA,ROT5,UX5,UY5
*VFILL,VALUE(1,3),DATA,ABS(ROT5/100),ABS(UX5/79.2),ABS(UY5/65.1)
/GOPR
/COM
/OUT,vm136,vrt,,APPEND
/COM,------------------- VM136 RESULTS COMPARISON -------------
/COM,
/COM,    F=58.523 lb  |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.1,'  ',F12.1,'   ',1F15.3)
/COM,----------------------------------------------------------
/OUT 
/NOPR
*VFILL,VALUE(1,1),DATA,-120,80.3,-87.7
*VFILL,VALUE(1,2),DATA,ROT6,UX6,UY6
*VFILL,VALUE(1,3),DATA,ABS(ROT6/120),ABS(UX6/80.3),ABS(UY6/87.7)
/GOPR
/COM
/OUT,vm136,vrt,,APPEND
/COM,------------------- VM136 RESULTS COMPARISON -------------
/COM,
/COM,    F=72.634 lb  |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.1,'  ',F12.1,'   ',1F15.3)
/COM,----------------------------------------------------------
/OUT
FINISH
*LIST,vm136,vrt

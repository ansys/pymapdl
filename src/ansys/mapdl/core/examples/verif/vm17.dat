/COM,ANSYS MEDIA REL. 2023R1 (11/04/2022) REF. VERIF. MANUAL: REL. 2023R1
/VERIFY,VM17
JPGPRF,500,100,1         ! MACRO TO SET PREFS FOR JPEG PLOTS
/SHOW,JPEG
/PREP7
/TITLE, VM17, SNAP-THROUGH BUCKLING OF A HINGED SHELL
!    CHANG, C.C.,"PERIODICALLY RESTARTED QUASI-NEWTON UPDATES IN
!    IN CONSTANT ARC-LENGTH METHOD", COMPUTERS AND STRUCTURES,
!    VOL. 41, NO. 5, PP. 963-972, 1991.
/TITLE, VM17, SNAP-THROUGH BUCKLING OF A HINGED SHELL
C***    USING SHELL181 ELEMENTS
/PREP7
ANTYPE,STATIC            ! STATIC ANALYSIS
ET,1,SHELL181
SECT,1,SHELL
SECD,6.350,1             ! SHELL THICKNESS
MP,EX,1,3102.75
MP,NUXY,1,0.3
!  CREATE FINITE ELEMENT MODEL
R1 = 2540                ! SHELL MID-SURFACE RADIUS
L  = 254                 ! HALF THE LENGTH
PI = 4*ATAN(1)           ! VALUE OF PI COMPUTED
THETA = 0.1*180/PI       ! 0.1 RADIANS CONVERTED TO DEGREES
CSYS,1                   ! CYLINDRICAL CO-ORDINATE SYSTEM
N,1,R1,90                ! NODES 1 AND 2 ARE CREATED AT POINTS
N,2,R1,90,L              ! A AND B RESPECTIVELY.
N,3,R1,(90-THETA)
N,4,R1,(90-THETA),L
FILL,1,2,1,5,1
FILL,2,4,1,6,1
FILL,4,3,1,7,1
FILL,3,1,1,8,1
FILL,8,6,1,9,1          
E,3,8,9,7
E,7,9,6,4
E,8,1,5,9
E,9,5,2,6
FINISH
*CREATE,SOLVIT,MAC
/PREP7
!  APPLY BOUNDARY CONDITIONS
NSEL,S,LOC,Z,0
DSYM,SYMM,Z
NSEL,S,LOC,Y,90
DSYM,SYMM,X
NSEL,S,LOC,Y,(90-THETA)
D,ALL,UX,,,,,UY,UZ
NSEL,ALL
FINISH
!   SOLUTION PHASE
! SINCE THE SOLUTION OUTPUT IS SUBSTANTIAL IT IS DIVERTED TO A SCRATCH FILE
/OUTPUT,SCRATCH
/SOLUTION
NLGEOM,ON                ! LARGE DEFLECTION TURNED ON
OUTRES,,1                ! WRITE SOLUTION ON RESULTS FILE FOR EVERY SUBSTEP 
F,1,FY,-250              ! 1/4 TH OF THE TOTAL LOAD APPLIED DUE TO SYMMETRY
NSUBST,30                ! BEGIN WITH 30 SUBSTEPS
ARCLEN,ON,5              ! ARC-LENGTH SOLUTION TECHNIQUE TURNED ON WITH
                         !  MAX. ARC-LENGTH KEPT AT 5 TO COMPUTE AND STORE
			 !  SUFFICIENT INTERMEDIATE SOLUTION INFORMATION
SOLVE
FINISH
/OUTPUT
!   POSTPROCESSING PHASE
/POST26
NSOL,2,1,U,Y             ! STORE UY DISPLACEMENT OF NODE 1
NSOL,3,2,U,Y             ! STORE UY DISPLACEMENT OF NODE 2
PROD,4,1,,,LOAD,,,4*250  ! TOTAL LOAD IS 4*250 DUE TO QUARTER SYMMETRY
PROD,5,2,,,,,,-1         ! CHANGE SIGNS OF THE DISPLACEMENT VALUES
PROD,6,3,,,,,,-1
*GET,UY1,VARI,2,EXTREM,VMIN
*GET,UY2,VARI,3,EXTREM,VMIN
PRVAR,2,3,4              ! PRINT STORED INFORMATION
/AXLAB,X, DEFLECTION (MM)
/AXLAB,Y, TOTAL LOAD (N)
/GRID,1
/XRANGE,0,35
/YRANGE,-500,1050
XVAR,5   
PLVAR,4                  ! PLOT LOAD WITH RESPECT TO -UY OF NODE 1
/NOERASE
XVAR,6
PLVAR,4                  ! PLOT LOAD WITH RESPECT TO -UY OF NODE 2
/ERASE
*DIM,LABEL,CHAR,2,2
*DIM,VALUE,,2,3
LABEL(1,1) = 'UY @A ','UY @B '
LABEL(1,2) = 'mm ','mm '
*VFILL,VALUE(1,1),DATA,-30,-26 
*VFILL,VALUE(1,2),DATA,UY1,UY2
*VFILL,VALUE(1,3),DATA,ABS(UY1/30) ,ABS(UY2/26 )
FINISH
*END
SOLVIT
SAVE,TABLE_1              
/CLEAR, NOSTART          ! CLEAR DATABASE FOR NEXT SOLUTION
/TITLE, VM17, SNAP-THROUGH BUCKLING OF A HINGED SHELL
C***    USING SHELL281 ELEMENTS
/PREP7
ANTYPE,STATIC            ! STATIC ANALYSIS
ET,1,SHELL281
SECT,1,SHELL
SECD,6.350,1             ! SHELL THICKNESS
MP,EX,1,3102.75
MP,NUXY,1,0.3
!  CREATE FINITE ELEMENT MODEL
R1 = 2540                ! SHELL MID-SURFACE RADIUS
L  = 254                 ! HALF THE LENGTH
PI = 4*ATAN(1)           ! VALUE OF PI COMPUTED
THETA = 0.1*180/PI       ! 0.1 RADIANS CONVERTED TO DEGREES
CSYS,1                   ! CYLINDRICAL CO-ORDINATE SYSTEM
N,1,R1,90                ! NODES 1 AND 2 ARE CREATED AT POINTS
N,2,R1,90,L              ! A AND B RESPECTIVELY.
N,3,R1,(90-THETA)
N,4,R1,(90-THETA),L
FILL,1,2,3,5,1
FILL,2,4,3,8,1
FILL,4,3,3,11,1
FILL,3,1,3,14,1
FILL,6,12,3,17,1  
FILL,9,15,3,20,1
NUMMRG,NODE
E,3,12,18,15,13,19,22,14  
E,12,4,9,18,11,10,20,19
E,15,18,6,1,22,17,5,16
E,18,9,2,6,20,8,7,17 
FINISH
SOLVIT
SAVE,TABLE_2
/NOPR
RESUME,TABLE_1
/COM
/OUT,vm17,vrt
/COM,------------------- VM17 RESULTS COMPARISON ---------------------
/GOPR
/COM,
/COM,SHELL181
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.1,'  ',F14.1,'   ',1F15.3)
/NOPR
RESUME,TABLE_2
/GOPR
/COM,
/COM,SHELL281
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.1,'  ',F14.1,'   ',1F15.3)
/COM,-------------------------------------------------------------
/OUT
FINISH
*LIST,vm17,vrt
/DELETE,TABLE_1
/DELETE,TABLE_2
/DELETE,SOLVIT,MAC

/COM,ANSYS MEDIA REL. 150 (11/8/2013) REF. VERIF. MANUAL: REL. 150
/VERIFY,VM1
/PREP7
/TITLE, VM1, STATICALLY INDETERMINATE REACTION FORCE ANALYSIS
C***      STR. OF MATL., TIMOSHENKO, PART 1, 3RD ED., PAGE 26, PROB.10
ANTYPE,STATIC                  ! STATIC ANALYSIS
ET,1,LINK180
SECTYPE,1,LINK
SECDATA,1  			       ! CROSS SECTIONAL AREA (ARBITRARY) = 1
MP,EX,1,30E6
N,1
N,2,,4
N,3,,7
N,4,,10
E,1,2                          ! DEFINE ELEMENTS
EGEN,3,1,1
D,1,ALL,,,4,3                  ! BOUNDARY CONDITIONS AND LOADING
F,2,FY,-500
F,3,FY,-1000
FINISH
/SOLU    
OUTPR,BASIC,1
OUTPR,NLOAD,1
SOLVE
FINISH
/POST1
NSEL,S,LOC,Y,10
FSUM
*GET,REAC_1,FSUM,,ITEM,FY
NSEL,S,LOC,Y,0
FSUM
*GET,REAC_2,FSUM,,ITEM,FY

*DIM,LABEL,CHAR,2
*DIM,VALUE,,2,3
LABEL(1) = 'R1, lb','R2, lb '
*VFILL,VALUE(1,1),DATA,900.0,600.0
*VFILL,VALUE(1,2),DATA,ABS(REAC_1),ABS(REAC_2)
*VFILL,VALUE(1,3),DATA,ABS(REAC_1 / 900) ,ABS( REAC_2 / 600)
/OUT,vm1,vrt
/COM
/COM,------------------- VM1 RESULTS COMPARISON ---------------------
/COM,
/COM,         |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
*VWRITE,LABEL(1),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,'   ',F10.1,'  ',F10.1,'   ',1F5.3)
/COM,----------------------------------------------------------------
/OUT
FINISH
*LIST,vm1,vrt

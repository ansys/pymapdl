/COM,ANSYS MEDIA REL. 2023R1 (11/04/2022) REF. VERIF. MANUAL: REL. 2023R1
/VERIFY,VM91
/PREP7                     
JPGPRF,500,100,1             ! MACRO TO SET PREFS FOR JPEG PLOTS
/SHOW,JPEG
/TITLE, VM91, LARGE ROTATION OF A SWINGING PENDULUM
C*** VIBRATION THEORY AND APPLICATIONS, THOMSON, 2ND PRINTING, PAGE 138,EX 5.4-1
/NOPR
ANTYPE,TRANS          ! NONLINEAR TRANSIENT DYNAMIC ANALYSIS
NLGEOM,ON             ! LARGE DEFLECTION OPTION 
ET,1,LINK180
ET,2,MASS21,,,2
SECTYPE,1,LINK   
SECDATA,0.1           ! AREA = .1
R,2,.5                ! MASS = 0.5
MP,EX,1,3E7
N,1
N,2,60,-80
E,1,2                 ! ROD
TYPE,2
REAL,2
E,2                   ! MASS
SAVE
FINISH
*CREATE,SLV
/out,scratch
/SOLU    
ACEL,,386
KBC,1                 ! STEP BOUNDARY CONDITION
D,1,UZ,,,2
D,1,UX,,,,,UY
TIME,.01              ! INITIAL L.S. TO ATTAIN FINAL ACCELERATION
NSUBST,5
OUTRES,,1
LSWRITE,1             ! WRITE LOAD STEP FILE 1
TIME,.82071           ! SUCCEEDING LOAD STEPS AT T/4 INCREMENTS
NSUBST,8
OUTRES,,1
LSWRITE,2             ! WRITE LOAD STEP FILE 2
TIME,1.64142 
LSWRITE,3             ! WRITE LOAD STEP FILE 3
TIME,2.46213
LSWRITE,4             ! WRITE LOAD STEP FILE 4
TIME,3.28284
LSWRITE,5             ! WRITE LOAD STEP FILE 5
LSSOLVE,1,5,1         ! READ IN LOAD STEP FILES 1 THROUGH 5 AND SOLVE
FINISH
/out
*END
*USE,SLV
*CREATE,P26
/POST26
NSOL,2,2,U,X,UX2       ! STORE NODE 2 DISPLACEMENTS
NSOL,3,2,U,Y,UY2
PRVAR,2,3              ! PRINT DISPLACEMENTS VS. TIME  
/AXLAB,Y,DISPLACEMENTS ! Y-AXIS LABEL
PLVAR,2,3              ! DISPLAY DISPLACEMENTS VS. TIME.
T = 3.28284
*GET,DEFX_Q,VARI,2,RTIME,T/4
*GET,DEFY_Q,VARI,3,RTIME,T/4
*GET,DEFX_H,VARI,2,RTIME,T/2
*GET,DEFY_H,VARI,3,RTIME,T/2
*GET,DEFX_3Q,VARI,2,RTIME,3*T/4
*GET,DEFY_3Q,VARI,3,RTIME,3*T/4
*GET,DEFX_F,VARI,2,RTIME,T
*GET,DEFY_F,VARI,3,RTIME,T
*DIM,LABEL,CHAR,8,2
*DIM,VALUE,,8,3
LABEL(1,1) = 'DEFX,in ','DEFY,in ','DEFX,in ','DEFY,in ','DEFX,in ','DEFY,in '
LABEL(7,1) = 'DEFX,in ','DEFY,in '
LABEL(1,2) = 'TIME=T/4','TIME=T/4','TIME=T/2','TIME=T/2','TIM=3T/4','TIM=3T/4'
LABEL(7,2) = 'TIME= T ','TIME= T '
*END
*USE,P26
*VFILL,VALUE(1,1),DATA,-60,-20,-120,0,-60,-20,0,0
*VFILL,VALUE(1,2),DATA,DEFX_Q,DEFY_Q,DEFX_H,DEFY_H,DEFX_3Q,DEFY_3Q,DEFX_F,DEFY_F
*VFILL,VALUE(1,3),DATA,ABS(DEFX_Q/60),ABS(DEFY_Q/20),ABS(DEFX_H/120),0,ABS(DEFX_3Q/60)
*VFILL,VALUE(6,3),DATA,ABS(DEFY_3Q/20),0,0
SAVE,TABLE1
FINISH
RESUME,TABLE1
/OUT,vm91,vrt
/COM,
/COM,------------------- VM91 RESULTS COMPARISON---------------
/COM,
/COM, LINK180 Results |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.4,'  ',F14.4,'   ',1F15.3)
/COM,----------------------------------------------------------
/OUT
FINISH
/DELETE,vm91,s01
/DELETE,vm91,s02
/DELETE,vm91,s03
/DELETE,vm91,s04
/DELETE,vm91,s05
FINISH
*LIST,vm91,vrt

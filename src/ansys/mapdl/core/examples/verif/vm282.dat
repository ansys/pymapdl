/COM,ANSYS MEDIA REL. 150 (11/8/2013) REF. VERIF. MANUAL: REL. 150
/VERIFY,VM282
/TITLE,VM282,STEADY STATE VIBRATION ANALYSIS OF PISTON-FLUID SYSTEM
/COM, --------------------------------------------------------------
/COM, REFERENCE:
/COM, F. AXISA and J. ANTUNES, MODELLING OF MECHANICAL SYSTEMS: 
/COM, FLUID-STRUCTURE INTERACTION, VOLUME 3, 2007, PAGE:486.
/COM, --------------------------------------------------------------
JPGPRF,500,100,1

/OUT,SCRATCH
! *** FLUID COLUMN ***
!---------------------
LF = 24										! TUBE LENGTH
RAD = 0.125									! TUBE RADIUS
RHOF = 1000									! FLUID VELOCITY
SONCF = 1000     							! SONIC VELOCITY (SPEED OF SOUND IN A FLUID)
! *** CALCULATED FLUID PARAMETERS ***
!------------------------------------
PI = ACOS(-1)
SF = PI*RAD**2
MF = RHOF*SF*LF
KF = RHOF*SONCF**2*SF/LF
! *** PISTON (UNDAMPED -> MODAL DAMPING) ***
!-------------------------------------------
KS = KF 									! SPRING STIFFNESS (GAMMA = KF/KS = 1)
MS = MF/2                                   ! POINT MASS  (MU = MF/MS = 2)
F0 = 1000									! APPLIED FORCE
/PREP7
LOCAL,13,0,0,0,0, , , 90,1,1,  
WPCSYS,-1,13,   
CYl4,0,0,RAD, , , ,LF  
CSYS,0
N,1,-1,0,0
N,2,0,0,0
ET,1,COMBIN14                           	! ELEMENT 1 - COMBIN14
KEYOPT,1,3,0                            	! UX DEGREE OF FREEDOM
R,1,KS
ET,2,MASS21                             	! ELEMENT 2 - MASS21
KEYOPT,2,3,2                            	! 3D MASS WITHOUT ROTARY INERTIA
R,2,MS
ET,3,FLUID30,,0,,0							! ELEMENT 3 - FLUID WITH UNSYM COUPLING
R,3
MP,DENS,3,RHOF
MP,SONC,3,SONCF
ET,4,CONTA174								! ELEMENT 4 - CONTACT ELEMENTS
R,4
KEYOPT,4,2,2               					! MPC STYLE CONTACT
KEYOPT,4,4,2               					! RIGID CERIG STYLE LOAD
KEYOPT,4,12,5              					! BONDED CONTACT 
ET,5,TARGE170								! ELEMENT 5 - PILOT NODE
KEYOPT,5,2,1               					! DON'T FIX THE PILOT NODE
KEYOPT,5,4,111111
TYPE,1
REAL,1
EN,1,1,2
TYPE,2
REAL,2
EN,2,2
TYPE,3
MAT,3
REAL,3
ESIZE,LF/200
VSWEEP,1
VSEL,S,,,1
NSEL,R,LOC,X,0
TYPE,4
REAL,4
ESURF
TYPE,5
TSHAPE,PILO
E,2
TSHAPE
ALLSEL,ALL,ALL
ESEL,S,ENAME,,30
NSEL,S,LOC,X,0
SF,ALL,FSI
ALLSEL,ALL,ALL
D,1,ALL
D, 2, UY, , , , , UZ, ROTX, ROTY, ROTZ
F,2,FX,F0
ALLSEL,ALL,ALL
SAVE
FINISH
! *** SOLUTION CONTROLS FOR MODAL ANALYSIS *** 
! --------------------------------------------
/SOLUTION
ANTYPE,MODAL
MODOPT,UNSYM,10,,,,,BOTH
MXPAND,ALL
SOLVE
*GET,FREQ_1,MODE,2,FREQ
*GET,FREQ_2,MODE,3,FREQ
*GET,FREQ_3,MODE,4,FREQ
*GET,FREQ_4,MODE,5,FREQ
*GET,FREQ_5,MODE,6,FREQ
FINISH
! *** SOLUTION CONTROLS FOR MSUP HARMONIC ANALYSIS *** 
! ----------------------------------------------------
/SOLUTION
ANTYPE,HARM
HROPT,MSUP
HARFRQ,0.0,100.0
KBC,1
NSUB,1000
OUTRES,ERASE
OUTRES,ALL,NONE
OUTRES,NSOL,ALL
! *** MODAL DAMPING RATIOS ***
! ----------------------------
MDAMP,1,0.0,2.0453E-02,3.6948E-03,7.0040E-04,2.2468E-04,9.8247E-05
FDELE,ALL
LVSCALE,1
SOLVE
FINISH
! *** EXPANSION PASS *** 
! ----------------------
/SOLUTION
EXPASS,ON
NUMEXP,ALL,,,NO
SOLVE
FINISH
/POST1
FILE,vm282,rst
SET,,,,3,1
*GET,RES_1,NODE,2,U,X
*GET,PRES_1,NODE,1712,PRES
SET,,,,3,9.9
*GET,RES_2,NODE,2,U,X
*GET,PRES_2,NODE,1712,PRES
FINISH
! *** TIME HISTORY POST PROCESSOR *** 
! -----------------------------------
/POST26
/GROPT,LOGX,ON
/GROPT,LOGY,ON
NSOL,2,2,U,X
/SHOW,PNG
PLVAR,2
PRCPLX,1
PRVAR,2
EXTREME,2
NPRES = NODE(LF/2,0,0)
NSOL,3,NPRES,PRES
PLVAR,3
PRVAR,3
EXTREME,3
/SHOW,CLOSE
FINISH
/OUT,
/COM, -----------------------------
/COM, FREQUENCY (Hz) VALUES
/COM, -----------------------------
*STATUS,FREQ_1
*STATUS,FREQ_2
*STATUS,FREQ_3
*STATUS,FREQ_4
*STATUS,FREQ_5
/COM, -----------------------------
/COM, RESPONSE AMPLITUDE OF PISTON
/COM, -----------------------------
*STATUS,RES_1
*STATUS,RES_2
/COM, ------------------------------------------
/COM, PRESSURE AMPLITUDE AT MID-COLUMN OF FLUID
/COM, ------------------------------------------
*STATUS,PRES_1
*STATUS,PRES_2
/OUT,SCRATCH

*DIM,LABEL,CHAR,5,3
*DIM,VALUE,,5,3
LABEL(1,1) = 'FREQ(f1),      ','FREQ(f2)  ','FREQ(f3)  ','FREQ(f4)  ','FREQ(f5)  '  
LABEL(1,2) = '     Hz','     Hz','     Hz','     Hz','     Hz' 
*VFILL,VALUE(1,1),DATA,9.916,24.583,43.729,63.895,84.395
*VFILL,VALUE(1,2),DATA,FREQ_1,FREQ_2,FREQ_3,FREQ_4,FREQ_5
*VFILL,VALUE(1,3),DATA,FREQ_1/9.9167,FREQ_2/24.5833,FREQ_3/43.7292,FREQ_4/63.8958,FREQ_5/84.3958

*DIM,LAB1,CHAR,2,2
*DIM,VALUE1,,2,3
LAB1(1,1) = 'RES_1, ','RES_f1  '
LAB1(1,2) = '  M','  M'
*VFILL,VALUE1(1,1),DATA,2.47E-04,5.76E-03
*VFILL,VALUE1(1,2),DATA,RES_1,RES_2
*VFILL,VALUE1(1,3),DATA,ABS(RES_1)/2.47E-04,ABS(RES_2)/5.76E-03

*DIM,LAB2,CHAR,2,2
*DIM,VALUE2,,2,3
LAB2(1,1) = 'PRES_1, ','PRES_f1  '
LAB2(1,2) = '  MPa','  MPa'
*VFILL,VALUE2(1,1),DATA,1.03E+04,2.64E+05
*VFILL,VALUE2(1,2),DATA,PRES_1,PRES_2
*VFILL,VALUE2(1,3),DATA,ABS(PRES_1)/1.03E+04,ABS(PRES_2)/2.64E+05
/COM,
/OUT,vm282,vrt
/COM,------------------- VM282 RESULTS COMPARISON --------------
/COM,
/COM,                   |   TARGET   |   Mechanical APDL   			|   RATIO
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F12.3,'  ',F16.3,'   ',1F15.3)
/COM,
/COM,RESPONSE AMPLITUDE OF PISTON
/COM,
*VWRITE,LAB1(1,1),LAB1(1,2),VALUE1(1,1),VALUE1(1,2),VALUE1(1,3)
(1X,A8,A8,'   ',F12.5,'  ',F16.5,'   ',1F15.3)
/COM,
/COM,PRESSURE AMPLITUDE AT MID-COLUMN
/COM,
*VWRITE,LAB2(1,1),LAB2(1,2),VALUE2(1,1),VALUE2(1,2),VALUE2(1,3)
(1X,A8,A8,'   ',F12.3,'  ',F16.3,'   ',1F15.3)
/COM,----------------------------------------------------------

/OUT,
FINISH
*LIST,vm282,vrt
FINISH

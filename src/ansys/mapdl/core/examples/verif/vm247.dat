/COM,ANSYS MEDIA REL. 2023R1 (11/04/2022) REF. VERIF. MANUAL: REL. 2023R1
/VERIFY,VM247
JPGPRF,500,100,1                                         ! MACRO TO SET PREFS FOR JPEG PLOTS
/SHOW,JPEG
/TITLE, VM247, CAMPBELL DIAGRAMS AND CRITICAL SPEEDS USING SYMMETRIC BEARINGS
/COM,   REF: "THE DYNAMICS OF ROTOR-BEARING SYSTEMS USING FINITE ELEMENTS"
/COM,   JOURNAL OF ENG. FOR INDUSTRY - MAY 1976
/COM
JPGPRF,500,100,1
/SHOW,JPEG
/PREP7
*DIM,SPIN,,4		                                    ! SPIN VELOCITY (RPM)
SPIN(1) = 0.
SPIN(2) = 35000.
SPIN(3) = 70000.
SPIN(4) = 105000.
RO = 7806		                                          ! MATERIAL #1 : STEEL
PEX = 2.078E+11
PGXY = 1.E+12 		                                    ! NO SHEAR
MP,EX,1,PEX
MP,DENS,1,RO
MP,GXY,1,PGXY
ET,1,BEAM188,,,2                                            ! ELEMENT TYPE #1 : SHAFT
NBDIAM = 18                                                 ! SHAFT SECTION PROPERTIES
*DIM,DIAM,ARRAY,NBDIAM
DIAM(1) = 1.02E-2
DIAM(2) = 2.04E-2
DIAM(3) = 1.52E-2
DIAM(4) = 4.06E-2
DIAM(5) = DIAM(4)
DIAM(6) = 6.6E-2
DIAM(7) = DIAM(6)
DIAM(8) = 5.08E-2
DIAM(9) = DIAM(8)
DIAM(10) = 2.54E-2
DIAM(11) = DIAM(10)
DIAM(12) = 3.04E-2
DIAM(13) = DIAM(12)
DIAM(14) = 2.54E-2
DIAM(15) = DIAM(14)
DIAM(16) = 7.62E-2
DIAM(17) = 4.06E-2
DIAM(18) = DIAM(17)
*DO,I,1,NBDIAM
   SECTYPE,I,BEAM,CSOLID
   SECDATA,DIAM(I)/2
*ENDDO
SECTYPE,7,BEAM,CTUBE
SECDATA,(DIAM(7)/2 - (DIAM(7) - 3.04E-2)/2),DIAM(7)/2
SECTYPE,8,BEAM,CTUBE
SECDATA,(DIAM(8)/2 - (DIAM(8) - 3.56E-2)/2),DIAM(8)/2
SECTYPE,18,BEAM,CTUBE
SECDATA,(DIAM(18)/2 - (DIAM(18) - 3.04E-2)/2),DIAM(18)/2
ET,2,MASS21                                                ! ELEMENT TYPE #2 : DISK
R,20,1.401,1.401,1.401,0.002,0.00136,0.00136               ! REAL FOR DISK
ET,3,COMBIN14                                              ! ELEMENT TYPE #3 : BEARINGS
KEYOPT,3,2,2	                                         ! Y DIRECTION
ET,4,COMBIN14
KEYOPT,4,2,3	                                         ! Z DIRECTION
R,30,4.378E+7                                              ! BEARINGS
N,1 ,0.
N,2 ,1.27E-2
N,3 ,5.08E-2
N,4 ,7.62E-2
N,5 ,8.89E-2
N,6 ,10.16E-2
N,7 ,10.67E-2
N,8 ,11.43E-2
N,9 ,12.7E-2
N,10,13.46E-2
N,11,16.51E-2
N,12,19.05E-2
N,13,22.86E-2
N,14,26.67E-2
N,15,28.7E-2
N,16,30.48E-2
N,17,31.5E-2
N,18,34.54E-2
N,19,35.5E-2
BRG = 0		                                        ! BEARING "LENGTH" FOR VISUALISATION
N,20,16.51E-2,BRG
N,21,16.51E-2,,BRG
N,22,28.7E-2,BRG
N,23,28.7E-2,,BRG
TYPE,1                                                    ! CREATE SHAFT ELEMENTS
MAT,1
*DO,I,1,NBDIAM
   SECNUM,I
   E,I,I+1
*ENDDO
TYPE,2                                                    ! CREATE DISK ELEMENTS
REAL,20
E,5
TYPE,3                                                    ! CREATE BEARING ELEMENTS
REAL,30
E,11,20
E,15,22
TYPE,4
REAL,30
E,11,21
E,15,23
FINI
/SOLU
D,ALL,UX                                                  ! NO TRACTION & NO TORSION
D,ALL,ROTX
D,20,ALL
D,21,ALL
D,22,ALL
D,23,ALL
RATIO = 4*ATAN(1)/30
ANTYPE,MODAL
CORIOLIS,ON,,,ON                                         ! CORIOLIS ON IN A STATIONARY REFERENCE FRAME
NBF = 20
MODOPT,QRDAMP,NBF,,,ON
QRDOPT,ON								   ! REUSE FLAG ON
/OUT,SCRATCH
*DO,I,1,4
   OMEGA,SPIN(I)*RATIO
   MXPAND,NBF
   SOLVE
*ENDDO
FINI
/POST1
PRCAMP,,1.,RPM                                          ! PRINT CAMPBELL VALUES FOR SLOPE=1, UNIT= RPM
PLCAMP,,1.,RPM
*GET,CRIC1,CAMP,1,VCRI,,,
*GET,CRIC2,CAMP,2,VCRI,,,
*GET,CRIC3,CAMP,3,VCRI,,,
*GET,CRIC4,CAMP,4,VCRI,,,
*GET,CRIC5,CAMP,5,VCRI,,,
*GET,CRIC6,CAMP,6,VCRI,,,

PRCAMP,,4.,RPM                                          ! PRINT CAMPBELL VALUES FOR SLOPE=4, UNIT= RPM
PLCAMP,,4.,RPM
*GET,CRIC7,CAMP,1,VCRI,,,
*GET,CRIC8,CAMP,2,VCRI,,,
*GET,CRIC9,CAMP,3,VCRI,,,
*GET,CRIC10,CAMP,4,VCRI,,,
*GET,CRIC11,CAMP,5,VCRI,,,
*GET,CRIC12,CAMP,6,VCRI,,,

*DIM,LABEL,CHAR,1,12
*DIM,VALUE,,12,3
LABEL(1,1) = 'CRIC1'
LABEL(1,2) = 'CRIC2'
LABEL(1,3) = 'CRIC3'
LABEL(1,4) = 'CRIC4'
LABEL(1,5) = 'CRIC5'
LABEL(1,6) = 'CRIC6'
LABEL(1,7) = 'CRIC7'
LABEL(1,8) = 'CRIC8'
LABEL(1,9) = 'CRIC9'
LABEL(1,10) = 'CRIC10'
LABEL(1,11) = 'CRIC11'
LABEL(1,12) = 'CRIC12'
/COM,
/COM, WHIRL SPEEDS OBTAINED FOR SLOPE = 1
/COM,
*VFILL,VALUE(1,1),DATA,15470                            ! CRITICAL SPEED VALUE OBTAINED FROM REFERENCE
*VFILL,VALUE(1,2),DATA,CRIC1
*VFILL,VALUE(1,3),DATA,ABS(CRIC1/15470)
*VFILL,VALUE(2,1),DATA,17159                            ! CRITICAL SPEED VALUE OBTAINED FROM REFERENCE
*VFILL,VALUE(2,2),DATA,CRIC2
*VFILL,VALUE(2,3),DATA,ABS(CRIC2/17159)
*VFILL,VALUE(3,1),DATA,46612                            ! CRITICAL SPEED VALUE OBTAINED FROM REFERENCE
*VFILL,VALUE(3,2),DATA,CRIC3
*VFILL,VALUE(3,3),DATA,ABS(CRIC3/46612)
*VFILL,VALUE(4,1),DATA,49983                            ! CRITICAL SPEED VALUE OBTAINED FROM REFERENCE
*VFILL,VALUE(4,2),DATA,CRIC4
*VFILL,VALUE(4,3),DATA,ABS(CRIC4/49983)
*VFILL,VALUE(5,1),DATA,64752                            ! CRITICAL SPEED VALUE OBTAINED FROM REFERENCE
*VFILL,VALUE(5,2),DATA,CRIC5
*VFILL,VALUE(5,3),DATA,ABS(CRIC5/64752)
*VFILL,VALUE(6,1),DATA,96457                            ! CRITICAL SPEED VALUE OBTAINED FROM REFERENCE
*VFILL,VALUE(6,2),DATA,CRIC6
*VFILL,VALUE(6,3),DATA,ABS(CRIC6/96457)
/COM,
/COM, WHIRL SPEEDS OBTAINED FOR SLOPE = 4
/COM,
*VFILL,VALUE(7,1),DATA,4015                             ! CRITICAL SPEED VALUE OBTAINED FROM REFERENCE/4
*VFILL,VALUE(7,2),DATA,CRIC7
*VFILL,VALUE(7,3),DATA,ABS(CRIC7/4015)
*VFILL,VALUE(8,1),DATA,4120.25                          ! CRITICAL SPEED VALUE OBTAINED FROM REFERENCE/4
*VFILL,VALUE(8,2),DATA,CRIC8
*VFILL,VALUE(8,3),DATA,ABS(CRIC8/4120)
*VFILL,VALUE(9,1),DATA,11989.25                         ! CRITICAL SPEED VALUE OBTAINED FROM REFERENCE/4
*VFILL,VALUE(9,2),DATA,CRIC9
*VFILL,VALUE(9,3),DATA,ABS(CRIC9/11989)
*VFILL,VALUE(10,1),DATA,12200                           ! CRITICAL SPEED VALUE OBTAINED FROM REFERENCE/4
*VFILL,VALUE(10,2),DATA,CRIC10
*VFILL,VALUE(10,3),DATA,ABS(CRIC10/12200)
*VFILL,VALUE(11,1),DATA,18184.25                        ! CRITICAL SPEED VALUE OBTAINED FROM REFERENCE/4
*VFILL,VALUE(11,2),DATA,CRIC11
*VFILL,VALUE(11,3),DATA,ABS(CRIC11/18184)
*VFILL,VALUE(12,1),DATA,20162.25                        ! CRITICAL SPEED VALUE OBTAINED FROM REFERENCE/4
*VFILL,VALUE(12,2),DATA,CRIC12
*VFILL,VALUE(12,3),DATA,ABS(CRIC12/20162)
/COM
/OUT,vm247,vrt
/COM,------------ VM247 RESULTS COMPARISON --------------
/COM,
/COM,        |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
/COM, ---------------------------------------
/COM, WHIRL SPEEDS WITH SLOPE = 1.0
/COM, --------------------------------------
*VWRITE,LABEL(1,1),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,'   ',F10.4,'  ',F14.4,'   ',F15.3)
*VWRITE,LABEL(1,2),VALUE(2,1),VALUE(2,2),VALUE(2,3)
(1X,A8,'   ',F10.4,'  ',F14.4,'   ',F15.3)
*VWRITE,LABEL(1,3),VALUE(3,1),VALUE(3,2),VALUE(3,3)
(1X,A8,'   ',F10.4,'  ',F14.4,'   ',F15.3)
*VWRITE,LABEL(1,4),VALUE(4,1),VALUE(4,2),VALUE(4,3)
(1X,A8,'   ',F10.4,'  ',F14.4,'   ',F15.3)
*VWRITE,LABEL(1,5),VALUE(5,1),VALUE(5,2),VALUE(5,3)
(1X,A8,'   ',F10.4,'  ',F14.4,'   ',F15.3)
*VWRITE,LABEL(1,6),VALUE(6,1),VALUE(6,2),VALUE(6,3)
(1X,A8,'   ',F10.4,'  ',F14.4,'   ',F15.3)
/COM,
/COM, ------------------------------------
/COM, WHIRL SPEEDS WITH SLOPE = 4.0
/COM, ------------------------------------
*VWRITE,LABEL(1,7),VALUE(7,1),VALUE(7,2),VALUE(7,3)
(1X,A8,'   ',F10.4,'  ',F14.4,'   ',F15.3)
*VWRITE,LABEL(1,8),VALUE(8,1),VALUE(8,2),VALUE(8,3)
(1X,A8,'   ',F10.4,'  ',F14.4,'   ',F15.3)
*VWRITE,LABEL(1,9),VALUE(9,1),VALUE(9,2),VALUE(9,3)
(1X,A8,'   ',F10.4,'  ',F14.4,'   ',F15.3)
*VWRITE,LABEL(1,10),VALUE(10,1),VALUE(10,2),VALUE(10,3)
(1X,A8,'   ',F10.4,'  ',F14.4,'   ',F15.3)
*VWRITE,LABEL(1,11),VALUE(11,1),VALUE(11,2),VALUE(11,3)
(1X,A8,'   ',F10.4,'  ',F14.4,'   ',F15.3)
*VWRITE,LABEL(1,12),VALUE(12,1),VALUE(12,2),VALUE(12,3)
(1X,A8,'   ',F10.4,'  ',F14.4,'   ',F15.3)
/COM
/OUT
FINISH
*LIST,vm247,vrt
/EXIT,NOSAVE

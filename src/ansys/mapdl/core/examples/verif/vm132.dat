/COM,ANSYS MEDIA REL. 2023R1 (11/04/2022) REF. VERIF. MANUAL: REL. 2023R1
/VERIFY,VM132
/PREP7
/TITLE, VM132, STRESS RELAXATION OF A BOLT DUE TO CREEP
C***    STR. OF MATL., TIMOSHENKO, PART 2, 3RD ED., PAGE 531
ANTYPE,STATIC
ET,1,LINK180             ! SPAR ELEMENT
SECTYPE,1,LINK   
SECDATA,1
MP,EX,1,30E6
TB,CREEP,1,1,1,1         ! IMPLICIT CREEP WITH STRAIN HARDENING
TBDATA,1,4.8E-30,7       ! CREEP PROPERTIES
N,1
N,2,10
E,1,2
INIS,SET,DTYP,EPEL       ! APPLY INITIAL STRAIN 
INIS,DEFI,1,ALL,ALL,ALL,1/30000
BFUNIF,TEMP,900        ! UNIFORM TEMPERATURE
TIME,1000
KBC,1
D,ALL,ALL              ! FIX ALL DOFS
FINISH
/SOLU    
NSUBST,100,100,100
RATE,ON,ON
OUTPR,BASIC,10         ! PRINT BASIC SOLUTION FOR EVERY 10TH SUBSTEP
OUTRES,ESOL,1          ! STORE ELEMENT SOLUTION FOR EVERY SUBSTEP
/OUT,SCRATCH
AUTOTS,OFF
NEQIT,25
TINTP,,,,0.5,,0.2
SOLVE
FINISH
/POST26
ESOL,2,1,,LS,1,SIG     ! STORE AXIAL STRESS
/OUT,
PRVAR,2                ! PRINT AXIAL STRESS VS TIME
*GET,T190,VARI,2,RTIME,190
*GET,T420,VARI,2,RTIME,420
*GET,T690,VARI,2,RTIME,690
*GET,T880,VARI,2,RTIME,880
*GET,T950,VARI,2,RTIME,950
*status,parm
*DIM,LABEL,CHAR,5,2
*DIM,VALUE,,5,3
LABEL(1,1) = 'SIG @ ','SIG @ ','SIG @ ','SIG @ ','SIG @ '
LABEL(1,2) = '190 hr','420 hr','690 hr','880 hr','950 hr'
*VFILL,VALUE(1,1),DATA,975,950,925,910,905
*VFILL,VALUE(1,2),DATA,T190,T420,T690,T880,T950
V1 = ABS(T190/975) 
V2 = ABS(T420/950) 
V3 = ABS(T690/925)
V4 = ABS(T880/910) 
V5 = ABS(T950/905) 
*VFILL,VALUE(1,3),DATA,V1,V2,V3,V4,V5
/COM
/OUT,vm132,vrt
/COM,------------------- VM132 RESULTS COMPARISON -------------
/COM,
/COM,                 |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.0,'  ',F12.0,'   ',1F15.2)
/COM,----------------------------------------------------------
/OUT
FINISH
*LIST,vm132,vrt


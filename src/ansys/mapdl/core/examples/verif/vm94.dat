/COM,ANSYS MEDIA REL. 2023R1 (11/04/2022) REF. VERIF. MANUAL: REL. 2023R1
/VERIFY,VM94
/PREP7
/TITLE, VM94, HEAT GENERATING PLATE
C***        PRINCIPLES OF HEAT TRANSFER, KREITH, 2ND. PRINTING, PAGE 42, EX. 2-9
ANTYPE,STATIC
ET,1,LINK33
ET,2,LINK34
R,1,1                        ! AREA = 1
MP,KXX,1,25                  ! CONDUCTIVITY
MP,HF,1,13.969738            ! CONVECTION COEFFICIENT
N,1
N,5,((.5/12)*.5)
FILL
N,6,((.5/12)*.5)
E,1,2                        ! LINK32 ELEMENTS (CONDUCTION)
EGEN,4,1,1
TYPE,2
E,5,6                        ! LINK34 ELEMENT (CONVECTION)
D,6,TEMP,150                 ! SPECIFY "FLUID" TEMPERATURES
ESEL,S,ELEM,,1,4
BFE,ALL,HGEN,,1E5            ! HEAT GENERATION
ESEL,ALL
FINISH
/SOLU    
SOLVE
FINISH
/POST1
PRNSOL,TEMP                  ! PRINT NODAL TEMPERATURES
PRNLD,HEAT                   ! PRINT NODAL HEAT FLOW RATES
FINISH
/POST1
*GET,TC,NODE,1,TEMP
FINISH
/POST26
ESOL,2,5,6,HEAT,,HEAT
STORE
*GET,HEAT,VARI,2,EXTREM,VMAX
*DIM,LABEL,CHAR,2,2
*DIM,VALUE,,2,3
LABEL(1,1) = 'Qf, BTU/','Tc,     '
LABEL(1,2) = 'hr      ',' F      '
*VFILL,VALUE(1,1),DATA,2083.3,299.1
*VFILL,VALUE(1,2),DATA,HEAT,TC
*VFILL,VALUE(1,3),DATA,ABS(HEAT/2083.3) ,ABS(TC/299.1)
/COM
/OUT,vm94,vrt
/COM,------------------- VM94 RESULTS COMPARISON --------------
/COM,
/COM,                 |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.1,'  ',F14.1,'   ',1F15.3)
/COM,----------------------------------------------------------
/OUT
FINISH
*LIST,vm94,vrt

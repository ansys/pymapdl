! Copyright (C) 2024 ANSYS, Inc. and/or its affiliates.
! SPDX-License-Identifier: MIT
! 
!
! Permission is hereby granted, free of charge, to any person obtaining a copy
! of this software and associated documentation files (the "Software"), to deal
! in the Software without restriction, including without limitation the rights
! to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
! copies of the Software, and to permit persons to whom the Software is
! furnished to do so, subject to the following conditions:
!
! The above copyright notice and this permission notice shall be included in all
! copies or substantial portions of the Software.
!
! THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
! IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
! FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
! AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
! LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
! OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
! SOFTWARE.

/COM,ANSYS MEDIA REL. 2023R2 (05/12/2023) REF. VERIF. MANUAL: REL. 2023R2
/VERIFY,VM307
/TITLE,VM307,VECTOR-ORIENTED AND TAPERED DISTRIBUTED EDGE LOAD ON A RECTANGULAR PLATE
/COM,
/COM, REF: R. C. HIBBELER, ENGINEERING MECHANICS - STATICS, 
/COM,      12TH ED. PEARSON, 2010 PAGE. 183
/COM,

/PREP7
ET,1,181,,,2
THICK = 1

SECT,1,SHELL
SECD,THICK,1

MP,EX,1,9E7
MP,NUXY,1,0.3
MP,DENS,1,0.5

BLC4,,,10,1,0

! RECTANGULAR PLATE WITH LENGTH 10 MM AND WIDTH 1 MM
N,1,   0.0000,    0.0000,    0.0000
N,2,   10.000,    0.0000,    0.0000
N,3,   10.000,    1.0000,    0.0000
N,4,   0.0000,    1.0000,    0.0000

E, 1, 2, 3, 4
FINI

/SOLU
NSEL,S,LOC,X
D,ALL,ALL
NSEL,ALL
ALLSEL

NLGEOM,ON
OUTRES,ALL,ALL
NSUBST,1,1,1

! TAPERED VECTOR-ORIENTED EDGE LOAD APPLIED ON PROJECTED AREA WITHOUT TANGENTIAL COMPONENT
SFCONTROL,2,,1,1,,1,,,2
SFE,ALL,5,PRES,,-1000,-1000

ALLSEL
CNVTOL,F,1,1E-3
NROPT,UNSYM
SOLVE
FINI

/POST1
SET,LAST
FSUM
*GET,RF_XT01,FSUM,,ITEM,FX
*GET,RF_YT01,FSUM,,ITEM,FY

*DIM,LABEL,CHAR,2
*DIM,VALUE,,2,3
LABEL(1) = 'A_FORCEX','A_FORCEY'
*VFILL,VALUE(1,1),DATA, -2286.737, -34227.9144,
*VFILL,VALUE(1,2),DATA, RF_XT01, RF_YT01
*VFILL,VALUE(1,3),DATA, ABS(RF_XT01 / 2286.737 ), ABS( RF_YT01 / 34227.9144 )
/OUT,vm307,vrt
/COM
/COM,------------------- VM307 RESULTS COMPARISON -------------------
/COM,
/COM,   F     |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
*VWRITE,LABEL(1),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,'   ',F10.3,'  ',F14.3,' ',1F15.3)
/COM,----------------------------------------------------------------
/OUT
FINISH
*LIST,vm307,vrt

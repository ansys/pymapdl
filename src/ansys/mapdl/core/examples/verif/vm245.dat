/COM,ANSYS MEDIA REL. 150 (11/8/2013) REF. VERIF. MANUAL: REL. 150
/VERIFY,VM245
/TITLE, VM245,SQUEEZE FILM DAMPING-RECTANGULAR PLATE
/COM, REFERENCE:  COUPLED FLUID-STRUCTURAL DOMAIN SOLVER AND ROM
/COM,		  EXTRACTION FOR MEMS, J. MEHNER, 1/31/03
/COM,			BENCHMARK #1: RECTANGULAR PLATE
/COM,
/COM,	CHARACTERISTICS:
/COM,	 UNIFORM VELOCITY (FLUE) LOAD, SINGLE SIDE
/COM,	 UMKS UNITS, GLOBAL CARTESIAN C.S.   
/COM,    BLOCK LANCHOS, JCG SOLVERS,TRI MESH
/COM,    4-NODE QUAD OPTION, THK AND AMBIENT PRESSURE
/COM,	 REFERENCE PRESSURE, MEAN FREE PATH
/COM,	 COMPUTE AND EXTRACT SQUEEZE FILM AND DAMPING COEFFICIENT
/COM,
/COM,************************ INPUT PARAMETERS ********************************
/COM,
/COM, PLATE LENGTH = 2000 UM
/COM, PLATE WIDTH = 1000 UM
/COM, FILM THICKNESS = 5 UM
/COM, AMBIENT PRESSURE = 0.1 KG/(UM)(S^2)
/COM, DYNAMIC VISCOSITY = 18.3E-12 KG/(UM)(S)
/COM, ARBITRARY UNIFORM VELOCITY = 2000 UM/S
/COM, OPERATING FREQUENCY = 100000 HZ
/COM,
/COM,************************ EXPECTED RESULTS ********************************
/COM,
/COM, DAMPING COEFFICIENT AT 100 KHZ = 15.29E-3
/COM, SQUEEZE STIFFNESS COEFFICIENT AT 100 KHZ = 28.65E3
/COM,
/COM,**************************************************************************
/COM,

/PREP7

ET,1,FLUID136

A=.001E6				! PLATE WIDTH (UM)
B=.002E6				! PLATE LENGTH (UM)
D=5					! GAP (UM)
PO=0.1					! NORMNAL PRESSURE (KG/(UM)(S^2))
VISC=18.3E-12				! VISCOSITY (KG/UM/S)
VELO=0.002E6     			! ARBITRARY UNIFORM VELOCITY (UM/SEC)
AREA=A*B	        		! PLATE AREA
FREQ=100000     			! OPERATING FREQUENCY (HZ.) 
OMEGA=2*3.14159*FREQ

MP,VISC,1,VISC
R,1,D,,,PO

RECTNG,0,B,0,A
MSHAPE,1
ESIZE,,40
AMESH,ALL

NSEL,EXT
D,ALL,PRESS,0		        	! SET PRESSURE TO ZERO
NSEL,ALL

BFE,ALL,FLUE,,VELO

FINISH

/SOLU
ANTYP,HARM	 			! HARMONIC THERMAL ANALYSIS
HARFRQ,FREQ
EQSLV,JCG
SOLVE
FINISH

/POST1
/OUT,SCRATCH
SET,1,1
ETABLE,PRESR,PRESS			! EXTRACT "REAL" PRESSURE
ETABLE,EAREA,VOLU
SMULT,FORR,PRESR,EAREA			! COMPUTE "REAL" FORCE
SSUM
*GET,FRE,SSUM,,ITEM,FORR
SET,1,1,,1
ETABLE,PRESI,PRESS			! EXTRACT "IMAGINARY" PRESSURE
SMULT,FORI,PRESI,EAREA			! COMPUTE "IMAGINARY" PRESSURE
SSUM
*GET,FIM,SSUM,,ITEM,FORI

K=ABS(FIM*OMEGA/VELO)			! COMPUTE EQUIVALENT STIFFNESS
C=ABS(FRE/VELO)				! COMPUTE EQUIVALENT DAMPING

FINISH

/POST1
*GET,EFFECTIVE_VISCOSITY,ELEM,1,NMISC,1
*GET,ACTUAL_GAP_SEPARATION,ELEM,1,NMISC,2
/OUT
/COM,
/COM,************************** OUTPUT RESULTS ******************************* 
/COM,
*VWRITE, EFFECTIVE_VISCOSITY   
(/' EFFECTIVE VISCOSITY = ',E13.6)  
*VWRITE, ACTUAL_GAP_SEPARATION 
(/' ACTUAL GAP SEPARATION = ',E13.6)
 *VWRITE, K   
(/' EQUIVALENT STIFFNESS = ',E13.6)  
*VWRITE, C 
(/' EQUIVALENT DAMPING = ',E13.6)
/COM,
/COM,**************************************************************************
*DIM,LABEL,CHAR,2,2
*DIM,VALUE,,2,3
LABEL(1,1) = 'K ','C '
LABEL(1,2) = 'NS/M','N/M'
*VFILL,VALUE(1,1),DATA,28.65E3,15.29E-3
*VFILL,VALUE(1,2),DATA,K,C
*VFILL,VALUE(1,3),DATA,ABS(K/28.65E3),ABS(C/15.29E-3)
/COM
/OUT,vm245,vrt 
/COM,------------------- VM245 RESULTS COMPARISON -------------
/COM,
/COM,                 |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.4,'  ',F14.4,'   ',1F15.3)
/COM,
/COM,----------------------------------------------------------
/OUT
FINISH
*LIST,vm245,vrt

FINISH






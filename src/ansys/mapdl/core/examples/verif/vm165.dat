/COM,ANSYS MEDIA REL. 2023R1 (11/04/2022) REF. VERIF. MANUAL: REL. 2023R1
/VERIFY,VM165
/TITLE, VM165, CURRENT CARRYING FERROMAGNETIC CONDUCTOR
/COM  REFERENCE:PRINCIPLES OF ELECTRIC AND MAGNETIC FIELDS, BOAST, PAGE 225
/COM
/COM
JPGPRF,500,100,1             ! MACRO TO SET PREFS FOR JPEG PLOTS
/SHOW,JPEG
/PREP7
SMRT,OFF
ET,1,PLANE233               ! 2-D MAGNETIC SOLID
ET,2,INFIN110               ! 2-D INFINITE BOUNDARY ELEMENT
EMUNIT,MKS                  ! MKS UNITS
MP,MURX,1,1                 ! SET RELATIVE PERMEABILITY FOR AIR TO 1
TB,BH,2                     ! B-H CURVE FOR MATERIAL 2
TBPT,,150,.21               ! H AND B RESPECTIVELY
TBPT,,300,.55
TBPT,,460,.80
TBPT,,640,.95
TBPT,,720,1.0
TBPT,,890,1.1
TBPT,,1020,1.15
TBPT,,1280,1.25
TBPT,,1900,1.40
TBPLOT,BH,2
TBPLOT,NB,2
/WIND,1,TOP
/WIND,2,BOTTOM
/GTYPE,1,GRPH,1
/GTYPE,2,GRPH,1
/GCMD,1,TBPLOT,BH,2
/GCMD,2,TBPLOT,NB,2
GPLOT
/WIND,2,OFF
/WIND,1,FULL
SF=.0254               ! SET CONVERSION (INCHES TO METERS)
CSYS,1
K,1
K,2,.3,-2.5            ! INNER RADIUS OF RING
K,3,.45,-2.5           ! OUTER RADIUS OF RING
K,4,.75,-2.5           
k,5,1.50,-2.5          ! OUTER RADIUS OF SURROUNDING AIR
KPSCALE,ALL,,,SF,,,,,1 ! MOVE ORIGINAL KEYPOINTS TO NEW POSITION
CSYS,0
L,1,2
LESIZE,1,,,5
L,2,3
LESIZE,2,,,6
L,3,4
L,4,5
LSYMM,2,ALL
NUMMRG,KP              ! MERGE KEYPOINTS
L,4,9                  ! DEFINE OUTER RADIUS LINE SEGMENT
L,5,10
A,2,7,1,1
A,2,3,8,7
A,3,4,9,8
A,4,5,10,9
ASEL,S,AREA,,2
AATT,2                 ! ASSIGN MATERIAL 2 TO STEEL AREA
ASEL,ALL
ESIZE,,1               ! DEFAULT ELEMENT DIVISIONS=1
TYPE,2
AMESH,4                ! CREATING INFIN110 ELEMENTS
TYPE,1
AMESH,1,2
AMESH,3                ! CREATING ELECTROMAGNETIC ELEMENTS
ALLSEL,ALL
CSYS,1
NSEL,S,LOC,X,1.5*SF
SF,ALL,INF
NSEL,ALL
CSYS,0
FINISH
/SOLU                  ! ENTER SOLVER
BFA,2,JS,,,438559      ! APPLY CURRENT DENSITY JS(Z)	
SOLVE
FINISH

/POST1
CSYS,1
NSEL,S,LOC,X,.325*SF
NSEL,A,LOC,X,.375*SF
NSEL,A,LOC,X,.425*SF
RSYS,1                 ! SET RESULTS C.S. TO CYLINDRICAL
PRNSOL,B,COMP          ! PRINT NODAL FLUX DENSITY
csys,1
*GET,B1,NODE,node(0.325*SF,-2.5,0),B,SUM
*GET,B2,NODE,node(0.375*SF,-2.5,0),B,SUM
*GET,B3,NODE,node(0.425*SF,-2.5,0),B,SUM
*DIM,LABEL,CHAR,3,2
*DIM,VALUE,,3,3
LABEL(1,1) = '@ R=','@ R=','@ R='
LABEL(1,2) = '.325','.375','.425'
*VFILL,VALUE(1,1),DATA,.48,1.03,1.22
*VFILL,VALUE(1,2),DATA,B1,B2,B3
*VFILL,VALUE(1,3),DATA,ABS(B1/.48),ABS(B2/1.03),ABS(B3/1.22)
/COM
/OUT,vm165,vrt,
/COM,PLANE233
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.2,'  ',F13.2,'   ',1F15.3)
/COM,-----------------------------------------------------------
/OUT
FINISH
*LIST,vm165,vrt

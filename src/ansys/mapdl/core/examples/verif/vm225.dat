/COM,ANSYS MEDIA REL. 2023R1 (11/04/2022) REF. VERIF. MANUAL: REL. 2023R1
/VERIFY,VM225
JPGPRF,500,100,1             ! MACRO TO SET PREFS FOR JPEG PLOTS
/SHOW,JPEG
/TITLE, VM225, BEAM WITH PRETENSION LOAD
C***    USING 3-D SOLID185
/COM,  
/PREP7					!ENTER PREPROCESSOR
MP,PRXY,,0.3
BLOCK,,12,,1,,.5			!CREATE BLOCK
/VIEW,1,1,2,3				!CHANGE VIEW
ET,1,SOLID185				!SET ELEMENT TYPE
MP,EX,1,30E6, 				!DEFINE YOUNG'S MODULUS
MP,PRXY,1,0.3               !DEFINE POISSON'S RATIO
MP,DENS,1,.283,				!DEFINE DENSITY
DA,2,SYMM				    !DEFINE SYMMETRY BC ON AREA
DA,3,SYMM				    !DEFINE SYMMETRY BC ON AREA
DA,5,UX					    !DEFINE DOF ON AREA
DA,6,UX				    	!DEFINE DOF ON AREA
VMESH,ALL			    	!MESH VOLUME
PSMESH, , ,1000,ALL,,0,X,6,,,,EEE,NNN   !APPLY LOAD VIA PRE-TENSION ELEMENT
SLOAD,1,PL01,LOCK,FORC,125,1,2
EPLOT
SAVE                                    !SAVE DATABASE FOR SECOND SOLUTION                                 
FINISH					    !EXIT PREP7
/out,scratch
/SOLUTION					!ENTER SOLVER
SOLVE		    			!SOLVE
FINISH  		    		!EXIT SOLVER
/out
*CREATE,RES3D,MAC           !CREATE MACRO TO RETRIEVE RESULTS
/POST1 				    	!ENTER POST PROCESSOR
NSORT,S,INT,1,0,,			!SORT STRESS RESULTS
*GET,MAXNFEA2,SORT,,IMAX	!GET NODE VALUE
*GET,SIGFEA2,NODE,MAXNFEA2,S,INT	!GET MAXIMUM VON MISSES STRESS
NSORT,U,X,SUM,1,0,,,		!SORT DEFLECTION RESULTS
*GET,MAXNFEA2,SORT,,IMAX	!GET NODE VALUE
*GET,UXFEA2,NODE,MAXNFEA2,U,X,SUM	!GET MAXIMUM UX VALUE
*STAT,UXFEA2				!LIST PARAMETER VALUE
*STAT,SIGFEA2				!LIST PARAMETER VALUE
/COM,**********************************
/COM,*** CLASSICAL ANALYSIS RESULTS ***
/COM,**********************************
SIGCA = 250
UXCA = 0.0001
*DIM,LABEL,CHAR,2
*DIM,VALUE,,2,3
LABEL(1) = 'SIGCA ','UXCA '
*VFILL,VALUE(1,1),DATA,SIGCA,UXCA
*VFILL,VALUE(1,2),DATA,SIGFEA2,UXFEA2
*VFILL,VALUE(1,3),DATA,ABS(SIGCA / SIGFEA2) ,ABS(UXCA / UXFEA2)
FINISH
*END
RES3D                                   !EXECUTE MACRO TO RETRIEVE RESULTS
SAVE,TABLE_1

/CLEAR, NOSTART                         !CLEAR DATABASE FOR SECOND SOLUTION
/TITLE, VM225, BEAM WITH PRETENSION LOAD
C***    USING 3-D SOLID186 
/PREP7
RESUME                                  !RESUME DATABASE
ET,1,SOLID186                           !ANALYZE AGAIN USING 3-D SOLID186 
FINISH                                  !EXIT PREP7
/out,scratch
/SOLUTION           					!ENTER SOLVER
SOLVE					                !SOLVE
FINISH  				                !EXIT SOLVER
/out
RES3D                                   !EXECUTE MACRO TO RETRIEVE RESULTS
SAVE,TABLE_2

/NOPR
RESUME,TABLE_1
/GOPR
/OUT,vm225,vrt
/COM
/COM,------------------- VM225 RESULTS COMPARISON ---------------------
/COM,
/COM,          |   TARGET   |    Mechanical APDL    |    RATIO
/COM,
/COM, SOLID185
/COM,
*VWRITE,LABEL(1),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,'   ',F10.5,'   ',F14.5,'     ',1F15.3)
/NOPR
RESUME,TABLE_2
/GOPR
/COM,
/COM, SOLID186
/COM,
*VWRITE,LABEL(1),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,'   ',F10.5,'   ',F14.5,'     ',1F15.3)
/COM,----------------------------------------------------------------
/OUT
FINISH
*LIST,vm225,vrt

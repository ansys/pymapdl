/COM,ANSYS MEDIA REL. 2023R1 (11/04/2022) REF. VERIF. MANUAL: REL. 2023R1
/VERIFY,VM35
/PREP7
/TITLE,VM35, BIMETALLIC LAYERED CANTILEVER PLATE WITH THERMAL LOADING
C***     ROARK AND YOUNG, FORMULAS FOR STRESS AND STRAIN, PP. 113-114.
C*** USING SHELL281
ANTYPE,STATIC
ET,1,SHELL281
SECTYPE,1,SHELL
SECDATA,0.05,1,0       ! LAYER 1: 0.05 THICK, MAT'L 1, THETA 0 
SECDATA,0.05,2,0       ! LAYER 2: 0.05 THICK, MAT'L 2, THETA 0,
MP,EX,1,3E7            ! MATERIAL PROPERTIES
MP,EX,2,3E7
MP,ALPX,1,1E-5
MP,ALPX,2,2E-5
MP,NUXY,1,0
MP,NUXY,2,0
N,1                    ! DEFINE GEOMETRY
N,12,,1
N,22,10,1
N,11,10
FILL,1,11,9,2,1
FILL,12,22,9,13,1
FILL,1,12,1,23  
FILL,3,14,1,24
FILL,5,16,1,25
FILL,7,18,1,26
FILL,9,20,1,27
FILL,11,22,1,28
E,1,12,14,3,23,13,24,2
E,3,14,16,5,24,15,25,4
E,5,16,18,7,25,17,26,6
E,7,18,20,9,26,19,27,8
E,9,20,22,11,27,21,28,10
NSEL,S,LOC,X
NSEL,R,LOC,Y,.5
D,ALL,ALL              ! FIX ONE END OF CANTILEVER
NSEL,S,LOC,Y,0.5
DSYM,SYMM,Y            ! SYMMETRY PLANE DOWN CENTERLINE
NSEL,ALL
TREF,70
BFUNIF,TEMP,170        ! DEFINE UNIFORM TEMPERATURE
FINISH
/SOLU    
OUTPR,NSOL,1
OUTPR,RSOL,1
/OUT,SCRATCH
SOLVE
FINISH
/POST1
SHELL,TOP              ! SELECT TOP SURFACE FOR STRESS PRINT
/OUT,
PRNSOL,S,COMP
NSEL,S,LOC,X,10        ! SELECT CENTERLINE OF FREE END FOR DISPLACEMENT PRINT
NSEL,R,LOC,Y,.5
PRNSOL,U,COMP
RT_NODE = NODE (10,.5,0)
*GET,DEF_Z,NODE,RT_NODE,U,Z
*GET,DEF_X,NODE,RT_NODE,U,X
*GET,OUT_STRS,NODE,1,S,X
*DIM,LABEL,CHAR,3,2
*DIM,VALUE,,3,3
LABEL(1,1) = 'RT_END U','RT_END U','LFT_END '
LABEL(1,2) = 'Z (in)  ','X (in)  ','STRS psi'
*VFILL,VALUE(1,1),DATA,.750,.015,7500
*VFILL,VALUE(1,2),DATA,DEF_Z,DEF_X,OUT_STRS
*VFILL,VALUE(1,3),DATA,ABS(DEF_Z/.750) ,ABS(DEF_X/.015),ABS(OUT_STRS/7500)
SAVE,TABLE_1
RESUME,TABLE_1
/COM
/OUT,vm35,vrt
/COM,------------------- VM35 RESULTS COMPARISON ---------------
/COM,
/COM,                 |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
/COM, SHELL281
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.3,'  ',F14.3,'   ',1F15.3)
/COM,-----------------------------------------------------------
/OUT
FINISH
*LIST,vm35,vrt
/DELETE,TABLE_1

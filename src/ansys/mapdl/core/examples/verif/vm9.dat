/COM,ANSYS MEDIA REL. 150 (11/8/2013) REF. VERIF. MANUAL: REL. 150
/VERIFY,VM9
/PREP7
/TITLE, VM9, LARGE LATERAL DEFLECTION OF UNEQUAL STIFFNESS SPRINGS
/COM, REF: G.N. VANDERPLAATS, "NUMERICAL OPTIMIZATION TECHNIQUES FOR
/COM,      ENGINEERING DESIGN", PP 72-73, MCGRAW-HILL, 1984
ET,1,COMBIN14,,,2          ! UX AND UY DOF ELEMENT
ET,3,COMBIN40,,,,,,2       ! ALL MASS IS AT NODE J, UX DOF ELEMENT
ET,4,COMBIN40,,,2,,,2      ! ALL MASS IS AT NODE J, UY DOF ELEMENT
R,1,1                      ! SPRING STIFFNESS = 1
R,2,8                      ! SPRING STIFFNESS = 8
/COM USE COMBIN40 MASS, K, AND DAMPING C, TO APPROX. CRITICAL DAMPING
R,3,,1.41,1                ! C = 1.41, M = 1
R,4,,2,1                   ! C = 2, M = 1
N,1
N,2,,10
N,3,,20
N,4,-1,10
N,5,,9
E,1,2                      ! ELEMENT 1 IS SPRING ELEMENT WITH STIFFNESS 1
REAL,2
E,2,3                      ! ELEMENT 2 IS SPRING ELEMENT WITH STIFFNESS 8
TYPE,3
REAL,3
E,4,2                      ! ELEMENT 3 IS COMBINATION ELEMENT WITH C = 1.41
TYPE,4
REAL,4
E,5,2                      ! ELEMENT 4 IS COMBINATION ELEMENT WITH C = 2
NSEL,U,NODE,,2
D,ALL,ALL
NSEL,ALL
FINISH
/SOLU    
ANTYPE,TRANS               ! FULL TRANSIENT DYNAMIC ANALYSIS
NLGEOM,ON                  ! LARGE DEFLECTION
KBC,1                      ! STEP BOUNDARY CONDITION
F,2,FX,5
F,2,FY,5
AUTOTS,ON
NSUBST,30
OUTPR,,LAST
OUTPR,VENG,LAST
TIME,15                    ! ARBITRARY TIME FOR SLOW DYNAMICS
/OUT,SCRATCH
SOLVE
FINISH
/POST1
SET,,,,,15                 ! USE ITERATION WHEN TIME = 15
/OUT,
ETABLE,SENE,SENE           ! STORE STRAIN ENERGY
SSUM                       ! SUM ALL ACTIVE ENTRIES IN ELEMENT STRESS TABLE
*GET,ST_EN,SSUM,,ITEM,SENE
PRNSOL,U,COMP              ! PRINT DISPLACEMENTS IN GLOBAL COORDINATE SYSTEM
*GET,DEF_X,NODE,2,U,X
*GET,DEF_Y,NODE,2,U,Y
*DIM,LABEL,CHAR,3,2
*DIM,VALUE,,3,3
LABEL(1,1) = 'STRAIN E','DEF_X (C','DEF_Y (C'
LABEL(1,2) = ', N-cm  ','m)      ','m)      '
*VFILL,VALUE(1,1),DATA,24.01,8.631,4.533
*VFILL,VALUE(1,2),DATA,ST_EN ,DEF_X,DEF_Y
*VFILL,VALUE(1,3),DATA,ABS(ST_EN/24.01), ABS(8.631/DEF_X), ABS(DEF_Y/4.533 )
/COM
/OUT,vm9,vrt
/COM,------------------- VM9 RESULTS COMPARISON ---------------------
/COM,
/COM,                 |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.3,'  ',F14.3,'   ',1F15.3)
/COM,----------------------------------------------------------------

/OUT
FINISH
*LIST,vm9,vrt

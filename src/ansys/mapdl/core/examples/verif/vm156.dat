/COM,ANSYS MEDIA REL. 150 (11/8/2013) REF. VERIF. MANUAL: REL. 150
/VERIFY,VM156
/PREP7
/TITLE, VM156, NATURAL FREQUENCY OF NONLINEAR SPRING-MASS SYSTEM
C***            VIBRATION PROBS. IN ENGR., TIMOSHENKO, 3RD. ED., PAGE 141
C***             USING NONLINEAR SPRING ELEMENT (COMBIN39)
ANTYPE,TRANS                 ! NONLINEAR TRANSIENT DYNAMIC ANALYSIS
ET,1,COMBIN39,,,2            ! ELEMENT WITH DISPLACEMENT ALONG NODAL Y-AXIS
ET,2,MASS21,,,4              ! MASS WITHOUT ROTARY INERTIA
R,1,0.0,0.0,.1,.204,.2,.432  ! SPRING DATA
RMORE,.3,.708,.4,1.056,.5,1.5
RMORE,.6,2.064,.7,2.772,.8,3.648
RMORE,.9,4.716,1.0,6.0
R,2,2588E-6                  ! MASS DATA
N,1
N,2
E,1,2
TYPE,2
REAL,2
E,2
D,1,ALL
D,2,UX
IC,2,UY,-1                   ! INITIAL DISPLACEMENT AND VELOCITY
KBC,1                        ! STEP LOADING
SAVE
FINISH
/SOLU    
TRNOPT, , , , , ,HHT
SOLCONTROL,0
CNVTOL,F,1,1E-4              ! FORCE CONVERGENCE CRITERIA
OUTRES,NSOL,1
NSUBST,5
OUTPR,BASIC,NONE
TIME,.0002                   ! TIME TO ALLOW INITIAL CHANGE IN ACCELERATION
LSWRITE                      ! WRITE LOAD STEP FILE 1
NSUBST,40
OUTPR,BASIC,LAST
TIME,0.18                    ! TIME ARBITRARILY SELECTED
LSWRITE                      ! WRITE LOAD STEP FILE 2
/OUT,SCRATCH
LSSOLVE,1,2,1                ! READ IN 2 LOAD STEPS AND SOLVE
FINISH
/POST26
TIMERANGE,.003,.18
NSOL,2,2,U,Y,2UY   
/OUT,
PRVAR,2		             ! PRINT DISPLACEMENTS
*GET,PER,VARI,2,EXTREM,TMIN
*status,parm
FINISH
/POST1
*DIM,LABEL,CHAR,1,2
*DIM,VALUE,,1,3
LABEL(1,1) = 'PERIOD '
LABEL(1,2) = 'sec'
*VFILL,VALUE(1,1),DATA,.1447
*VFILL,VALUE(1,2),DATA,PER
*VFILL,VALUE(1,3),DATA,ABS(PER/.1447)
SAVE,TABLE_1
FINISH
/CLEAR,NOSTART
RESUME,TABLE_1
/OUT,vm156,vrt
/COM,------------------- VM156 RESULTS COMPARISON --------------
/COM,
/COM,     STIF39      |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.4,'  ',F13.4,'   ',1F15.3)
/COM,-----------------------------------------------------------
/OUT
/COM, 
FINISH
/DELETE,TABLE_1
FINISH
*LIST,vm156,vrt

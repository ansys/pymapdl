! Copyright (C) 2024 ANSYS, Inc. and/or its affiliates.
! SPDX-License-Identifier: MIT
! 
!
! Permission is hereby granted, free of charge, to any person obtaining a copy
! of this software and associated documentation files (the "Software"), to deal
! in the Software without restriction, including without limitation the rights
! to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
! copies of the Software, and to permit persons to whom the Software is
! furnished to do so, subject to the following conditions:
!
! The above copyright notice and this permission notice shall be included in all
! copies or substantial portions of the Software.
!
! THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
! IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
! FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
! AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
! LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
! OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
! SOFTWARE.

/COM,ANSYS MEDIA REL. 2023R2 (05/12/2023) REF. VERIF. MANUAL: REL. 2023R2
/VERIFY,VM112
/PREP7
/TITLE, VM112, COOLING OF A SPHERICAL BODY
C***       PRINCIPLES OF HEAT TRANSFER, KREITH, 2ND. PRINTING, PAGE 143, EX. 4-5
C***       USING PLANE77 ELEMENTS (MULTI-NODE ELEMENTS)
ANTYPE,TRANS                    ! THERMAL ANALYSIS
ET,1,PLANE77,,,1                ! AXISYMMETRIC ELEMENTS
MP,KXX,1,(1/3)
MP,DENS,1,62
MP,C,1,1.0752677
CSYS,1                          ! CYLINDRICAL COORDINATE SYSTEM
N,1
N,2,(1/18),-7.5
N,4,(1/6),-7.5
FILL
NGEN,2,20,1,4,1,,15
N,14,(1/6)
E,2,22,1,1
E,3,23,22,2
E,4,24,23,3,14
CP,1,TEMP,2,22                  ! COUPLING TO ENSURE CIRCUMFERENTIAL SYMMETRY
CPSGEN,2,1,1             
CP,3,TEMP,4,14,24
AUTOTS,ON                       ! USE AUTOMATIC TIME STEPPING
OUTPR,BASIC,LAST                
TIME,6
DELTIM,0.15
BFUNIF,TEMP,65
KBC,1                           ! STEP BOUNDARY CONDITIONS
NSEL,S,LOC,X,(1/6)
SF,ALL,CONV,2,25                ! CONVECTION ON ELEMENT SURFACE
NSEL,ALL
FINISH
/SOLU    
SOLVE
FINISH
/POST1
PRNSOL,TEMP                     ! PRINT NODAL TEMPERATURES
*GET,TEMP,NODE,1,TEMP
*DIM,LABEL,CHAR,1,2
*DIM,VALUE,,1,3
LABEL(1,1) = '       T'
LABEL(1,2) = ', F     '
*VFILL,VALUE(1,1),DATA,28
*VFILL,VALUE(1,2),DATA,TEMP
*VFILL,VALUE(1,3),DATA,ABS(TEMP/28)
/COM
/OUT,vm112,vrt
/COM,------------------- VM112 RESULTS COMPARISON --------------
/COM,
/COM,                 |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.1,'  ',F12.1,'   ',1F15.3)
/COM,-----------------------------------------------------------
/OUT
FINISH
*LIST,vm112,vrt


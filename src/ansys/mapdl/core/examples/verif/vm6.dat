/COM,ANSYS MEDIA REL. 150 (11/8/2013) REF. VERIF. MANUAL: REL. 150
/VERIFY,VM6
/PREP7
SMRT,OFF
/TITLE, VM6, PINCHED CYLINDER
/COM,  REF: COOK, CONCEPTS AND APPL. OF FEA 2ND ED., 1981, PP. 284-287. 
C***         USING SHELL181 ELEMENTS
/PREP7
SMRT,OFF
ANTYPE,STATIC             
ET,1,SHELL181, , ,2
SECTYPE,1,SHELL
SECDATA,0.094,1,0,5
MP,EX,,10.5E6
MP,NUXY,,.3125
CSYS,1
K,1,4.953                 ! DEFINE MODEL GEOMETRY
K,2,4.953,,5.175
KGEN,2,1,2,1,,90
A,1,2,4,3
ESIZE,,8
AMESH,1
CSYS,0
NSEL,S,LOC,X,0
DSYM,SYMM,X,0
NSEL,S,LOC,Y,0
DSYM,SYMM,Y,0
NSEL,S,LOC,Z,0
DSYM,SYMM,Z,0
NSEL,ALL
FK,3,FY,-25
FINISH
/SOLU    
SOLVE
FINISH
/POST1
NSEL,S,LOC,Y,4.953              ! SELECT NODE AT LOAD APPLICATION
NSEL,R,LOC,Z,0
NSEL,R,LOC,X,0
PRNSOL,U,COMP                   ! PRINT DISPLACEMENTS AND VECTOR SUM
TOP_NODE = NODE (4.953,90,0)
*GET,DISP,NODE,TOP_NODE,U,Y
*DIM,LABEL,CHAR,1
*DIM,VALUE,,1,3
LABEL(1) = 'DEF_IN'
*VFILL,VALUE(1,1),DATA,0.1139
*VFILL,VALUE(1,2),DATA,ABS(DISP)
*VFILL,VALUE(1,3),DATA,ABS( DISP /0.1139 )
SAVE,TABLE_1
FINISH
/CLEAR,NOSTART
/TITLE, VM6, PINCHED CYLINDER
C***         USING SHELL281 ELEMENTS
/PREP7
SMRT,OFF
ANTYPE,STATIC             
ET,1,SHELL281
SECTYPE,1,SHELL
SECDATA,0.094,1,0,5
MP,EX,,10.5E6
MP,NUXY,,.3125
CSYS,1
K,1,4.953                 ! DEFINE MODEL GEOMETRY
K,2,4.953,,5.175
KGEN,2,1,2,1,,90
A,1,2,4,3
ESIZE,,8
AMESH,1
CSYS,0
NSEL,S,LOC,X,0
DSYM,SYMM,X,0
NSEL,S,LOC,Y,0
DSYM,SYMM,Y,0
NSEL,S,LOC,Z,0
DSYM,SYMM,Z,0
NSEL,ALL
FK,3,FY,-25
FINISH
/SOLU    
SOLVE
FINISH
/POST1
NSEL,S,LOC,Y,4.953              ! SELECT NODE AT LOAD APPLICATION
NSEL,R,LOC,Z,0
NSEL,R,LOC,X,0
PRNSOL,U,COMP                   ! PRINT DISPLACEMENTS AND VECTOR SUM
TOP_NODE = NODE (4.953,90,0)
*GET,DISP,NODE,TOP_NODE,U,Y
*DIM,LABEL,CHAR,1
*DIM,VALUE,,1,3
LABEL(1) = 'DEF_IN'
*VFILL,VALUE(1,1),DATA,0.1139
*VFILL,VALUE(1,2),DATA,ABS(DISP)
*VFILL,VALUE(1,3),DATA,ABS( DISP /0.1139 )
SAVE,TABLE_2
RESUME,TABLE_1
/COM
/OUT,vm6,vrt
/COM,------------------- VM6 RESULTS COMPARISON ---------------------
/COM,
/COM,              |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
/COM,
/COM,
/COM,SHELL181
/COM,
*VWRITE,LABEL(1),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A10,'   ',F10.4,'  ',F14.4,'   ',1F13.3)
/NOPR
RESUME,TABLE_2
/GOPR
/COM,
/COM,SHELL281
/COM,
*VWRITE,LABEL(1),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A10,'   ',F10.4,'  ',F14.4,'   ',1F13.3)
/COM,-----------------------------------------------------------------
/OUT
FINISH
/DEL,TABLE_1
/DEL,TABLE_2
FINISH
*LIST,vm6,vrt

! Copyright (C) 2024 ANSYS, Inc. and/or its affiliates.
! SPDX-License-Identifier: MIT
! 
!
! Permission is hereby granted, free of charge, to any person obtaining a copy
! of this software and associated documentation files (the "Software"), to deal
! in the Software without restriction, including without limitation the rights
! to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
! copies of the Software, and to permit persons to whom the Software is
! furnished to do so, subject to the following conditions:
!
! The above copyright notice and this permission notice shall be included in all
! copies or substantial portions of the Software.
!
! THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
! IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
! FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
! AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
! LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
! OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
! SOFTWARE.

/COM,ANSYS MEDIA REL. 2023R2 (05/12/2023) REF. VERIF. MANUAL: REL. 2023R2
/VERIFY,VM6
/PREP7
/TITLE, VM6, PINCHED CYLINDER
/COM,  REF: COOK, CONCEPTS AND APPL. OF FEA 2ND ED., 1981, PP. 284-287. 
C***         USING SHELL181 ELEMENTS
/PREP7
ANTYPE,STATIC             
ET,1,SHELL181, , ,2
SECTYPE,1,SHELL
SECDATA,0.094,1,0,5
MP,EX,,10.5E6
MP,NUXY,,.3125
CSYS,1
N,1,4.953                 ! DEFINE FE MODEL 
N,9,4.953,,5.175
FILL
NGEN,9,10,1,9,1,,90/8
E,1,11,12,2
EGEN,8,1,-1
EGEN,8,10,-8
SAVE                      ! SAVE DATABASE FOR LATER USE
CSYS,0
NSEL,S,LOC,X,0
DSYM,SYMM,X,0
NSEL,S,LOC,Y,0
DSYM,SYMM,Y,0
NSEL,S,LOC,Z,0
DSYM,SYMM,Z,0
NSEL,ALL
TOP_NODE = NODE (4.953,90,0)
F,TOP_NODE,FY,-25
FINISH
/SOLU    
SOLVE
FINISH
/POST1
NSEL,S,LOC,Y,4.953              ! SELECT NODE AT LOAD APPLICATION
NSEL,R,LOC,Z,0
NSEL,R,LOC,X,0
PRNSOL,U,COMP                   ! PRINT DISPLACEMENTS AND VECTOR SUM
*GET,DISP,NODE,TOP_NODE,U,Y
*DIM,LABEL,CHAR,1
*DIM,VALUE,,1,3
LABEL(1) = 'DEF_IN'
*VFILL,VALUE(1,1),DATA,0.1139
*VFILL,VALUE(1,2),DATA,ABS(DISP)
*VFILL,VALUE(1,3),DATA,ABS( DISP /0.1139 )
SAVE,TABLE_1
FINISH
/CLEAR,NOSTART
/TITLE, VM6, PINCHED CYLINDER
C***         USING SHELL281 ELEMENTS
/PREP7
RESUME            
ET,1,SHELL281
EMID,ADD
CSYS,0
NSEL,S,LOC,X,0
DSYM,SYMM,X,0
NSEL,S,LOC,Y,0
DSYM,SYMM,Y,0
NSEL,S,LOC,Z,0
DSYM,SYMM,Z,0
NSEL,ALL
TOP_NODE = NODE (4.953,90,0)
F,TOP_NODE,FY,-25
FINISH
/SOLU    
SOLVE
FINISH
/POST1
NSEL,S,LOC,Y,4.953              ! SELECT NODE AT LOAD APPLICATION
NSEL,R,LOC,Z,0
NSEL,R,LOC,X,0
PRNSOL,U,COMP                   ! PRINT DISPLACEMENTS AND VECTOR SUM
*GET,DISP,NODE,TOP_NODE,U,Y
*DIM,LABEL,CHAR,1
*DIM,VALUE,,1,3
LABEL(1) = 'DEF_IN'
*VFILL,VALUE(1,1),DATA,0.1139
*VFILL,VALUE(1,2),DATA,ABS(DISP)
*VFILL,VALUE(1,3),DATA,ABS( DISP /0.1139 )
SAVE,TABLE_2
RESUME,TABLE_1
/COM
/OUT,vm6,vrt
/COM,------------------- VM6 RESULTS COMPARISON ---------------------
/COM,
/COM,              |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
/COM,
/COM,
/COM,SHELL181
/COM,
*VWRITE,LABEL(1),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A10,'   ',F10.4,'  ',F14.4,'   ',1F13.3)
/NOPR
RESUME,TABLE_2
/GOPR
/COM,
/COM,SHELL281
/COM,
*VWRITE,LABEL(1),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A10,'   ',F10.4,'  ',F14.4,'   ',1F13.3)
/COM,-----------------------------------------------------------------
/OUT
FINISH
/DEL,TABLE_1
/DEL,TABLE_2
FINISH
*LIST,vm6,vrt

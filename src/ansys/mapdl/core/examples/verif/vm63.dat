/COM,ANSYS MEDIA REL. 150 (11/8/2013) REF. VERIF. MANUAL: REL. 150
/VERIFY,VM63
/PREP7
SMRT,OFF
/TITLE, VM63, STATIC HERTZ CONTACT PROBLEM SOLVED USING CONTAC178 ELEMENTS
/COM  REF:  TIMOSHENKO AND GOODIER, THEORY OF ELASTICITY, 3RD ED., ART. 140.
ET,1,PLANE82,,,1        ! AXISYMMETRIC ELEMENTS
ET,2,PLANE183,,,1
ET,3,CONTA178,,4        ! NODAL CONTACT
R,1  
RMOD,1,7,1              !CONTACT NORMAL ALONG UY
MP,EX,1,1E3
MP,NUXY,1,.3
LOCAL,11,1,0,8,0        ! LOCAL CYLINDRICAL C.S. AT CENTERLINE
K,1,8,-90               ! DEFINE KEYPOINTS
K,2,8
K,3,7.5,-90
K,4,7.5
K,5
K,6,8,-82.65            ! PLACE KEYPOINT AND NODE AT EXPECTED CONTACT RADIUS
K,7,7.5,-82.65
L,1,3                   ! DEFINE LINES
L,2,4  
L,6,7  
LESIZE,ALL,,,1          ! DEFINE ELEMENT DIVISIONS ON ALL EXISTING LINES
A,1,6,7,3               ! DEFINE AREAS
A,6,2,4,7
A,3,7,4,5
LOCAL,12,0,0,8,0
ARSYM,Y,1,3,1           ! CREATE HALF-SYMMETRY MODEL
NUMMRG,KPOI
ESIZE,,4                ! DEFINE ELEMENT DIVISIONS ON REMAINING LINES
LESIZE,4,,,5
*REPEAT,2,1
LESIZE,6,,,8,8
LESIZE,7,,,8,(1/8)
LESIZE,10,,,1
*REPEAT,2,2
LESIZE,9,,,6,.2
TYPE,1                  ! CREATE NODES AND ELEMENTS
AMESH,1,2,1
AMESH,4,5,1
TYPE,2
MSHAPE,1,2D
MSHKEY,0
AMESH,3,6,3
CSYS,0
N,1001,-1,1E-8          !NODE 1001 IS THE GROUND
D,1001,ALL              !X POSITION DOES NOT MATTER IN THIS CASE BECAUSE
                        !THE CONTACT NORMAL IS ONLY ALONG UY
TYPE,3
REAL,1
EN,205,1001,2           !USE THE SAME ELEMENT NUMBERS AS VM63 FOR POST-PROC
EN,201,1001,4
EN,202,1001,6
EN,203,1001,8
EN,204,1001,10
EN,206,1001,31

MODMSH,NOCHECK
TYPE,1                  ! REMOVE MIDSIDE NODES ALONG CONTACT SURFACE
EMODIF,7,7,0
*REPEAT,6,1
MODMSH,CHECK
FINISH
/SOLU    
NSEL,S,LOC,X,-.01,.01   ! BOUNDARY CONDITIONS AND LOADING
D,ALL,UX,0
NSEL,R,LOC,Y,0
D,ALL,UY,0
NSEL,ALL             
LOAD=0
*CREATE,LOADSTEP        ! MACRO TO INCREMENTALLY APPLY LOAD
FK,8,FY,ARG1
SOLVE
*END
*DO,I,1,3
 LOAD=LOAD-10
 *USE,LOADSTEP,LOAD*6.2831853
*ENDDO
FINISH
/POST1                  ! POSTPROCESS
/OUT,
SET,3
ESEL,,TYPE,,3
ETABLE,RFOR,SMISC,1
NSLE
PRETAB,RFOR             ! PRINT REACTION FORCE TO DETERMINE CONTACT AREA
SSUM                    ! SUM OF REACTION FORCE
NLIST                   ! LIST COORDINATES OF NODES OF CONTACT SURFACE
PRNSOL,U,COMP           ! LIST DISPLACEMENTS OF NODES
/COM    CALCULATE RATIO OF A - ACTUAL TO A - TARGET
PI=(4*ATAN(1))
LOAD=-(LOAD)*(2*PI)
ATAR=(0.88*((LOAD*0.008)**(1/3)))   ! A - TARGET
*GET,EMAX,ELEM,,NUM,MAX
*DO,ENUM,201,EMAX                   ! START SEARCH FROM ELEM 201
 *GET,GRFR,ELEM,ENUM,ETAB,RFOR      ! FIND LAST ELEMENT IN CONTACT
 *IF,GRFR,EQ,0.0,EXIT
*ENDDO
ESEL,,ELEM,,(ENUM-1)    ! SELECT LAST CONTACTING ELEMENT
NSLE                    ! SELECT NODES ATTACHED TO SELECTED ELEMENTS
*GET,NMIN,NODE,0,NUM,MIN
NODX=NX(NMIN)
NODY=NY(NMIN)
NUX =UX(NMIN)
NUY =UY(NMIN)
AACT=NODX+NUX           ! A - ACTUAL
YCHK=NODY+NUY
RATA=(AACT/ATAR)        ! RATIO
*status,parm
*DIM,LABEL,CHAR,1,2
*DIM,VALUE,,1,3
LABEL(1,1) = '      A,'
LABEL(1,2) = ' mm     '
*VFILL,VALUE(1,1),DATA,1.010 
*VFILL,VALUE(1,2),DATA,AACT
*VFILL,VALUE(1,3),DATA,ABS(AACT/1.010)
/COM
/OUT,vm63,vrt,,append
/COM,---------------------------------------------------------------------------
/COM,----------VM63 RESULTS COMPARISON (OBTAINED USING CONTACT178 ELEMENTS)-----
/COM,
/COM,                 |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.3,'  ',F14.3,'   ',1F15.3)
/COM,---------------------------------------------------------------------------
/OUT
FINISH
*LIST,vm63,vrt

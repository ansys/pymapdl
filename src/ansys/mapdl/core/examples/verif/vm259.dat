/COM,ANSYS MEDIA REL. 2023R1 (11/04/2022) REF. VERIF. MANUAL: REL. 2023R1
/VERIFY,VM259
JPGPRF,500,100,1
/SHOW,JPEG
/COM,  VERIFICATION MANUAL FOR SPRS ANALYSIS WITH MISSING MASS AND RIGID RESPONSES, REL 12.0
/TITLE,VM259,MISSING MASS WITH RIGID RESPONSES EFFECTS IN SPECTRUM ANALYSIS FOR BM3 PIPING MODEL
/COM, REFERENCE: " REEVALUATION OF REGULATORY GUIDANCE ON MODAL RESPONSE COMBINATION 
/COM,              METHODS FOR SEISMIC RESPONSE SPECTRUM ANALYSIS"
/COM,              R.MORANTE,Y.WANG, BROOKHAVEN NATIONAL LABORATORY,DECEMBER 1999
/COM,              U.S. NUCLEAR REGULATORY COMMISSION
/OUT,SCRATCH
/FILNAME,MODEL
/PREP7
YOUNGMODULUS = 2.9e+7					! YOUNG'S MODULUS
NU = 0.3                                		! POISSON RATIO
SHEARMODULUS = YOUNGMODULUS/(2*(1+NU))  		! SHEAR MODULUS
ET,1,PIPE16
R, 1, 3.500, 0.2160           				! OUTER DIAMETER, WALL THICKNESS
MP,EX,  1, YOUNGMODULUS
MP,NUXY,1, NU
MP,GXY ,1, SHEARMODULUS
MP,DENS,1, 1.043e-3
ET,2,PIPE16
R, 2, 4.500, 0.2370           				! OUTER DIAMETER, WALL THICKNESS
MP,EX,  2, YOUNGMODULUS
MP,NUXY,2, NU
MP,GXY ,2, SHEARMODULUS
MP,DENS,2, 1.107e-3
ET,3,PIPE16
R, 3, 8.625, 0.3220            				! OUTER DIAMETER, WALL THICKNESS
MP,EX,  3, YOUNGMODULUS
MP,NUXY,3, NU
MP,GXY ,3, SHEARMODULUS
MP,DENS,3, 1.253e-3
ET,4,PIPE18
R, 4, 3.500, 0.2160,  4.500    				! OUTER DIAMETER, WALL THICKNESS, RADIUS OF CURVATURE
MP,EX,  4, YOUNGMODULUS
MP,NUXY,4, NU
MP,GXY ,4, SHEARMODULUS
MP,DENS,4, 1.043e-3
ET,5,PIPE18
R, 5, 4.500, 0.2370,  6.000    				! OUTER DIAMETER, WALL THICKNESS, RADIUS OF CURVATURE
MP,EX,  5, YOUNGMODULUS
MP,NUXY,5, NU
MP,GXY ,5, SHEARMODULUS
MP,DENS,5, 1.107e-3
ET,6,PIPE18
R, 6, 8.625, 0.3220, 12.000   				! OUTER DIAMETER, WALL THICKNESS, RADIUS OF CURVATURE
MP,EX,  6, YOUNGMODULUS
MP,NUXY,6, NU
MP,GXY ,6, SHEARMODULUS
MP,DENS,6, 1.253e-3
ET,7,COMBIN14,,1					! UX DEGREE OF FREEDOM
ET,8,COMBIN14,,2					! UY DEGREE OF FREEDOM
ET,9,COMBIN14,,3					! UZ DEGREE OF FREEDOM
R, 7, 1.e+5            					! STIFFNESS 
R, 8, 1.e+8            					! STIFFNESS 
R, 9, 1.e+11            				! STIFFNESS 
/COM, ANCHORS
ET,10,COMBIN14,,4					! ROTX DEGREE OF FREEDOM
ET,11,COMBIN14,,5					! ROTY DEGREE OF FREEDOM
ET,12,COMBIN14,,6					! ROTZ DEGREE OF FREEDOM
R,10, 1.e+20

N,  1, 
N,  2,  15.000, 
N,  3,  19.500,   -4.500 
N,  4,  19.500, -180.000 
N,  5,  19.500, -199.500 
N,  6,  19.500, -204.000,   4.500 
N,  7,  19.500, -204.000, 139.500 
N,  8,  24.000, -204.000, 144.000 
N,  9,  96.000, -204.000, 144.000 
N, 10, 254.000, -204.000, 144.000 
N, 11, 333.000, -204.000, 144.000 
N, 12, 411.000, -204.000, 144.000 
N, 13, 483.000, -204.000, 144.000 
N, 14, 487.500, -204.000, 148.500 
N, 15, 487.500, -204.000, 192.000 
N, 16, 487.500, -204.000, 235.500
N, 17, 492.000, -204.000, 240.000 
N, 18, 575.000, -204.000, 240.000 
N, 19, 723.000, -204.000, 240.000 
N, 20, 727.500, -208.500, 240.000 
N, 21, 727.500, -264.000, 240.000 
N, 22, 727.500, -264.000, 205.000 
N, 23, 727.500, -264.000, 190.000 
N, 24, 733.500, -264.000, 184.000 
N, 25, 753.500, -264.000, 184.000 
N, 26, 845.500, -264.000, 184.000 
N, 27, 851.500, -264.000, 178.000 
N, 28, 851.500, -264.000, 160.000 
N, 29, 851.500, -264.000, 142.000 
N, 30, 851.500, -270.000, 136.000 
N, 31, 851.500, -360.000, 136.000 
N, 32, 727.500, -264.000, 255.000 
N, 33, 727.500, -264.000, 270.000 
N, 34, 727.500, -264.000, 306.000 
N, 35, 727.500, -264.000, 414.000 
N, 36, 739.500, -264.000, 426.000 
N, 37, 847.500, -264.000, 426.000
N, 38, 955.500, -264.000, 426.000
/COM, NODES FOR CURVATURE
N, 203,  15.000,   -4.500
N, 506,  19.500, -199.500,   4.500
N, 708,  24.000, -204.000, 139.500
N,1314, 483.000, -204.000, 148.500
N,1617, 492.000, -204.000, 235.500
N,1920, 723.000, -208.500, 240.000
N,2324, 733.500, -264.000, 190.000
N,2627, 845.500, -264.000, 178.000
N,2930, 851.500, -270.000, 142.000
N,3536, 739.500, -264.000, 414.000
/COM, NODES FOR ELASTIC SUPPORT
DIST = 50.0 						! VISUALIZATION
N,10001,        -DIST
N,20001,             ,          DIST
N,30001,             ,              , -DIST
N,10004,  19.500+DIST, -180.000 
N,30004,  19.500     , -180.000     , -DIST 
N,20007,  19.500     , -204.000+DIST, 139.500 
N,20011, 333.000     , -204.000+DIST, 144.000 
N,30011, 333.000     , -204.000     , 144.000-DIST 
N,10015, 487.500-DIST, -204.000     , 192.000 
N,20017, 492.000     , -204.000-DIST, 240.000 
N,30017, 492.000     , -204.000     , 240.000-DIST 
N,10023, 727.500-DIST, -264.000     , 190.000 
N,20023, 727.500     , -264.000+DIST, 190.000
N,10031, 851.500+DIST, -360.000     , 136.000 
N,20031, 851.500     , -360.000-DIST, 136.000 
N,30031, 851.500     , -360.000     , 136.000-DIST 
N,20036, 739.500     , -264.000-DIST, 426.000 
N,30036, 739.500     , -264.000     , 426.000-DIST  
N,10038, 955.500+DIST, -264.000     , 426.000
N,20038, 955.500     , -264.000-DIST, 426.000
N,30038, 955.500     , -264.000     , 426.000-DIST
/COM,  STRAIGHT PIPE ELEMENTS
TYPE,1
REAL,1
MAT,1
E, 1, 2
E, 3, 4
E, 4, 5
E, 6, 7
E, 8, 9
E, 9,10
E,10,11
E,11,12
E,12,13
E,14,15
E,15,16
E,17,18
E,18,19
E,20,21
TYPE,2
REAL,2
MAT,2
E,21,22
E,22,23
E,24,25
E,25,26
E,27,28
E,28,29
E,30,31
TYPE,3
REAL,3
MAT,3
E,21,32
E,32,33
E,33,34
E,34,35
E,36,37
E,37,38
/COM, CURVED PIPE ELEMENTS
TYPE,4
REAL,4
MAT,4
E,2,3,203
E,5,6,506
E,7,8,708
E,13,14,1314
E,16,17,1617
E,19,20,1920
TYPE,5
REAL,5
MAT,5
E,23,24,2324
E,26,27,2627
E,29,30,2930
TYPE,6
REAL,6
MAT,6
E,35,36,3536
/COM,  ELASTIC SUPPORTS AND ANCHORS
TYPE,7
REAL,8
E, 4,10004
REAL,7
E,15,10015
REAL,7
E,23,10023
REAL,9
E, 1,10001
E,31,10031
E,38,10038
TYPE,8
REAL,8
E, 7,20007
REAL,8
E,11,20011
REAL,8
E,17,20017
REAL,8
E,23,20023
REAL,8
E,36,20036
REAL,9
E, 1,20001
E,31,20031
E,38,20038
TYPE,9
REAL,8
E, 4,30004
REAL,7
E,11,30011
REAL,7
E,17,30017
REAL,7
E,36,30036
REAL,9
E, 1,30001
E,31,30031
E,38,30038
TYPE,10
REAL,10
E, 1,10001
E,31,10031
E,38,10038
TYPE,11
REAL,10
E, 1,20001
E,31,20031
E,38,20038
TYPE,12
REAL,10
E, 1,30001
E,31,30031
E,38,30038
NSEL,S,NODE,,10000,40000
D,ALL,ALL,
ALLSEL,ALL
SAVE,MODEL,DB
FINI
/CLEAR,NOSTART
/FILNAME,CASE1
RESUME,MODEL,DB
FINI
/OUT,
/COM, 
/COM,  MODAL ANALYSIS WITH LUMPED MASS
/COM, 
/OUT,SCRATCH
/SOLU
ANTYPE,MODAL
MODOPT,LANB,14
LUMPM,ON					! LUMPED MASS MATRIX FORMULATION
MXPAND,14,,,YES
SOLVE
SAVE
*DIM,LABEL,,14
*DIM,FREQ_ANS,,14  				! FREQUENCIES OBTAINED FROM Mechanical APDL
*DIM,FREQ_EXP,,14       			! FREQUENCIES FROM REFERENCE
*DIM,FREQ_ERR,,14
*DO,I,1,14
LABEL(I)=I
*ENDDO
*DO,I,1,14
*GET,FREQ_ANS(I),MODE,I,FREQ
*ENDDO
*VFILL,FREQ_EXP,DATA,2.91,4.39,5.52,5.70,6.98,7.34,7.88,10.30,11.06,11.23
*VFILL,FREQ_EXP(11),DATA,11.50,12.43,13.88,16.12
*STAT,FREQ_ANS
*STAT,FREQ_EXP
*DO,I,1,14
FREQ_ERR(I)= ABS(FREQ_ANS(I)/(FREQ_EXP(I)))
*ENDDO
SAVE,TABLE_1
FINI
/OUT,
/COM, 
/COM,  SPECTRUM ANALYSIS WITH MISSING MASS
/COM, 
/OUT,SCRATCH
/SOLU
ANTYPE,SPECTRUM
SPOPT,SPRS,,
SVTYP,2,386.4
FREQ    , 0.20, 0.30, 0.40, 0.50, 0.60, 0.70, 0.80, 0.90, 1.00
SV, 0.01, 0.06, 0.13, 0.13, 0.20, 0.35, 0.39, 0.37, 0.41, 0.76
FREQ    , 1.10, 1.20, 1.30, 1.40, 1.50, 1.60, 1.70, 1.80, 1.90
SV, 0.01, 0.64, 0.59, 0.91, 1.03, 1.46, 0.95, 0.91, 1.61, 1.92
FREQ    , 2.00, 2.10, 2.20, 2.30, 2.40, 2.50, 2.60, 2.70, 2.80
SV, 0.01, 1.57, 1.18, 2.65, 2.85, 3.26, 4.47, 4.75, 5.29, 7.44
FREQ    , 2.90, 3.00, 3.15, 3.30, 3.45, 3.60, 3.80, 4.00, 4.20
SV, 0.01, 4.27, 4.61, 4.13, 3.96, 4.05, 2.44, 2.09, 2.29, 1.52
FREQ    , 4.40, 4.60, 4.80, 5.00, 5.25, 5.50, 5.75, 6.00, 6.25
SV, 0.01, 1.34, 1.37, 1.36, 1.31, 1.69, 1.27, 1.04, 0.76, 0.76
FREQ    , 6.50, 6.75, 7.00, 7.25, 7.50, 7.75, 8.00, 8.50, 9.00
SV, 0.01, 0.69, 0.70, 0.74, 0.70, 0.67, 0.66, 0.61, 0.75, 0.60
FREQ    , 9.50,10.00,10.50,11.00,11.50,12.00,12.50,13.00,13.50
SV, 0.01, 0.69, 0.61, 0.70, 0.59, 0.61, 0.56, 0.59, 0.59, 0.59
FREQ    ,14.00,14.50,15.00,16.00,17.00,18.00,20.00,22.00,25.00
SV, 0.01, 0.58, 0.59, 0.58, 0.55, 0.56, 0.55, 0.55, 0.55, 0.54
FREQ    ,28.00,31.00,34.00, 0.00, 0.00, 0.00, 0.00, 0.00, 0.00
SV, 0.01, 0.54, 0.54, 0.54, 0.00, 0.00, 0.00, 0.00, 0.00, 0.00
SVPLOT,OFF,0.01
SED,1,0,0                    			! EXCITATION IN X DIRECTION
SRSS,0.001,disp               			! SRSS MODE COMBINATION METHOD
MMASS,ON,0.54                 			! MISSING MASS ON WITH ZPA VALUE OF 0.54
SOLVE
SAVE
FINI
/POST1
/COM, TOTAL RESPONSE OF THE STRUCTURE 
/INPUT,CASE1,mcom
/OUT,
*GET,RF10001_1,NODE,10001,RF,FX
*GET,RF20001_1,NODE,20001,RF,FY
*GET,RF30001_1,NODE,30001,RF,FZ
*GET,RF10031_1,NODE,10031,RF,FX
*GET,RF20031_1,NODE,20031,RF,FY
*GET,RF30031_1,NODE,30031,RF,FZ
/OUT,SCRATCH
*DIM,LABEL,CHAR,1,6
*DIM,VALUE,,6,3
LABEL(1,1) = 'FX1'
LABEL(1,2) = 'FY1'
LABEL(1,3) = 'FZ1'
LABEL(1,4) = 'FX31'
LABEL(1,5) = 'FY31'
LABEL(1,6) = 'FZ31'
/COM,
/COM,  REACTION FORCES OBTAINED FROM REFERENCE AND FROM Mechanical APDL FOR NODES 1 AND 31
/COM,
*VFILL,VALUE(1,1),DATA,43.71*1.10           	! FX AT NODE 1 WITH SRSS COMBINATION
*VFILL,VALUE(1,2),DATA,RF10001_1              	! FX AT NODE 10001 OBTAINED FROM Mechanical APDL
*VFILL,VALUE(1,3),DATA,ABS(RF10001_1/(43.71*1.10))
*VFILL,VALUE(2,1),DATA,4.36*1.26              	! FY AT NODE 1 WITH SRSS COMBINATION
*VFILL,VALUE(2,2),DATA,RF20001_1              	! FY AT NODE 20001 OBTAINED FROM Mechanical APDL
*VFILL,VALUE(2,3),DATA,ABS(RF20001_1/(4.36*1.26))
*VFILL,VALUE(3,1),DATA,1.60*4.74              	! FZ AT NODE 1 WITH SRSS COMBINATION
*VFILL,VALUE(3,2),DATA,RF30001_1              	! FZ AT NODE 30001 OBTAINED FROM Mechanical APDL
*VFILL,VALUE(3,3),DATA,ABS(RF30001_1/(4.74*1.60))
*VFILL,VALUE(4,1),DATA,55.05*0.92             	! FX AT NODE 31 WITH SRSS COMBINATION
*VFILL,VALUE(4,2),DATA,RF10031_1              	! FX AT NODE 10031 OBTAINED FROM Mechanical APDL
*VFILL,VALUE(4,3),DATA,ABS(RF10031_1/(55.05*0.92))
*VFILL,VALUE(5,1),DATA,14.17*1.75             	! FY AT NODE 31 WITH SRSS COMBINATION
*VFILL,VALUE(5,2),DATA,RF20031_1              	! FY AT NODE 20031 OBTAINED FROM Mechanical APDL
*VFILL,VALUE(5,3),DATA,ABS(RF20031_1/(14.17*1.75))
*VFILL,VALUE(6,1),DATA,16.08*1.97            	! FZ AT NODE 31 WITH SRSS COMBINATION
*VFILL,VALUE(6,2),DATA,RF30031_1             	! FZ AT NODE 30031 OBTAINED FROM Mechanical APDL
*VFILL,VALUE(6,3),DATA,ABS(RF30031_1/(16.08*1.97))
SAVE,TABLE_2
FINISH
/CLEAR,NOSTART
/OUT,
/COM,  
/COM,  SPECTRUM ANALYSIS WITH RIGID RESPONSES AND MISSING MASS (USING LINDLEY METHOD)
/COM,  
/OUT,SCRATCH
/FILNAME,CASE2
RESUME,MODEL,DB
FINI
/SOLU
ANTYPE,MODAL
MODOPT,LANB,14
LUMPM,ON                                 	! LUMPED MASS MATRIX FORMULATION
MXPAND,14,,,YES              
SOLVE
SAVE,
FINI
/SOLU
ANTYPE,SPECTRUM
SPOPT,SPRS,,
SVTYP,2,386.4
FREQ    , 0.20, 0.30, 0.40, 0.50, 0.60, 0.70, 0.80, 0.90, 1.00
SV, 0.01, 0.06, 0.13, 0.13, 0.20, 0.35, 0.39, 0.37, 0.41, 0.76
FREQ    , 1.10, 1.20, 1.30, 1.40, 1.50, 1.60, 1.70, 1.80, 1.90
SV, 0.01, 0.64, 0.59, 0.91, 1.03, 1.46, 0.95, 0.91, 1.61, 1.92
FREQ    , 2.00, 2.10, 2.20, 2.30, 2.40, 2.50, 2.60, 2.70, 2.80
SV, 0.01, 1.57, 1.18, 2.65, 2.85, 3.26, 4.47, 4.75, 5.29, 7.44
FREQ    , 2.90, 3.00, 3.15, 3.30, 3.45, 3.60, 3.80, 4.00, 4.20
SV, 0.01, 4.27, 4.61, 4.13, 3.96, 4.05, 2.44, 2.09, 2.29, 1.52
FREQ    , 4.40, 4.60, 4.80, 5.00, 5.25, 5.50, 5.75, 6.00, 6.25
SV, 0.01, 1.34, 1.37, 1.36, 1.31, 1.69, 1.27, 1.04, 0.76, 0.76
FREQ    , 6.50, 6.75, 7.00, 7.25, 7.50, 7.75, 8.00, 8.50, 9.00
SV, 0.01, 0.69, 0.70, 0.74, 0.70, 0.67, 0.66, 0.61, 0.75, 0.60
FREQ    , 9.50,10.00,10.50,11.00,11.50,12.00,12.50,13.00,13.50
SV, 0.01, 0.69, 0.61, 0.70, 0.59, 0.61, 0.56, 0.59, 0.59, 0.59
FREQ    ,14.00,14.50,15.00,16.00,17.00,18.00,20.00,22.00,25.00
SV, 0.01, 0.58, 0.59, 0.58, 0.55, 0.56, 0.55, 0.55, 0.55, 0.54
FREQ    ,28.00,31.00,34.00, 0.00, 0.00, 0.00, 0.00, 0.00, 0.00
SV, 0.01, 0.54, 0.54, 0.54, 0.00, 0.00, 0.00, 0.00, 0.00, 0.00
SVPLOT,OFF,0.01
SED,1,0,0                             		! EXCITATION IN X DIRECTION
SRSS,0.001,DISP                           	! SRSS MODE COMBINATION METHOD
MMASS,ON,0.54                             	! MISSING MASS ON WITH ZPA = 0.54
RIGRESP,ON,LINDLEY,0.54                   	! RIGID RESPONSE USING LINDLEY METHOD
SOLVE
SAVE
FINI
/POST1
/COM, TOTAL RESPONSE OF THE STRUCTURE 
/INPUT,CASE2,mcom
/OUT,
*GET,RF10001_2,NODE,10001,RF,FX
*GET,RF20001_2,NODE,20001,RF,FY
*GET,RF30001_2,NODE,30001,RF,FZ
*GET,RF10031_2,NODE,10031,RF,FX
*GET,RF20031_2,NODE,20031,RF,FY
*GET,RF30031_2,NODE,30031,RF,FZ
/OUT,SCRATCH
*DIM,LABEL,CHAR,1,6
*DIM,VALUE,,6,3
LABEL(1,1) = 'FX1'
LABEL(1,2) = 'FY1'
LABEL(1,3) = 'FZ1'
LABEL(1,4) = 'FX31'
LABEL(1,5) = 'FY31'
LABEL(1,6) = 'FZ31'
/COM,
/COM,  REACTION FORCES OBTAINED FROM REFERENCE AND FROM Mechanical APDL FOR NODES 1 AND 31
/COM,
*VFILL,VALUE(1,1),DATA,43.71*1.06                 ! FX AT NODE 1 WITH SRSS COMBINATION
*VFILL,VALUE(1,2),DATA,RF10001_2                  ! FX AT NODE 10001 OBTAINED FROM Mechanical APDL
*VFILL,VALUE(1,3),DATA,ABS(RF10001_2/(43.71*1.06))
*VFILL,VALUE(2,1),DATA,4.36*0.85                  ! FY AT NODE 1 WITH SRSS COMBINATION
*VFILL,VALUE(2,2),DATA,RF20001_2                  ! FY AT NODE 20001 OBTAINED FROM Mechanical APDL
*VFILL,VALUE(2,3),DATA,ABS(RF20001_2/(4.36*0.85))
*VFILL,VALUE(3,1),DATA,1.60*2.21                  ! FZ AT NODE 1 WITH SRSS COMBINATION
*VFILL,VALUE(3,2),DATA,RF30001_2                  ! FZ AT NODE 30001 OBTAINED FROM Mechanical APDL
*VFILL,VALUE(3,3),DATA,ABS(RF30001_2/(2.21*1.60))
*VFILL,VALUE(4,1),DATA,55.05*1.02                 ! FX AT NODE 31 WITH SRSS COMBINATION
*VFILL,VALUE(4,2),DATA,RF10031_2                  ! FX AT NODE 10031 OBTAINED FROM Mechanical APDL
*VFILL,VALUE(4,3),DATA,ABS(RF10031_2/(55.05*1.02))
*VFILL,VALUE(5,1),DATA,14.17*1.26                 ! FY AT NODE 31 WITH SRSS COMBINATION
*VFILL,VALUE(5,2),DATA,RF20031_2                  ! FY AT NODE 20031 OBTAINED FROM Mechanical APDL
*VFILL,VALUE(5,3),DATA,ABS(RF20031_2/(14.17*1.26))
*VFILL,VALUE(6,1),DATA,16.08*1.43                 ! FZ AT NODE 31 WITH SRSS COMBINATION
*VFILL,VALUE(6,2),DATA,RF30031_2                  ! FZ AT NODE 30031 OBTAINED FROM Mechanical APDL
*VFILL,VALUE(6,3),DATA,ABS(RF30031_2/(16.08*1.43))
SAVE,TABLE_3
FINISH
/CLEAR,NOSTART
/OUT,
/COM,
/COM,  SPECTRUM ANALYSIS WITH RIGID RESPONSES AND MISSING MASS (GUPTA METHOD)
/COM,
/OUT,SCRATCH
/FILNAME,CASE3
RESUME,MODEL,DB
FINI
/SOLU
ANTYPE,MODAL
MODOPT,LANB,14
LUMPM,ON                                   	 ! LUMPED MASS MATRIX FORMULATION
MXPAND,14,,,YES
SOLVE
SAVE
FINI
/SOLU
ANTYPE,SPECTRUM
SPOPT,SPRS,,
SVTYP,2,386.4
FREQ    , 0.20, 0.30, 0.40, 0.50, 0.60, 0.70, 0.80, 0.90, 1.00
SV, 0.01, 0.06, 0.13, 0.13, 0.20, 0.35, 0.39, 0.37, 0.41, 0.76
FREQ    , 1.10, 1.20, 1.30, 1.40, 1.50, 1.60, 1.70, 1.80, 1.90
SV, 0.01, 0.64, 0.59, 0.91, 1.03, 1.46, 0.95, 0.91, 1.61, 1.92
FREQ    , 2.00, 2.10, 2.20, 2.30, 2.40, 2.50, 2.60, 2.70, 2.80
SV, 0.01, 1.57, 1.18, 2.65, 2.85, 3.26, 4.47, 4.75, 5.29, 7.44
FREQ    , 2.90, 3.00, 3.15, 3.30, 3.45, 3.60, 3.80, 4.00, 4.20
SV, 0.01, 4.27, 4.61, 4.13, 3.96, 4.05, 2.44, 2.09, 2.29, 1.52
FREQ    , 4.40, 4.60, 4.80, 5.00, 5.25, 5.50, 5.75, 6.00, 6.25
SV, 0.01, 1.34, 1.37, 1.36, 1.31, 1.69, 1.27, 1.04, 0.76, 0.76
FREQ    , 6.50, 6.75, 7.00, 7.25, 7.50, 7.75, 8.00, 8.50, 9.00
SV, 0.01, 0.69, 0.70, 0.74, 0.70, 0.67, 0.66, 0.61, 0.75, 0.60
FREQ    , 9.50,10.00,10.50,11.00,11.50,12.00,12.50,13.00,13.50
SV, 0.01, 0.69, 0.61, 0.70, 0.59, 0.61, 0.56, 0.59, 0.59, 0.59
FREQ    ,14.00,14.50,15.00,16.00,17.00,18.00,20.00,22.00,25.00
SV, 0.01, 0.58, 0.59, 0.58, 0.55, 0.56, 0.55, 0.55, 0.55, 0.54
FREQ    ,28.00,31.00,34.00, 0.00, 0.00, 0.00, 0.00, 0.00, 0.00
SV, 0.01, 0.54, 0.54, 0.54, 0.00, 0.00, 0.00, 0.00, 0.00, 0.00
SVPLOT,OFF,0.01
SED,1,0,0                                 	! EXCITATION IN X DIRECTION
SRSS,0.001,DISP                           	! SRSS MODE COMBINATION
MMASS,ON,0.54                             	! MISSING MASS ON WITH ZPA VALUE OF 0.54
RIGRESP,ON,GUPTA,2.8,6.0                  	! RIGID RESPONSE ON WITH GUPTA METHOD
SOLVE
SAVE
FINI
/POST1
/COM, TOTAL RESPONSE OF THE STRUCTURE 
/INPUT,CASE3,mcom
/OUT,
*GET,RF10001_3,NODE,10001,RF,FX
*GET,RF20001_3,NODE,20001,RF,FY
*GET,RF30001_3,NODE,30001,RF,FZ
*GET,RF10031_3,NODE,10031,RF,FX
*GET,RF20031_3,NODE,20031,RF,FY
*GET,RF30031_3,NODE,30031,RF,FZ
/OUT,SCRATCH
*DIM,LABEL,CHAR,1,6
*DIM,VALUE,,6,3
LABEL(1,1) = 'FX1'
LABEL(1,2) = 'FY1'
LABEL(1,3) = 'FZ1'
LABEL(1,4) = 'FX31'
LABEL(1,5) = 'FY31'
LABEL(1,6) = 'FZ31'
/COM,
/COM,  REACTION FORCES OBTAINED FROM REFERENCE AND FROM Mechanical APDL FOR NODES 1 AND 31
/COM,
*VFILL,VALUE(1,1),DATA,45.43                     ! FX AT NODE 1 WITH SRSS COMBINATION
*VFILL,VALUE(1,2),DATA,RF10001_3                 ! FX AT NODE 10001 OBTAINED FROM Mechanical APDL
*VFILL,VALUE(1,3),DATA,ABS(RF10001_3/(45.43))
*VFILL,VALUE(2,1),DATA,3.08                      ! FY AT NODE 1 WITH SRSS COMBINATION
*VFILL,VALUE(2,2),DATA,RF20001_3                 ! FY AT NODE 20001 OBTAINED FROM Mechanical APDL
*VFILL,VALUE(2,3),DATA,ABS(RF20001_3/(3.08))
*VFILL,VALUE(3,1),DATA,1.34                      ! FZ AT NODE 1 WITH SRSS COMBINATION
*VFILL,VALUE(3,2),DATA,RF30001_3                 ! FZ AT NODE 30001 OBTAINED FROM Mechanical APDL
*VFILL,VALUE(3,3),DATA,ABS(RF30001_3/(1.34))
*VFILL,VALUE(4,1),DATA,56.06                     ! FX AT NODE 31 WITH SRSS COMBINATION
*VFILL,VALUE(4,2),DATA,RF10031_3                 ! FX AT NODE 10031 OBTAINED FROM Mechanical APDL
*VFILL,VALUE(4,3),DATA,ABS(RF10031_3/(56.06))
*VFILL,VALUE(5,1),DATA,14.19                     ! FY AT NODE 31 WITH SRSS COMBINATION
*VFILL,VALUE(5,2),DATA,RF20031_3                 ! FY AT NODE 20031 OBTAINED FROM Mechanical APDL
*VFILL,VALUE(5,3),DATA,ABS(RF20031_3/(14.19))
*VFILL,VALUE(6,1),DATA,13.95                     ! FZ AT NODE 31 WITH SRSS COMBINATION
*VFILL,VALUE(6,2),DATA,RF30031_3                 ! FZ AT NODE 30031 OBTAINED FROM Mechanical APDL
*VFILL,VALUE(6,3),DATA,ABS(RF30031_3/(13.95))
SAVE,TABLE_4
FINISH
RESUME,TABLE_1
/COM,
/OUT,vm259,vrt
/COM, 
/COM, ------------------------ VM259 RESULTS COMPARISON ---------------------------
/COM,
/COM,           |  TARGET  |   Mechanical APDL  |  RATIO
/COM,
/COM,
/COM, ==================================
/COM,   FREQUENCIES FROM MODAL ANALYSIS
/COM, ==================================
*VWRITE,LABEL(1),FREQ_EXP(1),FREQ_ANS(1),FREQ_ERR(1)
(1X,F3.0,4X,F10.4,4X,F14.4,4X,F15.3)
/COM,
/COM,
/NOPR,
RESUME,TABLE_2
/GOPR
/COM,
/COM,
/COM, ======================================
/COM,  SPECTRUM ANALYSIS WITH MISSING MASS
/COM, ======================================
/COM,
*VWRITE,LABEL(1,1),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,'   ',F10.4,'  ',F14.4,'   ',F15.3)
*VWRITE,LABEL(1,2),VALUE(2,1),VALUE(2,2),VALUE(2,3)
(1X,A8,'   ',F10.4,'  ',F14.4,'   ',F15.3)
*VWRITE,LABEL(1,3),VALUE(3,1),VALUE(3,2),VALUE(3,3)
(1X,A8,'   ',F10.4,'  ',F14.4,'   ',F15.3)
*VWRITE,LABEL(1,4),VALUE(4,1),VALUE(4,2),VALUE(4,3)
(1X,A8,'   ',F10.4,'  ',F14.4,'   ',F15.3)
*VWRITE,LABEL(1,5),VALUE(5,1),VALUE(5,2),VALUE(5,3)
(1X,A8,'   ',F10.4,'  ',F14.4,'   ',F15.3)
*VWRITE,LABEL(1,6),VALUE(6,1),VALUE(6,2),VALUE(6,3)
(1X,A8,'   ',F10.4,'  ',F14.4,'   ',F15.3)
/COM,
/COM,
/NOPR
RESUME,TABLE_3
/GOPR
/COM,
/COM, ===================================================================
/COM,  SPECTRUM ANALYSIS WITH MISSING MASS AND RIGID RESPONSES (LINDLEY)
/COM, ===================================================================
/COM,
/COM,
*VWRITE,LABEL(1,1),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,'   ',F10.4,'  ',F14.4,'   ',F15.3)
*VWRITE,LABEL(1,2),VALUE(2,1),VALUE(2,2),VALUE(2,3)
(1X,A8,'   ',F10.4,'  ',F14.4,'   ',F15.3)
*VWRITE,LABEL(1,3),VALUE(3,1),VALUE(3,2),VALUE(3,3)
(1X,A8,'   ',F10.4,'  ',F14.4,'   ',F15.3)
*VWRITE,LABEL(1,4),VALUE(4,1),VALUE(4,2),VALUE(4,3)
(1X,A8,'   ',F10.4,'  ',F14.4,'   ',F15.3)
*VWRITE,LABEL(1,5),VALUE(5,1),VALUE(5,2),VALUE(5,3)
(1X,A8,'   ',F10.4,'  ',F14.4,'   ',F15.3)
*VWRITE,LABEL(1,6),VALUE(6,1),VALUE(6,2),VALUE(6,3)
(1X,A8,'   ',F10.4,'  ',F14.4,'   ',F15.3)
/NOPR
RESUME,TABLE_4
/GOPR
/COM,
/COM, ==================================================================
/COM,  SPECTRUM ANALYSIS WITH MISSING MASS AND RIGID RESPONSES (GUPTA)
/COM, ===================================================================
/COM,
/COM,
*VWRITE,LABEL(1,1),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,'   ',F10.4,'  ',F14.4,'   ',F15.3)
*VWRITE,LABEL(1,2),VALUE(2,1),VALUE(2,2),VALUE(2,3)
(1X,A8,'   ',F10.4,'  ',F14.4,'   ',F15.3)
*VWRITE,LABEL(1,3),VALUE(3,1),VALUE(3,2),VALUE(3,3)
(1X,A8,'   ',F10.4,'  ',F14.4,'   ',F15.3)
*VWRITE,LABEL(1,4),VALUE(4,1),VALUE(4,2),VALUE(4,3)
(1X,A8,'   ',F10.4,'  ',F14.4,'   ',F15.3)
*VWRITE,LABEL(1,5),VALUE(5,1),VALUE(5,2),VALUE(5,3)
(1X,A8,'   ',F10.4,'  ',F14.4,'   ',F15.3)
*VWRITE,LABEL(1,6),VALUE(6,1),VALUE(6,2),VALUE(6,3)
(1X,A8,'   ',F10.4,'  ',F14.4,'   ',F15.3)
/NOPR
/COM,
/COM,---------------------------------------------------------------------------------------
/OUT
*LIST,vm259,vrt
FINISH

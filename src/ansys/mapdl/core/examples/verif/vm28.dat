/COM,ANSYS MEDIA REL. 2023R1 (11/04/2022) REF. VERIF. MANUAL: REL. 2023R1
/VERIFY,VM28
/PREP7
/TITLE, VM28,  TRANSIENT HEAT TRANSFER IN AN INFINITE SLAB
/COM,   "HEAT TRANSFER", HOLMAN, 4TH ED., PG. 106
/NOPR
ANTYPE,TRANS
ET,1,PLANE77
MP,KXX,1,54
MP,DENS,1,7833
MP,C,1,465
N,1
N,12,,1
FILL,,,,,,,,.1                      ! BIAS MESH TOWARD SURFACE WITH 1:10 RATIO
NGEN,2,12,1,12,1,.1                 ! GENERATE EDGE NODES
E,1,13,14,2
EGEN,11,1,-1                        ! GENERATE ELEMENTS FROM EDGE NODES
EMID                                ! PLACE MIDSIDE NODES ON ELEMENTS
NSEL,S,LOC,X,0
NLIST,ALL                           ! LIST NODES ALONG LENGTH
NSEL,S,LOC,Y,1
SF,ALL,CONV,50,1000                 ! APPLY CONVECTION TO TOP SURFACE
NSEL,ALL
TUNIF,0                             ! DEFINE INITIAL TEMPERATURES, T(0)
FINISH
/SOLU    
KBC,1                               ! STEP BOUNDARY CONDITIONS
DELTIM,10                           ! MINIMUM TIME STEP
TIME,2000.0                         ! TIME AT END OF TRANSIENT
OUTRES,,ALL
AUTOTS,ON
NEQIT,25
CNVTOL,HEAT,,,,1.0
TINTP,,,,0.5,,0.2
/OUT,SCRATCH
SOLVE
FINISH
/POST26
NSOL,2,11,TEMP,,T11                 ! STORE TEMPERATURES AT SELECT NODES
NSOL,3,9,TEMP,,T9
NSOL,4,7,TEMP,,T7
NSOL,5,5,TEMP,,T5
NSOL,6,1,TEMP,,T1
/OUT,
PRVAR,2,3,4,5,6                     ! PRINT TEMPERATURE SOLUTION VS. TIME
*GET,TEMP_11,NODE,11,TEMP
*GET,TEMP_9,NODE,9,TEMP
*GET,TEMP_7,NODE,7,TEMP
*GET,TEMP_5,NODE,5,TEMP

*DIM,LABEL,CHAR,4,2
*DIM,VALUE,,4,3
LABEL(1,1) = 'TEMP (C)','TEMP (C)','TEMP (C)','TEMP (C)'
LABEL(1,2) = ' NODE 11',' NODE 9 ',' NODE 7 ',' NODE 5 '
*VFILL,VALUE(1,1),DATA,140,98.9,51.8,14.5
*VFILL,VALUE(1,2),DATA,TEMP_11,TEMP_9,TEMP_7,TEMP_5
*VFILL,VALUE(1,3),DATA,ABS(TEMP_11/140),ABS(TEMP_9/98.9),ABS(TEMP_7/51.8),ABS(TEMP_5/14.5)
/COM
/OUT,vm28,vrt
/COM,------------------- VM28 RESULTS COMPARISON ---------------
/COM,
/COM,                 |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.1,'  ',F14.1,'   ',1F15.3)
/COM,-----------------------------------------------------------

/OUT
FINISH
*LIST,vm28,vrt


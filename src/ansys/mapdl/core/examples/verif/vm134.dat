/COM,ANSYS MEDIA REL. 2023R1 (11/04/2022) REF. VERIF. MANUAL: REL. 2023R1
/VERIFY,VM134
/PREP7
/TITLE, VM134, PLASTIC BENDING OF A CLAMPED I-BEAM
C***    THE ANALYSIS OF STRUCTURES, N.J. HOFF, 1964, P. 388
ET,1,BEAM188,,,3
MP,EX  ,1,29E6
MP,PRXY,1,0.3
EX=29E6
ET=5.8E6
EP=EX*ET/(EX-ET)
TB,PLAS,1,1,,BKIN             ! BILINEAR KINEMATIC HARDENING BEHAVIOR
TBTEMP,0.0
TBDATA,1,38000,EP             ! YIELD STRESSES AND PLASTIC TANGENT MODULUS
SECTYPE,1,BEAM,I
SECDATA,19.80,19.80,10.6,0.504,0.504,0.001
SECCONTROL,1E15               ! OVERRIDE THE PROGRAM-CALCULATED TRANSVERSE SHEAR STIFFNESS
N,1
N,10,72
FILL
N,100,,,1
E,1,2,100
*REPEAT,9,1,1
D,1,ALL                       ! FIX ONE END
D,10,ROTY,,,,,UY,ROTX,ROTZ    ! SYMMETRIC MID-SPAN B.C.
FINISH
/SOLU
SFBEAM,1,1,PRES,2190          ! LOAD STEP 1: W1
*REPEAT,9,1
OUTPR,BASIC,LAST              ! PRINT BASIC SOLUTION FOR LAST SUBSTEP
/OUT,SCRATCH
SOLVE
FINISH
/POST1
/OUT,
*GET,DEF1,NODE,10,UZ
FINISH
/POST26
RFORCE,2,10,M,Y
RFORCE,3,1,M,Y
PLVAR,2,3
*GET,MID1,VARI,2,EXTREM,VMAX
*GET,END1,VARI,3,EXTREM,VMAX
FINISH
/SOLU
SFBEAM,1,1,PRES,3771          ! LOAD STEP 2: W2
*REPEAT,9,1
/OUT,SCRATCH
SOLVE
FINISH
/POST1
/OUT,
*GET,DEF2,NODE,10,UZ
FINISH
/POST26
RFORCE,2,10,M,Y
RFORCE,3,1,M,Y
PLVAR,2,3
*GET,MID2,VARI,2,EXTREM,VMAX
*GET,END2,VARI,3,EXTREM,VMAX
FINISH
/SOLU
SFBEAM,1,1,PRES,9039          ! LOAD STEP 3: W3; PLASTIC YIELDING
*REPEAT,9,1
/OUT,SCRATCH
SOLVE
FINISH
/POST1
/OUT,
*GET,DEF3,NODE,10,UZ
FINISH
/POST26
RFORCE,2,10,M,Y
RFORCE,3,1,M,Y
PLVAR,2,3
*GET,MID3,VARI,2,EXTREM,VMAX
*GET,END3,VARI,3,EXTREM,VMAX
ESEL,S,ELEM,,1,1
FINISH
/POST1
ETABLE,ESA1,LEPEL,1
ESORT,ESA1,,,1
*GET,EPELA,SORT,,MAX
ETABLE,ESB1,LEPPL,1
ESORT,ESB1,,,1
*GET,EPPLA,SORT,,MAX
FINISH
/POST26
ESEL,S,ELEM,,9,9
FINISH
/POST1
ETABLE,ESA2,LEPEL,1
ESORT,ESA2,,,1
*GET,EPELB,SORT,,MAX
ETABLE,ESB2,LEPPL,1
ESORT,ESB2,,,1
*GET,EPPLB,SORT,,MAX
*SET,ENDST,ABS(EPPLA+EPELA)
*SET,MIDST,ABS(EPELB+EPPLB)
*status,parm
FINISH
*DIM,LABEL,CHAR,3,2
*DIM,VALUE,,3,3
LABEL(1,1) = 'MID DEFL  ','MOM END ','MID MOM '
LABEL(1,2) = ' in ','in-lb','in-lb'
*VFILL,VALUE(1,1),DATA,-.160,-3.784E6,-1.892E6
*VFILL,VALUE(1,2),DATA,DEF1,END1,MID1
*VFILL,VALUE(1,3),DATA,ABS(DEF1/(-.160)),ABS(END1/(-3.784E6)),ABS(MID1/(-1.892E6))
/COM
/OUT,vm134,vrt
/COM,------------------- VM134 RESULTS COMPARISON ------------------
/COM,
/COM,   W1=2190 lb/in  |      TARGET   |      Mechanical APDL   | RATIO
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F13.3,'  ',F22.3,'   ',1F16.2)
/COM,---------------------------------------------------------------
/OUT
/NOPR
LABEL(1,1) = 'MID DEFL  ','MOM END ','MID MOM '
LABEL(1,2) = ' in ','in/lb','in/lb'
*VFILL,VALUE(1,1),DATA,-.357,-5.98E6,-3.78E6
*VFILL,VALUE(1,2),DATA,DEF2,END2,MID2
*VFILL,VALUE(1,3),DATA,ABS(DEF2/(-.357)),ABS(END2/(-5.98E6)),ABS(MID2/(-3.78E6))
/GOPR
/COM
/OUT,vm134,vrt,,APPEND
/COM,------------------- VM134 RESULTS COMPARISON ------------------
/COM,
/COM,   W2=3771 lb/in  |      TARGET   |      Mechanical APDL   | RATIO
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F13.3,'  ',F22.3,'   ',1F16.2)
/COM,---------------------------------------------------------------
/OUT
/NOPR
*DIM,LABEL1,CHAR,5,2
*DIM,VALUE1,,5,3
LABEL1(1,1) = 'MID DEFL  ','MOM END ','MID MOM ','TTL END ','TTL MID '
LABEL1(1,2) = ' in ','in-lb','in-lb','STRAIN','STRAIN'
*VFILL,VALUE1(1,1),DATA,-2.09,-1.51E7,-8.36E6,.02,.0089
*VFILL,VALUE1(1,2),DATA,DEF3,END3,MID3,ENDST,MIDST
V1 = ABS(DEF3/(-2.09))
V2 = ABS(END3/(-1.51E7))
V3 = ABS(MID3/(-8.36E6))
V4 = ABS(ENDST/.02)
V5 = ABS(MIDST/.0089)
*VFILL,VALUE1(1,3),DATA,V1,V2,V3,V4,V5
/GOPR
/COM
/OUT,vm134,vrt,,APPEND
/COM,------------------- VM134 RESULTS COMPARISON ------------------
/COM,
/COM,   W3=9039 lb/in  |        TARGET   |        Mechanical APDL   | RATIO
/COM,
*VWRITE,LABEL1(1,1),LABEL1(1,2),VALUE1(1,1),VALUE1(1,2),VALUE1(1,3)
(1X,A8,A8,'   ',F15.4,'  ',F22.4,'   ',1F16.2)
/COM,---------------------------------------------------------------
/OUT
/OUT,vm134,vrt,,APPEND
/COM,---------------------------------------------------------------
/COM,NOTE: THERE ARE VERIFIED RESULTS IN VM134 NOT CONTAINED IN
/COM,THIS TABLE
/COM,---------------------------------------------------------------
/OUT
FINISH
*LIST,vm134,vrt

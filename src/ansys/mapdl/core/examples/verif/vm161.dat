/COM,ANSYS MEDIA REL. 2023R1 (11/04/2022) REF. VERIF. MANUAL: REL. 2023R1
/VERIFY,VM161
/PREP7
/TITLE, VM161, HEAT FLOW FROM AN INSULATED PIPE
C***  PRINCIPLES OF HEAT TRANSFER, KREITH, 2ND. PRINTING, PAGE 36, EX. 2-7
ANTYPE,STATIC
ET,1,SOLID90
MP,KXX,1,25
MP,KXX,2,.11
CSYS,1                       ! CYLINDRICAL C.S.
N,1,.12791666
N,2,.14583333
N,3,.1875
NGEN,3,3,1,3,1,,15
NGEN,3,10,1,9,1,,,.5
E,1,21,27,7,2,22,28,8
EMORE,11,24,17,4,12,25,18,5
EMORE,30,34,36,32
EGEN,2,1,1
MAT,2
EMODIF,2
NSEL,S,LOC,X,.12791666
SF,ALL,CONV,40,300           ! CONVECTION ON THE INSIDE
NSEL,S,LOC,X,.1875
SF,ALL,CONV,4,80             ! CONVECTION ON THE OUTSIDE
NSEL,ALL
OUTPR,,1
FINISH
/SOLU    
/OUT,SCRATCH
SOLVE
FINISH
/POST1
SET,1,1
/OUT,
ESEL,S,ELEM,,1,1
FSUM,HEAT
*GET,Q1,FSUM,0,ITEM,HEAT
*SET,Q,ABS((Q1*360/30))
*DIM,LABEL,CHAR,1,2
*DIM,VALUE,,1,3
LABEL(1,1) = 'q '
LABEL(1,2) = 'BTU/hr'
*VFILL,VALUE(1,1),DATA,362
*VFILL,VALUE(1,2),DATA,Q
*VFILL,VALUE(1,3),DATA,ABS(Q/362)
/COM
/OUT,vm161,vrt
/COM,------------------- VM161 RESULTS COMPARISON -------------
/COM,
/COM,                 |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.1,'  ',F13.1,'   ',1F15.3)
/COM,----------------------------------------------------------
/OUT
FINISH
*LIST,vm161,vrt

/COM,ANSYS MEDIA REL. 150 (11/8/2013) REF. VERIF. MANUAL: REL. 150
/VERIFY,vm224
JPGPRF,500,100,1             ! MACRO TO SET PREFS FOR JPEG PLOTS
/SHOW,JPEG
/TITLE,VM224, Implicit Creep Under Biaxial Load
/COM, NAFEMS Fundamental Tests of Creep Behavior, Becker and Hyde
/NOPR
/COM,
/COM, 2D CREEP TESTS WITH BIAXIAL CONSTANT LOAD, 
/COM, REFERENCE: TEST 10(A) FROM NAFEMS R0027. 
/COM,
/COM, EXPECTED RESULTS:
/COM,       TIME | EPCRX  | EPCRY                    
/COM,   ----------------------------           
/COM,        0.0 | 0.0    | -0.0    
/COM,        0.1 | 0.0427 | -0.0427 
/COM,        1.0 | 0.135  | -0.135  
/COM,        5.0 | 0.3019 | -0.3019 
/COM,       10.0 | 0.4269 | -0.4269 
/COM,       50.0 | 0.9546 | -0.9546 
/COM,      100.0 | 1.35   | -1.35   
/COM,      500.0 | 3.019  | -3.019  
/COM,     1000.0 | 4.2691 | -4.2691 
/COM,
/PREP7
C*** PARAMETRIC INPUT FOR CREEP CONSTANTS (PRIMARY CREEP)!***
*SET,C1,1.5625E-14			!ASSIGN VALUE
*SET,C2,5.0				!ASSIGN VALUE
*SET,C3,-0.5				!ASSIGN VALUE
*SET,C4,0				!ASSIGN VALUE
C***  TIME PARAMETER
*SET,HOUR,1000				!ASSIGN VALUE
C***   ELASTIC CONSTANT
MP,EX,1,200E3				!DEFINE YOUNG'S MODULUS
MP,NUXY,1,0.3				!DEFINE POISON'S RATIO
TUNIF,HOT				!ASSIGN TEMP TO NODES
TOFF,OFFS				!SPECIFY TEMP RELATIVE TO ABSOLUTE VALUES
TB,CREEP,1,,,6				!ACTIVATE DATA TABLE
TBDATA,1,C1,C2,C3,C4			!DEFINE DATA FOR TABLE
SAVE					!SAVE 
/PREP7
N,1,0,0,0
N,2,100,0
N,3,100,100
N,4,0,100
N,5,50,0
N,6,100,50
N,7,50,100
N,8,0,50
N,9,50,50
ET,1,PLANE182
KEYOPT,1,1,1
KEYOPT,1,3,0
E,1,5,9,8
E,5,2,6,9
E,9,6,3,7
E,8,9,7,4
NSEL,ALL
NSEL,S,LOC,X
D,ALL,UX
NSEL,S,LOC,Y
D,ALL,UY
NSEL,ALL
/SOLU
NSEL,S,LOC,X,100
SF,ALL,PRES,-200
NSEL,ALL
NSEL,S,LOC,Y,100
SF,ALL,PRES,200
NSEL,ALL
RATE, OFF
DELT,1.0E-8,1.0E-9,1.0E-8
TIME, 1.0E-8
/OUT,SCRATCH
OUTRES,ESOL,ALL
SOLVE
/OUT 
RATE, ON
DELT,1E-5,1E-5,100
TIME,1000
/OUT,SCRATCH
SOLVE
/OUT
FINISH
/POST26
ESOL,2,1,,EPCR,X
ESOL,3,1,,EPCR,Y
PRVAR,2,3
PLVAR,2,3
*GET,RES1X,VARI,2,RTIME,1000
*GET,RES1Y,VARI,3,RTIME,1000
FINISH
PARSAV,ALL
RESUME
PARRES,CHANGE
/PREP7
N,1,0,0,0
N,2,100,0
N,3,100,100
N,4,0,100
N,5,50,0
N,6,100,50
N,7,50,100
N,8,0,50
ET,1,PLANE183
KEYOPT,1,3,0
E,1,2,3,4,5,6,7,8
NSEL,ALL
NSEL,S,LOC,X
D,ALL,UX
NSEL,S,LOC,Y
D,ALL,UY
NSEL,ALL
/SOLU
NSEL,S,LOC,X,100
SF,ALL,PRES,-200
NSEL,ALL
NSEL,S,LOC,Y,100
SF,ALL,PRES,200
NSEL,ALL
RATE, OFF
DELT,1.0E-8,1.0E-9,1.0E-8
TIME, 1.0E-8
/OUT,SCRATCH
SOLVE
/OUT 
RATE, ON
DELT,1E-5,1E-5,100
TIME,1000
/OUT,SCRATCH
OUTRES,ESOL,ALL
SOLVE
/OUT
FINISH
/POST26
ESOL,2,1,,EPCR,X
ESOL,3,1,,EPCR,Y
PRVAR,2,3
PLVAR,2,3
*GET,RES2X,VARI,2,RTIME,1000
*GET,RES2Y,VARI,3,RTIME,1000

*DIM,LABEL1,CHAR,2
*DIM,VALUE1,,2,3
LABEL1(1) = ' ECRXX ',' ECRYY '
*VFILL,VALUE1(1,1),DATA,4.2691,-4.2691
*VFILL,VALUE1(1,2),DATA,RES1X,RES1Y
*VFILL,VALUE1(1,3),DATA,ABS(RES1X/4.2691),ABS(RES1Y/(-4.2691))

*DIM,LABEL2,CHAR,2
*DIM,VALUE2,,2,3
LABEL2(1) = ' ECRXX ',' ECRYY '
*VFILL,VALUE2(1,1),DATA,4.2691,-4.2691
*VFILL,VALUE2(1,2),DATA,RES2X,RES2Y
*VFILL,VALUE2(1,3),DATA,ABS(RES2X/4.2691),ABS(RES2Y/(-4.2691))

/OUT,vm224,vrt
/COM
/COM,------------------- VM224 RESULTS COMPARISON ---------------------
/COM,
/COM,            |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
/COM, PLANE182
/COM,
*VWRITE,LABEL1(1),VALUE1(1,1),VALUE1(1,2),VALUE1(1,3)
(1X,A8,'      ',F7.4,'      ',F14.4,'     ',1F15.3)
/COM,
/COM, PLANE183
/COM,
*VWRITE,LABEL2(1),VALUE2(1,1),VALUE2(1,2),VALUE2(1,3)
(1X,A8,'      ',F7.4,'      ',F14.4,'     ',1F15.3)
/COM,----------------------------------------------------------------
/OUT

FINISH
*LIST,vm224,vrt




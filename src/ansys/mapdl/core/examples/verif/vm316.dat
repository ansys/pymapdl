/COM,ANSYS MEDIA REL. 2023R1 (11/04/2022) REF. VERIF. MANUAL: REL. 2023R1
/VERIFY,VM316
/TITLE,VM316,J-INTEGRAL FOR A MIDDLE-CRACK TENSION SPECIMEN UNDER ELASTIC-PLASTIC DEFORMATION
/COM,
/COM, REFERENCE: ANDERSON,T.L.,"THE EPRI J-ESTIMATION PROCEDURE", IN
/COM,            FRACTURE MECHANICS: FUNDAMENTALS AND APPLICATIONS, CRC PRESS,
/COM,            3RD EDITION, 2005, PG:398-444
/COM,
/OUT,VM316_SCRATCH
/COM, PARAMETER FOR CREATING THE CDB FILES TO STORE GEOMETRY AND MESH DATA
/COM,
_GEOMGEN = 0   ! SET TO 1 TO WRITE CDB FILES IF ANY INPUT FILE MODIFICATIONS
!***********************************************************************************
! PLANE STRESS
!***********************************************************************************

! REFERENCE RESULT FOR PLANE STRESS CONDITION
!********************************************************
! (ANDERSON,FRACTURE MECHANICS,2005,TABLE A9.11,PG-444)
A=10                ! HALF CRACK LENGTH, MM
W=4*A               ! HALF WIDTH, MM
E=200000            ! ELASTIC MODULUS, N/MM2
NU=0.3              ! POISSON'S RATIO
PI=3.14159
! RAMBERG-OSGOOD PARAMETERS
SY=400              ! YIELD STRESS, MPA
ALPHA=1.0
N=10
EY=SY/E

P=22500             ! LOAD ON FULL GEOMETRY SPECIMEN, N/MM
P0=2*(W-A)*SY       ! LIMIT LOAD
!----------------------------------------------
! ELASTIC PART,  JEL
!----------------------------------------------
! INITIAL ESTIMATE OF K1
FI=SQRT(1/COS(PI*A/(2*W)))
K1I=(P/(2*W))*SQRT(PI*A)*FI

! EFFECTIVE CRACK LENGTH AFTER PLASTIC ZONE CORRECTION
! (ANDERSON,FRACTURE MECHANICS,2005,PG-400)
BETA=2             ! 2 FOR PLANE STRESS, 6 FOR PLANE STRAIN
AEFF=A+((N-1)/(N+1))*((K1I/SY)**2)/((BETA*PI)*(1+(P/P0)**2))

! FINAL ESTIMATE OF K1
F=SQRT(1/COS(PI*AEFF/(2*W)))
K1=(P/(2*W))*SQRT(PI*AEFF)*F

! JEL CALCULATION
JEL=K1**2/E

!----------------------------------------------
! PLASTIC PART,  JPL
!----------------------------------------------
H1=2.86   ! FROM TABLE A9.11 (ANDERSON,FRACTURE MECHANICS)
JPL=ALPHA*EY*SY*(W-A)*(A/W)*H1*(P/P0)**(N+1)

!----------------------------------------------
! TOTAL J = JEL + JPL
!----------------------------------------------
J_REF_PS=JEL+JPL     ! J, PLANE STRESS

! FRACTION OF JPL IN TOTAL J
JPL_FRAC = JPL/J_REF_PS

/OUT,VM316_SCRATCH,,,APPEND

PARSAV

!********************************************************

/PREP7
*IF,_GEOMGEN,EQ,1,THEN

A=10                ! HALF CRACK LENGTH, MM
W=4*A               ! HALF WIDTH, MM
L=5*W               ! HALF LENGTH, MM

E=200000            ! ELASTIC MODULUS, N/MM2
NU=0.3              ! POISSON'S RATIO

PLOAD=P/2           ! HALF TENSILE LOAD, N/MM
PRS=PLOAD/W         ! TRACTION LOAD ON TOP FACE, N/MM

ET,1,183
KEYOPT,1,3,0        ! PLANE STRESS=0, PLANE STRAIN=2
MP,EX,1,E
MP,NUXY,1,NU

! RAMBERG-OSGOOD PARAMETERS
SY=400              ! YIELD STRESS, MPA
ALPHA=1.0
N=10

! MISO PLASTICITY DEFINITION CLOSE TO RAMBERG-OSGOOD RELATION
TB,PLAS,1,,,MISO
TBPT,DEFI,0.0,300         ! PLASTIC STRAIN, STRESS
TBPT,DEFI,5.26E-04,350
TBPT,DEFI,2.00E-03,400
TBPT,DEFI,3.67E-03,425
TBPT,DEFI,6.49E-03,450
TBPT,DEFI,1.12E-02,475
TBPT,DEFI,1.86E-02,500
TBPT,DEFI,3.03E-02,525
TBPT,DEFI,4.83E-02,550
TBPT,DEFI,7.54E-02,575
TBPT,DEFI,1.15E-01,600
TBPT,DEFI,1.73E-01,625
TBPT,DEFI,2.57E-01,650
TBPT,DEFI,3.75E-01,675
TBPT,DEFI,5.39E-01,700
TBPT,DEFI,7.65E-01,725
TBPT,DEFI,1.07,750
TBPT,DEFI,1.49,775
TBPT,DEFI,2.05,800


K,1,
K,2,W-A
K,3,W-A,L
K,4,-A,L
K,5,-A
K,6,A
K,7,A,A
K,8,-A,A
K,9,2*A
K,10,2*A,2*A
K,11,-A,2*A

A,1,6,7,8,5
A,6,9,10,11,8,7
A,9,2,3,4,11,10

KSCON,1,A/16,0,8
ESIZE,A/16
AMESH,1
ESIZE,A/8
AMESH,2
ESIZE,A/4
AMESH,3
ALLSEL,ALL

! SYMMETRY CONDITIONS FOR LEFT AND BOTTOM FACES
NSEL,S,LOC,X,-A
DSYM,SYMM,X,0
ALLSEL,ALL

NSEL,S,LOC,Y,0
NSEL,R,LOC,X,0,W-A
DSYM,SYMM,Y,0
ALLSEL,ALL

NSEL,S,LOC,Y,0
NSEL,R,LOC,X,0          ! CRACK-FRONT-NODE SELECTED
CM,CRACK1,NODE
ALLSEL,ALL

! DEFINE CRACK-EXTENSION DIRECTION CALCULATION-ASSIST NODE
CASSIST=NODE(-A,0,0)

CDWRITE,DB,vm316-1,cdb
*ELSE
CDREAD,DB,vm316-1,cdb
*ENDIF


FINISH

!-----------------------------------------------------------------------------------

/SOLU
ANTYPE,STATIC
OUTRES,NSOL,ALL
OUTRES,CINT,ALL
NSUBS,40,1000,40
TIME,1.0

! LOADING ON TOP FACE
NSEL,S,LOC,Y,L
SF,ALL,PRES,-PRS
ALLSEL,ALL

! JINT PARAMETER
CINT,NEW,1
CINT,TYPE,JINT
CINT,CTNC,CRACK1,CASSIST
CINT,NCON,10
CINT,NORM,0,2
CINT,SYMM,ON

ALLSEL,ALL

SOLVE

FINISH

!-----------------------------------------------------------------------------------

/POST1

! J-INTEGRAL (AVERAGE, CONTOUR-2 TO 10)
JAVG=0
*DO,NN,2,10
   *GET,PVAL,CINT,1,CTIP,1,CONTOUR,NN,DTYPE,JINT
   JAVG = JAVG + PVAL/9
*ENDDO

*DIM,LABEL,CHAR,1,2
*DIM,VALUE,,1,3

LABEL(1,1)='JINT'

*VFILL,VALUE(1,1),DATA,J_REF_PS
*VFILL,VALUE(1,2),DATA,JAVG
*VFILL,VALUE(1,3),DATA,J_REF_PS/JAVG

SAVE,TABLE_1
FINISH
/CLEAR,NOSTART

!***********************************************************************************
! PLANE STRAIN
!***********************************************************************************
/OUT,VM316_SCRATCH,,,APPEND

PARRES,CHANGE

! REFERENCE RESULT FOR PLANE STRAIN CONDITION
!********************************************************
! (ANDERSON,FRACTURE MECHANICS,2005,TABLE A9.10,PG-443)
A=10                ! HALF CRACK LENGTH, MM
W=4*A               ! HALF WIDTH, MM
E=200000            ! ELASTIC MODULUS, N/MM2
NU=0.3              ! POISSON'S RATIO
PI=3.14159
! RAMBERG-OSGOOD PARAMETERS
SY=400              ! YIELD STRESS, MPA
ALPHA=1.0
N=10
EY=SY/E

P=22500             ! LOAD ON FULL GEOMETRY SPECIMEN, N/MM
P0=(4/SQRT(3))*(W-A)*SY  ! LIMIT LOAD
!----------------------------------------------
! ELASTIC PART,  JEL
!----------------------------------------------
! INITIAL ESTIMATE OF K1
FI=SQRT(1/COS(PI*A/(2*W)))
K1I=(P/(2*W))*SQRT(PI*A)*FI

! EFFECTIVE CRACK LENGTH AFTER PLASTIC ZONE CORRECTION
! (ANDERSON,FRACTURE MECHANICS,2005,PG-400)
BETA=6             ! 2 FOR PLANE STRESS, 6 FOR PLANE STRAIN
AEFF=A+((N-1)/(N+1))*((K1I/SY)**2)/((BETA*PI)*(1+(P/P0)**2))

! FINAL ESTIMATE OF K1
F=SQRT(1/COS(PI*AEFF/(2*W)))
K1=(P/(2*W))*SQRT(PI*AEFF)*F

! JEL CALCULATION
JEL=(1-NU**2)*K1**2/E

!----------------------------------------------
! PLASTIC PART,  JPL
!----------------------------------------------
H1=2.92   ! FROM TABLE A9.10 (ANDERSON,FRACTURE MECHANICS)
JPL=ALPHA*EY*SY*(W-A)*(A/W)*H1*(P/P0)**(N+1)

!----------------------------------------------
! TOTAL J = JEL + JPL
!----------------------------------------------
J_REF_PE=JEL+JPL     ! J, PLANE STRAIN

! FRACTION OF JPL IN TOTAL J
JPL_FRAC = JPL/J_REF_PE

/OUT,VM316_SCRATCH,,,APPEND

PARSAV

!********************************************************

/PREP7
*IF,_GEOMGEN,EQ,1,THEN

A=10                ! HALF CRACK LENGTH, MM
W=4*A               ! HALF WIDTH, MM
L=5*W               ! HALF LENGTH, MM

E=200000            ! ELASTIC MODULUS, N/MM2
NU=0.3              ! POISSON'S RATIO

PLOAD=P/2           ! HALF TENSILE LOAD, N/MM
PRS=PLOAD/W         ! TRACTION LOAD ON TOP FACE, N/MM

ET,1,183
KEYOPT,1,3,2        ! PLANE STRESS=0, PLANE STRAIN=2
MP,EX,1,E
MP,NUXY,1,NU

! RAMBERG-OSGOOD PARAMETERS
SY=400              ! YIELD STRESS, MPA
ALPHA=1.0
N=10

! MISO PLASTICITY DEFINITION CLOSE TO RAMBERG-OSGOOD RELATION
TB,PLAS,1,,,MISO
TBPT,DEFI,0.0,300         ! PLASTIC STRAIN, STRESS
TBPT,DEFI,5.26E-04,350
TBPT,DEFI,2.00E-03,400
TBPT,DEFI,3.67E-03,425
TBPT,DEFI,6.49E-03,450
TBPT,DEFI,1.12E-02,475
TBPT,DEFI,1.86E-02,500
TBPT,DEFI,3.03E-02,525
TBPT,DEFI,4.83E-02,550
TBPT,DEFI,7.54E-02,575
TBPT,DEFI,1.15E-01,600
TBPT,DEFI,1.73E-01,625
TBPT,DEFI,2.57E-01,650
TBPT,DEFI,3.75E-01,675
TBPT,DEFI,5.39E-01,700
TBPT,DEFI,7.65E-01,725
TBPT,DEFI,1.07,750
TBPT,DEFI,1.49,775
TBPT,DEFI,2.05,800


K,1,
K,2,W-A
K,3,W-A,L
K,4,-A,L
K,5,-A
K,6,A
K,7,A,A
K,8,-A,A
K,9,2*A
K,10,2*A,2*A
K,11,-A,2*A

A,1,6,7,8,5
A,6,9,10,11,8,7
A,9,2,3,4,11,10

KSCON,1,A/16,0,8
ESIZE,A/16
AMESH,1
ESIZE,A/8
AMESH,2
ESIZE,A/4
AMESH,3
ALLSEL,ALL

! SYMMETRY CONDITIONS FOR LEFT AND BOTTOM FACES
NSEL,S,LOC,X,-A
DSYM,SYMM,X,0
ALLSEL,ALL

NSEL,S,LOC,Y,0
NSEL,R,LOC,X,0,W-A
DSYM,SYMM,Y,0
ALLSEL,ALL

NSEL,S,LOC,Y,0
NSEL,R,LOC,X,0          ! CRACK-FRONT-NODE SELECTED
CM,CRACK1,NODE
ALLSEL,ALL

! DEFINE CRACK-EXTENSION DIRECTION CALCULATION-ASSIST NODE
CASSIST=NODE(-A,0,0)

CDWRITE,DB,vm316-2,cdb
*ELSE
CDREAD,DB,vm316-2,cdb
*ENDIF


FINISH

!-----------------------------------------------------------------------------------

/SOLU
ANTYPE,STATIC
OUTRES,NSOL,ALL
OUTRES,CINT,ALL
NSUBS,40,1000,40
TIME,1.0

! LOADING ON TOP FACE
NSEL,S,LOC,Y,L
SF,ALL,PRES,-PRS
ALLSEL,ALL

! JINT PARAMETER
CINT,NEW,1
CINT,TYPE,JINT
CINT,CTNC,CRACK1,CASSIST
CINT,NCON,10
CINT,NORM,0,2
CINT,SYMM,ON

ALLSEL,ALL

SOLVE

FINISH

!-----------------------------------------------------------------------------------

/POST1

! J-INTEGRAL (AVERAGE, CONTOUR-2 TO 10)
JAVG=0
*DO,NN,2,10
   *GET,PVAL,CINT,1,CTIP,1,CONTOUR,NN,DTYPE,JINT
   JAVG = JAVG + PVAL/9
*ENDDO

*DIM,LABEL,CHAR,1,2
*DIM,VALUE,,1,3

LABEL(1,1)='JINT'

*VFILL,VALUE(1,1),DATA,J_REF_PE
*VFILL,VALUE(1,2),DATA,JAVG
*VFILL,VALUE(1,3),DATA,J_REF_PE/JAVG

SAVE,TABLE_2
FINISH

!***********************************************************************************

RESUME,TABLE_1
/OUT,vm316,vrt
/COM,
/COM, ----------------------- VM316 RESULT COMPARISON --------------------------
/COM,
/COM,                      | TARGET | MECHANICAL APDL | RATIO
/COM,
/COM,  PLANE STRESS
/COM,
*VWRITE,LABEL(1,1),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A14,'   ',E12.3,'    ',E12.3,'    ',F12.3)
/COM,
/COM,
/NOPR,
RESUME,TABLE_2
/COM,
/COM,
/COM,  PLANE STRAIN
/COM,
*VWRITE,LABEL(1,1),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A14,'   ',E12.3,'    ',E12.3,'    ',F12.3)
/COM,
/COM,
/COM, --------------------------------------------------------------------------
/OUT,
*LIST,vm316,vrt
/OUT,VM316_SCRATCH,,,APPEND
/DELETE,TABLE_1
/DELETE,TABLE_2

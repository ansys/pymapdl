! Copyright (C) 2024 ANSYS, Inc. and/or its affiliates.
! SPDX-License-Identifier: MIT
! 
!
! Permission is hereby granted, free of charge, to any person obtaining a copy
! of this software and associated documentation files (the "Software"), to deal
! in the Software without restriction, including without limitation the rights
! to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
! copies of the Software, and to permit persons to whom the Software is
! furnished to do so, subject to the following conditions:
!
! The above copyright notice and this permission notice shall be included in all
! copies or substantial portions of the Software.
!
! THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
! IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
! FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
! AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
! LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
! OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
! SOFTWARE.

/COM,ANSYS MEDIA REL. 2023R2 (05/12/2023) REF. VERIF. MANUAL: REL. 2023R2
/VERIFY,vmlsb2-le11-185
/TITLE,vmlsb2-le11-185,TAPERED CYLINDER WITH TEMPERATURE LOADING
/COM, REFERENCE: NAFEMS BENCHMARKS,REPORT:LSB2,1990-06-15/2
/COM, ORIGINIAL TEST NUMBER: LE11
/OUT,SCRATCH
/PREP7
ET,1,SOLID185			! SOLID 185 ELEMENT
KEYOPT,1,2,2			! ENCHANCED STRAIN FORMULATION

MP,EX,1,210E9
MP,NUXY,1,0.3
MP,ALPX,1,2.3E-4
MP,ALPZ,1,2.3E-4
MP,ALPY,1,2.3E-4

K,1,1.39,0,0
K,2,1.21,0,-0.707
K,3,0,0,0
K,4,1,0,0
K,5,1,0,-1.39
K,6,1,0,-1.79
K,7,0.70710,0,-1.79
K,8,0.70710,0,-0.70710

LARC,4,8,3,1
LARC,1,2,3,1.4

L,4,1
L,2,5
L,5,6
L,6,7
L,7,8

AL,1,3,2,4,5,6,7

K,10,0,0,-5E3
VROTATE,1,,,,,,3,10,90,1

KDELE,3
KDELE,10

LESIZE,1,,,8
LESIZE,3,,,4
LESIZE,2,,,6
LESIZE,7,,,6
LESIZE,4,,,4
LESIZE,5,,,2
LESIZE,6,,,2

VSWEEP,1,1,9
ALLSEL,ALL

ASEL,S,AREA,,1
NSLA,S,1
D,ALL,UY,0
ALLSEL,ALL

ASEL,S,AREA,,9
NSLA,S,1
D,ALL,UX,0
ALLSEL,ALL

ASEL,S,AREA,,4,8,4
NSLA,S,1
D,ALL,UZ,0
ALLSEL,ALL

*GET,NNODE,NODE,0,COUNT		! GET THE NUMBER OF NODES
*DO,I,1,NNODE,1			! DO OPERATION FROM 1 TO MAXIMUM NODE NUMBER
*GET,NXX,NODE,I,LOC,X		! GET THE X LOCATION OF NODE
*GET,NYY,NODE,I,LOC,Y		! GET THE Y LOCATION OF NODE
*GET,NZZ,NODE,I,LOC,Z		! GET THE Z LOCATION OF NODE
TN1 = SQRT((NXX**2)+(NYY**2))
TN  = TN1-NZZ			! NEGATIVE SIGN SINCE THE GEOMETRY IS MODELED IN NEGATIVE Z AXIS
BF,I,TEMP,TN
*ENDDO
ALLSEL,ALL
FINI

/SOLU
ANTYPE,STATIC
OUTRES,ALL,ALL
NSUBS,10,10,10
TIME,1
SOLVE
FINI

/POST1
SET,LAST
N1 = NODE(1,0,0)
*GET,SZZ,NODE,N1,S,Z
*DIM,LABEL,CHAR,1
*DIM,VALUE,,1,3
LABEL(1,1) = 'DIRECT_STRESS'
*VFILL,VALUE(1,1),DATA,-105
*VFILL,VALUE(1,2),DATA,(SZZ/1000000)
*VFILL,VALUE(1,3),DATA,((SZZ/1000000)/(-105))
/OUT,vmlsb2-le11-185,vrt
/COM,------------------- vmlsb2-le11-185 RESULTS COMPARISON --------------
/COM,
/COM,           |  TARGET  |   Mechanical APDL  |  RATIO
/COM,
/COM, 
*VWRITE,LABEL(1,1),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,'   ',F10.4,'  ',F14.4,'   ',F15.3)
/COM,-------------------------------------------------------------------------
/OUT,
FINISH
*LIST,vmlsb2-le11-185,vrt
	

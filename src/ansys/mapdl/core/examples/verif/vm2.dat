! Copyright (C) 2024 ANSYS, Inc. and/or its affiliates.
! SPDX-License-Identifier: MIT
! 
!
! Permission is hereby granted, free of charge, to any person obtaining a copy
! of this software and associated documentation files (the "Software"), to deal
! in the Software without restriction, including without limitation the rights
! to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
! copies of the Software, and to permit persons to whom the Software is
! furnished to do so, subject to the following conditions:
!
! The above copyright notice and this permission notice shall be included in all
! copies or substantial portions of the Software.
!
! THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
! IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
! FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
! AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
! LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
! OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
! SOFTWARE.

/COM,ANSYS MEDIA REL. 2023R2 (05/12/2023) REF. VERIF. MANUAL: REL. 2023R2
/VERIFY,VM2
JPGPRF,500,100,1             ! MACRO TO SET PREFS FOR JPEG PLOTS
/SHOW,JPEG
/PREP7
MP,PRXY,,0.3
/TITLE, VM2, BEAM STRESSES AND DEFLECTIONS
C***        STR. OF MATL., TIMOSHENKO, PART 1, 3RD ED., PAGE 98, PROB. 4
ANTYPE,STATIC
ET,1,BEAM188
KEYOPT,1,9,3                  ! OUTPUT AT 9 INTERMEDIATE LOCATIONS
KEYOPT,1,3,3                  ! CUBIC SHAPE FUNCTION
SECT,1,BEAM,I
W_F=1.048394965
W_W=0.6856481
SECD,15,15,28+(2*W_F),W_F,W_F,W_W
MP,EX,1,30E6
N,1                           ! DEFINE NODES AND ELEMENTS
N,5,480
N,6,60,1 $ N,10,420,1
FILL,1,5
FILL,6,10
E,1,2,6
EGEN,4,1,1
D,2,UX,,,,,UY                 ! BOUNDARY CONDITIONS AND LOADING
D,4,UY
NSEL,S,LOC,Y,0
D,ALL,UZ
D,ALL,ROTX
D,ALL,ROTY
NALL
SFBEAM,1,1,PRES,(10000/12)
SFBEAM,4,1,PRES,(1E4/12)
FINISH
/SOLU
OUTPR,BASIC,1
/OUT,SCRATCH
SOLVE
FINISH
/POST1
SET,1,1
/OUT,
PRNSOL,U,COMP
PRNSOL,ROT,COMP
PLDISP,1
MID_NODE = NODE (240,,, )
*GET,DISP,NODE,MID_NODE,U,Y
MID_ELM = ENEARN (MID_NODE)
ETABLE,STRS,LS,1
*GET,STRSS,ELEM,MID_ELM,ETAB,STRS
*DIM,LABEL,CHAR,2
*DIM,VALUE,,2,3
LABEL(1) = 'STRS_psi','DEF_in'
*VFILL,VALUE(1,1),DATA,-11400,0.182
*VFILL,VALUE(1,2),DATA,STRSS,DISP
*VFILL,VALUE(1,3),DATA,ABS(STRSS /11400 ) ,ABS( DISP /0.182 )
/OUT,vm2,vrt
/COM
/COM,-------------------VM2 RESULTS COMPARISON ---------------------
/COM,
/COM,         |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
*VWRITE,LABEL(1),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,'   ',F10.3,'  ',F14.3,'   ',1F15.3)
/COM,---------------------------------------------------------------
/OUT
FINISH
*LIST,vm2,vrt



/COM,ANSYS MEDIA REL. 150 (11/8/2013) REF. VERIF. MANUAL: REL. 150
/VERIFY,VM139
/PREP7
/TITLE, VM139, BENDING OF A LONG UNIFORMLY LOADED RECTANGULAR PLATE
C***            STR. OF MATL., TIMOSHENKO, PART 2, 3RD ED., PAGE 80
C***            USING SHELL181 ELEMENTS
ANTYPE,STATIC
ET,1,SHELL181,,,2
KEYOPT,1,8,2
SECTYPE,1,SHELL
SECDATA,0.375,1,0,3

MP,EX,1,30E6
MP,NUXY,1,0.3

RECT,0,22.5,0,9
LSEL,S,LINE,,1,3,2
LESIZE,ALL,,,8
LSEL,ALL
LSEL,S,LINE,,2,4,2
LESIZE,ALL,,,1
LSEL,ALL
AMESH,1
D,1,UX,,,,,UY,UZ
D,11,UX,,,,,UZ
D,ALL,ROTZ
NSEL,S,LOC,X,22.5
DSYM,SYMM,X,0,22.5       ! SYMMETRY B.C.'S AT CENTERLINE
NSEL,ALL
FINISH
/SOLU    
SFE,ALL,2,PRES,0,10 
OUTPR,BASIC,LAST
/OUT,SCRATCH
SOLVE                    ! ANALYSIS 1 : SMALL DEFLECTION SOLUTION
FINISH
/POST1
/OUT,
SET,1                    ! ANALYSIS 1 : SMALL DEFLECTION SOLUTION
NSEL,S,LOC,Y,0
SHELL,BOT                ! BOTTOM STRESSES ALONG LENGTH
PRNSOL,S,COMP
*GET,S2,NODE,2,S,X
PRNSOL,S,PRIN
NSEL,ALL
FINISH
/SOLU    
NLGEOM,ON                ! LARGE DEFLECTION OPTION
/OUT,SCRATCH
SOLVE                    ! ANALYSIS 2 : LARGE DEFLECTION SOLUTION
FINISH
/POST1
/OUT,
SET,1                    ! ANALYSIS 2 : CONVERGED LARGE DEFLECTION SOLUTION
NSEL,S,LOC,Y,0
SHELL,MID                ! MIDDLE STRESSES ALONG LENGTH
PRNSOL,S,COMP
*GET,S3,NODE,1,S,X
PRNSOL,S,PRIN
SHELL,BOT                ! BOTTOM STRESSES ALONG LENGTH
PRNSOL,S,COMP
*GET,S4,NODE,2,S,X
PRNSOL,S,PRIN
*DIM,LABEL,CHAR,1,2
*DIM,VALUE,,1,3
LABEL(1,1) = 'BOT PRS '
LABEL(1,2) = 'psi'
*VFILL,VALUE(1,1),DATA,108000
*VFILL,VALUE(1,2),DATA,S2
*VFILL,VALUE(1,3),DATA,ABS(S2/108000)
SAVE,TABLE1
*DIM,LABEL1,CHAR,2,2
*DIM,VALUE1,,2,3
LABEL1(1,1) = 'MID PRS ','BOT PRS '
LABEL1(1,2) = 'psi ','psi '
*VFILL,VALUE1(1,1),DATA,11240,25280
*VFILL,VALUE1(1,2),DATA,S3,S4
*VFILL,VALUE1(1,3),DATA,ABS(S3/11240),ABS(S4/25280)
FINISH
SAVE,TABLE2

/CLEAR, NOSTART          ! CLEAR DATABASE FOR SECOND SOLUTION
/PREP7
/TITLE, VM139, BENDING OF A LONG UNIFORMLY LOADED RECTANGULAR PLATE
C***           USING SOLSH190 ELEMENTS
ANTYPE,STATIC
ET,1,SOLSH190,,0
MP,EX,1,30E6
MP,NUXY,1,0.3
BLOCK,0,22.5,0,9,0,-0.375
LSEL,S,LINE,,9,12,1
LESIZE,ALL, , ,2
LSEL,INVE
LESIZE,ALL, , ,10
LSEL,ALL
VMESH,1
NSEL,S,LOC,Z,-0.375/2
NSEL,R,LOC,X,0
D,ALL,UX
D,ALL,UZ
NSEL,R,LOC,Y,0
D,ALL,UY
ALLSEL,ALL
NSEL,S,LOC,X,22.5
DSYM,SYMM,X,0,22.5       ! SYMMETRY B.C.'S AT CENTERLINE
NSEL,ALL
FINISH
/SOLU
NSEL,S,LOC,Z,-0.375
ESLN,S
SFE,ALL,6,PRES,0,10 
ALLSEL,ALL
/OUT,SCRATCH
SOLVE                    ! ANALYSIS 1 : SMALL DEFLECTION SOLUTION
FINISH
/POST1
/OUT,
SET,1                    ! ANALYSIS 1 : SMALL DEFLECTION SOLUTION
NSEL,S,LOC,Z,-0.375/2    ! MID STRESSES ALONG LENGTH
NSEL,R,LOC,Y,0
NSEL,R,LOC,X,22.5
*GET,ND1,NODE,0,NUM,MAX  ! NODE AT SHELL-MID   X = 0.0, Y = 0.0, Z = -0.375/2
NSEL,S,LOC,Z,-0.375
NSEL,R,LOC,Y,0
NSEL,R,LOC,X,22.5        
*GET,ND2,NODE,0,NUM,MAX  ! NODE AT SHELL-BOT   X = 22.5, Y = 0.0, Z = -0.375
NSEL,S,LOC,Z,-0.375      ! BOTTOM STRESSES ALONG LENGTH
NSEL,R,LOC,Y,0
PRNSOL,S,COMP
*GET,S2,NODE,ND2,S,X
PRNSOL,S,PRIN
NSEL,ALL
FINISH
/SOLU    
NLGEOM,ON                ! LARGE DEFLECTION OPTION
/OUT,SCRATCH
SOLVE                    ! ANALYSIS 2 : LARGE DEFLECTION SOLUTION
FINISH
/POST1
/OUT,
SET,1                    ! ANALYSIS 2 : CONVERGED LARGE DEFLECTION SOLUTION
NSEL,S,LOC,Z,-0.375/2    ! MIDDLE STRESSES ALONG LENGTH
NSEL,R,LOC,Y,0
PRNSOL,S,COMP
*GET,S3,NODE,ND1,S,X
PRNSOL,S,PRIN
NSEL,S,LOC,Z,-0.375      ! BOTTOM STRESSES ALONG LENGTH
NSEL,S,LOC,Y,0
PRNSOL,S,COMP
*GET,S4,NODE,ND2,S,X
PRNSOL,S,PRIN
*DIM,LABEL,CHAR,1,2
*DIM,VALUE,,1,3
LABEL(1,1) = 'BOT PRS '
LABEL(1,2) = 'PSI'
*VFILL,VALUE(1,1),DATA,108000
*VFILL,VALUE(1,2),DATA,S2
*VFILL,VALUE(1,3),DATA,ABS(S2/108000)
SAVE,TABLE3
*DIM,LABEL1,CHAR,2,2
*DIM,VALUE1,,2,3
LABEL1(1,1) = 'MID PRS ','BOT PRS '
LABEL1(1,2) = 'PSI ','PSI '
*VFILL,VALUE1(1,1),DATA,11240,25280
*VFILL,VALUE1(1,2),DATA,S3,S4
*VFILL,VALUE1(1,3),DATA,ABS(S3/11240),ABS(S4/25280)
SAVE,TABLE4
RESUME,TABLE1
/OUT,vm139,vrt
/COM,------------------- VM139 RESULTS COMPARISON --------------
/COM,
/COM,                |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
/COM,SMALL DEFLECTION SOLUTION USING SHELL181:
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.0,'  ',F12.0,'   ',1F15.3)
/NOPR
RESUME,TABLE2
/GOPR
/COM,
/COM,LARGE DEFLECTION SOLUTION USING SHELL181:
/COM,
*VWRITE,LABEL1(1,1),LABEL1(1,2),VALUE1(1,1),VALUE1(1,2),VALUE1(1,3)
(1X,A8,A8,'   ',F10.0,'  ',F12.0,'   ',1F15.3)
/NOPR
RESUME,TABLE3
/GOPR
/COM,
/COM,SMALL DEFLECTION SOLUTION USING SOLSH190:
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.0,'  ',F12.0,'   ',1F15.3)
/NOPR
RESUME,TABLE4
/GOPR
/COM,
/COM,LARGE DEFLECTION SOLUTION USING SOLSH190:
/COM,
*VWRITE,LABEL1(1,1),LABEL1(1,2),VALUE1(1,1),VALUE1(1,2),VALUE1(1,3)
(1X,A8,A8,'   ',F10.0,'  ',F12.0,'   ',1F15.3)
/COM,-----------------------------------------------------------
/COM,
/COM,-----------------------------------------------------------
/COM,NOTE: THERE ARE VERIFIED RESULTS IN VM139  NOT CONTAINED IN
/COM,THIS TABLE
/COM,-----------------------------------------------------------
/OUT
FINISH
*LIST,vm139,vrt

/COM,ANSYS MEDIA REL. 2023R1 (11/04/2022) REF. VERIF. MANUAL: REL. 2023R1
/VERIFY,vm296
/TITLE,vm296,RADIAL EXPANSION OF A THERMOPLASTIC CYLINDER
/COM,
/COM, REFERENCE: "THERMOMECHANICAL MODELING OF METALS AT FINITE STRAINS: 
/COM, FIRST AND MIXED ORDER FINITE ELEMENTS", LAURENT ADAM, JEAN-PHILIPPE PONTHOT,
/COM, INTERNATIONAL JOURNAL OF SOLIDS AND STRUCTURES 42 (2005) 5615-5655
/COM,
/COM,
/COM, ***** USING PLANE222 *****
/COM,
RI=0.1              	! INTERNAL RADIUS
RE=0.2              	! EXTERNAL RADIUS
DX=0.13             	! RADIAL DISPLACEMENT

TREF=293            	! REFERENCE TEMPERATURE, K

/PREP7
ET,1,PLANE222,11        ! STRUCTRAL-THERMAL COUPLING
KEYOPT,1,3,1          	! AXISYMMETRIC
KEYOPT,1,9,1 	        ! SUPPRESSED THERMOELASTIC DAMPING

MP,EX,1,7.E10       	! YOUNG'S MODULUS
MP,NUXY,1,0.3       	! POISSON'S RATIO
MP,ALPX,1,23.8E-6   	! THERMAL EXPANSION COEFFICIENT
MP,KXX,1,150        	! CONDUCTIVITY
MP,DENS,1,2700      	! DENSITY
MP,C,1,900          	! SPECIFIC HEAT
MP,QRATE,1,0.9      	! TAYLOR-QUINNEY FACTOR

EX=7.E10
ET=210E6
EP=EX*ET/(EX-ET)
TB,PLAS,1,6,2,BISO	! BILINEAR ISOTROPIC HARDENING MATERIAL
TBTEMP,293		
TBDATA,1,70E6,EP	! YIELD STRESS AND PLASTIC TANGENT MODULUS AT T=293K
TBTEMP,313		
TBDATA,1,69.58E6,EP     ! YIELD STRESS AND PLASTIC TANGENT MODULUS AT T=313K
TBTEMP,333
TBDATA,1,69.16E6,EP     ! YIELD STRESS AND PLASTIC TANGENT MODULUS AT T=333K
TBTEMP,353
TBDATA,1,68.74E6,EP     ! YIELD STRESS AND PLASTIC TANGENT MODULUS AT T=353K
TBTEMP,373
TBDATA,1,68.32E6,EP     ! YIELD STRESS AND PLASTIC TANGENT MODULUS AT T=373K
TBTEMP,393
TBDATA,1,67.9E6,EP      ! YIELD STRESS AND PLASTIC TANGENT MODULUS AT T=393K

RECT,RI,RE,0,RE-RI
TYPE,1
MAT,1
LESIZE,1,,,10
LESIZE,2,,,1
LESIZE,3,,,10
LESIZE,4,,,1
AMESH,1			! GENERATE AXISYMMETRIC PLANE222 ELEMENTS
ALLSEL,ALL

NSEL,S,LOC,Y,0
NSEL,A,LOC,Y,RE-RI
D,ALL,UY,0		! CONSTRAIN UY DOF AT TOP AND BOTTOM FACES TO
			! SIMULATE INFINETLY LONG CYLINDER
NSEL,S,LOC,X,RI
D,ALL,UX,DX		! APPLY RADIAL DISPLACEMENT ALONG INNER SURFACE OF CYLINDER
ALLSEL,ALL

TREF,TREF		! REFERENCE TEMPERATURE
IC,ALL,TEMP,TREF	! INITIAL CONDITION
FINISH

/COM,
/COM, ***** SOLUTION IN TIME = 0.001 SEC (ADIABATIC PROCESS) *****
/COM,
/SOLUTION
/OUT,SCRATCH
ANTYPE,TRANSIENT	! TRANSIENT ANALYSIS
NLGEOM,ON		! LARGE DEFLECTION ON 
TINTP,,,,1.0		! TRANSIENT INTEGRATION PARAMETER
KBC,0			! RAMPED LOADING
AUTOTS,OFF
NSUB,20,100,5		! NUMBER OF SUBSTEPS
OUTRES,ALL,ALL
CNVTOL,HEAT,1.E-2,1.E-2 ! CONVERGENCE TOLERANCE
TIME,0.001		! END TIME
SOLVE
FINISH

/POST26
/OUT,SCRATCH
NSEL,S,LOC,X,0.1
NSEL,R,LOC,Y,0.1
ND=NDNEXT(0)
NSOL,2,ND,TEMP		! TEMPERATURE AT NODE =ND
FILLDATA,3,,,,-1
FILLDATA,4,,,,293.0
PROD,5,3,4
ADD,6,2,5
NSOL,7,ND,U,X,UX	! UX AT NODE=ND

/OUT
PRVAR,7,6
/OUT,SCRATCH
/GRID,1
/AXLAB,X,Displacement of inner surface[m]
/AXLAB,Y,Temperature variation [K]
/XRANGE,0,0.14
/GROPT,DIVX,14
/YRANGE,0,60
/GROPT,DIVY,6
XVAR,7
/SHOW,PNG
PLVAR,6
ALLSEL,ALL
FINISH

/POST1
SET,1,LAST
/SHOW,PNG
PLNSOL,TEMP
*GET,MAX_TEMP,PLNSOL,0,MAX	! MAXIMUM TEMPERATURE AT TIME=0.001SECONDS
*GET,MIN_TEMP,PLNSOL,0,MIN	! MINIMUM TEMPERATURE AT TIME=0.001 SECONDS
PLNSOL,EPPL,EQV
*GET,MAX_EPPLEQV,PLNSOL,0,MAX	! MAXIMUM PLASTIC STRAIN AT TIME=0.001 SECONDS
*GET,MIN_EPPLEQV,PLNSOL,0,MIN	! MINIMUM PLASTIC STRAIN AT TIME=0.001 SECONDS
/SHOW,CLOSE

MAX_DT = MAX_TEMP - TREF
MIN_DT = MIN_TEMP - TREF

/OUT
*STAT,MAX_DT
*STAT,MIN_DT
*STAT,MAX_EPPLEQV
*STAT,MIN_EPPLEQV

*DIM,LABEL1,CHAR,4,1
*DIM,VALUE1,,4,3
*DIM,VALUE1_REF,,4,3
*DIM,VALUE1_RATIO,,4,3

LABEL1(1,1) = 'DT_MIN ','DT_MAX ','EPPL_MIN ','EPPL_MAX '
*VFILL,VALUE1(1,1),DATA,MIN_DT,MAX_DT,MIN_EPPLEQV,MAX_EPPLEQV
*VFILL,VALUE1_REF(1,2),DATA,16.5,57.0,0.426,0.932			! FROM TABLE2
*VFILL,VALUE1_RATIO(1,3),DATA,ABS(MIN_DT/16.5),ABS(MAX_DT/57.0),ABS(MIN_EPPLEQV/0.426),ABS(MAX_EPPLEQV/0.932)
SAVE,TABLE_1

PATH,TEMP1,2,,100		! DEFINE PATH NAME,NUMBER OF POINTS AND DIVISION
PPATH,1,NODE,0.1,0.1,0		! NODES TO DEFINE PATH GEOMETRY
PPATH,2,NODE,0.2,0.1,0
PDEF,TEMP1,TEMP,,NOAV		! INTERPOLATE TEMPERATURE RESULTS 
PDEF,UX,U,X,NOAV		! INTERPOLATE UX RESULTS

/OUT
PRPATH,UX,TEMP1			! PRINT PATH ITEMS
/SHOW,PNG
PLPATH,TEMP1
FINISH

/COM,
/COM, ***** SOLUTION IN TIME = 1.3 SEC *****
/COM,
/SOLUTION
/OUT,SCRATCH
ANTYPE,TRANSIENT		 ! TRANSIENT ANALYSIS
NLGEOM,ON			 ! LARGE DEFLECTION ON 
KBC,0				 ! RAMPED LOADING
TINTP,,,,1.0			 ! TRANSIENT INTEGRATION PARAMETER
NSUB,20,100,5			 ! NUMBER OF SUBSTEPS
OUTRES,ALL,ALL			 
CNVTOL,HEAT,1.E-2,1.E-2		 ! CONVERGENCE TOLERANCE
TIME,1.3			 ! END TIME
SOLVE
FINISH

/POST1
SET,LAST
PATH,TEMP1,2,,100		! DEFINE PATH NAME,NUMBER OF POINTS AND DIVISION
PPATH,1,NODE,0.1,0.1,0		! NODES TO DEFINE PATH GEOMETRY
PPATH,2,NODE,0.2,0.1,0
PDEF,TEMP2,TEMP,,NOAV		! INTERPOLATE TEMPERATURE RESULTS 
PDEF,UX,U,X,NOAV		! INTERPOLATE UX RESULTS
/OUT
PRPATH,UX,TEMP2			! PRINT PATH ITEMS
/SHOW,PNG
PLPATH,TEMP2
ALLSEL,ALL
FINISH

/COM,
/COM, ***** SOLUTION IN TIME = 13 SEC *****
/COM,

/SOLUTION
/OUT,SCRATCH
ANTYPE,TRANSIENT		! TRANSIENT ANALYSIS
NLGEOM,ON			! LARGE DEFLECTION ON
TINTP,,,,1.0			! TRANSIENT TIME INTEGRATION PARAMETERS
KBC,0				! RAMPED LOADING
NSUB,20,100,5			! NUMBER OF SUBSTEPS
OUTRES,ALL,ALL
CNVTOL,HEAT,1.E-2,1.E-2		! CONVERGENCE TOLERANCES
TIME,13.0			! END TIME
SOLVE
FINISH

/POST1
SET,LAST
PATH,TEMP1,2,,100		! DEFINE PATH NAME,NUMBER OF POINTS AND DIVISION
PPATH,1,NODE,0.1,0.1,0		! NODES TO DEFINE PATH GEOMETRY
PPATH,2,NODE,0.2,0.1,0
PDEF,TEMP3,TEMP,,NOAV		! INTERPOLATE TEMPERATURE RESULTS 
PDEF,UX,U,X,NOAV		! INTERPOLATE UX RESULTS
/OUT
PRPATH,UX,TEMP3			! PRINT PATH ITEMS
/SHOW,PNG
PLPATH,TEMP3
ALLSEL,ALL
FINISH

/COM,
/COM, ***** SOLUTION IN TIME = 130 SEC *****
/COM,

/SOLUTION
/OUT,SCRATCH
ANTY,TRANSIENT			! TRANSIENT ANALYSIS
NLGEOM,ON			! LARGE DEFLECTION ON
TINTP,,,,1.0			! TRANSIENT TIME INTEGRATION PARAMETERS
KBC,0				! RAMPED LOADING
NSUB,50,200,5			! NUMBER OF SUBSTEPS
OUTRES,ALL,ALL	
CNVTOL,HEAT,1.E-2,1.E-2		! CONVERGENCE TOLERANCE
TIME,130			! END TIME 
SOLVE
FINISH

/POST1
SET,LAST
PATH,TEMP1,2,,100		! DEFINE PATH NAME,NUMBER OF POINTS AND DIVISION
PPATH,1,NODE,0.1,0.1,0		! NODES TO DEFINE PATH GEOMETRY
PPATH,2,NODE,0.2,0.1,0
PDEF,TEMP4,TEMP,,NOAV		! INTERPOLATE TEMPERATURE RESULTS 
PDEF,UX,U,X,NOAV		! INTERPOLATE UX RESULTS
/OUT
PRPATH,UX,TEMP4			! PRINT PATH ITEMS
/SHOW,PNG
PLPATH,TEMP4
ALLS
FINISH
/CLEAR,NOSTART

/COM,
/COM, ***** USING PLANE223 *****
/COM,
/NOPR
RI=0.1              ! INTERNAL RADIUS
RE=0.2              ! EXTERNAL RADIUS
DX=0.13             ! RADIAL DISPLACEMENT

TREF=293            ! REFERENCE TEMPERATURE, K

/PREP7
ET,1,PLANE223,11    ! STRUCTRAL-THERMAL COUPLING
KEYO,1,3,1          ! AXISYMMETRIC
KEYO,1,9,1          ! SUPPRESSED THERMOELASTIC DAMPING

MP,EX,1,7.E10       ! YOUNG'S MODULUS
MP,NUXY,1,0.3       ! POISSON'S RATIO
MP,ALPX,1,23.8E-6   ! THERMAL EXPANSION COEFFICIENT
MP,KXX,1,150        ! THERMAL CONDUCTIVITY
MP,DENS,1,2700      ! DENSITY
MP,C,1,900          ! SPECIFIC HEAT
MP,QRATE,1,0.9      ! TAYLOR-QUINNEY FACTOR

EX=7.E10
ET=210E6
EP=EX*ET/(EX-ET)
TB,PLAS,1,6,2,BISO	! BILINEAR ISOTROPIC HARDENING MATERIAL
TBTEMP,293		
TBDATA,1,70E6,EP	! YIELD STRESS AND PLASTIC TANGENT MODULUS AT T=293K
TBTEMP,313		
TBDATA,1,69.58E6,EP     ! YIELD STRESS AND PLASTIC TANGENT MODULUS AT T=313K
TBTEMP,333
TBDATA,1,69.16E6,EP     ! YIELD STRESS AND PLASTIC TANGENT MODULUS AT T=333K
TBTEMP,353
TBDATA,1,68.74E6,EP     ! YIELD STRESS AND PLASTIC TANGENT MODULUS AT T=353K
TBTEMP,373
TBDATA,1,68.32E6,EP     ! YIELD STRESS AND PLASTIC TANGENT MODULUS AT T=373K
TBTEMP,393
TBDATA,1,67.9E6,EP      ! YIELD STRESS AND PLASTIC TANGENT MODULUS AT T=393K

RECT,RI,RE,0,RE-RI
TYPE,1
MAT,1
LESIZE,1,,,10
LESIZE,2,,,1
LESIZE,3,,,10
LESIZE,4,,,1
AMESH,1			! GENERATE AXISYMMETRIC PLANE223 ELEMENTS
ALLSEL,ALL

NSEL,S,LOC,Y,0
NSEL,A,LOC,Y,RE-RI
D,ALL,UY,0		! CONSTRAIN UY DOF AT TOP AND BOTTOM FACES TO
			! SIMULATE INFINETLY LONG CYLINDER
NSEL,S,LOC,X,RI
D,ALL,UX,DX		! APPLY RADIAL DISPLACEMENT ALONG INNER SURFACE OF CYLINDER
ALLSEL,ALL

TREF,TREF		! REFERENCE TEMPERATURE
IC,ALL,TEMP,TREF	! INITIAL CONDITION
FINISH

/COM,
/COM, ***** SOLUTION IN TIME = 0.001 SEC (ADIABATIC PROCESS) *****
/COM,
/SOLUTION
/OUT,SCRATCH
ANTY,TRANSIENT		! TRANSIENT ANALYSIS
NLGEOM,ON		! LARGE DEFLECTION ON 
TINTP,,,,1.0		! TRANSIENT INTEGRATION PARAMETER
KBC,0			! RAMPED LOADING
NSUB,20,100,5		! NUMBER OF SUBSTEPS
OUTRES,ALL,ALL
CNVTOL,HEAT,1.E-2,1.E-2 ! CONVERGENCE TOLERANCE
TIME,0.001		! END TIME
SOLVE
FINISH

/POST26
/OUT,SCRATCH
NSEL,S,LOC,X,0.1
NSEL,R,LOC,Y,0.1
ND=NDNEXT(0)
NSOL,2,ND,TEMP		! TEMPERATURE AT NODE =ND
FILLDATA,3,,,,-1
FILLDATA,4,,,,293.0
PROD,5,3,4
ADD,6,2,5
NSOL,7,ND,U,X,UX	! UX AT NODE=ND

/OUT
PRVAR,7,6
/OUT,SCRATCH
/GRID,1
/AXLAB,X,Displacement of inner surface[m]
/AXLAB,Y,Temperature variation [K]
/XRANGE,0,0.14
/GROPT,DIVX,14
/YRANGE,0,70
/GROPT,DIVY,7
XVAR,7
/SHOW,PNG
PLVAR,6
ALLSEL,ALL
FINISH

/POST1
SET,1,LAST
/SHOW,PNG
PLNSOL,TEMP
*GET,MAX_TEMP,PLNSOL,0,MAX	! MAXIMUM TEMPERATURE AT TIME=0.001SECONDS
*GET,MIN_TEMP,PLNSOL,0,MIN	! MINIMUM TEMPERATURE AT TIME=0.001 SECONDS
PLNSOL,EPPL,EQV
*GET,MAX_EPPLEQV,PLNSOL,0,MAX	! MAXIMUM PLASTIC STRAIN AT TIME=0.001 SECONDS
*GET,MIN_EPPLEQV,PLNSOL,0,MIN	! MINIMUM PLASTIC STRAIN AT TIME=0.001 SECONDS
/SHOW,CLOSE

MAX_DT = MAX_TEMP - TREF
MIN_DT = MIN_TEMP - TREF

/OUT
*STAT,MAX_DT
*STAT,MIN_DT
*STAT,MAX_EPPLEQV
*STAT,MIN_EPPLEQV

*DIM,LABEL2,CHAR,4,1
*DIM,VALUE2,,4,3
*DIM,VALUE2_REF,,4,3
*DIM,VALUE2_RATIO,,4,3

LABEL2(1,1) = 'DT_MIN ','DT_MAX ','EPPL_MIN ','EPPL_MAX '
*VFILL,VALUE2(1,1),DATA,MIN_DT,MAX_DT,MIN_EPPLEQV,MAX_EPPLEQV
*VFILL,VALUE2_REF(1,2),DATA,17.4,58.9,0.426,0.935
*VFILL,VALUE2_RATIO(1,3),DATA,ABS(MIN_DT/17.4),ABS(MAX_DT/58.9),ABS(MIN_EPPLEQV/0.426),ABS(MAX_EPPLEQV/0.935)
SAVE,TABLE_2

PATH,TEMP1,2,,100		! DEFINE PATH NAME,NUMBER OF POINTS AND DIVISION
PPATH,1,NODE,0.1,0.1,0		! NODES TO DEFINE PATH GEOMETRY
PPATH,2,NODE,0.2,0.1,0
PDEF,TEMP1,TEMP,,NOAV		! INTERPOLATE TEMPERATURE RESULTS 
PDEF,UX,U,X,NOAV		! INTERPOLATE UX RESULTS

/OUT
PRPATH,UX,TEMP1			! PRINT PATH ITEMS
/SHOW,PNG
PLPATH,TEMP1
FINISH

/COM,
/COM, ***** SOLUTION IN TIME = 1.3 SEC *****
/COM,
/SOLUTION
/OUT,SCRATCH
ANTYPE,TRANSIENT		 ! TRANSIENT ANALYSIS
NLGEOM,ON			 ! LARGE DEFLECTION ON 
KBC,0				 ! RAMPED LOADING
TINTP,,,,1.0			 ! TRANSIENT INTEGRATION PARAMETER
NSUB,20,100,5			 ! NUMBER OF SUBSTEPS
OUTRES,ALL,ALL			 
CNVTOL,HEAT,1.E-2,1.E-2		 ! CONVERGENCE TOLERANCE
TIME,1.3			 ! END TIME
SOLVE
FINISH

/POST1
SET,LAST
PATH,TEMP1,2,,100		! DEFINE PATH NAME,NUMBER OF POINTS AND DIVISION
PPATH,1,NODE,0.1,0.1,0		! NODES TO DEFINE PATH GEOMETRY
PPATH,2,NODE,0.2,0.1,0
PDEF,TEMP2,TEMP,,NOAV		! INTERPOLATE TEMPERATURE RESULTS 
PDEF,UX,U,X,NOAV		! INTERPOLATE UX RESULTS
/OUT
PRPATH,UX,TEMP2			! PRINT PATH ITEMS
/SHOW,PNG
PLPATH,TEMP2
ALLSEL,ALL
FINISH

/COM,
/COM, ***** SOLUTION IN TIME = 13 SEC *****
/COM,

/SOLUTION
/OUT,SCRATCH
ANTYPE,TRANSIENT		! TRANSIENT ANALYSIS
NLGEOM,ON			! LARGE DEFLECTION ON
TINTP,,,,1.0			! TRANSIENT TIME INTEGRATION PARAMETERS
KBC,0				! RAMPED LOADING
NSUB,20,100,5			! NUMBER OF SUBSTEPS
OUTRES,ALL,ALL
CNVTOL,HEAT,1.E-2,1.E-2		! CONVERGENCE TOLERANCES
TIME,13.0			! END TIME
SOLVE
FINISH

/POST1
SET,LAST
PATH,TEMP1,2,,100		! DEFINE PATH NAME,NUMBER OF POINTS AND DIVISION
PPATH,1,NODE,0.1,0.1,0		! NODES TO DEFINE PATH GEOMETRY
PPATH,2,NODE,0.2,0.1,0
PDEF,TEMP3,TEMP,,NOAV		! INTERPOLATE TEMPERATURE RESULTS 
PDEF,UX,U,X,NOAV		! INTERPOLATE UX RESULTS
/OUT
PRPATH,UX,TEMP3			! PRINT PATH ITEMS
/SHOW,PNG
PLPATH,TEMP3
ALLSEL,ALL
FINISH


/COM,
/COM, ***** SOLUTION IN TIME = 130 SEC *****
/COM,

/SOLUTION
/OUT,SCRATCH
ANTY,TRANSIENT			! TRANSIENT ANALYSIS
NLGEOM,ON			! LARGE DEFLECTION ON
TINTP,,,,1.0			! TRANSIENT TIME INTEGRATION PARAMETERS
KBC,0				! RAMPED LOADING
NSUB,20,100,5			! NUMBER OF SUBSTEPS
OUTRES,ALL,ALL	
CNVTOL,HEAT,1.E-2,1.E-2		! CONVERGENCE TOLERANCE
TIME,130			! END TIME 
SOLVE
FINISH

/POST1
SET,LAST
PATH,TEMP1,2,,100		! DEFINE PATH NAME,NUMBER OF POINTS AND DIVISION
PPATH,1,NODE,0.1,0.1,0		! NODES TO DEFINE PATH GEOMETRY
PPATH,2,NODE,0.2,0.1,0
PDEF,TEMP4,TEMP,,NOAV		! INTERPOLATE TEMPERATURE RESULTS 
PDEF,UX,U,X,NOAV		! INTERPOLATE UX RESULTS
/OUT
PRPATH,UX,TEMP4			! PRINT PATH ITEMS
/SHOW,PNG
PLPATH,TEMP4
ALLSEL,ALL
/COM,
RESUME,TABLE_1
/OUT,vm296,vrt
/COM,---------------------- RESULT COMPARISON: PLANE222 -----------------------
/COM,
/COM,                |  TARGET  |  MECHANICAL APDL  |  RATIO
/COM, 
/COM, 
/GOPR
*VWRITE,LABEL1(1,1),VALUE1_REF(1,2),VALUE1(1,1),VALUE1_RATIO(1,3)
(1X,A8,'     ',F10.3,'  ',F14.3,'   ',1F15.3)
/COM,
/NOPR
RESUME,TABLE_2
/COM,--------------------------------------------------------------------------
/COM,
/COM,---------------------- RESULT COMPARISON: PLANE223 -----------------------
/COM,
/COM,                |  TARGET  |  MECHANICAL APDL  |  RATIO
/COM, 
/COM, 
/GOPR
*VWRITE,LABEL2(1,1),VALUE2_REF(1,2),VALUE2(1,1),VALUE2_RATIO(1,3)
(1X,A8,'     ',F10.3,'  ',F14.3,'   ',1F15.3)
/COM,
/COM,--------------------------------------------------------------------------
/COM,
/OUT,
*LIST,vm296,vrt
FINISH
/EXIT,NOSAVE

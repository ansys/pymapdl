/COM,ANSYS MEDIA REL. 2023R1 (11/04/2022) REF. VERIF. MANUAL: REL. 2023R1
/VERIFY,VM89
/PREP7
/TITLE, VM89, NATURAL FREQUENCIES OF A TWO-MASS-SPRING SYSTEM
C*** VIBRATION THEORY AND APPLICATIONS, THOMSON, 2ND PRINTING, PAGE 163,EX 6.2-2
ANTYPE,MODAL       ! MODE-FREQUENCY ANALYSIS
MODOPT,LANB,2
ET,1,COMBIN14,,,2
ET,2,MASS21,,,4
R,1,200            ! SPRING CONSTANT = 200
R,2,800            ! SPRING CONSTANT = 800
R,3,.5             ! MASS = .5
R,4,1              ! MASS = 1
N,1
N,4,1
FILL
E,1,2              ! SPRING ELEMENT (TYPE,1) AND K = 200 (REAL,1)
TYPE,2
REAL,3
E,2                ! MASS ELEMENT (TYPE,2) AND MASS = .5 (REAL,3)
TYPE,1
REAL,2
E,2,3              ! SPRING ELEMENT (TYPE,1) AND K = 800 (REAL,2)
TYPE,2

REAL,4
E,3                ! MASS ELEMENT (TYPE,2) AND MASS = 1 (REAL,4)
TYPE,1
REAL,1
E,3,4              ! SPRING ELEMENT (TYPE,1) AND K = 200 (REAL,1)
OUTPR,BASIC,1
D,1,UY,,,4
D,1,UX,,,4,3
FINISH
/SOLU    
SOLVE

/post1
SET,1,1
*GET,UX2_F1,NODE,2,U,X
*GET,UX3_F1,NODE,3,U,X
SET,1,2
*GET,UX2_F2,NODE,2,U,X
*GET,UX3_F2,NODE,3,U,X

*GET,FREQ1,MODE,1,FREQ
*GET,FREQ2,MODE,2,FREQ

*DIM,LABEL,CHAR,4,2
*DIM,VALUE,,4,4

LABEL(1,1) = 'f1'

LABEL(1,2) = '(Hz)'

LABEL(2,1) = 'f2'
LABEL(2,2) = '(Hz)'

LABEL(3,1) = 'A1/A2 @1'
LABEL(3,2) = ' '

LABEL(4,1) = 'A1/A2 @2'
LABEL(4,2) = ' '


*VFILL,VALUE(1,1),DATA,2.5814
*VFILL,VALUE(1,2),DATA,FREQ1
*VFILL,VALUE(1,3),DATA,ABS(FREQ1/2.5814 ) 

*VFILL,VALUE(2,1),DATA,8.3263
*VFILL,VALUE(2,2),DATA,FREQ2
*VFILL,VALUE(2,3),DATA,ABS( FREQ2/8.3263)

*VFILL,VALUE(3,1),DATA,0.92116
*VFILL,VALUE(3,2),DATA,ux2_f1/ux3_f1
*VFILL,VALUE(3,3),DATA,ABS(ux2_f1/ux3_f1/0.92116)

*VFILL,VALUE(4,1),DATA,-2.1711
*VFILL,VALUE(4,2),DATA,ux2_f2/ux3_f2
*VFILL,VALUE(4,3),DATA,ABS(ux2_f2/ux3_f2/2.1711)

/COM
/OUT,vm89,vrt
/COM,------------------- VM89 RESULTS COMPARISON ---------------
/COM,
/COM,                 |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.4,'  ',F14.4,'   ',1F15.3)
/COM,
/COM,-----------------------------------------------------------
/COM,
/OUT
FINISH
*LIST,vm89,vrt

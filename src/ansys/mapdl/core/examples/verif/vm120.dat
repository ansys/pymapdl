/COM,ANSYS MEDIA REL. 2023R1 (11/04/2022) REF. VERIF. MANUAL: REL. 2023R1
/VERIFY,VM120
JPGPRF,500,100,1             ! MACRO TO SET PREFS FOR JPEG PLOTS
/SHOW,JPEG
/PREP7
SMRT,OFF
/TITLE, VM120,  MICROSTRIP TRANSMISSION LINE ANALYSIS
! BEREN AND KAIRES (REF. 56)
ANTYPE,STATIC                       ! ELECTROSTATIC ANALYSIS
ET,1,PLANE121                       ! USE 2-D 8-NODE ELECTROSTATIC ELEMENT
PER=8.85E-14                        ! DEFINE FREE-SPACE PERMITTIVITY
EMUNIT,EPZRO,PER
V1=1.5                              ! DEFINE STRIP POTENTIAL
V0=0.5                              ! DEFINE GROUND POTENTIAL
MP,PERX,1,10                        ! SUBSTRATE PERMITTIVITY
MP,PERX,2,1                         ! FREE SPACE PERMITTIVITY
K,1
K,2,5
K,3,,1
K,4,.5,1                            ! DEFINE GEOMETRY
K,5,5,1
K,6,,10
K,7,5,10
DESIZE,8,5,30
L,1,2
L,2,5
L,5,4
L,4,3
L,3,1
L,5,7
L,7,6
L,6,3
AL,1,2,3,4,5
AL,4,3,6,7,8
ASEL,S,AREA,,2
AATT,2
ASEL,ALL                           ! SET AREA ATTRIBUTES FOR AIR
AMESH,ALL
NSEL,S,LOC,Y,1                     ! SELECT NODES ON MICROSTRIP
NSEL,R,LOC,X,0,.5
CM,CON1,NODE
!D,ALL,VOLT,V1                      ! APPLY STRIP POTENTIAL
NSEL,S,LOC,Y,0
NSEL,A,LOC,Y,10
NSEL,A,LOC,X,5                     ! SELECT EXTERIOR NODES
CM,CON2,NODE
!D,ALL,VOLT,V0                      ! APPLY GROUND POTENTIAL
NSEL,ALL
FINISH
/SOLUTION
CMATRIX,2,'CON',2,0                ! CALCULATE CAPACITANCE USING CMATRIX MACRO
FINISH
/POST1
SET,LAST
ETABLE,EFX,EF,X                    ! STORE POTENTIAL FIELD GRADIENTS
ETABLE,EFY,EF,Y
/NUMBER,1
PLNSOL,VOLT                        ! DISPLAY EQUIPOTENTIAL LINES
/DIST,1,2.2                        ! FOCUS IN ON MICROSTRIP REGION
/FOCUS,1,2,1.5
PLVECT,EFX,EFY                     ! DISPLAY VECTOR ELECTRIC FIELD (VECTOR)
*DIM,LABEL,CHAR,1,2
*DIM,VALUE,,1,3
LABEL(1,1) = 'CAPACITA'
LABEL(1,2) = 'NCE,pF/m'
*VFILL,VALUE(1,1),DATA,178.1
*VFILL,VALUE(1,2),DATA,CMATRIX(1,1,1)*1E14
*VFILL,VALUE(1,3),DATA,ABS((CMATRIX(1,1,1)*1E14)/178.1)
/COM
/OUT,vm120,vrt
/COM,------------------- VM120 RESULTS COMPARISON -------------
/COM,
/COM,                 |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.1,'  ',F12.1,'   ',1F15.3)
/COM,----------------------------------------------------------
/OUT
FINISH
*LIST,vm120,vrt

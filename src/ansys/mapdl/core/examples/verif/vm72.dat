/COM,ANSYS MEDIA REL. 2023R1 (11/04/2022) REF. VERIF. MANUAL: REL. 2023R1
/VERIFY,VM72
/PREP7                    
JPGPRF,500,100,1             ! MACRO TO SET PREFS FOR JPEG PLOTS
/SHOW,JPEG
/TITLE, VM72, LOGARITHMIC DECREMENT
C*** VIBRATION THEORY AND APPLICATIONS, THOMSON, 2ND PRINTING, PAGE 45, EX 2.3-1
ET,1,COMBIN40,,,2         ! Y DOF ELEMENT
R,1,30,.12,.02590673      ! SPRING CONSTANT = 30, C=.12, MASS=.02590673
N,1
N,2
E,1,2
D,2,UY,0
FINISH

/SOLU
ANTYPE,MODAL
MODOPT,QRDAMP,1           ! USE QRDAMP EIGENSOLVER
MXPAND,1,,,YES
SOLVE
FINISH

/SOLU
ANTYPE,TRANSIENT
TRNOPT,MSUP,1             ! PERFORM MODE SUPERPOSITION TRANSIENT SOLVE
DELTIM,.003               ! INTEGRATION TIME STEP SIZE
TIME,.003
KBC,1                     ! STEP BOUNDARY CONDITIONS
F,1,FY,30                 ! APPLY INITIAL FORCE
OUTPR,BASIC,1
OUTRES,NSOL,1
SOLVE                     ! STATIC SOLUTION AT FIRST LOAD STEP
TIME,.69                  ! TIME TO INCLUDE ALMOST FOUR CYCLES
F,1,FY,0                  ! REMOVE FORCE
SOLVE
FINISH

/POST26
FILE,,rdsp
NSOL,2,1,U,Y,UY           ! STORE UY DISPLACEMENTS OF NODE 1 AS UY
NPRINT,20                 ! PRINT EVERY 20 POINTS
/GRID,1                   ! TURN GRID ON
/AXLAB,Y,DISP             ! Y-AXIS LABEL AS DISP
PLVAR,2                   ! DISPLAY VARIABLE 2 V/S TIME
*GET,AMP1,VARI,2,RTIME,0
*GET,AMP2,VARI,2,RTIME,.186
*GET,AMP3,VARI,2,RTIME,.372
*GET,AMP4,VARI,2,RTIME,.558
R1_2 = AMP1/AMP2
R2_3 = AMP2/AMP3
R3_4 = AMP3/AMP4
TD1_2 = .186 - 0
TD2_3 = .372 - .186
TD3_4 = .558 - .372
*DIM,LABEL_1,CHAR,3,2
*DIM,VALUE_1,,3,4
LABEL_1(1,1) = 'PEAK NUM','MAX. AMP','TIME,   '
LABEL_1(1,2) = 'BER     ',', in    ',' sec    '
*VFILL,VALUE_1(1,1),DATA,1,AMP1,0  
*VFILL,VALUE_1(1,2),DATA,2,AMP2,.186
*VFILL,VALUE_1(1,3),DATA,3,AMP3,.372
*VFILL,VALUE_1(1,4),DATA,4,AMP4,.558
*DIM,LABEL_2,CHAR,6,2
*DIM,VALUE_2,,6,3
LABEL_2(1,1) = '       R','       R','       R','       (','       (','       ('
LABEL_2(1,2) = ',1_2    ',',2_3    ',',3_4    ','TD)1_2  ','TD)2_3  ','TD)3_4  '
*VFILL,VALUE_2(1,1),DATA,1.5350,1.5350,1.5350,.18507,.18507,.18507
*VFILL,VALUE_2(1,2),DATA,R1_2,R2_3,R3_4,TD1_2,TD2_3,TD3_4
*VFILL,VALUE_2(1,3),DATA,ABS(R1_2/1.535),ABS(R2_3/1.5350),ABS(R3_4/1.535)
*VFILL,VALUE_2(4,3),DATA,ABS(TD1_2/.18507),ABS(TD2_3/.18507),ABS(TD3_4/.18507)
/COM
/OUT,vm72,vrt
/COM,------------------- VM72 RESULTS COMPARISON --------------
/COM,
*VWRITE,LABEL_1(1,1),LABEL_1(1,2),VALUE_1(1,1),VALUE_1(1,2),VALUE_1(1,3),VALUE_1(1,4)
(1X,A8,A8,'   ',F7.5,'  ',F7.5,'  ',F7.5,'  ',F7.5)
/COM,
/COM,
/COM,                 |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
*VWRITE,LABEL_2(1,1),LABEL_2(1,2),VALUE_2(1,1),VALUE_2(1,2),VALUE_2(1,3)
(1X,A8,A8,'   ',F10.5,'  ',F14.5,'   ',1F15.3)
/COM,----------------------------------------------------------
/OUT
FINISH
*LIST,vm72,vrt

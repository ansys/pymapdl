/COM,ANSYS MEDIA REL. 2023R1 (11/04/2022) REF. VERIF. MANUAL: REL. 2023R1
/VERIFY,VM66
/PREP7
SMRT,OFF
/TITLE, VM66, VIBRATION OF A FLAT PLATE
C***          VIBRATION PROBS. IN ENGR., TIMOSHENKO, 3RD. ED., PAGE 338, ART. 53
C***          USING SHELL63 ELEMENTS WITH SHELL OPTION
ANTYPE,MODAL                   ! MODE-FREQUENCY ANALYSIS
ET,1,SHELL63
R,1,1                          ! THICKNESS = 1
MXPAND,1                       ! EXPAND FIRST MODE
MODOPT,LANB,2,,,             
MP,EX,1,30E6
MP,DENS,1,728E-6
MP,NUXY,1,0
K,1,,-2 
K,2,16,-2
KGEN,2,1,2,1,,4
L,1,3 
L,2,4
LESIZE,ALL,,,2
A,1,2,4,3
ESIZE,,4
AMESH,1
NSEL,S,LOC,X,16 
D,ALL,ALL
NSEL,ALL
OUTPR,ALL,1
FINISH
*CREATE,SOLVIT,MAC             ! CREATE MACRO TO SOLVE AND RETRIEVE RESULTS
/out,scratch
/SOLU    
SOLVE
/out
*GET,FREQ,MODE,1,FREQ
*DIM,LABEL,CHAR,1,2
*DIM,VALUE,,1,3
LABEL(1,1) = '     f, '
LABEL(1,2) = 'Hz      '
*VFILL,VALUE(1,1),DATA,128.09
*VFILL,VALUE(1,2),DATA,FREQ
*VFILL,VALUE(1,3),DATA,ABS(FREQ/128.09)
FINISH
*END
SOLVIT                         ! EXECUTE MACRO TO SOLVE AND RETRIEVE RESULTS
SAVE,TABLE_1
/CLEAR, NOSTART                ! CLEAR DATABASE FOR SECOND SOLUTION
/TITLE, VM66, VIBRATION OF A FLAT PLATE
C***          USING SOLSH190 ELEMENTS
/PREP7
SMRT,OFF
ANTYPE,MODAL                   ! MODE-FREQUENCY ANALYSIS
ET,1,SHELL181                  ! USED ONLY TO GENERATE AREA MESH AND THEN DELETED
ET,2,SOLSH190                  ! ANALYZE AGAIN USING 3-D SOLSH190
THICK   = 1                    ! THICKNESS = 1
THKDENS = 1                    ! THICKNESS DENSITY = 1
SECT,1,SHELL
SECD,1,1                       ! THICKNESS = 1
MXPAND,1                       ! EXPAND FIRST MODE
MODOPT,LANB,2,,
MP,EX,1,30E6
MP,DENS,1,728E-6
MP,NUXY,1,0
K,1,,-2 
K,2,16,-2
KGEN,2,1,2,1,,4
L,1,3 
L,2,4
LESIZE,ALL,,,2
A,1,2,4,3
ESIZE,,4
AMESH,1
ESIZE,0,THKDENS               ! THICKNESS DENSITY FOR SOLSH190  
EXTOPT,ACLEAR,1               ! CLEAR AREA WHEN VOLUME GENERATION IS DONE
VOFFSET,1,THICK               ! GENERATES A VOLUME, OFFSET FROM A GIVEN AREA
NSEL,S,LOC,X,16 
D,ALL,ALL
NSEL,ALL
OUTPR,ALL,1
FINISH
SOLVIT                        ! EXECUTE MACRO TO SOLVE AND RETRIEVE RESULTS
SAVE,TABLE_2
/CLEAR, NOSTART               ! CLEAR DATABASE FOR SECOND SOLUTION
/TITLE, VM66, VIBRATION OF A FLAT PLATE
C***          USING SHELL181 ELEMENTS
/PREP7
SMRT,OFF
ANTYPE,MODAL                   ! MODE-FREQUENCY ANALYSIS
ET,1,SHELL181                 
SECT,1,SHELL
SECD,1,1                       ! THICKNESS = 1
MXPAND,1                       ! EXPAND FIRST MODE
MODOPT,LANB,2,,,
MP,EX,1,30E6
MP,DENS,1,728E-6
MP,NUXY,1,0
K,1,,-2 
K,2,16,-2
KGEN,2,1,2,1,,4
L,1,3 
L,2,4
LESIZE,ALL,,,2
A,1,2,4,3
ESIZE,,4
AMESH,1
NSEL,S,LOC,X,16 
D,ALL,ALL
NSEL,ALL
OUTPR,ALL,1
FINISH
SOLVIT                        ! EXECUTE MACRO TO SOLVE AND RETRIEVE RESULTS
SAVE,TABLE_3
/CLEAR, NOSTART               ! CLEAR DATABASE FOR SECOND SOLUTION
/TITLE, VM66, VIBRATION OF A FLAT PLATE
C***          USING SHELL281 ELEMENTS
/PREP7
SMRT,OFF
ANTYPE,MODAL                   ! MODE-FREQUENCY ANALYSIS
ET,1,SHELL281
SECT,1,SHELL
SECD,1,1                       ! THICKNESS = 1                 
MXPAND,1                       ! EXPAND FIRST MODE
MODOPT,LANB,2,,,
MP,EX,1,30E6
MP,DENS,1,728E-6
MP,NUXY,1,0
K,1,,-2 
K,2,16,-2
KGEN,2,1,2,1,,4
L,1,3 
L,2,4
LESIZE,ALL,,,2
A,1,2,4,3
ESIZE,,4
AMESH,1
NSEL,S,LOC,X,16 
D,ALL,ALL
NSEL,ALL
OUTPR,ALL,1
FINISH
SOLVIT                        ! EXECUTE MACRO TO SOLVE AND RETRIEVE RESULTS
SAVE,TABLE_4
/NOPR
RESUME,TABLE_1
/COM
/OUT,vm66,vrt
/COM,------------------- VM66 RESULTS COMPARISON --------------
/COM,
/COM,                 |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
/COM,
/COM, SHELL63
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.2,'  ',F14.2,'   ',1F15.3)
/NOPR
RESUME,TABLE_2
/GOPR
/COM,
/COM, SOLSH190
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.2,'  ',F14.2,'   ',1F15.3)
/NOPR
RESUME,TABLE_3
/GOPR
/COM,
/COM, SHELL181
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.2,'  ',F14.2,'   ',1F15.3)
/NOPR
RESUME,TABLE_4
/GOPR
/COM,
/COM, SHELL281
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.2,'  ',F14.2,'   ',1F15.3)
/COM,----------------------------------------------------------
/OUT
FINISH
*LIST,vm66,vrt
/DELETE,SOLVIT,MAC
/DELETE,TABLE_1
/DELETE,TABLE_2
/DELETE,TABLE_3
/DELETE,TABLE_4

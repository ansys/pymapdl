/COM,ANSYS MEDIA REL. 2023R1 (11/04/2022) REF. VERIF. MANUAL: REL. 2023R1
/VERIFY,VM29
/PREP7
/TITLE, VM29, FRICTION ON A SUPPORT BLOCK
C***VECTOR MECHANICS FOR ENGINEERS, BEER AND JOHNSTON, 1962, PAGE 283, PROB. 8.2
ANTYPE,STATIC
ET,1,CONTAC12
R,1,-20,1E7                ! THETA = -20, STIFFNESS = 1E7
MP,MU,1,.3                 ! COEFFICIENT OF FRICTION
N,1                        ! CREATE NODES
N,2
E,1,2                      ! CREATE ELEMENT
D,1,ALL                    ! BOUNDARY CONDITIONS AND LOADS
F,2,FX,-5.7674             ! STICKING LOAD
F,2,FY,-100
NSUBST,1                   ! LIMIT TO ONE ITERATION TO PREVENT DIVERGENCE
OUTPR,BASIC,ALL            ! PRINT NODAL DOF, REACTION & ELEMENT SOLUTION
OUTPR,NLOAD,ALL            ! PRINT ELEMENT NODAL LOADS
KBC,1                      ! STEP CHANGE IN B.C.'S
FINISH
/SOLU    
/OUT,SCRATCH
SOLVE
FINISH
/POST1
ETAB,NOR_FC1,SMISC,1
ETAB,SLI_FC1,SMISC,2
/OUT,
*GET,NORM_FC1,ELEM,1,ETAB,NOR_FC1
*GET,SLID_FC1,ELEM,1,ETAB,SLI_FC1
*DIM,LABEL,CHAR,2,2
*DIM,VALUE_C1,,2,3
LABEL(1,1) = 'NORMAL ','SLIDING '
LABEL(1,2) = 'FORCE lb','FORCE lb'
*VFILL,VALUE_C1(1,1),DATA,-95.942,28.783
*VFILL,VALUE_C1(1,2),DATA,NORM_FC1,SLID_FC1
*VFILL,VALUE_C1(1,3),DATA,ABS(NORM_FC1/95.942),ABS(SLID_FC1/28.783)
SAVE,TABLE_1
FINISH
/SOLU
F,2,FX,-5.76720            ! SLIDING LOAD
/OUT,SCRATCH
SOLVE
FINISH
/POST1
ETAB,NOR_FC2,SMISC,1
ETAB,SLI_FC2,SMISC,2
/OUT,
*GET,NORM_FC2,ELEM,1,ETAB,NOR_FC2
*GET,SLID_FC2,ELEM,1,ETAB,SLI_FC2
*DIM,VALUE_C2,,2,3
LABEL(1,1) = 'NORMAL ','SLIDING '
LABEL(1,2) = 'FORCE lb','FORCE lb'
*VFILL,VALUE_C2(1,1),DATA,-95.942,28.783
*VFILL,VALUE_C2(1,2),DATA,NORM_FC2,SLID_FC2
*VFILL,VALUE_C2(1,3),DATA,ABS(NORM_FC2/95.942),ABS(SLID_FC2/28.783)
SAVE,TABLE_2
FINISH
/CLEAR,NOSTART
/COM,
/COM, 
/COM, SOLVING THE SAME PROBLEM USING CONTA178 ELEMENTS
/COM,
/COM,
/PREP7
ANTYPE,STATIC
ET,1,CONTA178                   ! USING CONTA178 ELEMENTS
KEYOPT,1,2,1                    ! PURE PENALTY METHOD
KEYOPT,1,5,0                    ! CONTACT NORMAL BASED ON REAL CONSTANTS
KEYOPT,1,10,0                   ! STANDARD CONTACT BEHAVIOR
R,1,-1E7,,,-1E7,,0.3420201      ! DEFINE FKN,START,FKS,NX,NY,NZ
RMORE,0.939692,0                   
N,1,0,0,0
N,2,0,0,0
MP,MU,1,.3                 ! COEFFICIENT OF FRICTION
MP,EX,1,1.0
TYPE,1
MAT,1
REAL,1
E,1,2                      ! CREATE ELEMENT
D,1,ALL                    ! BOUNDARY CONDITIONS AND LOADS
D,2,UZ                     ! 2D ONLY
F,2,FX,-5.7674             ! STICKING LOAD
F,2,FY,-100
NSUBST,1,                  ! LIMIT TO ONE ITERATION TO PREVENT DIVERGENCE
AUTOTS,ON
TIME,1.0
OUTPR,BASIC,ALL            ! PRINT NODAL DOF, REACTION & ELEMENT SOLUTION
OUTPR,NLOAD,ALL            ! PRINT ELEMENT NODAL LOADS
KBC,1                      ! STEP CHANGE IN B.C.'S
FINISH
/SOLU    
/OUT,SCRATCH
SOLVE
FINISH
/POST1
RSYS,SOLU
ETAB,NOR_FC3,SMISC,1
ETAB,SLI_FC3,SMISC,2
/OUT,
*GET,NORM_FC3,ELEM,1,ETAB,NOR_FC3
*GET,SLID_FC3,ELEM,1,ETAB,SLI_FC3
*DIM,LABEL,CHAR,2,2
*DIM,VALUE_C3,,2,3
LABEL(1,1) = 'NORMAL ','SLIDING '
LABEL(1,2) = 'FORCE lb','FORCE lb'
*VFILL,VALUE_C3(1,1),DATA,-95.942,-28.783
*VFILL,VALUE_C3(1,2),DATA,NORM_FC3,SLID_FC3
*VFILL,VALUE_C3(1,3),DATA,ABS(NORM_FC3/95.942),ABS(SLID_FC3/(-28.783))
SAVE,TABLE_3
FINISH
/SOLU
F,2,FX,-5.76720            ! SLIDING LOAD
/OUT,SCRATCH
SOLVE
FINISH
/POST1
ETAB,NOR_FC4,SMISC,1
ETAB,SLI_FC4,SMISC,2
/OUT,
*GET,NORM_FC4,ELEM,1,ETAB,NOR_FC4
*GET,SLID_FC4,ELEM,1,ETAB,SLI_FC4
*DIM,VALUE_C4,,2,3
LABEL(1,1) = 'NORMAL ','SLIDING '
LABEL(1,2) = 'FORCE lb','FORCE lb'
*VFILL,VALUE_C4(1,1),DATA,-95.942,-28.783
*VFILL,VALUE_C4(1,2),DATA,NORM_FC4,SLID_FC4
*VFILL,VALUE_C4(1,3),DATA,ABS(NORM_FC4/95.942),ABS(SLID_FC4/(-28.783))
SAVE,TABLE_4

RESUME,TABLE_1
/COM
/OUT,vm29,vrt
/COM,------------------- VM29 RESULTS COMPARISON ---------------
/COM,
/COM,                 |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
/COM,FX = 5.7674 LB AND MODEL IS STICKING (USING CONTA12 ELEMENTS):
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE_C1(1,1),VALUE_C1(1,2),VALUE_C1(1,3)
(1X,A8,A8,'   ',F10.3,'  ',F14.3,'   ',1F15.3)
/NOPR
RESUME,TABLE_2
/GOPR
/COM,
/COM,FX = 5.76720 LB AND MODEL IS SLIDING (USING CONTA12 ELEMENTS):
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE_C2(1,1),VALUE_C2(1,2),VALUE_C2(1,3)
(1X,A8,A8,'   ',F10.3,'  ',F14.3,'   ',1F15.3)
/COM
/NOPR
RESUME,TABLE_3
/GOPR
/COM,
/COM,FX = 5.7674 LB AND MODEL IS STICKING (USING CONTA178 ELEMENTS):
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE_C3(1,1),VALUE_C3(1,2),VALUE_C3(1,3)
(1X,A8,A8,'   ',F10.3,'  ',F14.3,'   ',1F15.3)
/COM,
/NOPR
RESUME,TABLE_4
/GOPR
/COM,
/COM,FX = 5.76720 LB AND MODEL IS SLIDING (USING CONTA178 ELEMENTS):
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE_C4(1,1),VALUE_C4(1,2),VALUE_C4(1,3)
(1X,A8,A8,'   ',F10.3,'  ',F14.3,'   ',1F15.3)
/COM,
/COM,-----------------------------------------------------------
/OUT
FINISH
*LIST,vm29,vrt


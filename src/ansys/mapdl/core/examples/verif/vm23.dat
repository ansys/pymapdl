/COM,ANSYS MEDIA REL. 2023R1 (11/04/2022) REF. VERIF. MANUAL: REL. 2023R1
/VERIFY,VM23
/PREP7
/TITLE, VM23, THERMAL-STRUCTURAL CONTACT OF TWO BODIES
/COM
/COM,  SOLVING USING PLANE13 ELEMENTS
/COM,
ET,1,PLANE13,4,,2        ! COUPLE-FIELD ELEMENT TYPE 
ET,2,CONTA175,1          ! CONTACT ELEMENT TYPE 
ET,3,TARGE169            ! TARGET ELEMENT TYPE
MP,EX,1,10E6             ! YOUNG'S MODULUS
MP,KXX,1,250             ! CONDUCTIVITY
MP,ALPX,1,12E-6          ! THERMAL EXPANSION COEFFICIENT
MP,PRXY,,0.3
R,2,,,-1000,-0.005
RMORE,,,,,,-100
RMORE,,100
RMORE
RMORE,0.01
!      SET UP FINITE ELEMENT MODEL
N,1
N,2,0.4
N,3,(0.4+0.0035)
N,4,(0.9+0.0035)
NGEN,2,4,1,4,1,,0.1
E,1,2,6,5                ! PLANE13 ELEMENTS 
E,3,4,8,7
TYPE,2                   ! CONTACT ELEMENTS
REAL,2
E,2
E,6
TYPE,3                   ! TARGET ELEMENTS
REAL,2
NSEL,S,NODE,,3,7,4
ESLN
ESURF
ALLSEL
!      APPLY INITIAL BOUNDARY CONDITIONS
D,ALL,AZ
D,1,UY,,,4,1
D,1,UX,,,5,4
D,4,UX,,,8,4
TREF,100
FINISH
/SOLU
NLGEOM,ON                ! LARGE DEFLECTION EFFECTS TURNED ON
D,1,TEMP,500,,5,4
D,3,TEMP,100,,4
D,7,TEMP,100,,8
/OUT,SCRATCH
SOLVE                    ! FIRST LOAD STEP
OUTRES,ALL,ALL           ! STORE ALL DATA 
DDELE,3,TEMP,7,4
D,4,TEMP,850,,8,4
NSUBST,3
SOLVE                    ! SECOND LOAD STEP
D,4,TEMP,100,,8,4
SOLVE                    ! THIRD LOAD STEP
FINISH

/POST1
/OUT,
INRES,NSOL,MISC          ! RETRIEVE NODAL AND MISCELLANEOUS DATA
SUBSET,2,2               ! READ LOAD STEP 2, SUBSTEP 2 DATA 
ESEL,S,,,3,4
ETABLE,HEAT-FLO,SMISC,14 ! STORE HEAT FLOWS FOR CONTACT ELEMENTS
SSUM
*GET,HEAT_C1,SSUM,,ITEM,HEAT-FLO
NSEL,S,,,2,6,4 
PRNSOL,TEMP
*GET,TEMP_C1,NODE,2,TEMP
APPEND,2,3               ! APPEND (OVERWRITE IN THIS CASE) BY 
                         !  LOAD STEP 2 AND SUBSTEP 3 DATA
ETABLE,REFL
SSUM
*GET,HEAT_C2,SSUM,,ITEM,HEAT-FLO
PRNSOL,TEMP
*GET,TEMP_C2,NODE,2,TEMP
SUBSET,3,3               ! READ LOAD STEP 3, SUBSTEP 3 DATA
ETABLE,REFL
PRETAB
*GET,TEMP_C3,ELEM,4,ETAB,HEAT-FLO
PRNSOL,TEMP
ALLSEL,ALL

*DIM,LABEL,CHAR,2,2
*DIM,LABEL_C3,CHAR,1,2
*DIM,VALUE_C1,,2,3
*DIM,VALUE_C2,,2,3
*DIM,VALUE_C3,,1,2
LABEL(1,1) = 'TEMP AT ','HEAT FLO'
LABEL(1,2) = 'EA2 (C) ','W (W)   '
LABEL_C3(1,1) = 'HEAT FLO'
LABEL_C3(1,2) = 'W (W)   '
*VFILL,VALUE_C1(1,1),DATA,539.02,2439.02    
*VFILL,VALUE_C1(1,2),DATA,TEMP_C1,HEAT_C1
*VFILL,VALUE_C1(1,3),DATA,ABS(TEMP_C1/539.02  ) ,ABS( HEAT_C1/2439.02 )
*VFILL,VALUE_C2(1,1),DATA,636.59,8536.59
*VFILL,VALUE_C2(1,2),DATA,TEMP_C2,HEAT_C2
*VFILL,VALUE_C2(1,3),DATA,ABS(TEMP_C2/636.59 ) ,ABS( HEAT_C2/8536.59 )
*VFILL,VALUE_C3(1,1),DATA,0
*VFILL,VALUE_C3(1,2),DATA,TEMP_C3
SAVE,TABLE_1
FINISH
/CLEAR,NOSTART

/COM, 
/COM,  SOLVING USING PLANE223 ELEMENT WITH WEAK COUPLING BETWEEN U AND TEMP DOF
/COM,

/PREP7
ET,1,PLANE223,11,1       ! COUPLE-FIELD ELEMENT TYPE, WEAK COUPLING
ET,2,CONTA175,1          ! CONTACT ELEMENT TYPE 
ET,3,TARGE169            ! TARGET ELEMENT TYPE
MP,EX,1,10E6             ! YOUNG'S MODULUS
MP,KXX,1,250             ! CONDUCTIVITY
MP,ALPX,1,12E-6          ! THERMAL EXPANSION COEFFICIENT
MP,PRXY,,0.3
R,2,,,-1000,-0.005
RMORE,,,,,,-100
RMORE,,100
RMORE
RMORE,0.01
!      SET UP FINITE ELEMENT MODEL
N,1
N,2,0.4
N,3,(0.4+0.0035)
N,4,(0.9+0.0035)
NGEN,2,4,1,4,1,,0.1
E,1,2,6,5                ! PLANE223 ELEMENTS 
E,3,4,8,7
TYPE,2                   ! CONTACT ELEMENTS
REAL,2
E,2
E,6
TYPE,3                   ! TARGET ELEMENTS
REAL,2
NSEL,S,NODE,,3,7,4
ESLN
ESURF
ALLSEL
!      APPLY INITIAL BOUNDARY CONDITIONS
D,1,UY,,,4,1
D,1,UX,,,5,4
D,4,UX,,,8,4
TREF,100
ERESX,YES
FINISH
/SOLU
NLGEOM,ON                ! LARGE DEFLECTION EFFECTS TURNED ON
D,1,TEMP,500,,5,4
D,3,TEMP,100,,4
D,7,TEMP,100,,8
/OUT,SCRATCH
SOLVE                    ! FIRST LOAD STEP
OUTRES,ALL,ALL           ! STORE ALL DATA 
DDELE,3,TEMP,7,4
D,4,TEMP,850,,8,4
NSUBST,3
SOLVE                    ! SECOND LOAD STEP
D,4,TEMP,100,,8,4
SOLVE                    ! THIRD LOAD STEP
FINISH

/POST1
/OUT,
INRES,NSOL,MISC,EPEL,EPTH      ! RETRIEVE NODAL, MISCELLANEOUS, AND STRAIN DATA
SUBSET,2,2               ! READ LOAD STEP 2, SUBSTEP 2 DATA 
ESEL,S,,,3,4
ETABLE,HEAT-FLO,SMISC,14 ! STORE HEAT FLOWS FOR CONTACT ELEMENTS
SSUM
*GET,HEAT_C1B,SSUM,,ITEM,HEAT-FLO
NSEL,S,,,2,6,4 
PRNSOL,TEMP
*GET,TEMP_C1B,NODE,2,TEMP
APPEND,2,3               ! APPEND (OVERWRITE IN THIS CASE) BY 
                         !  LOAD STEP 2 AND SUBSTEP 3 DATA
ETABLE,REFL
SSUM
*GET,HEAT_C2B,SSUM,,ITEM,HEAT-FLO
PRNSOL,TEMP
*GET,TEMP_C2B,NODE,2,TEMP
SUBSET,3,3               ! READ LOAD STEP 3, SUBSTEP 3 DATA
ETABLE,REFL
PRETAB
*GET,TEMP_C3B,ELEM,4,ETAB,HEAT-FLO
PRNSOL,TEMP
ALLSEL,ALL

*DIM,LABEL,CHAR,2,2
*DIM,LABEL_C3,CHAR,1,2
*DIM,VALUE_C1,,2,3
*DIM,VALUE_C2,,2,3
*DIM,VALUE_C3,,1,2
LABEL(1,1) = 'TEMP AT ','HEAT FLO'
LABEL(1,2) = 'EA2 (C) ','W (W)   '
LABEL_C3(1,1) = 'HEAT FLO'
LABEL_C3(1,2) = 'W (W)   '
*VFILL,VALUE_C1(1,1),DATA,539.01,2450.64    
*VFILL,VALUE_C1(1,2),DATA,TEMP_C1B,HEAT_C1B
*VFILL,VALUE_C1(1,3),DATA,ABS(TEMP_C1B/539.01  ) ,ABS( HEAT_C1B/2450.64 )
*VFILL,VALUE_C2(1,1),DATA,636.65,8588.71
*VFILL,VALUE_C2(1,2),DATA,TEMP_C2B,HEAT_C2B
*VFILL,VALUE_C2(1,3),DATA,ABS(TEMP_C2B/636.65 ) ,ABS( HEAT_C2B/8588.71 )
*VFILL,VALUE_C3(1,1),DATA,0
*VFILL,VALUE_C3(1,2),DATA,TEMP_C3B
SAVE,TABLE_2
FINISH
RESUME,TABLE_1
/COM
/NOPR
/OUT,vm23,vrt
/COM,------------------- VM23 RESULTS COMPARISON ---------------------
/COM,
/COM,                   |    TARGET    |   Mechanical APDL   |   RATIO
/COM,
/COM,
/COM,  USING PLANE13 ELEMENTS
/COM,
/COM,TEMP AT EB2 = 600 C:
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE_C1(1,1),VALUE_C1(1,2),VALUE_C1(1,3)
(1X,A8,A8,'   ',F11.2,'  ',F15.2,'   ',1F15.3)
/COM,
/COM,TEMP AT EB2 = 850 C:
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE_C2(1,1),VALUE_C2(1,2),VALUE_C2(1,3)
(1X,A8,A8,'   ',F11.2,'  ',F15.2,'   ',1F15.3)
/COM,
/COM,TEMP AT EB2 = 100 C:
*VWRITE,LABEL_C3(1,1),LABEL_C3(1,2),VALUE_C3(1,1),VALUE_C3(1,2)
(1X,A8,A8,'   ',F11.1,'  ',F11.1)
/COM,
RESUME,TABLE_2
/COM,
/COM, USING PLANE223 ELEMENTS
/COM,
/COM,TEMP AT EB2 = 600 C:
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE_C1(1,1),VALUE_C1(1,2),VALUE_C1(1,3)
(1X,A8,A8,'   ',F11.2,'  ',F15.2,'   ',1F15.3)
/COM,
/COM,TEMP AT EB2 = 850 C:
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE_C2(1,1),VALUE_C2(1,2),VALUE_C2(1,3)
(1X,A8,A8,'   ',F11.2,'  ',F15.2,'   ',1F15.3)
/COM,
/COM,TEMP AT EB2 = 100 C:
*VWRITE,LABEL_C3(1,1),LABEL_C3(1,2),VALUE_C3(1,1),VALUE_C3(1,2)
(1X,A8,A8,'   ',F11.2,'  ',F11.2)
/COM,
/COM,-----------------------------------------------------------------
/OUT
FINISH
*LIST,vm23,vrt
/delete,TABLE_1
/delete,TABLE_2
/exit,nosave

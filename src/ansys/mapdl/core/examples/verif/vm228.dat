/COM,ANSYS MEDIA REL. 2023R1 (11/04/2022) REF. VERIF. MANUAL: REL. 2023R1
/VERIFY,VM228
JPGPRF,500,100,1
/SHOW,JPEG
/TITLE,VM228, RADIATION BETWEEN INFINITE COAXIAL CYLINDERS
C*** USING PLANE35

TIN=1000
TOUT=100
_GEOMGEN=0
/PREP7
*IF, _GEOMGEN,EQ,1, THEN
  ET,1,PLANE35			!CREATE 2D THERMAL ELEMENTS
  MPTEMP,,,,,,,,			!SET MATERIAL PROPERTIES
  MPTEMP,1,0
  MPDATA,EX,1,,30E6
  MPDATA,PRXY,1,,.27
  MPTEMP,,,,,,,,
  MPTEMP,1,0
  MPDATA,DENS,1,,.27
  MPTEMP,,,,,,,,
  MPTEMP,1,0
  MPDATA,KXX,1,,1
  MPTEMP,,,,,,,,
  MPTEMP,1,0
  MPDATA,C,1,,.21
  CYL4,0,0,0.5,,1
  CYL4,0,0,4, ,5
  MSHAPE,1,2D
  MSHKEY,0
  SMRT,4
  AMES,ALL
  
  LSEL,S,,,1,4
  LSEL,A,,,13,16
  SFL,ALL,RDSF,1, ,1,		!SET ALL FACING SURFACES TO EMISSIVITY 1
  LSEL,S,,,9,12
  DL,ALL, ,TEMP,TOUT,1		!APPLY UNIFORM TEMPERATURE TO EXTERIOR
  LSEL,S,,,5,8
  DL,ALL, ,TEMP,TIN,1		!APPLY UNIFORM TEMPERATURE TO INTERIOR
  ASEL,S, , ,       1
  ESLA,S
  CM,INSIDE,ELEM
  ASEL,S, , ,       2
  ESLA,S
  CM,OUTSIDE,ELEM
  ALLSEL
  CDWRITE,ALL,VM228-1,CDB

*ELSE
  CDREAD,ALL,VM228-1,CDB
*ENDIF

FINI

/AUX12
STFCONST=0.119E-10
HEMIOPT,,,,,,,,,,,,,,,,,,,0
STEF,STFCONST				!SET STEFAN-BOLTZMAN CONSTANT FOR MODEL
TOFFST,0.E+00				!SET TEMPERATURE OFFSET
RADOPT,,0.1,0.E+00,1000,0.1,0.1  	!SET RADIOSITY OPTIONS
SPCTEMP,1,0.E+00			!SET TEMPERATURE FOR RADIATION TO SPACE

/OUT,SCRATCH
VFOPT,NEW
VFOPT,READ
VFQUERY,INSIDE,INSIDE			!EXTRACT VIEW FACTOR FROM INTERIOR CYLINDER ELEMENTS DUE TO INTERIOR CYLINDER
*GET,VFAVG1,RAD,,VFAVG			!DETERMINE VIEW FACTOR FOR ENTIRE SURFACE
VFQUERY,OUTSIDE,INSIDE			!EXTRACT VIEW FACTOR FROM EXTERIOR CYLINDER ELEMENTS DUE TO INTERIOR CYLINDER
*GET,VFAVG2,RAD,,VFAVG			!DETERMINE VIEW FACTOR FOR ENTIRE SURFACE
VFQUERY,OUTSIDE,OUTSIDE			!EXTRACT VIEW FACTOR FROM EXTERIOR CYLINDER ELEMENTS DUE TO EXTERIOR CYLINDER
*GET,VFAVG3,RAD,,VFAVG			!DETERMINE VIEW FACTOR FOR ENTIRE SURFACE
FINISH


/SOLU
TIME,1
DELTIM,0.5
SOLVE
FINI

/POST1
LSEL,S, , ,1,4
LSEL,A, , ,13,16
NSLL,S,1
*GET,TI,NODE,NODE(0,-1,0),TEMP			!INSIDE CYLINDER SURFACE TEMP
*GET,TO,NODE,NODE(0,-4,0),TEMP			!OUTSIDE CYLINDER SURFACE TEMP
*GET,HFI,NODE,NODE(0,-1,0),TF,SUM			!INSIDE CYLINDER HEAT FLUX
*GET,HFO,NODE,NODE(0,-4,0),TF,SUM			!OUTSIDE CYLINDER HEAT FLUX

HFIEXP=ABS(TO**4-TI**4)*STFCONST/1
HFOEXP=ABS(TO**4-TI**4)*STFCONST/4

HFIERR=(HFI/HFIEXP)
HFOERR=(HFO/HFOEXP)
/OUT,
*STATUS,PARM
*DIM,VALUE,,5,3
*VFILL,VALUE(1,1),DATA,0,0.25,0.75,HFIEXP,HFOEXP
*VFILL,VALUE(1,2),DATA,VFAVG1,VFAVG2,VFAVG3,HFI,HFO
*VFILL,VALUE(1,3),DATA,0.000,VFAVG2/0.25,VFAVG3/0.75,HFIERR,HFOERR
*DIM,LABEL,CHAR,10,2
LABEL(1,1) = 'VF(1-1)','VF(2-1)','VF(2-2)','HFINSIDE','HFOUTSIDE'
SAVE,TABLE_1
FINI
/CLEAR,NOSTART

!******************************************************************************

/TITLE,VM228, RADIATION BETWEEN INFINITE COAXIAL CYLINDERS
C*** USING PLANE35 AND SURF251


TIN=1000
TOUT=100
_GEOMGEN=0
/PREP7
*IF, _GEOMGEN,EQ,1, THEN
  ET,1,PLANE35			!CREATE 2D THERMAL ELEMENTS
  MPTEMP,,,,,,,,			!SET MATERIAL PROPERTIES
  MPTEMP,1,0
  MPDATA,EX,1,,30E6
  MPDATA,PRXY,1,,.27
  MPTEMP,,,,,,,,
  MPTEMP,1,0
  MPDATA,DENS,1,,.27
  MPTEMP,,,,,,,,
  MPTEMP,1,0
  MPDATA,KXX,1,,1
  MPTEMP,,,,,,,,
  MPTEMP,1,0
  MPDATA,C,1,,.21
  CYL4,0,0,0.5,,1
  CYL4,0,0,4, ,5
  MSHAPE,1,2D
  MSHKEY,0
  SMRT,4
  AMES,ALL
  LSEL,S,,,1,4
  LSEL,A,,,13,16
  SFL,ALL,RDSF,1, ,1,		!SET ALL FACING SURFACES TO EMISSIVITY 1
  LSEL,S,,,9,12
  DL,ALL, ,TEMP,TOUT,1		!APPLY UNIFORM TEMPERATURE TO EXTERIOR
  LSEL,S,,,5,8
  DL,ALL, ,TEMP,TIN,1		!APPLY UNIFORM TEMPERATURE TO INTERIOR
  ASEL,S, , ,       1
  ESLA,S
  CM,INSIDE,ELEM
  ASEL,S, , ,       2
  ESLA,S
  CM,OUTSIDE,ELEM
  ALLSEL
  CDWRITE,ALL,VM228-2,CDB

*ELSE
  CDREAD,ALL,VM228-2,CDB
*ENDIF
/OUT,SCRATCH
  RDEC,,0.5
  RSURF
FINI
  
/AUX12
STFCONST=0.119E-10
HEMIOPT,,,,,,,,,,,,,,,,,,,0
STEF,STFCONST				!SET STEFAN-BOLTZMAN CONSTANT FOR MODEL
TOFFST,0.E+00				!SET TEMPERATURE OFFSET
RADOPT,,0.1,0.E+00,1000,0.1,0.1  	!SET RADIOSITY OPTIONS
SPCTEMP,1,0.E+00			!SET TEMPERATURE FOR RADIATION TO SPACE

VFOPT,NEW			!CALCULATE RADIOSITY VIEW FACTORS
NSEL,S,LOC,X,-1,1
NSEL,R,LOC,Y,-1,1
ESLN,S,1
ESEL,R,TYPE,,2
CM,INRS,ELEM
NSEL,S,LOC,X,-1,1
NSEL,R,LOC,Y,-1,1
NSEL,INVERT
ESLN,S,1
ESEL,R,TYPE,,2
CM,OUTRS,ELEM
ALLSEL

VFQUERY,INRS,INRS			!EXTRACT VIEW FACTOR FROM INTERIOR CYLINDER ELEMENTS DUE TO INTERIOR CYLINDER
*GET,VFAVG1,RAD,,VFAVG			!DETERMINE VIEW FACTOR FOR ENTIRE SURFACE
VFQUERY,OUTRS,INRS			!EXTRACT VIEW FACTOR FROM EXTERIOR CYLINDER ELEMENTS DUE TO INTERIOR CYLINDER
*GET,VFAVG2,RAD,,VFAVG			!DETERMINE VIEW FACTOR FOR ENTIRE SURFACE
VFQUERY,OUTRS,OUTRS			!EXTRACT VIEW FACTOR FROM EXTERIOR CYLINDER ELEMENTS DUE TO EXTERIOR CYLINDER
*GET,VFAVG3,RAD,,VFAVG			!DETERMINE VIEW FACTOR FOR ENTIRE SURFACE
ALLSEL
FINI

/SOLU
TIME,1
DELTIM,0.5
SOLVE
FINI

/POST1
LSEL,S, , ,1,4
LSEL,A, , ,13,16
NSLL,S,1
*GET,TI,NODE,NODE(0,-1,0),TEMP			!INSIDE CYLINDER SURFACE TEMP
*GET,TO,NODE,NODE(0,-4,0),TEMP			!OUTSIDE CYLINDER SURFACE TEMP
*GET,HFI,NODE,NODE(0,-1,0),TF,SUM			!INSIDE CYLINDER HEAT FLUX
*GET,HFO,NODE,NODE(0,-4,0),TF,SUM			!OUTSIDE CYLINDER HEAT FLUX

HFIEXP=ABS(TO**4-TI**4)*STFCONST/1	! CALCULATE EXPECTED RADIATION FLUX
HFOEXP=ABS(TO**4-TI**4)*STFCONST/4

HFIERR=(HFI/HFIEXP)
HFOERR=(HFO/HFOEXP)

*STATUS,PARM
*DIM,VALUE,,5,3
*VFILL,VALUE(1,1),DATA,0,0.25,0.75,HFIEXP,HFOEXP
*VFILL,VALUE(1,2),DATA,VFAVG1,VFAVG2,VFAVG3,HFI,HFO
*VFILL,VALUE(1,3),DATA,0.000,VFAVG2/0.25,VFAVG3/0.75,HFIERR,HFOERR
*DIM,LABEL,CHAR,10,2
LABEL(1,1) = 'VF(1-1)','VF(2-1)','VF(2-2)','HFINSIDE','HFOUTSIDE'
SAVE,TABLE_2
FINI
/CLEAR,NOSTART

!******************************************************************************

/NOPR
/OUT,vm228,vrt
/COM,
/COM,------------------- VM228 RESULTS COMPARISON --------------
/COM,
/COM,               |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
/COM,
RESUME,TABLE_1
/COM,USING PLANE35
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F8.3,'  ',F14.3,'   ',1F17.2)
/COM,
RESUME,TABLE_2
/COM,USING PLANE35 WITH RSURF (SURF251)
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F8.3,'  ',F14.3,'   ',1F17.2)
/COM,
/COM,-----------------------------------------------------------
FINISH
/OUT,
*LIST,vm228,vrt
/EXIT,NOSAVE

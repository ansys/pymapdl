/COM,ANSYS MEDIA REL. 2023R1 (11/04/2022) REF. VERIF. MANUAL: REL. 2023R1
/VERIFY,VM229
JPGPRF,500,100,1             ! MACRO TO SET PREFS FOR JPEG PLOTS
/SHOW,JPEG
/TITLE,VM229, FRICTION HEATING OF A SLIDING BLOCK
/COM, REF: WRIGGER AND MIEHE, COMP METH APPL ENGR 113, PP301-319
/OUT,SCRATCH
/PREP7
ET,1,PLANE13,4							! 2-D COUPLED-FIELD SOLID
RECT,,5,,1.25
RECT,,1.25,1.25,2.5
LESIZE,ALL,0.25, , ,1,1
AMESH,ALL
UIMP,1,EX, , ,70000,
UIMP,1,DENS, , ,2.7E-9,
UIMP,1,ALPX, , ,23.86E-6,
UIMP,1,NUXY, , ,0.3,
UIMP,1,MU, , ,0.2,
UIMP,1,KXX, , ,150,
UIMP,1,C, , ,9E8,
TOFFST,460
TUNIF,0.00
ET,2,TARGE169						! 2-D TARGET SEGMENT
ET,3,CONTA172						! 2-D 3-NODE SURFACE-TO-SURFACE CONTACT
KEYOPT,3,1,1
ASEL,S,,,1
NSLA,S,1
NSEL,R,LOC,Y,1.25
R,1
TYPE,2
ESURF
ALLSEL
ASEL,S,,,2
NSLA,S,1
NSEL,R,LOC,Y,1.25
TYPE,3
ESURF
ALLSEL,ALL
*DIM,PRE,TABLE,2,1,1,TIME
SFL,7,PRES, %PRE%
PRE(1,0,1) = 0
PRE(1,1,1) = 10
PRE(2,0,1) = 10
PRE(2,1,1) = 10
ALLSEL
SAVE
FINISH
/SOLU
NSUB,1
ASEL,S,,,2
NSLA,S,1
NSEL,R,LOC,X,1.25
D,ALL,UX,3.75
ALLSEL
ASEL,S,,,1
NSLA,S,1
D,ALL,UX,0
D,ALL,UY,0
ALLSEL
ANTYPE,TRANS
TIMINT,OFF,STRUC
TINTP,,,,1.0
NLGEOM,ON
TIME,3.75E-3
AUTO,ON
NSUB,100,10000,100
OUTRES,ALL,-10
NROP,UNSYM
/OUT,SCRATCH
SOLVE
TIME,1
AUTO,ON
TINTP,,,,1.0
NSUB,100,10000,10
OUTRES,ALL,LAST
SOLVE
/OUT
FINI
/POST1
ALLSEL
PLNSOL,TEMP
/COM  *****************************************************************
/COM   TEST FROM COMP. METH. APPL. MECH. ENG. VOL.113,P301,1994
/COM   SOLUTION TEMPERATURE = 1.235
/COM  *****************************************************************
ESEL,S,ENAME,,CONTA172
NSLE
PRNSOL,TEMP
*GET,TEMP1,NODE,130,TEMP
/COM  *****************************************************************
/COM   SOLUTION TEMPERATURE = 0.309
/COM  *****************************************************************
ESEL,S,ENAME,,TARGE169
NSLE
PRNSOL,TEMP
/OUT,
*GET,TEMP2,NODE,40,TEMP
*DIM,LABEL,CHAR,2
*DIM,VALUE,,2,3
LABEL(1) = 'TEMP1','TEMP2 '
*VFILL,VALUE(1,1),DATA,1.235,0.309
*VFILL,VALUE(1,2),DATA,TEMP1,TEMP2
*VFILL,VALUE(1,3),DATA,ABS(TEMP1 / 1.235) ,ABS(TEMP2 / 0.309)
SAVE,TABLE_1
FINISH

!* SOLVE USING K(2)=3 OF CONTA172
/OUT,SCRATCH
RESUME
/PREP7
KEYOPT,3,4,2						! ON NODAL POINT - NORMAL TO TARGET SURFACE
KEYOPT,3,2,3						! PURE LAGRANGE MULTIPLIER ON CONTACT NORMAL AND PENALTY ON TANGENT
FINISH
/SOLU
NSUB,1
ASEL,S,,,2
NSLA,S,1
NSEL,R,LOC,X,1.25
D,ALL,UX,3.75
ALLSEL
ASEL,S,,,1
NSLA,S,1
D,ALL,UX,0
D,ALL,UY,0
ALLSEL
ANTYPE,TRANS
TIMINT,OFF,STRUC
TINTP,,,,1.0
NLGEOM,ON
TIME,3.75E-3
AUTO,ON
NSUB,100,10000,100
OUTRES,ALL,-10
NROP,UNSYM
/OUT,SCRATCH
SOLVE
TIME,1
AUTO,ON
TINTP,,,,1.0
NSUB,100,10000,10
OUTRES,ALL,LAST
SOLVE
/OUT
FINI
/POST1
ALLSEL
PLNSOL,TEMP
/COM  *****************************************************************
/COM   TEST FROM COMP. METH. APPL. MECH. ENG. VOL.113,P301,1994
/COM   SOLUTION TEMPERATURE = 1.235
/COM  *****************************************************************
ESEL,S,ENAME,,CONTA172
NSLE
PRNSOL,TEMP
*GET,TEMP1,NODE,130,TEMP
/COM  *****************************************************************
/COM   SOLUTION TEMPERATURE = 0.309
/COM  *****************************************************************
ESEL,S,ENAME,,TARGE169
NSLE
PRNSOL,TEMP
/OUT,
*GET,TEMP2,NODE,40,TEMP
*DIM,LABEL,CHAR,2
*DIM,VALUE,,2,3
LABEL(1) = 'TEMP1','TEMP2 '
*VFILL,VALUE(1,1),DATA,1.235,0.309
*VFILL,VALUE(1,2),DATA,TEMP1,TEMP2
*VFILL,VALUE(1,3),DATA,ABS(TEMP1 / 1.235) ,ABS(TEMP2 / 0.309)
SAVE,TABLE_2
FINI
/COM,
/COM, -------------------------
/COM,  USING PLANE 223 ELEMENT
/COM, ------------------------
/CLEAR,NOSTART
/OUT,SCRATCH
/PREP7
ET,1,PLANE223,11							! 2-D STRUCTURAL-THERMAL SOLID
KEYOPT,1,9,1
RECT,,5,,1.25
RECT,,1.25,1.25,2.5
LESIZE,ALL,0.25, , ,1,1
AMESH,ALL
UIMP,1,EX, , ,70000,
UIMP,1,DENS, , ,2.7E-9,
UIMP,1,ALPX, , ,23.86E-6,
UIMP,1,NUXY, , ,0.3,
UIMP,1,MU, , ,0.2,
UIMP,1,KXX, , ,150,
UIMP,1,C, , ,9E8,
TOFFST,460
TUNIF,0.00
ET,2,TARGE169								! 2-D TARGET SEGMENT
ET,3,CONTA172								! 2-D 3-NODE SURFACE-TO-SURFACE CONTACT
KEYOPT,3,1,1
ASEL,S,,,1
NSLA,S,1
NSEL,R,LOC,Y,1.25
R,1
TYPE,2
ESURF
ALLSEL
ASEL,S,,,2
NSLA,S,1
NSEL,R,LOC,Y,1.25
TYPE,3
ESURF
ALLSEL,ALL
*DIM,PRE,TABLE,2,1,1,TIME
SFL,7,PRES, %PRE%
PRE(1,0,1) = 0
PRE(1,1,1) = 10
PRE(2,0,1) = 10
PRE(2,1,1) = 10
ALLSEL
SAVE
FINISH

/SOLU
NSUB,1
ASEL,S,,,2
NSLA,S,1
NSEL,R,LOC,X,1.25
D,ALL,UX,3.75
ALLSEL
ASEL,S,,,1
NSLA,S,1
D,ALL,UX,0
D,ALL,UY,0
ALLSEL
ANTYPE,TRANS
TIMINT,OFF,STRUC
TINTP,,,,1.0
NLGEOM,ON
TIME,3.75E-3
AUTO,ON
NSUB,100,10000,100
OUTRES,ALL,-10
/OUT,SCRATCH
SOLVE
TIME,1
AUTO,ON
TINTP,,,,1.0
NSUB,100,10000,10
OUTRES,ALL,LAST
SOLVE
/OUT
FINI
/POST1
ALLSEL
PLNSOL,TEMP
/COM  *****************************************************************
/COM   TEST FROM COMP. METH. APPL. MECH. ENG. VOL.113,P301,1994
/COM   SOLUTION TEMPERATURE = 1.235
/COM  *****************************************************************
ESEL,S,ENAME,,CONTA172
NSLE
PRNSOL,TEMP
*GET,TEMP1,NODE,NODE(0.5,1.25,0),TEMP
/COM  *****************************************************************
/COM   SOLUTION TEMPERATURE = 0.309
/COM  *****************************************************************
ESEL,S,ENAME,,TARGE169
NSLE
PRNSOL,TEMP
/OUT,
*GET,TEMP2,NODE,NODE(1.75,1.25,0),TEMP
*DIM,LABEL,CHAR,2
*DIM,VALUE,,2,3
LABEL(1) = 'TEMP1','TEMP2 '
*VFILL,VALUE(1,1),DATA,1.235,0.309
*VFILL,VALUE(1,2),DATA,TEMP1,TEMP2
*VFILL,VALUE(1,3),DATA,ABS(TEMP1 / 1.235) ,ABS(TEMP2 / 0.309)
SAVE,TABLE_3
/COM
RESUME,TABLE_1
/OUT,vm229,vrt
/COM
/COM,------------------- VM229 RESULTS COMPARISON ---------------------
/COM,
/COM,        |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
/COM, ------------
/COM,   PLANE13
/COM, ------------
/COM,
*VWRITE,LABEL(1),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,'  ',F7.4,'      ',F14.4,'     ',1F15.3)
/COM,
/COM,
/OUT,
RESUME,TABLE_2
/OUT,vm229,vrt,,APPEND
/COM,
/COM, ----------------------------------
/COM,  RESULTS USING K(2)=3 OF CONTA172
/COM, ----------------------------------
/COM,
*VWRITE,LABEL(1),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,'  ',F7.4,'      ',F14.4,'     ',1F15.3)
/COM,
/COM,
/OUT,
RESUME,TABLE_3
/OUT,vm229,vrt,,APPEND
/COM,
/COM, ------------
/COM,   PLANE223
/COM, ------------
/COM,
*VWRITE,LABEL(1),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,'  ',F7.4,'      ',F14.4,'     ',1F15.3)
/COM,
/COM,
/COM,----------------------------------------------------------------
FINISH
/OUT,
*LIST,vm229,vrt
FINI


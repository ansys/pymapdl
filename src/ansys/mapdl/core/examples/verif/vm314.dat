! Copyright (C) 2024 ANSYS, Inc. and/or its affiliates.
! SPDX-License-Identifier: MIT
! 
!
! Permission is hereby granted, free of charge, to any person obtaining a copy
! of this software and associated documentation files (the "Software"), to deal
! in the Software without restriction, including without limitation the rights
! to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
! copies of the Software, and to permit persons to whom the Software is
! furnished to do so, subject to the following conditions:
!
! The above copyright notice and this permission notice shall be included in all
! copies or substantial portions of the Software.
!
! THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
! IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
! FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
! AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
! LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
! OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
! SOFTWARE.

/COM,ANSYS MEDIA REL. 2023R2 (05/12/2023) REF. VERIF. MANUAL: REL. 2023R2
/VERIFY,VM314
/TITLE,VM314, STRESSES OF UHMWPE UNDER UNIAXIAL TENSION
/COM,   REF: JS BERGSTROM, JE BISCHOFF
/COM,   "AN ADVANCED THERMOMECHANICAL CONSTITUTIVE MODEL FOR UHMWPE""
/COM,   INTERNATIONAL JOURNAL OF STRUCTURAL CHANGES IN SOLIDS, VOL.2, 2010
/COM,
/PREP7
/OUT,vm314_SCRATCH
ET,1,SOLID185                        !* 3D 8-NODE STRUCTURAL SOLID ELEMENT

/COM, DEFINING THREE NETWORK MODEL MATERIAL PROPERTIES
MUA                    = 200      !MPA
THETAHAT               = 300      !K
LAMBDAL                = 3.25
KAPPA                  = 6000     !MPA
TAUHATA                = 3.25     !MPA
A                      = 0.073
MA                     = 20
N                      = 0.0
MUBI                   = 293      !MPA
MUBF                   = 79.1     !MPA
BETA                   = 31.9
TAUHATB                = 20.1     !MPA
MB                     = 20
MUC                    = 10.0     !MPA
Q                      = 0.23
THETA0                 = 300      !K

K = 6000               
MU = 250

YOUNG = 9.d0*K*MU/(3.d0*K+MU)
POSN  = (YOUNG-2.d0*MU)/(2.d0*MU)

TB, ELAS, 1,,,ISOT
TBDATA, 1, YOUNG, POSN

TB,TNM,1, , ,NETA
TBDATA,1,MUA         ! TBDATA, 1
TBDATA,2,TAUHATA     ! TBDATA, 5
TBDATA,3,MA          ! TBDATA, 7

TB,TNM,1, , ,NETB
TBDATA,1,MUBI        ! TBDATA, 9
TBDATA,2,MUBF        ! TBDATA, 10
TBDATA,3,BETA        ! TBDATA, 11
TBDATA,4,TAUHATB     ! TBDATA, 12
TBDATA,5,MB          ! TBDATA, 13

TB,TNM,1, , ,NETC
TBDATA,1,MUC         ! TBDATA, 14
TBDATA,2,Q           ! TBDATA, 15

TB,TNM,1, , ,FLOW
TBDATA,1,A           ! TBDATA, 6     
TBDATA,2,N           ! TBDATA, 8        

TB,TNM,1, , ,TDEP
TBDATA,1,THETAHAT    ! TBDATA, 2

TB,TNM,1, , ,LOCK
TBDATA,1,LAMBDAL     ! TBDATA, 3

TB,TNM,1, , ,BULK
TBDATA,1,KAPPA       ! TBDATA, 4

MP,REFT,1,THETA0     ! TBDATA, 17
TOFFS, 0

BLOCK,0.00,100.00,0.00,100.00,0.00,100.00
ESIZE,100
TYPE,1
MAT,1
VMESH,1

NSEL,S,LOC,X
D,ALL,UX
NSEL,S,LOC,Y
D,ALL,UY
NSEL,S,LOC,Z
D,ALL,UZ
NSEL,ALL
BFUNIF,TEMP,300
FINISH
/SOLU
TIME, 372                 ! ENG STRAIN RATE =0.007/S
NLGEOM, ON
OUTRES,ALL,LAST
NSUBST,50,50,50
TIME,1
NSEL,S,LOC,Y,100
D,ALL,UY,260               ! TENSION LOADING
ALLSEL
/OUT,SCRATCH

SOLVE
FINISH

/POST1
SET,1,LAST
/OUT,
*GET, S_Y, NODE, NODE(1,1,0),S,Y
*GET,EPEL_Y, NODE, NODE(1,1,0),EPEL, Y
/COM, REFERENCE RESULTS:
SY_REF=174
RATIO=S_Y/SY_REF
/COM
/OUT,vm314,vrt
/COM,
/COM,-------------------- VM314 RESULTS COMPARISON -------------------------
/COM,
/COM,    |   TARGET   | Mechanical APDL |  RATIO
/COM,
*VWRITE,SY_REF,S_Y,RATIO
(8X,F8.3,'    ',F8.3,'         ',F5.3)
/COM,-----------------------------------------------------------------------
/OUT
*LIST,vm314,vrt
FINISH

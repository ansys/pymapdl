! Copyright (C) 2024 ANSYS, Inc. and/or its affiliates.
! SPDX-License-Identifier: MIT
! 
!
! Permission is hereby granted, free of charge, to any person obtaining a copy
! of this software and associated documentation files (the "Software"), to deal
! in the Software without restriction, including without limitation the rights
! to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
! copies of the Software, and to permit persons to whom the Software is
! furnished to do so, subject to the following conditions:
!
! The above copyright notice and this permission notice shall be included in all
! copies or substantial portions of the Software.
!
! THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
! IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
! FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
! AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
! LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
! OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
! SOFTWARE.

/COM,ANSYS MEDIA REL. 2023R2 (05/12/2023) REF. VERIF. MANUAL: REL. 2023R2
/VERIFY,VMR049-PL1A-182
/TITLE, VMR049-PL1A-182, 2D PLANE STRAIN PLASTICITY BENCHMARK
/COM, REFERENCE: NAFEMS REPORT-R0049
/SHOW,JPEG
JPGPRF,500,100,1

/PREP7
R = 2.5E-5
ET,1,182,,,
KEYOPT,1,3,2
N,1,,,,
N,2,0,1,,
N,3,1,0,,
N,4,1,1,,
E, 1,3,4,2
MP,EX,1,250E3,
MP,NUXY,1,0.25,
TB,PLAS,1,,,BISO
TBDATA,1,5, 0.0
TB,HILL,1
TBDATA,1,1.0,1.0,1.0,1.0,1.0,1.0
NSEL,S,LOC,X
D,ALL,UX
NSEL,S,LOC,Y
D,ALL,UY
NSEL,S,LOC,Y,1.0
D,ALL,UY
FINISH

/SOLU
NLGEOM,ON
NSEL,S,LOC,X,1.0
D,ALL,UX,R
NSEL,ALL
NSUBST,10,10,10,
OUTRES,ALL,5
/OUT,SCRATCH
SOLVE
NSEL,S,LOC,X,1.0
D,ALL,UX,2*R
NSEL,ALL
NSUBST,10,10,10,
OUTRES,ALL,5
SOLVE
NSEL,S,LOC,Y,1.0
D,ALL,UY,R
NSEL,ALL
NSUBST,10,10,10,
OUTRES,ALL,5
SOLVE
NSEL,S,LOC,Y,1.0
D,ALL,UY,2*R
NSEL,ALL
NSUBST,10,10,10,
OUTRES,ALL,5
SOLVE
NSEL,S,LOC,X,1.0
D,ALL,UX,R
NSEL,ALL
NSUBST,10,10,10,
OUTRES,ALL,5
SOLVE
NSEL,S,LOC,X,1.0
D,ALL,UX,0.0
NSEL,ALL
NSUBST,10,10,10,
OUTRES,ALL,5
SOLVE
NSEL,S,LOC,Y,1.0
D,ALL,UY,R
NSEL,ALL
NSUBST,10,10,10,
OUTRES,ALL,5
SOLVE
NSEL,S,LOC,Y,1.0
D,ALL,UY,0.0
NSEL,ALL
NSUBST,10,10,10,
OUTRES,ALL,5
SOLVE
FINISH

/POST26
/GROPT,VIEW,0
/GTHK,CURVE,1
/GROPT,FILL,OFF
/GRID,1
/GTHK,GRID,1
/GROPT,CGRID,1
/AXLAB,X,STEP
/AXLAB,Y,STRESS
/GTHK,AXIS,1
/GRTYP,0
/XRANGE,0,8
/YRANGE,-10,25
/OUT,
ESOL,2,1,4,S,X,
ESOL,3,1,4,S,Y,
ESOL,4,1,4,S,Z,
ESOL,5,1,4,S,EQV,
PLVAR,2,3,4,5
PRVAR,2,3,4,5

*DIM,VALUEX,ARRAY,8,1
*DO,JJ,1,8,1
*GET,VALUEX(JJ,1),VARI,2,RTIME,JJ
*ENDDO
R1=VALUEX(1,1)/7.500
R2=VALUEX(2,1)/11.666
R3=VALUEX(3,1)/14.166
R4=VALUEX(4,1)/16.418
R5=VALUEX(5,1)/9.927
R6=VALUEX(6,1)/5.134
R7=VALUEX(7,1)/2.635
R8=VALUEX(8,1)/1.218

*DIM,VALUEY,ARRAY,8,1
*DO,JJ,1,8,1
*GET,VALUEY(JJ,1),VARI,3,RTIME,JJ
*ENDDO
R9=VALUEY(1,1)/2.500
R10=VALUEY(2,1)/6.666
R11=VALUEY(3,1)/14.166
R12=VALUEY(4,1)/19.669
R13=VALUEY(5,1)/15.622
R14=VALUEY(6,1)/10.745
R15=VALUEY(7,1)/3.245
R16=VALUEY(8,1)/(-3.715)

*DIM,VALUEZ,ARRAY,8,1
*DO,JJ,1,8,1
*GET,VALUEZ(JJ,1),VARI,4,RTIME,JJ
*ENDDO
R17=VALUEZ(1,1)/2.500
R18=VALUEZ(2,1)/6.666
R19=VALUEZ(3,1)/9.166
R20=VALUEZ(4,1)/13.912
R21=VALUEZ(5,1)/11.951
R22=VALUEZ(6,1)/9.120
R23=VALUEZ(7,1)/6.620
R24=VALUEZ(8,1)/3.521

*DIM,VALUEEF,ARRAY,8,1
*DO,JJ,1,8,1
*GET,VALUEEF(JJ,1),VARI,5,RTIME,JJ
*ENDDO
R25=VALUEEF(1,1)/5.000
R26=VALUEEF(2,1)/5.000
R27=VALUEEF(3,1)/5.000
R28=VALUEEF(4,1)/5.000
R29=VALUEEF(5,1)/5.000
R30=VALUEEF(6,1)/5.000
R31=VALUEEF(7,1)/3.719
R32=VALUEEF(8,1)/5.000

*DIM,STEP,CHAR,10
*DIM,TARGETX,CHAR,10
*DIM,TARGETY,CHAR,10
*DIM,TARGETZ,CHAR,10
*DIM,TARGETEF,CHAR,10
*DIM,RATIOX,,8,1
*DIM,RATIOY,,8,1
*DIM,RATIOZ,,8,1
*DIM,RATIOEF,,8,1
STEP(1)='1.0','2.0','3.0','4.0','5.0','6.0','7.0','8.0'
TARGETX(1)='7.500','11.666','14.166','16.418','9.927','5.134','2.635','1.218'
TARGETY(1)='2.500','6.666','14.166','19.669','15.622','10.745','3.245','-3.715'
TARGETZ(1)='2.500','6.666','9.166','13.914','11.951','9.120','6.620','3.521'
TARGETEF(1)='5.000','5.000','5.000','5.000','5.000','5.000','3.719','5.000'
*VFILL,RATIOX,DATA,R1,R2,R3,R4,R5,R6,R7,R8
*VFILL,RATIOY,DATA,R9,R10,R11,R12,R13,R14,R15,R16
*VFILL,RATIOZ,DATA,R17,R18,R19,R20,R21,R22,R23,R24
*VFILL,RATIOEF,DATA,R25,R26,R27,R28,R29,R30,R31,R32
/COM,
/COM, ----------- VMR049-PL1A-182 RESULTS COMPARISON --------------
/COM,
/COM, vmr049-pl1a-182.jpeg RESULTS SHOULD MATCH R0049 NAFEMS MANUAL
/COM, GRAPH RESULTS ON PAGE 49, FIGURE 2.14(A).  THE RESULTS
/COM, DISPLAYED ARE INCREMENTED FOR THIS PURPOSE.
/COM,
/COM,
/COM, ------ VMR049-PL1A-182 STRESS RESULTS IN X DIRECTION -------
/COM,
/COM, |  STEP  |  TARGET  |  Mechanical APDL  |  RATIO
/COM,
*VWRITE,STEP(1),TARGETX(1),VALUEX(1,1),RATIOX(1,1)
(1X,A8,'   ',1x,A8,'   ',F14.3,'   ',F12.3,'   ')
/COM,
/COM, ------ VMR049-PL1A-182 STRESS RESULTS IN Y DIRECTION -------
/COM,
/COM, |  STEP  |  TARGET  |  Mechanical APDL  |  RATIO
/COM,
*VWRITE,STEP(1),TARGETY(1),VALUEY(1,1),RATIOY(1,1)
(1X,A8,'   ',1x,A8,'   ',F14.3,'   ',F12.3,'   ')
/COM,
/COM,
/COM, ------ VMR049-PL1A-182 STRESS RESULTS IN Z DIRECTION -------
/COM,
/COM, |  STEP  |  TARGET  |  Mechanical APDL  |  RATIO
/COM,
*VWRITE,STEP(1),TARGETZ(1),VALUEZ(1,1),RATIOZ(1,1)
(1X,A8,'   ',1x,A8,'   ',F14.3,'   ',F12.3,'   ')
/COM,
/COM,
/COM, ------ VMR049-PL1A-182 EFFECTIVE STRESS RESULTS -------
/COM,
/COM, |  STEP  |  TARGET  |  Mechanical APDL  |  RATIO
/COM,
*VWRITE,STEP(1),TARGETEF(1),VALUEEF(1,1),RATIOEF(1,1)
(1X,A8,'   ',1x,A8,'   ',F14.3,'   ',F12.3,'   ')
/COM,
/COM,
FINISH

/POST26
*DIM,LABEL1,CHAR,3
*DIM,VALUE1,,3,3
LABEL1(1) = ' SX ',' SY ',' SZ '
*VFILL,VALUE1(1,1),DATA,VALUEX(6,1),VALUEY(6,1),VALUEZ(6,1)
*VFILL,VALUE1(1,2),DATA,R6,R14,R22
*DIM,LABEL2,CHAR,2
LABEL2(1) = 'vmr049-','pl1a-182'

/OUT,vmr049-pl1a-182,vrt
/COM
/COM,------------------- vmr049-pl1a RESULTS COMPARISON ---------------------
/COM,
/COM,            |   Mechanical APDL   |   RATIO  |         INPUT         |
/COM,
/COM, PLANE182
/COM, RESULTS LISTED USING LOAD STEP 6
*VWRITE,LABEL1(1),VALUE1(1,1),VALUE1(1,2),LABEL2(1),LABEL2(2)
(1X,A8,'      ',F14.4,'      ',F9.4,'     ',A7,A8)
*VWRITE,LABEL1(2),VALUE1(2,1),VALUE1(2,2),LABEL2(1),LABEL2(2)
(1X,A8,'      ',F14.4,'      ',F9.4,'     ',A7,A8)
*VWRITE,LABEL1(3),VALUE1(3,1),VALUE1(3,2),LABEL2(1),LABEL2(2)
(1X,A8,'      ',F14.4,'      ',F9.4,'     ',A7,A8)
/COM,
/COM,----------------------------------------------------------------
/OUT

FINISH
*LIST,vmr049-pl1a-182,vrt

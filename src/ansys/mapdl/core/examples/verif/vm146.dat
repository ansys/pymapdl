/COM,ANSYS MEDIA REL. 150 (11/8/2013) REF. VERIF. MANUAL: REL. 150
/VERIFY,VM146
/PREP7
/TITLE, VM146, BENDING OF A REINFORCED CONCRETE BEAM
C***   STR. OF MATL., TIMOSHENKO, PART 1, 3RD ED., PAGE 221
 
ANTYPE,STATIC
ET,1,SOLID65,,,,,2           ! REINFORCED CONCRETE SOLID ELEMENT
ET,2,LINK180                 ! STEEL RODS
ET,3,PIPE288		     ! DUMMY ELEMENTS FOR CONSTRAINT EQUATIONS
KEYOPT,3,3,3       	     ! CUBIC SHAPE FUNCTION
KEYOPT,3,4,2		     ! THICK PIPE THEORY
R,1
SECTYPE,2,LINK   
SECDATA,0.15		     	     ! HALF AREA OF ROD             
SECTYPE,3,PIPE
SECDATA,1,0.5,8
MP,EX,1,2E6                  ! CONCRETE PROPERTIES
MP,NUXY,1,0
TB,CONCR,1
TBDATA,3,0.0,-1              ! ZERO TENSILE CRACKING STRENGTH
                             ! REMOVE CRUSHING CAPABILITY
MP,EX,2,30E6                 ! STEEL PROPERTIES
MP,NUXY,2,0.3
N,1
N,2,1.5
NGEN,5,2,1,2,1,,1.5
NGEN,2,10,1,10,1,,,5
E,7,8,10,9,17,18,20,19
TYPE,3                       ! DEFINE DUMMY ELEMENTS FOR ROTZ DOF
SECNUM,3
E,10,8
E,20,18
EGEN,4,-2,1,3

TYPE,2
MAT,2                        ! REINFORCING RODS AT THE BOTTOM SURFACE
SECNUM,2
E,1,2
E,11,12
CE,1,, 2,UX,-1, 6,UX,1, 6,ROTZ,3        ! CONSTRAINT EQUATION TO ENSURE
CE,2,,12,UX,-1,16,UX,1,16,ROTZ,3        !  PLANE SECTION REMAINS PLANE
CE,3,, 4,UX,-1, 6,UX,1, 6,ROTZ,1.5
CE,4,,14,UX,-1,16,UX,1,16,ROTZ,1.5
CE,5,, 8,UX,-1, 6,UX,1, 6,ROTZ,-1.5
CE,6,,18,UX,-1,16,UX,1,16,ROTZ,-1.5
CE,7,,10,UX,-1, 6,UX,1, 6,ROTZ,-3
CE,8,,20,UX,-1,16,UX,1,16,ROTZ,-3
NSEL,S,LOC,X
D,ALL,ALL                               ! FIX NODES IN Y-Z PLANE
NSEL,ALL
D,ALL,ROTY                              ! CONSTRAIN UNNEEDED PIPE ROTATIONS
F,6,MZ,300,,16,10                       ! APPLY BENDING MOMENT
FINISH
/SOLU    
AUTOTS,ON
NSUBST,5
OUTPR,,LAST
/OUT,SCRATCH
SOLVE
/POST1
/OUT,
ESEL,S,ELEM,,1,1
*GET,SCON,NODE,9,S,X
ESEL,S,ELEM,,13,13
ETAB,ST,LS,1
ESORT,ST
*GET,STL,SORT,,MAX
*status,parm
*DIM,LABEL,CHAR,2,2
*DIM,VALUE,,2,3
LABEL(1,1) = 'TSTR STL ','TSTR CON'
LABEL(1,2) = ' psi',' psi'
*VFILL,VALUE(1,1),DATA,387.28,-18.54
*VFILL,VALUE(1,2),DATA,STL,SCON
*VFILL,VALUE(1,3),DATA,ABS(STL/387.28),ABS(SCON/18.54)
SAVE,TABLE_1
FINI
/CLEAR,NOSTART

/NOPR
/COM
/OUT,vm146,vrt
/COM,------------------- VM146 RESULTS COMPARISON --------------
/COM,
/COM,                 |     TARGET   |     Mechanical APDL   |   RATIO
/COM,
RESUME,TABLE_1
/COM,
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F12.2,'  ',F14.2,'   ',1F15.3)
/COM,
/COM,
/COM,-----------------------------------------------------------


/COM,NOTE: THERE ARE VERIFIED RESULTS IN VM146 NOT CONTAINED IN
/COM,THIS TABLE
/COM,-----------------------------------------------------------
/OUT
FINISH
*LIST,vm146,vrt




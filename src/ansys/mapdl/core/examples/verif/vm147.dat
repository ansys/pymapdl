! Copyright (C) 2024 ANSYS, Inc. and/or its affiliates.
! SPDX-License-Identifier: MIT
! 
!
! Permission is hereby granted, free of charge, to any person obtaining a copy
! of this software and associated documentation files (the "Software"), to deal
! in the Software without restriction, including without limitation the rights
! to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
! copies of the Software, and to permit persons to whom the Software is
! furnished to do so, subject to the following conditions:
!
! The above copyright notice and this permission notice shall be included in all
! copies or substantial portions of the Software.
!
! THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
! IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
! FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
! AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
! LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
! OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
! SOFTWARE.

/COM,ANSYS MEDIA REL. 2023R2 (05/12/2023) REF. VERIF. MANUAL: REL. 2023R2
/VERIFY,VM147
/PREP7
/TITLE, VM147, GRAY-BODY RADIATION WITHIN A FRUSTRUM OF A CONE
!       REF:  SIEGEL, R., HOWELL J.R., "THERMAL RADIATION HEAT TRANSFER"
!             2ND EDITION, HEMISPHERE PUBLISHING CORPORATION, 1981.
ET,1,LINK33           ! HEAT CONDUCTING BAR
N,1                   
N,2,0.075
N,3,0.075
N,4,0.05,0.075
N,5,0.05,0.075
N,6,0,0.075
MAT,1                 ! SURFACE 1 (LOWER SURFACE)
E,1,2
MAT,2                 ! SURFACE 2 (INSULATED OUTSIDE SURFACE)
E,3,4
MAT,3                 ! SURFACE 3 (TOP SURFACE)
E,5,6
FINISH
/AUX12
EMIS,1,0.6
EMIS,2,0.8
EMIS,3,0.5
VTYPE,1               ! NON-HIDDEN (FAST) METHOD
GEOM,1,50             ! 2-D AXISYMMETRIC GEOM WITH 50 FACETS 
MPRINT,1
STEF,5.6696E-8        ! STEFAN-BOLTZMANN CONSTANT IN MKS UNITS
WRITE,CONE        ! WRITE RADIATION SUBSTRUCTURE MATRIX
FINISH
/CLEAR,NOSTART        ! CLEAR DATABASE; DO NOT READ START.ANS FILE
/PREP7
ET,1,SURF151,,,1,1    ! 2-D AXISYMMETRIC THERMAL SURFACE EFFECT ELEMENT 
KEYOPT,1,8,1          !  WITH HEAT FLUX LOADS
ET,2,MATRIX50,1       ! RADIATION SUBSTRUCTURE MATRIX (SUPERELEMENT)
N,1
N,2,0.075
E,2,1                 ! LOWER SURFACE FOR HEAT FLUX
TYPE,2
SE,CONE,SUB           ! READ IN RADIATION SUPERELEMENT
FINISH
/SOLU
ANTYPE,STATIC         ! STEADY-STATE THERMAL ANALYSIS
SFE,1,1,HFLUX,,6000   ! APPLY HEAT FLUX LOAD ON SURFACE EFFECT ELEMENT
D,5,TEMP,550,,6       
TUNIF,500             ! STARTING UNIFORM TEMPERATURE FOR NONLINEAR SOLUTION
SOLVE
FINISH
/POST1
NSEL,S,NODE,,1,2      ! SELECT LOWER SURFACE NODES
PRNSOL,TEMP           ! LIST TEMPERATURES
*GET,T1,NODE,1,TEMP
*status,parm
*DIM,LABEL,CHAR,1,2
*DIM,VALUE,,1,3
LABEL(1,1) = 'TEMP '
LABEL(1,2) = ' K'
*VFILL,VALUE(1,1),DATA,904
*VFILL,VALUE(1,2),DATA,T1
*VFILL,VALUE(1,3),DATA,ABS(T1/904)
/COM
/OUT,vm147,vrt
/COM,------------------- VM147 RESULTS COMPARISON --------------
/COM,
/COM,                 |     TARGET   |     Mechanical APDL   |   RATIO
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F12.0,'  ',F14.0,'   ',1F15.3)
/COM,-----------------------------------------------------------
/OUT
FINISH
*LIST,vm147,vrt


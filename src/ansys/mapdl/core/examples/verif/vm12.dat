/COM,ANSYS MEDIA REL. 150 (11/8/2013) REF. VERIF. MANUAL: REL. 150
/VERIFY,VM12
/PREP7
/SHOW,JPEG
/TITLE, VM12, COMBINED BENDING AND TORSION
C***      USING PIPE16     STR. OF MATL., TIMOSHENKO, PART 1, 3RD ED., PAGE 299, PROB. 2

ANTYPE,STATIC
ET,1,PIPE16
R,1,4.67017,2.33508		! REAL CONSTANTS FOR SOLID CROSS SECTION
MP,EX,1,30E6
MP,NUXY,1,.3
N,1
N,2,,,300
E,1,2
D,1,ALL
F,2,MZ,9000
F,2,FX,-250
FINISH
/SOLU    
OUTPR,BASIC,1
SOLVE
FINISH
/POST1
ETABLE,P_STRS,NMISC,86
ETABLE,SHR,NMISC,88
*GET,P_STRESS,ELEM,1,ETAB,P_STRS
*GET,SHEAR,ELEM,1,ETAB,SHR

*DIM,LABEL,CHAR,2,2
*DIM,VALUE,,2,3
LABEL(1,1) = 'MAX PRIN','MAX SH S'
LABEL(1,2) = 'STRS psi','TRS psi '
*VFILL,VALUE(1,1),DATA,7527,3777
*VFILL,VALUE(1,2),DATA,P_STRESS,(SHEAR/2)
*VFILL,VALUE(1,3),DATA,ABS(P_STRESS/7527 ) ,ABS((SHEAR/2)/3777 )
SAVE,TABLE_1
FINI
/CLEAR,NOSTART


!*********************************************************************


/PREP7
/SHOW,JPEG
C***     USING PIPE288       

ANTYPE,STATIC
ET,1,PIPE288
SECTYPE,1,PIPE
SECDATA,4.67017,2.33508
KEYOPT,1,3,3			!CUBIC SHAPE FUNCTION
MP,EX,1,30E6
MP,NUXY,1,.3
N,1
N,2,,,300
E,1,2
D,1,ALL
F,2,MZ,9000
F,2,FX,-250
FINISH
/SOLU    
OUTPR,BASIC,1
SOLVE
FINISH
/POST1
SET,LAST
/GRAPHICS,POWER
/ESHAPE,1
/VIEW,1,1,1,1
PLESOL,S,1
*GET,P_STRESS,PLNSOL,0,MAX
PLESOL,S,INT
*GET,SHEAR,PLNSOL,0,MAX

*DIM,LABEL,CHAR,2,2
*DIM,VALUE,,2,3
LABEL(1,1) = 'MAX PRIN','MAX SH S'
LABEL(1,2) = 'STRS psi','TRS psi '
*VFILL,VALUE(1,1),DATA,7527,3777
*VFILL,VALUE(1,2),DATA,P_STRESS,(SHEAR/2)
*VFILL,VALUE(1,3),DATA,ABS(P_STRESS/7527 ) ,ABS((SHEAR/2)/3777 )
SAVE,TABLE_2
FINI
/CLEAR,NOSTART


/NOPR
/COM
/OUT,vm12,vrt
/COM,------------------- VM12 RESULTS COMPARISON ---------------------
/COM,
/COM,                 |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
RESUME,TABLE_1
/COM,USING PIPE16
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.0,'  ',F12.0,'   ',1F15.3)
/COM,
RESUME,TABLE_2
/COM,USING PIPE288
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.0,'  ',F12.0,'   ',1F15.3)
/COM,
/COM,-----------------------------------------------------------------

/OUT
FINISH
*LIST,vm12,vrt


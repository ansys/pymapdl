/COM,ANSYS MEDIA REL. 2023R1 (11/04/2022) REF. VERIF. MANUAL: REL. 2023R1
/VERIFY,VM199
/TITLE,VM199,OIL FILM BEARING SUPPORTING A ROTATING SHAFT AND SUBJECTED TO A STATIC LOAD
/COM
/COM  REFERENCE: MICHAEL I.FRISWELL, JOHN E.T.PENNY, SEAMUS D.GARVEY,
/COM             ARTHUR W.LESS, " DYNAMICS OF ROTATING MACHINES" CAMBRIDGE
/COM             UNIVERSITY PRESS, 2010, PG 181

/PREP7

/COM,
/COM,  TRANSIENT ANALYIS IS PERFORMED TO DETERMINE THE
/COM,  EQUILIBRIUM POSITION OF THE SHAFT
/COM,

PI = 4*ATAN(1)
MASS = 53.51682             ! TO APPLY STATIC LOAD OF 525 N, F=M*G
XCLEAR = 1.0E-4             ! RADIAL CLEARANCE OF BEARING IN m
LENGTH = 0.03               ! LENGTH OF THE BEARING IN m
RADIUS = 0.05               ! RADIUS OF THE ROTOR IN m
MU = 0.1                    ! VISCOSITY OF OIL IN PA S
OMEGAREV = 1500             ! VELOCITY OF SHAFT IN REV/MIN
OMEGAJ = OMEGAREV*PI/30     ! VELOCITY OF SHAFT IN RAD/SEC

NBCYC = 5                   ! NUMBER OF CYCLES FOR TRANSIENT ANALYSIS
TEND = NBCYC*2*PI/OMEGAJ    ! END TIME
DT = 1E-4                   ! TIME INCREMENT

/COM,  NODES

N,1,-1,0,0
N,2,0,0,0
N,3,0,-1,0

ET,1,MASS21                 ! 3D MASS ELEMENT WITH ROTARY INERTIA
R,1,MASS,MASS,MASS          ! MASS OF THE SHAFT

ET,2,COMBI214               ! 2-D SPRING DAMPER ELEMENT
KEYOPT,2,1,2                ! CYLINDRICAL JOURNAL BEARING, INTEGRATION OF
                            ! REYNOLDS EQUATION

R,2,XCLEAR,LENGTH,RADIUS    ! BEARING PARAMETERS

MP,VISC,2,MU                ! VISCOSITY OF OIL

TYPE,1
REAL,1
E,2                         ! SHAFT

TYPE,2
REAL,2
E,1,2,3                     ! BEARING

D,ALL,ALL,0
DDELE,2,UX                  ! CONSTRAIN ALL DOF EXCEPT UX AND UY @ NODE 2
DDELE,2,UY
FINISH

/SOLUTION
ANTYPE,TRANSIENT            ! TRANSIENT ANALYSIS
NLGEOM,ON                   ! NON-LINEAR ANALYSIS
ACEL,0,9.81,0               ! ACCELERATION ALONG GLOBAL Y DIRECTION TO APPLY
                            ! LOAD F=525 N
D,2,OMGZ,OMEGAJ             ! VELOCITY OF THE SHAFT
TIME,TEND                   ! END TIME
DELTIM,DT                   ! TIME INCREMENT
OUTRES,ALL,ALL
/OUT,SCRATCH
SOLVE
FINISH

/POST26
NUMVAR,20
NSOL,2,2,U,X,UX2            ! UX DISPLACEMENT AT NODE 2
NSOL,3,2,U,Y,UY2            ! UY DISPLACEMENT AT NODE 2

ESOL,6,2,2,SMISC,1,FX       ! PRESSURE FORCE ALONG X
ESOL,7,2,2,SMISC,2,FY       ! PRESSURE FORCE ALONG Y

ESOL,8,2,2,NMISC,3,MOFP     ! MAXIMUM FLUID PRESSURE

PROD,10,6,6,,FX_2
PROD,11,7,7,,FY_2
ADD,12,10,11,,FX_2+FY_2
SQRT,13,12,,,FAMPL          ! FORCE AMPLITUDE

PROD,15,2,2,,UX2_2
PROD,16,3,3,,UY2_2
ADD,17,15,16,,UX2_2+UY2_2
SQRT,18,17,,ECCE            ! ECCENTRICITY


PRVAR,2,3
PRVAR,6,7,8

/OUT,
EXTREM

*GET,UXEQ,VARI,2,REAL,0.20
*GET,UYEQ,VARI,3,REAL,0.20
*GET,ECCEQ,VARI,18,REAL,0.20

/OUT,SCRATCH

/SHOW,PNG,REV
XVAR,2
/AXLAB,X,UX@node2
/AXLAB,Y,UY@node2
PLVAR,3                     ! SHAFT CENTER ORBIT
/RESET
XVAR,0
PLVAR,6,7                   ! BEARING FORCES
PLVAR,8                     ! MAXIMUM PRESSURE

/SHOW,CLOSE
PRVAR,2,3,18

PARSAV,ALL,VM199_C214,PARM
FINISH
/CLEAR,NOSTART
/TITLE,VM199,OIL FILM BEARING SUPPORTING A ROTATING SHAFT AND SUBJECTED TO A STATIC LOAD
/OUT,
/COM,
/COM,  PERFORMING STATIC SOLVE WITH PERTURBATION INCREMENT
/COM,  TO DETERMINE BEARING STIFFNESS AND DAMPING

PARRES,CHANGE,VM199_C214,PARM

PERTINC = 1E-5              ! PERTURBATION INCREMENT

UX1 = UXEQ                  ! EQULIBRIUM POSITION FROM PREVIOUS ANALYSIS
UY1 = UYEQ
VELOC1 = 0                  ! VELOCITY ALONG X DIRECTION
VELOC2 = 0                  ! VELOCITY ALONG Y DIRECTION

/PREP7

/COM, DEFINING NODES

N,1,-1,0,0
N,2,0,0,0
N,3,0,-1,0

ET,1,MASS21,,,2             ! 3D MASS WITHOUT ROTARY INERTIA
R,1,MASS                    ! MASS OF THE SHAFT- UNUSED

ET,2,COMBI214               ! 2-D SPRING DAMPER ELEMENT
KEYOPT,2,1,2                ! CYLINDRICAL JOURNAL BEARING, INTEGRATION OF
                            ! REYNOLDS EQUATION

R,2,XCLEAR,LENGTH,RADIUS,VELOC1,VELOC2,PERTINC

MP,VISC,2,MU

TYPE,1
REAL,1
E,2                         ! SHAFT

TYPE,2
REAL,2
E,1,2,3                     ! BEARING

D,ALL,ALL,0
DDELE,2,UX                  ! CONSTRAIN ALL DOF EXCEPT UX AND UY @ NODE 2
DDELE,2,UY
FINISH

/SOLUTION
ANTYPE,STATIC               ! STATIC ANALYSIS
OMEGA,,,OMEGAJ              ! VELOCITY OF THE SHAFT IN RAD/SEC
D,2,UX,UX1                  ! DISPLACEMENT ALONG X
D,2,UY,UY1                  ! DISPLACEMENT ALONG Y
OUTRES,ALL,ALL
SOLVE
FINISH

/POST1
SET,LAST

/OUT,SCRATCH
ESEL,S,ELEM,,2

ETABLE,KXX,NMISC,7
ETABLE,KYY,NMISC,8
ETABLE,KXY,NMISC,9
ETABLE,KYX,NMISC,10

PRETAB,KXX,KYY,KXY,KYX      ! BEARING STIFFNESS IN N/m

*GET,KXXA,ETAB,1,ELEM,2
*GET,KYYA,ETAB,2,ELEM,2
*GET,KXYA,ETAB,3,ELEM,2
*GET,KYXA,ETABM,4,ELEM,2


ETABLE,CXX,NMISC,11
ETABLE,CYY,NMISC,12
ETABLE,CXY,NMISC,13
ETABLE,CYX,NMISC,14

PRETAB,CXX,CYY,CXY,CYX      ! BEARING DAMPING IN Ns/m

*GET,CXXA,ETAB,5,ELEM,2
*GET,CYYA,ETAB,6,ELEM,2
*GET,CXYA,ETAB,7,ELEM,2
*GET,CYXA,ETAB,8,ELEM,2

ESEL,ALL

ECC = ECCEQ/XCLEAR         ! ECCENTRICITY RATIO

KXXA = KXXA/1E6            ! STIFFNESS IN MN/m
KYYA = KYYA/1E6            ! STIFFNESS IN MN/m
KXYA = KXYA/1E6            ! STIFFNESS IN MN/m
KYXA = KYXA/1E6            ! STIFFNESS IN MN/m

CXXA = CXXA/1E3            ! DAMPING IN kNs/m
CYYA = CYYA/1E3            ! DAMPING IN kNs/m
CXYA = CXYA/1E3            ! DAMPING IN kNs/m
CYXA = CYXA/1E3            ! DAMPING IN kNs/m


*DIM,LABEL,CHAR,9,2

LABEL(1,1) = 'ECCR'
LABEL(1,2) = '-'
LABEL(2,1) = 'KXX'
LABEL(2,2) = 'MN/m'
LABEL(3,1) = 'KYY'
LABEL(3,2) = 'MN/m'
LABEL(4,1) = 'KXY'
LABEL(4,2) = 'MN/m'
LABEL(5,1) = 'KYX'
LABEL(5,2) = 'MN/m'
LABEL(6,1) = 'CXX'
LABEL(6,2) = 'kNs/m'
LABEL(7,1) = 'CYY'
LABEL(7,2) = 'kNs/m'
LABEL(8,1) = 'CXY'
LABEL(8,2) = 'kNs/m'
LABEL(9,1) = 'CYX'
LABEL(9,2) = 'kNs/m'

*DIM,VALUE,,9,3

*VFILL,VALUE(1,1),DATA,0.2663       ! ECCENTRICITY VALUE FROM REFERENCE
*VFILL,VALUE(1,2),DATA,ECC          ! ECCENTRICITY FROM MAPDL
*VFILL,VALUE(1,3),DATA,ABS(0.2663/ECC)

*VFILL,VALUE(2,1),DATA,12.81        ! KXX VALUE FROM REFERENCE
*VFILL,VALUE(2,2),DATA,KXXA         ! KXX FROM MAPDL
*VFILL,VALUE(2,3),DATA,ABS(12.81/KXXA)

*VFILL,VALUE(3,1),DATA,8.815        ! KYY VALUE FROM REFERENCE
*VFILL,VALUE(3,2),DATA,KYYA         ! KYY FROM MAPDL
*VFILL,VALUE(3,3),DATA,ABS(8.815/KYYA)

*VFILL,VALUE(4,1),DATA,16.39        ! KXY VALUE FROM REFERENCE
*VFILL,VALUE(4,2),DATA,KXYA         ! KXY FROM MAPDL
*VFILL,VALUE(4,3),DATA,ABS(16.39/KXYA)

*VFILL,VALUE(5,1),DATA,-25.06       ! KYX VALUE FROM REFERENCE
*VFILL,VALUE(5,2),DATA,KYXA         ! KYX FROM MAPDL
*VFILL,VALUE(5,3),DATA,ABS(-25.06/KYXA)

*VFILL,VALUE(6,1),DATA,232.9        ! CXX VALUE FROM REFERENCE
*VFILL,VALUE(6,2),DATA,CXXA         ! CXX FROM MAPDL
*VFILL,VALUE(6,3),DATA,ABS(232.9/CXXA)

*VFILL,VALUE(7,1),DATA,294.9        ! CYY VALUE FROM REFERENCE
*VFILL,VALUE(7,2),DATA,CYYA         ! CYY FROM MAPDL
*VFILL,VALUE(7,3),DATA,ABS(294.9/CYYA)

*VFILL,VALUE(8,1),DATA,-81.92       ! CXY VALUE FROM REFERENCE
*VFILL,VALUE(8,2),DATA,CXYA         ! CXY FROM MAPDL
*VFILL,VALUE(8,3),DATA,ABS(-81.92/CXYA)

*VFILL,VALUE(9,1),DATA,-81.92       ! ECCENTRICITY VALUE FROM REFERENCE
*VFILL,VALUE(9,2),DATA,CYXA         ! CYX FROM MAPDL
*VFILL,VALUE(9,3),DATA,ABS(-81.92/CYXA)

/COM
/OUT,vm199,vrt
/COM,------------------- VM199 RESULTS COMPARISON --------------
/COM, * 2-D MODEL WITH COMBI214 AND MASS21 *
/COM,
/COM,                  |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.3,'  ',F15.3,'   ',1F15.3)
/COM,
/COM,
/COM,----------------------------------------------------------
/OUT,SCRATCH
FINISH

/CLEAR, NOSTART
/TITLE,VM199,OIL FILM BEARING SUPPORTING A ROTATING SHAFT AND SUBJECTED TO A STATIC LOAD
/OUT,
/COM, *********************************************************
/COM,   FLUID218 MODEL - BEARING CHARACTERISTICS CALCULATION
/COM, *********************************************************

/COM, ************************************************************************
/COM,  CASE 1: FORCE CALCULATION @ EQUILIBRIUM POSITION (UXEQ ,UYEQ)
/COM, ************************************************************************
/FILNAM,VM199_F218
PARRES,CHANGE,VM199_C214,PARM

PERTINC = 1E-6                           ! PERTURBATION INCREMENT
XX      = UXEQ                           ! EQUILIBRIUM POSITION
YY      = UYEQ

/PREP7

ET,1,185                                 ! ** MESH ROTOR - USED TO MESH BEARING FLUID
MP,EX,1,2.0E11
MP,NUXY,1,0.33
MP,DENS,1,0
CYLIND,0,RADIUS,-LENGTH/2,LENGTH/2,0,90
CYLIND,0,RADIUS,-LENGTH/2,LENGTH/2,90,180
CYLIND,0,RADIUS,-LENGTH/2,LENGTH/2,180,270
CYLIND,0,RADIUS,-LENGTH/2,LENGTH/2,270,360
NUMMRG,KP
ESIZE,0.001
TYPE,1
MAT,1
VMESH,ALL

ET, 2, 218                               ! ** MESH BEARING FLUID
KEYOPT,2,1,0                             ! PRESSURE ONLY DOF
SECTYPE,2,SHELL
SECDATA, XCLEAR
R, 2, XCLEAR,RADIUS, XX,YY
MP,VISC,2,MU
MP,DENS,2,ROSHAFT
TYPE,2
MAT,2
REAL,2
CSYS,1
NSEL,,LOC,X,RADIUS
ESLN
ESURF
CSYS,0
ALLSEL

VCLEAR,ALL                               ! ** REMOVE SOLID ELEMENTS
ETDELE,1

NSEL, ,LOC,Z,-LENGTH/2                   ! ** BOUNDARY CONDITIONS
NSEL,A,LOC,Z, LENGTH/2
D, ALL, PRES, 0                          ! ZERO PRESSURE AT BOTH ENDS
ALLSEL
FINISH
SAVE

/SOLU
ANTYPE, STATIC                           ! ** STATIC ANALYSIS
OMEGA,,, OMEGAJ
OUTRES,ALL,ALL
SOLVE
FINI

/OUT,SCRATCH
/POST1
SET,LAST

ESEL,,TYPE,,2                            ! ** SUM THE FORCES AND MOMENTS TO GET TOTAL QUANTITIES
NSLE
*GET,NELEM,ELEM,0,COUNT
F11 = 0
F12 = 0
IELEM = 0
*DO,ILOOP,1,NELEM
   IELEM = ELNEXT(IELEM)
   *GET,CON,ELEM,IELEM,NMISC,11
   F11 = F11 + CON
   *GET,CON,ELEM,IELEM,NMISC,12
   F12 = F12 + CON
*ENDDO
ALLSEL,ALL

/SHOW,PNG,REV                            ! ** PLOT PRESSURE SOLUTION
/VIEW,1,1,1,1
PLNSOL,PRES
*GET,PRESMAX,PLNSOL,0,MAX
/SHOW,CLOSE

PARSAV,ALL,VM199_F218,PARM
FINISH

/CLEAR,NOSTART
/TITLE,VM199,OIL FILM BEARING SUPPORTING A ROTATING SHAFT AND SUBJECTED TO A STATIC LOAD
/OUT,SCRATCH
/COM, **************************************************************
/COM,  CASE 2: FORCE CALCULATION FOR SMALL PERTURBATION DX
/COM, **************************************************************
/PREP7
RESUME,VM199_F218,db
RDEL,2

DX    = PERTINC                         ! ** PERTURBATION PARAMETERS
XXX   = XX + PERTINC

R,2,XCLEAR,RADIUS, XXX,YY
FINI

/SOLU
ANTYPE, STATIC
OMEGA,,, OMEGAJ
OUTRES,ALL,ALL
SOLVE
FINI

/POST1
SET,LAST

ESEL,,TYPE,,2                            ! ** SUM THE FORCES AND MOMENTS TO GET TOTAL QUANTITIES
NSLE
*GET,NELEM,ELEM,0,COUNT
F21 = 0
F22 = 0
IELEM = 0
*DO,ILOOP,1,NELEM
   IELEM = ELNEXT(IELEM)
   *GET,CON,ELEM,IELEM,NMISC,11
   F21 = F21 + CON
   *GET,CON,ELEM,IELEM,NMISC,12
   F22 = F22 + CON
*ENDDO
ALLSEL,ALL

PARRES,CHANGE,VM199_F218,PARM
PARSAV,ALL,VM199_F218,PARM
FINISH

/CLEAR,NOSTART
/TITLE,VM199,OIL FILM BEARING SUPPORTING A ROTATING SHAFT AND SUBJECTED TO A STATIC LOAD
/OUT,SCRATCH
/COM, **************************************************************
/COM,  CASE 3: FORCE CALCULATION FOR SMALL PERTURBATION DY
/COM, **************************************************************
/PREP7
RESUME,VM199_F218,db
RDEL,2

DY   = PERTINC                           ! ** PERTURBATION PARAMETERS
YYY  = YY + DY

R, 2, XCLEAR,RADIUS, XX,YYY
FINI

/SOLU
ANTYPE, STATIC
OMEGA,,, OMEGAJ
OUTRES,ALL,ALL
SOLVE
FINI

/POST1
SET,LAST

ESEL,,TYPE,,2                            ! ** SUM THE FORCES AND MOMENTS TO GET TOTAL QUANTITIES
NSLE
*GET,NELEM,ELEM,0,COUNT
F31 = 0
F32 = 0
IELEM = 0
*DO,ILOOP,1,NELEM
   IELEM = ELNEXT(IELEM)
   *GET,CON,ELEM,IELEM,NMISC,11
   F31 = F31 + CON
   *GET,CON,ELEM,IELEM,NMISC,12
   F32 = F32 + CON
*ENDDO
ALLSEL,ALL

*STATUS,F31
*STATUS,F32

PARRES,CHANGE,VM199_F218,PARM
PARSAV,ALL,VM199_F218,PARM
FINISH

/CLEAR,NOSTART
/TITLE,VM199,OIL FILM BEARING SUPPORTING A ROTATING SHAFT AND SUBJECTED TO A STATIC LOAD
/OUT,SCRATCH
/COM, *********************************************************************
/COM,  CASE 4: FORCE CALCULATION FOR SMALL PERTURBATION DXXDOT
/COM, *********************************************************************
/PREP7
RESUME,VM199_F218,db
RDEL,2

DXXDOT  = PERTINC                        ! ** PERTURBATION PARAMETERS
XXXDOT  = DXXDOT
VELOC1X = XXXDOT*XCLEAR*OMEGAJ

R, 2, XCLEAR,RADIUS, XX,YY, VELOC1X
FINI

/SOLU
ANTYPE, STATIC
OMEGA,,, OMEGAJ
OUTRES,ALL,ALL
SOLVE
FINI

/POST1
SET,LAST

ESEL,,TYPE,,2                            ! ** SUM THE FORCES AND MOMENTS TO GET TOTAL QUANTITIES
NSLE
*GET,NELEM,ELEM,0,COUNT
F41 = 0
F42 = 0
IELEM = 0
*DO,ILOOP,1,NELEM
   IELEM = ELNEXT(IELEM)
   *GET,CON,ELEM,IELEM,NMISC,11
   F41 = F41 + CON
   *GET,CON,ELEM,IELEM,NMISC,12
   F42 = F42 + CON
*ENDDO
ALLSEL,ALL

*STATUS,F41
*STATUS,F42

PARRES,CHANGE,VM199_F218,PARM
PARSAV,ALL,VM199_F218,PARM
FINISH

/CLEAR,NOSTART
/TITLE,VM199,OIL FILM BEARING SUPPORTING A ROTATING SHAFT AND SUBJECTED TO A STATIC LOAD
/OUT,SCRATCH
/COM, *********************************************************************
/COM,  CASE 5: FORCE CALCULATION FOR SMALL PERTURBATION DYYDOT
/COM, *********************************************************************
/PREP7
RESUME,VM199_F218,db
RDEL,2

DYYDOT  = PERTINC                        ! ** PERTURBATION PARAMETERS
YYYDOT  = DYYDOT
VELOC2Y = YYYDOT*XCLEAR*OMEGAJ

R, 2, XCLEAR,RADIUS, XX,YY,, VELOC2Y
FINI

/SOLU
ANTYPE, STATIC
OMEGA,,, OMEGAJ
OUTRES,ALL,ALL
SOLVE
FINI

/POST1
SET,LAST

ESEL,,TYPE,,2                            ! ** SUM THE FORCES AND MOMENTS TO GET TOTAL QUANTITIES
NSLE
*GET,NELEM,ELEM,0,COUNT
F51 = 0
F52 = 0
IELEM = 0
*DO,ILOOP,1,NELEM
   IELEM = ELNEXT(IELEM)
   *GET,CON,ELEM,IELEM,NMISC,11
   F51 = F51 + CON
   *GET,CON,ELEM,IELEM,NMISC,12
   F52 = F52 + CON
*ENDDO
ALLSEL,ALL

*STATUS,F51
*STATUS,F52

PARRES,CHANGE,VM199_F218,PARM
PARSAV,ALL,VM199_F218,PARM
FINISH

/COM, **************************************************
/COM,  BEARING CHARACTERISTICS CALCULATION
/COM, **************************************************

PARRES,NEW,VM199_F218,PARM

! ** STIFFNESS CHARACTERISTICS MN/m

KXXB = ((F21-F11)/DX)/1E6
KYYB = ((F32-F12)/DY)/1E6
KXYB = ((F31-F11)/DY)/1E6
KYXB = ((F22-F12)/DX)/1E6

! ** DAMPING CHARACTERISTICS kNs/m

DXXDOT = DXXDOT*XCLEAR*OMEGAJ
DYYDOT = DYYDOT*XCLEAR*OMEGAJ
CXXB = ((F41-F11)/DXXDOT)/1E3
CYYB = ((F52-F12)/DYYDOT)/1E3
CXYB = ((F51-F11)/DYYDOT)/1E3
CYXB = ((F42-F12)/DXXDOT)/1E3

*DIM,LABEL,CHAR,8,2

LABEL(1,1) = 'KXX'
LABEL(1,2) = 'MN/m'
LABEL(2,1) = 'KYY'
LABEL(2,2) = 'MN/m'
LABEL(3,1) = 'KXY'
LABEL(3,2) = 'MN/m'
LABEL(4,1) = 'KYX'
LABEL(4,2) = 'MN/m'
LABEL(5,1) = 'CXX'
LABEL(5,2) = 'kNs/m'
LABEL(6,1) = 'CYY'
LABEL(6,2) = 'kNs/m'
LABEL(7,1) = 'CXY'
LABEL(7,2) = 'kNs/m'
LABEL(8,1) = 'CYX'
LABEL(8,2) = 'kNs/m'

*DIM,VALUE,,8,3

*VFILL,VALUE(1,1),DATA,12.81        ! KXX VALUE FROM REFERENCE
*VFILL,VALUE(1,2),DATA,KXXB         ! KXX FROM MAPDL
*VFILL,VALUE(1,3),DATA,ABS(12.81/KXXB)

*VFILL,VALUE(2,1),DATA,8.815        ! KYY VALUE FROM REFERENCE
*VFILL,VALUE(2,2),DATA,KYYB         ! KYY FROM MAPDL
*VFILL,VALUE(2,3),DATA,ABS(8.815/KYYB)

*VFILL,VALUE(3,1),DATA,16.39        ! KXY VALUE FROM REFERENCE
*VFILL,VALUE(3,2),DATA,KXYB         ! KXY FROM MAPDL
*VFILL,VALUE(3,3),DATA,ABS(16.39/KXYB)

*VFILL,VALUE(4,1),DATA,-25.06       ! KYX VALUE FROM REFERENCE
*VFILL,VALUE(4,2),DATA,KYXB         ! KYX FROM MAPDL
*VFILL,VALUE(4,3),DATA,ABS(-25.06/KYXB)

*VFILL,VALUE(5,1),DATA,232.9        ! CXX VALUE FROM REFERENCE
*VFILL,VALUE(5,2),DATA,CXXB         ! CXX FROM MAPDL
*VFILL,VALUE(5,3),DATA,ABS(232.9/CXXB)

*VFILL,VALUE(6,1),DATA,294.9        ! CYY VALUE FROM REFERENCE
*VFILL,VALUE(6,2),DATA,CYYB         ! CYY FROM MAPDL
*VFILL,VALUE(6,3),DATA,ABS(294.9/CYYB)

*VFILL,VALUE(7,1),DATA,-81.92       ! CXY VALUE FROM REFERENCE
*VFILL,VALUE(7,2),DATA,CXYB         ! CXY FROM MAPDL
*VFILL,VALUE(7,3),DATA,ABS(-81.92/CXYB)

*VFILL,VALUE(8,1),DATA,-81.92       ! ECCENTRICITY VALUE FROM REFERENCE
*VFILL,VALUE(8,2),DATA,CYXB         ! CYX FROM MAPDL
*VFILL,VALUE(8,3),DATA,ABS(-81.92/CYXB)

/COM
/OUT,vm199,vrt,,APPEND
/COM,------------------- VM199 RESULTS COMPARISON --------------
/COM, * 3-D MODEL WITH FLUID218 AND SOLID185 *
/COM,
/COM,                  |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.3,'  ',F15.3,'   ',1F15.3)
/COM,
/COM,
/COM,----------------------------------------------------------
/OUT
*LIST,vm199,vrt
FINISH

/COM,ANSYS MEDIA REL. 2023R1 (11/04/2022) REF. VERIF. MANUAL: REL. 2023R1
/VERIFY,VM269
JPGPRF,500,100,1
/SHOW,JPEG
/TITLE,VM269, DEFORMATION OF TUBE AND SPHERE UNDER AXISYMMETRIC BOUNDARY CONDITIONS
/COM, REFERENCE: "Z.YOSIBASH, AXISYMMETRIC PRESSURE BOUNDARY LOADING FOR FINITE DEFORMATION
/COM,             ANALYSIS USING P-FEM, COMPUT. METHODS APPL. MECH. ENGRG., 196(2007)
/COM,             :1261-1277"
/COM,
/COM, *****TUBE***********
/PREP7  
AA=1
A=1+1
K=2e3                ! BULK MODULUS 
C10=0.5
MUA=2*C10

TB,HYPER,1,,,NEO     ! NEO-HOOKEAN
TBDATA,1,MUA         ! 
TBDATA,2,2/K         ! D=2/K

RECTNG,1,2,0,1
ET,1,PLANE182              
KEYOPT,1,3,1         ! AXISYMMETRIC
MAT,1                
ESIZE,0.2              
AMESH,1              

D,ALL,UY,0.          ! NO DEFORMATION IN VERTICAL DIRECTION 
ALLSEL
*GET,NN,NODE,0,COUNT
*DO,I,1,NN
  NSEL,S,NODE,,I
  D,ALL,UX,AA*NX(I)  ! LINEAR DEFORMATION IN RADIAL DIRECTION
*ENDDO
ALLSEL
FINISH

/SOL
ANTYPE,0
NLGEOM,ON
NSUBST,20,1000,5
OUTRES,ALL,ALL
SOLVE
FINISH

/POST1
*GET,SXX,NODE,NODE(2,1,0),S,X
*GET,SYY,NODE,NODE(2,1,0),S,Y
/OUT,
/COM, EXPECTED FROM REFERENCE PAPER
SRR_REF=(A*A-1)*(K+2*C10/3/A**(10/3))
SZZ_REF=(A*A-1)*(K-4*C10/3/A**(10/3))
SRR_ANSYS=SXX
SZZ_ANSYS=SYY
SAVE,TABLE_1
FINISH
/CLEAR,NOSTART
/COM, ********** SPHERE **********
/PREP7  
AA=1
A=1+AA
K=2e3                ! BULK MODULUS 
C10=0.5
MUA=2*C10

TB,HYPER,1,,,NEO     ! NEO-HOOKEAN
TBDATA,1,MUA         ! 
TBDATA,2,2/K         ! D=2/K 

SPH4,0,0,0.01,0.03  
VSBW,1 
VDELE,3
CSYS,0
WPLANE,,,,,,1,,,,1
VSBW,2
VDELE,1
WPLANE,,,,,,,1,1,,
VSBW,3
VDELE,1

ET,1,SOLID185              
MAT,1                          
VMESH,ALL              
CSYS,2
NROTAT,ALL

*GET,NN,NODE,0,COUNT
*DO,I,1,NN
  NSEL,S,NODE,,I
  D,ALL,UX,AA*NX(I)   ! LINEAR DEFORMATION IN RADIAL DIRECTION
  D,ALL,UY,0
  D,ALL,UZ,0
*ENDDO
ALLSEL
FINISH

/SOL
ANTYPE,0
NLGEOM,ON
NSUBST,20,1000,5
OUTRES,ALL,ALL
SOLVE
FINISH

/POST1
*GET,SXX_S,NODE,NODE(-0.03,0,0),S,X
/OUT
/COM, EXPECTED FROM REFERENCE PAPER
SRR_S_REF=(A*A*A-1)*K
SRR_S_ANSYS=SXX_S
SAVE,TABLE_2
FINI
/OUT,SCRATCH
RESUME,TABLE_1
/COM
/OUT,vm269,vrt
/COM,------------ vm269 RESULTS COMPARISON -------------------
/COM,
/COM,				|  TARGET   |   Mechanical APDL  |   RATIO
/COM,
/COM, ================
/COM,   TUBE MODEL
/COM, ================
*VWRITE,'S_RR',SRR_REF,SRR_ANSYS,SRR_ANSYS/SRR_REF
(1X,A3,' ',F10.4,'  ',F14.4,'   ',F15.3)
*VWRITE,'S_ZZ',SZZ_REF,SZZ_ANSYS,SZZ_ANSYS/SZZ_REF
(1X,A3,' ',F10.4,'  ',F14.4,'   ',F15.3)
/COM,
/COM,
/NOPR,
RESUME,TABLE_2
/GOPR,
/COM,
/COM,
/COM, ===============
/COM,  SPHERE MODEL
/COM, ===============
*VWRITE, 'S_RR',SRR_S_REF,SRR_S_ANSYS,SRR_S_ANSYS/SRR_S_REF
(1X,A3,' ',F10.4,'  ',F14.4,'   ',F15.3)
/COM,
/COM,------------------------------------------------------------
/OUT
FINISH
*LIST,vm269,vrt

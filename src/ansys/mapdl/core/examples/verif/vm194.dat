/COM,ANSYS MEDIA REL. 2023R1 (11/04/2022) REF. VERIF. MANUAL: REL. 2023R1
/VERIFY,VM194
/PREP7  
/TITLE, VM194, ELEMENT BIRTH/DEATH IN A FIXED BAR WITH THERMAL LOADING
/COM    ANY STANDARD MECHANICS OF MATERIALS TEXT
ET,1,LINK180        ! 3-D SPAR ELEMENTS
MP,EX,1,30E6        ! BAR MATERIAL PROPERTIES
MP,ALPX,1,.00005
MP,EX,2,30E6
MP,ALPX,2,.00005    ! MATERIAL PROPERTIES FOR RE-BORN ELEMENT
MP,REFT,2,100       ! REFERENCE TEMPERATURE FOR ELEMENT BIRTH
SECTYPE,1,LINK   
SECDATA,1
N,1 
N,4,10  
FILL
E,1,2    
EGEN,3,1,-1         ! GENERATE THREE ELEMENTS 
FINISH  

/SOLU   
ANTYPE,STATIC
D,1,ALL,,,4,3       ! FIX BOTH ENDS OF THE BAR
D,ALL,UZ
TREF,0              ! ZERO REFERENCE TEMPERATURE
TUNIF,100           ! UNIFORM TEMPERATURE THERMAL LOAD
NROPT,FULL
OUTPR,BASIC,ALL
/OUT,SCRATCH
SOLVE   
EKILL,2             ! KILL CENTER ELEMENT
SOLVE
EALIVE,2            ! RESURRECT CENTER ELEMENT
MPCHG,2,2           !  AND CHANGE TO MATERIAL 2 FOR STRAIN-FREE BIRTH
SOLVE
TUNIF,0             ! REMOVE THERMAL LOADING
SOLVE
FINISH

/POST1
SET,LAST
/OUT,
ESEL,S,ELEM,,1,2
ETABLE,FO,SMISC,1
ESORT,FO
*GET,F,SORT,,MAX
ETABLE,STR,LEPTH,1
ESORT,STR
*GET,S1,SORT,,MAX
*GET,S2,SORT,,MIN
*status,parm
*DIM,VALUE,,3,3
*DIM,LABEL,CHAR,3,2
LABEL(1,1) = 'PRESS ','MAX STRS ','MAX STRS '
LABEL(1,2) = 'psi','EL(1)','EL(2)'
*VFILL,VALUE(1,1),DATA,150000,0,-.005
*VFILL,VALUE(1,2),DATA,F,S1,S2
*VFILL,VALUE(1,3),DATA,ABS(F/150000),0,ABS(S2/.005)
/COM
/OUT,vm194,vrt
/COM,------------------- VM194 RESULTS COMPARISON --------------
/COM,
/COM,  LOAD STP 4      |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.3,'  ',F15.3,'   ',1F18.3)
/COM,-----------------------------------------------------------
/OUT
FINISH
*LIST,vm194,vrt

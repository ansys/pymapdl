/batch
/nopr

!!! need to input dofs and eqnslvr
!!! /debug,stat

dofs   = 100
eltype = 186


! define parameters
aspect=1.0
level=1
dof_nd=3
ndofs=dofs*100

*if,ndofs,lt,1001,then
a_size=.5
*elseif,ndofs,lt,50001,then
a_size=.1
*elseif,ndofs,lt,100001,then
a_size=.075
*elseif,ndofs,lt,350001,then
a_size=.050
*elseif,ndofs,lt,2000001,then
a_size=.030
*elseif,ndofs,lt,7500001,then
a_size=.015
*else
a_size=.010
*endif

/PREP7
shpp,off
ET,1,PLANE42               ! Define PLANE42 as element type 1
ET,2,eltype                ! Define SOLID186 as element type 2
*if,eltype,eq,186,then
  KEYOPT,2,1,0
*endif
MP,EX,1,38000
MP,DENS,1,1.033E-3
MP,NUXY,1,.3
K,1                        ! Define keypoint 1 at 0,0,0
K,2,2                      ! Define keypoint 2 at 2,0,0
K,3,2.3,.2                 ! Define keypoint 3 at 2.3,.2,0
K,4,1.9,.45                ! Define keypoint 4 at 1.9,.45,0
K,5,1,.25                  ! Define keypoint 5 at 1,.25,0
LSTR,1,2                   ! Create a straight line between keypoints 1 and 2
LSTR,5,1                   ! Create a straight line between keypoints 5 and 1
BSPLIN,2,3,4,5,,,-1,,,-1,-.25   ! Create a B-spline
AL,1,3,2
ESIZE,a_size
AMESH,1

*get,a_nodes,node,,count
*if,eltype,eq,186,then
  zdiv=ndofs/(11*a_nodes) - 1
*else
  zdiv=ndofs/(3*a_nodes) - 1
*endif
zlth=a_size*zdiv*aspect
zdiv=nint(zdiv)
esiz,,zdiv
*if,zlth,le,0,then
zlth=1
*endif

TYPE,2
VEXT,ALL,,,,,zlth
modmsh,detach
emid,add,all
allsel,all

FINI

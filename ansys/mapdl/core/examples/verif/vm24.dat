/COM,ANSYS MEDIA REL. 150 (11/8/2013) REF. VERIF. MANUAL: REL. 150
/VERIFY,VM24
/PREP7
/TITLE, VM24, PLASTIC HINGE IN A RECTANGULAR BEAM
C*** STR. OF MATLS., TIMOSHENKO, PART 2, 3RD ED., PG. 349, ART. 64
C*** USING BILINEAR KINEMATIC HARDENING PLASTICITY BEHAVIOR TO DESCRIBE  
C*** THE MATERIAL NONLINEARITY
ET,1,BEAM188
SECTYPE,1,BEAM,RECT
SECDATA,2,1,6,1           ! 2 x 1 CROSS-SECTION USING 6 CELLS THRU THE THICKNESS
MP,EX,1,30E6
MP,PRXY,1,0.3
TB,BKIN,1,1               ! BILINEAR KINEMATIC HARDENING
TBTEMP,70
TBDATA,1,36000,0          ! YIELD POINT AND ZERO TANGENT MODULUS
N,1                       ! DEFINE NODES
N,2,10
E,1,2                     ! DEFINE ELEMENT
D,1,ALL                   ! BOUNDARY CONDITIONS
DSYM,SYMM,Z               ! 2-DIMENSIONAL MODEL
FINISH
/SOLU
NSUBS,1    
OUTRES,EPPL,1             ! STORE PLASTIC STRAINS FOR EVERY SUBSTEP
CNVTOL,U                  ! CONVERGENCE CRITERION BASED UPON DISPLACEMENTS AND
CNVTOL,ROT                ! ROTATIONS
/OUT,SCRATCH
*DO,I,1,4
  TIME,1.0    
  F,2,MZ,(20000+(I*4000)) ! APPLY MOMENT LOAD
  SOLVE
*ENDDO
FINISH
/POST26
/OUT,
NSOL,2,2,U,Y,UY2          ! NODE 2 DISPLACEMENT
NSOL,3,2,ROT,Z,ROTZ       ! NODE 2 ROTATION, USED FOR COMPARISON
ESOL,4,1,,LEPPL,1,EPPLAXL ! AXIAL PLASTIC STRAIN
/OUT,vm24,vrt
PRVAR,2,3,4
/OUT
FINISH
*LIST,vm24,vrt

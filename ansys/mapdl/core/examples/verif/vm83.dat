/COM,ANSYS MEDIA REL. 150 (11/8/2013) REF. VERIF. MANUAL: REL. 150
/VERIFY,VM83
/PREP7                        
JPGPRF,500,100,1             ! MACRO TO SET PREFS FOR JPEG PLOTS
/SHOW,JPEG
/TITLE, VM83, IMPACT OF A BLOCK ON A SPRING SCALE
C***VECTOR MECHANICS FOR ENGINEERS, BEER AND JOHNSTON, 1962, PAGE 531, PROB 14.6
C*** WITH THANKS TO ALAN GOULD
ANTYPE,TRANS              ! NONLINEAR TRANSIENT DYNAMIC ANALYSIS
ET,1,COMBIN40,1,,2,,,2
ET,2,COMBIN40,,,2,,,2
R,1,100,,(25/386)         ! SPRING CONSTANT = 100, MASS = (25/386)
R,2,1E4,50.899,(50/386),71.75 ! SPRING CONSTANT = 1E4, C = 50.90,M=(50/386)
N,1,,-10
N,2
N,3,,72
TYPE,2
E,1,2
TYPE,1
REAL,2
E,2,3
ACEL,,386                 ! GRAVITY
FINISH
/SOLU    
TIMINT,OFF                ! TIME INTEGRATION TURNED OFF
KBC,1                     ! STEP THE LOAD
NSUBST,2                  ! TWO SUBSTEPS TO GET ZERO INITIAL VELOCITY
                          !  AND ACCELERATION
D,1,UY,,,3,2
TIME,1E-8                 ! NEAR ZERO TIME FOR FIRST LOAD STEP
/OUT,SCRATCH
SOLVE
TIMINT,ON                 ! TIME INTEGRATION TURNED ON
DDELE,3,UY                ! REMOVE THE CONSTRAINT AT NODE 3 (RELEASE THE BLOCK)
AUTOTS,ON                 ! AUTO TIME STEPPING ON
NSUBST,1400               ! MAXIMUM 1400 SUBSTEPS
OUTRES,NSOL,1
TIME,.7
SOLVE
FINISH
/POST26
/OUT,
NSOL,2,2,U,Y,UY           ! STORE DISPLACEMENTS UY OF APPROPRIATE NODES
NSOL,3,3,U,Y,UY
FILLDATA,4,,,,71.75       ! DEFINE VARIABLE 4 AS CONSTANT
ADD,5,3,4,,3OFFSET        ! CALCULATE VARIABLE 5 AS 3UY + 71.75
PRTIME,.65,.7             ! LIMIT TIME INTERVAL TO BE PRINTED
PRVAR,2,3                 ! PRINT VARIABLES 2 AND 3
/AXLAB,Y,INCH
PLVAR,2,5                 ! DISPLAY VARIABLES 2 AND 5
*GET,DEF_N2,VARI,2,RTIME,0.68897
*GET,DEF_N3,VARI,3,RTIME,0.68897
*DIM,LABEL,CHAR,2,2
*DIM,VALUE,,2,3
LABEL(1,1) = '   DEF, ','   Y,   '
LABEL(1,2) = 'in      ','in      '
*VFILL,VALUE(1,1),DATA,-7.7,-79.450
*VFILL,VALUE(1,2),DATA,DEF_N2,DEF_N3
*VFILL,VALUE(1,3),DATA,ABS(DEF_N2/7.7) ,ABS(DEF_N3/79.450)
/COM
/OUT,vm83,vrt
/COM,------------------- VM83 RESULTS COMPARISON --------------
/COM,
/COM,                 |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.4,'  ',F14.4,'   ',1F15.3)
/COM,----------------------------------------------------------
/OUT
FINISH
*LIST,vm83,vrt

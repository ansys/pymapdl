/COM,ANSYS MEDIA REL. 150 (11/8/2013) REF. VERIF. MANUAL: REL. 150
/VERIFY,VM11
/PREP7
/TITLE, VM11, RESIDUAL STRESS PROBLEM
C***           MECHANICS OF SOLIDS, CRANDALL AND DAHL, 1959, PAGE 234, PROB 5.31
ANTYPE,STATIC
ET,1,LINK180
SECTYPE,1,LINK   
SECDATA,1
MP,EX,1,30E6
TB,BKIN                           ! TABLE FOR BILINEAR KINEMATIC HARDENING
TBTEMP,100                        
TBDATA,1,30000                    ! YIELD STRESS
C*** DEFINE MODEL GEOMETRY USING PARAMETRIC EXPRESSIONS
L=100
*AFUN,DEG                         ! SET ANGULAR FUNCTION ARGUMENTS AND
                                  ! RESULTS TO DEGREES
THETA=30
XLOC=L*TAN(THETA)
N,1,-XLOC
N,3,XLOC
FILL
N,4,,-L
E,1,4
E,2,4
E,3,4
OUTPR,,1
D,1,ALL,,,3
F,4,FY,-51961.5                   ! APPLY LOAD F1
FINISH
/SOLU    
SOLVE
FINISH
/POST1
BOT_NODE = NODE (0,-100,0)
*GET,DEF,NODE,BOT_NODE,U,Y
FINISH
/SOLU
AUTOTS,ON                         ! TURN ON AUTOMATIC LOAD STEPPING
NSUBST,10                
OUTPR,,10
F,4,FY,-81961.5                   ! APPLY LOAD F2
SOLVE
NSUBST,5
OUTPR,,5
F,4,FY                            ! REMOVE LOAD F2
SOLVE
FINISH
/POST1
ETABLE,STRS,LS,1
*GET,STRSS,ELEM,2,ETAB,STRS

*DIM,LABEL,CHAR,2,2
*DIM,VALUE,,2,3
LABEL(1,1) = 'DEF AT F','STRESS  '
LABEL(1,2) = '1 (in)  ','(psi)   '
*VFILL,VALUE(1,1),DATA,-0.07533,-5650
*VFILL,VALUE(1,2),DATA,DEF,STRSS
*VFILL,VALUE(1,3),DATA,ABS(DEF/0.07533 ) ,ABS(STRSS/5650 )
/COM
/OUT,vm11,vrt
/COM,------------------- VM11 RESULTS COMPARISON ---------------------
/COM,
/COM,                 |   TARGET   |   ANSYS   |   RATIO
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F11.5,'  ',F11.5,'   ',1F5.3)
/COM,-----------------------------------------------------------------
/OUT
FINISH
*LIST,vm11,vrt






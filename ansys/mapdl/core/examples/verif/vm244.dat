/COM,ANSYS MEDIA REL. 150 (11/8/2013) REF. VERIF. MANUAL: REL. 150
/VERIFY,VM244         
/TITLE,VM244, MODAL ANALYSIS OF CYCLIC SYMMETRIC ANNULAR PLATES
C*** ROBERT D BLEVINS,"FORMULAS FOR NATURAL FREQUENCY AND MODE
C*** SHAPE", NEW YORK, NY, VAN NOSTRAND REINHOLD PUBLISHING INC
C*** 1979, PP. 246-247, 286-287  
_THETA1 = 0	        ! DEGREE, STARTING ANGLE FOR SECTION
_THETA2 = 30		! DEGREE, ENDING ANGLE FOR SECTION
_FLOWER = 23.381	! HZ, LOWER FREQUENCY LIMIT PER BLEVINS
_FREQDIF = 2.1		! PERCENT
_A = 500		! OUTSIDE RADIUS (MM)
_B = 185		! INSIDE RADIUS (MM)
_H = 5			! THICKNESS (MM)
_E = 70000		! YOUNGS (N/MM^2)
_V = 0.3		! POISON'S RATIO
_D = 2.7E-9		! DENSITY AL1100 H14 (NSEC^2/MM^4)
*DIM,VALUE,ARRAY,6,3
*DIM,LABEL,CHAR,6
LABEL(1)='SOLID185'
LABEL(2)='SOLID186'
LABEL(3)='SOLID187'
LABEL(4)='SHELL181'
LABEL(5)='SHELL281'
LABEL(6)='SOLSH190'
*DO,I,1,6,1
*IF,LABEL(I),EQ,'SHELL181',OR,LABEL(I),EQ,'SHELL281',THEN
C*** CYCSYM MODEL, SIMPLE SUPPORT OUTER EDGE, FREE INNER EDGE,
C*** SHELL ELEMENT
/PREP7
MP,EX,1,_E
MP,NUXY,1,_V
MP,DENS,1,_D
CYL4,0,0,_A,_THETA1,_B,_THETA2
R,1,_H
ET,1,LABEL(I)
AMESH,ALL
NSEL,S,LOC,X,_A
D,ALL,UY
NROTAT,ALL
D,ALL,UZ
NSEL,ALL
CYCLIC
FINISH
*ELSE
C*** CYCSYM MODEL, SIMPLE SUPPORT OUTER EDGE, FREE INNER EDGE
C*** SOLID ELEMENTS
/PREP7  
MP,EX,1,_E
MP,NUXY,1,_V
MP,DENS,1,_D
CSYS,6
WPCSYS,6
CYL4,0,0,_A,_THETA1,_B,_THETA2,_H
ET,1,LABEL(I)
*IF,LABEL(I),EQ,'SOLID185',THEN
KEYOPT,1,2,3     ! REDUCE STIFFNESS FORMULATION TO MAKE FREQUENCIES MATCH
*ELSEIF,LABEL(I),EQ,'SOLSH190',THEN
VEORIENT,1,THIN
*ENDIF
VMESH,ALL
CSYS,4
NSEL,S,LOC,X,_A-.00001,_A+.00001
NSEL,R,LOC,Z,0
D,ALL,UY
NROTAT,ALL
D,ALL,UZ
NSEL,ALL
CYCLIC
FINISH
*ENDIF
/SOLU
ANTYPE,2
MODOPT,LANB,3
MXPAND,3, , ,1
MODOPT,LANB,3,1,100000, ,OFF
CYCOPT,HINDEX,0,0
SOLVE
FINISH
/POST1
*GET,F%I%,MODE,1,FREQ
/COM,                 FREQUENCY COMPARE FOR ELEMENT %LABEL(I)%
*IF,(ABS((F%I% - _FLOWER)/_FLOWER))*100,LT,_FREQDIF,THEN
C*** FREQUENCIES COMPARE WITHIN BLEVINS RESULT!!!
*ELSE
C*** FAILURE: FREQUENCIES OUT OF ACCAPTABLE LIMITS!!!
FINISH
/EXIT,NOSAVE           ! EXIT DUE TO FAILURE
*ENDIF
FINISH
PARSAV,ALL
/CLEAR,NOSTART
PARRES,
*ENDDO
*VFILL,VALUE(1,1),DATA,_FLOWER,_FLOWER,_FLOWER,_FLOWER,_FLOWER,_FLOWER
*VFILL,VALUE(1,2),DATA,F1,F2,F3,F4,F5,F6
*VFILL,VALUE(1,3),DATA,ABS(F1/_FLOWER),ABS(F2/_FLOWER),ABS(F3/_FLOWER),ABS(F4/_FLOWER),ABS(F5/_FLOWER),ABS(F6/_FLOWER)
/OUT,vm244,vrt
/COM,------------------- VM244 RESULTS COMPARISON -------------
/COM,
/COM,                 |    TARGET   |    Mechanical APDL   |    RATIO
/COM,
*VWRITE,LABEL(1),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A10,'     ',E10.4,'    ',E14.4,'     ',1F15.3)
/COM,----------------------------------------------------------
/OUT
FINISH
*LIST,vm244,vrt                                                               

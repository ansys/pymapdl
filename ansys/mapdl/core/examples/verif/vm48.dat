/COM,ANSYS MEDIA REL. 150 (11/8/2013) REF. VERIF. MANUAL: REL. 150
/VERIFY,VM48
/TITLE, VM48, NATURAL FREQUENCY OF A MOTOR-GENERATOR
C*** VIBRATION THEORY AND APPLICATIONS, THOMSON, 2ND PRINTING, PAGE 10, EX 1.3-3
/PREP7
ET,1,BEAM188           ! 3D 2 NODE BEAM
SECTYPE,1,BEAM,CSOLID
SECDATA,0.375/2,20     ! RADIUS AND NUMBER OF DIVISION AROUND CIRCUMFERENCE

ET,2,MASS21            ! GENERALIZED MASS
R,2,,,,31E-3           ! REAL CONSTANT SET 2 IXX
MP,EX,1,31.2E6         ! DEFINE MODULUS OF ELASTICITY
MP,PRXY,,0.3
N,1                    ! BEGIN NODE DEFINITION
N,2,8
E,1,2                  ! DEFINE PIPE ELEMENT
TYPE,2                 ! DEFINE ACTIVE ELEMENT TYPE AS SET #2
REAL,2                 ! DEFINE ACTIVE REAL CONSTANT TYPE AS SET #2
E,2                    ! DEFINE MASS AT END OF PIPE
OUTPR,BASIC,1
D,ALL,ALL              ! CONSTRAIN ALL DOF'S
DDELE,2,ROTX           ! RELEASE TORSIONAL DOF AT NODE 2
FINISH
/SOLU 
ANTYPE,MODAL  
MODOPT,LANB,1,,, 
SOLVE
*GET,FREQ,MODE,1,FREQ
*DIM,LABEL,CHAR,1,2
*DIM,VALUE,,1,3
LABEL(1,1) = '      F,'
LABEL(1,2) = ' (Hz)  '
*VFILL,VALUE(1,1),DATA,48.781
*VFILL,VALUE(1,2),DATA,FREQ
*VFILL,VALUE(1,3),DATA,ABS(FREQ/48.781)
/COM
/OUT,vm48,vrt
/COM,------------------- VM48 RESULTS COMPARISON ---------------
/COM,
/COM,                 |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.3,'  ',F14.3,'   ',1F15.3)
/COM,----------------------------------------------------------

/OUT
FINISH
*LIST,vm48,vrt


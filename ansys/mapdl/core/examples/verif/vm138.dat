/COM,ANSYS MEDIA REL. 150 (11/8/2013) REF. VERIF. MANUAL: REL. 150
/VERIFY,VM138
/PREP7
/TITLE, VM138, LARGE DEFLECTION BENDING OF A CIRCULAR PLATE
C***    THEORY OF PLATES AND SHELLS, TIMOSHENKO, P. 401, EQ. 232
ANTYPE,STATIC
NLGEOM,ON                ! LARGE DEFLECTION OPTION
ET,1,SHELL208,,,2
SECTYPE,1,SHELL
SECDATA,0.0025
SECNUM,1
MP,EX,1,2E11
MP,NUXY,1,0.3
N,1
N,11,.25
FILL
E,1,2
EGEN,10,1,1
OUTPR,,LAST
NSEL,S,LOC,X,0
DSYM,SYMM,X              ! SYMMETRY B.C. AT X=0
NSEL,ALL
D,11,ALL                 ! FIX RIM
SFE,1,1,PRES,,6585.175   ! ELEMENT PRESSURE LOAD
*REPEAT,10,1
FINISH
/SOLU    
/OUT,SCRATCH
SOLVE
FINISH
/POST1
/OUT,
*GET,UY,NODE,1,U,Y
*STATUS,PARM
*DIM,LABEL,CHAR,1,2
*DIM,VALUE,,1,3
LABEL(1,1) = 'Y DISP '
LABEL(1,2) = ' m'
*VFILL,VALUE(1,1),DATA,-.00125
*VFILL,VALUE(1,2),DATA,UY
*VFILL,VALUE(1,3),DATA,ABS(UY/.00125)
/COM
/OUT,vm138,vrt
/COM,------------------- VM138 RESULTS COMPARISON -------------
/COM,
/COM,   LOAD STEP 3   |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.5,'  ',F12.5,'   ',1F15.3)
/COM,-----------------------------------------------------------
/OUT
FINISH
*LIST,vm138,vrt

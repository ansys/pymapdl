/VERIFY,VM240
/COM,ANSYS MEDIA REL. 150 (11/8/2013) REF. VERIF. MANUAL: REL. 150
/TITLE,VM240,THERMAL EXPANSION OF RIGID BEAMS IN A COMPOSITE BAR
/COM J.M. GERE, S.P. TIMOSHENKO, MECHANICS OF MATERIALS, 2ND EDITION,
/COM PWS PUBLISHERS, 1984, P. 20-21,71
/COM

/GRAPH,POWER
/AUTO
/VIEW,1,1,1,1
/PNUM,TYPE,1
/PNUM,MAT,1
/NUM,1
/ESHAPE,1

/PREP7
ET,1,185                      ! DEFINING SOLID ELEMENTS
MP,EX,1,10.0e6                ! MATERIAL 1 PROPERTIES
MP,NUXY,1,0.3
MP,EX,2,5.0e6                 ! MATERIAL 2 PROPERTIES
MP,NUXY,2,0.3
ET,2,184                      ! DEFINING RIGID BEAM ELEMENTS
KEYOPT,2,1,1
KEYOPT,2,2,1
TREF,0
MPTEMP,1,0,100
MPDATA,ALPX,3,1,0,0.0003      ! SETTING THERMAL COEFFICIENT FOR RIGID BEAMS
MPDATA,ALPY,3,1,0,0.0003
MPDATA,ALPZ,3,1,0,0.0003

BLOCK,0,40,0,4,0,2            ! CREATING GEOMETRY
BLOCK,0,40,0,4,2,4
VGLUE,1,2

TYPE,1
MAT,1
ESIZE,1
VSEL,S,VOLU,,1
VMESH,ALL                     ! MESHING 1ST MATERIAL
ALLSEL,ALL

MAT,2
VSEL,S,VOLU,,3
VMESH,ALL                     ! MESHING 2ND MATERIAL

TYPE,2
MAT,3
E,661,657                     ! GENERATING RIGID BEAM ELEMENTS
E,701,660
E,702,659
E,703,658
E,616,617
E,863,862
E,905,908
E,904,907
E,903,906
E,821,822
E,251,247
E,291,250
E,292,249
E,293,248
E,206,207
E,453,452
E,495,498
E,494,497
E,493,496
E,411,412
E,1,6
E,3,49
E,4,48
E,5,47
E,2,46

/SOLU
ANTYPE,STATIC
NROPT,FULL
SOLCON,ON
NLGEOM,ON
RESCON,DEFINE,NONE
OUTRES,ALL,ALL
AUTOTS,ON
NSUBST,10,1000,10

NSEL,S,LOC,X,0                ! APPLYING BOUNDARY CONDITIONS
D,ALL,ALL
ALLSEL,ALL

BF,ALL,TEMP,100               ! APPLYING TEMPERATURE LOAD
/OUT,SCRATCH
SOLVE                         ! SOLVING
FINISH

/POST1                        ! POSTPROCESSING
NSEL,S,LOC,X,20
NSEL,R,LOC,Y,2
NSEL,R,LOC,Z,1
/OUT,
*GET,STRESS1,NODE,557,S,EQV
ALLSEL,ALL
NSEL,S,LOC,X,20
NSEL,R,LOC,Y,2
NSEL,R,LOC,Z,3
*GET,STRESS2,NODE,967,S,EQV
*DIM,LABEL,CHAR,2
*DIM,VALUE,,2,3
LABEL(1) = 'SEQV1','SEQV2'
*VFILL,VALUE(1,1),DATA,300000,150000
*VFILL,VALUE(1,2),DATA,ABS(STRESS1),ABS(STRESS2)
*VFILL,VALUE(1,3),DATA,ABS(STRESS1 / 300000) ,ABS(STRESS2 / 150000)
/OUT,vm240,vrt
/COM
/COM,------------------- VM240 RESULTS COMPARISON -------------------
/COM,
/COM,              |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
*VWRITE,LABEL(1),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A10,'     ',E10.4,'    ',E14.4,'     ',1F15.3)
/OUT
FINISH
*LIST,vm240,vrt


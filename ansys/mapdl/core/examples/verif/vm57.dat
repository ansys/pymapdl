/COM,ANSYS MEDIA REL. 150 (11/8/2013) REF. VERIF. MANUAL: REL. 150
/VERIFY,VM57
/PREP7
/TITLE, VM57, TORSIONAL FREQUENCIES OF A DRILL PIPE
C*** VIBRATION THEORY AND APPLICATIONS, THOMSON, 2ND. PRINTING, PAGE 272, EX. 8.4-5
C*** USING PIPE16 ELEMENTS
/PREP7
ET,1,PIPE16
ET,2,MASS21
R,1,(4.5/12),(.335/12)					! GEOMETRIC PROPERTIES FOR PIPE ELEMENTS
R,2,,,,,,29.3						! GEOMETRIC PROPERTY FOR MASS ELEMENT
MP,EX,1,4.4928E9
MP,DENS,1,15.2174
MP,NUXY,1,.3
N,1
N,13,,,-5000
FILL
E,1,2
EGEN,12,1,1							! PIPE ELEMENTS
TYPE,2
REAL,2
E,13								! MASS ELEMENT
FINISH
/SOLU    
ANTYPE,MODAL						! MODE-FREQUENCY ANALYSIS
MODOPT,LANB,2						! EXTRACT FIRST TWO MODES
D,1,UX,,,13,,UY,UZ,ROTX,ROTY
D,1,ROTZ
OUTPR,,1
MXPAND,2,
/OUT,SCRATCH
SOLVE
FINISH
/POST1
/OUT,
*GET,FREQ1,MODE,1,FREQ
*GET,FREQ2,MODE,2,FREQ
*DIM,LABEL,CHAR,2,2
*DIM,VALUE,,2,3
LABEL(1,1) = '      f1','      f2'
LABEL(1,2) = ', Hz    ',', Hz    '
*VFILL,VALUE(1,1),DATA,.3833,1.260
*VFILL,VALUE(1,2),DATA,FREQ1,FREQ2
*VFILL,VALUE(1,3),DATA,ABS(FREQ1/.3833),ABS(FREQ2/1.260)
SAVE,TABLE_1
FINISH
/CLEAR,NOSTART
C*** USING PIPE288 ELEMENTS
/PREP7
ET,1,PIPE288,,,2,2
ET,2,MASS21
R,2,,,,,,29.3						! GEOMETRIC PROPERTY FOR MASS ELEMENT
MP,EX,1,4.4928E9
MP,DENS,1,15.2174
MP,NUXY,1,.3
SECTYPE,1,PIPE
SECDATA,(4.5/12),(0.335/12)				! DIAMETER = 4.5/12, WALL THICKNESS = 0.335/12
N,1
N,13,,,-5000
FILL
E,1,2
EGEN,12,1,1							! PIPE ELEMENTS
TYPE,2
REAL,2
E,13								! MASS ELEMENT
FINISH
/SOLU    
ANTYPE,MODAL						! MODE-FREQUENCY ANALYSIS
MODOPT,LANB,2						! EXTRACT FIRST TWO MODES
D,1,UX,,,13,,UY,UZ,ROTX,ROTY
D,1,ROTZ
OUTPR,,1
MXPAND,2
/OUT,SCRATCH
SOLVE
FINISH
/POST1
/OUT,
*GET,FREQ1,MODE,1,FREQ
*GET,FREQ2,MODE,2,FREQ
*DIM,LABEL,CHAR,2,2
*DIM,VALUE,,2,3
LABEL(1,1) = '      f1','      f2'
LABEL(1,2) = ', Hz    ',', Hz    '
*VFILL,VALUE(1,1),DATA,.3833,1.260
*VFILL,VALUE(1,2),DATA,FREQ1,FREQ2
*VFILL,VALUE(1,3),DATA,ABS(FREQ1/.3833),ABS(FREQ2/1.260)
SAVE,TABLE_2
FINISH
/CLEAR,NOSTART
C*** USING PIPE289 ELEMENTS
/PREP7
ET,1,PIPE289,,,,2
ET,2,MASS21
R,2,,,,,,29.3						! GEOMETRIC PROPERTY FOR MASS ELEMENT
MP,EX,1,4.4928E9
MP,DENS,1,15.2174
MP,NUXY,1,.3
SECTYPE,1,PIPE
SECDATA,(4.5/12),(0.335/12)				! DIAMETER = 4.5/12, WALL THICKNESS = 0.335/12
K,1,
K,2,,,-5000
L,1,2
LESIZE,1,,,11
TYPE,1
LMESH,1							! PIPE ELEMENTS
ALLSEL,ALL
TYPE,2
REAL,2
E,2								! MASS ELEMENT
FINISH
/SOLU    
ANTYPE,MODAL						! MODE-FREQUENCY ANALYSIS
MODOPT,LANB,2						! EXTRACT FIRST TWO MODES
D,ALL,ALL,0
DDELE,ALL,ROTZ
D,1,ROTZ
OUTPR,,1
MXPAND,2
/OUT,SCRATCH
SOLVE
FINISH
/POST1
/OUT,
*GET,FREQ1,MODE,1,FREQ
*GET,FREQ2,MODE,2,FREQ
*DIM,LABEL,CHAR,2,2
*DIM,VALUE,,2,3
LABEL(1,1) = '      f1','      f2'
LABEL(1,2) = ', Hz    ',', Hz    '
*VFILL,VALUE(1,1),DATA,.3833,1.260
*VFILL,VALUE(1,2),DATA,FREQ1,FREQ2
*VFILL,VALUE(1,3),DATA,ABS(FREQ1/.3833),ABS(FREQ2/1.260)
SAVE,TABLE_3
FINISH
/CLEAR,NOSTART
C****	USING BEAM188 ELEMENTS
/PREP7
ET,1,BEAM188,,,2
ET,2,MASS21
R,2,,,,,,29.3						! GEOMETRIC PROPERTY FOR MASS ELEMENT
SECTYPE,1,BEAM,CTUBE					! HOLLOW CYLINDER BEAM
SECDATA,3.83/24,4.5/24					! OD = (4.5/2)/12, ID = (3.83/2)/12
MP,EX,1,4.4928E9
MP,DENS,1,15.2174
MP,NUXY,1,.3
N,1
N,13,,,-5000
FILL
E,1,2
EGEN,12,1,1							! BEAM ELEMENTS
TYPE,2
REAL,2
E,13								! MASS ELEMENT
FINISH
/SOLU
ANTYPE,MODAL						! MODE-FREQUENCY ANALYSIS
MODOPT,LANB,2						! EXTRACT FIRST TWO MODES
D,1,UX,,,13,,UY,UZ,ROTX,ROTY
D,1,ROTZ
OUTPR,,1
MXPAND,2
/OUT,SCRATCH
SOLVE
FINISH
/POST1
/OUT,
*GET,FREQ1,MODE,1,FREQ
*GET,FREQ2,MODE,2,FREQ
*DIM,LABEL,CHAR,2,2
*DIM,VALUE,,2,3
LABEL(1,1) = '      f1','      f2'
LABEL(1,2) = ', Hz    ',', Hz    '
*VFILL,VALUE(1,1),DATA,.3833,1.26
*VFILL,VALUE(1,2),DATA,FREQ1,FREQ2
*VFILL,VALUE(1,3),DATA,ABS(FREQ1/.3833),ABS(FREQ2/1.26)
SAVE,TABLE_4
FINISH
/CLEAR,NOSTART
C**** USING BEAM189 ELEMENTS
/PREP7
ET,1,BEAM189
ET,2,MASS21
R,2,,,,,,29.3           				! GEOMETRIC PROPERTY FOR MASS ELEMENT
SECTYPE,1,BEAM,CTUBE					! HOLLOW CYLINDER BEAM
SECDATA,3.83/24,4.5/24					! OD = (4.5/2)/12, ID = (3.83/2)/12
MP,EX,1,4.4928E9
MP,DENS,1,15.2174
MP,NUXY,1,.3
K,1,0,0,0
K,2,0,0,-5000
L,1,2
LESIZE,1,,,11
TYPE,1
LMESH,1							! BEAM ELEMENTS
ALLSEL,ALL
TYPE,2
REAL,2
E,2								! MASS ELEMENT
FINI
/SOLU
ANTYPE,MODAL
MODOPT,LANB,2
MXPAND,2
D,ALL,ALL,0
DDELE,ALL,ROTZ
D,1,ROTZ,0
OUTPR,,1
/OUT,SCRATCH
SOLVE
FINISH
/POST1
/OUT,
*GET,FREQ1,MODE,1,FREQ
*GET,FREQ2,MODE,2,FREQ
*DIM,LABEL,CHAR,2,2
*DIM,VALUE,,2,3
LABEL(1,1) = '      f1','      f2'
LABEL(1,2) = ', Hz    ',', Hz    '
*VFILL,VALUE(1,1),DATA,.3833,1.26
*VFILL,VALUE(1,2),DATA,FREQ1,FREQ2
*VFILL,VALUE(1,3),DATA,ABS(FREQ1/.3833),ABS(FREQ2/1.26)
SAVE,TABLE_5
FINISH
/CLEAR,NOSTART
RESUME,TABLE_1
/COM
/OUT,vm57,vrt
/COM,------------------- VM57 RESULTS COMPARISON --------------
/COM,
/COM,                 |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
/COM,RESULTS USING PIPE16 ELEMENTS:
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.4,'  ',F14.4,'   ',1F15.3)
/NOPR
RESUME,TABLE_2
/GOPR
/COM,
/COM,RESULTS USING PIPE288 ELEMENTS:
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.4,'  ',F14.4,'   ',1F15.3)
/NOPR
RESUME,TABLE_3
/GOPR
/COM,
/COM,RESULTS USING PIPE289 ELEMENTS:
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.4,'  ',F14.4,'   ',1F15.3)
/NOPR
RESUME,TABLE_4
/GOPR
/COM,
/COM,RESULTS USING BEAM188 ELEMENTS:
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.4,'  ',F14.4,'   ',1F15.3)
/NOPR
RESUME,TABLE_5
/GOPR
/COM,
/COM,RESULTS USING BEAM189 ELEMENTS:
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.4,'  ',F14.4,'   ',1F15.3)
/COM,-----------------------------------------------------------
/OUT
FINISH
*LIST,vm57,vrt

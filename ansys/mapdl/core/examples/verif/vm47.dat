/COM,ANSYS MEDIA REL. 150 (11/8/2013) REF. VERIF. MANUAL: REL. 150
/VERIFY,VM47
/PREP7
/TITLE, VM47, TORSIONAL FREQUENCY OF A SUSPENDED DISK
C*** VIBRATION THEORY AND APPLICATIONS, THOMSON, 2ND PRINTING, PAGE 10, EX 1.3-2
ANTYPE,MODAL
MODOPT,LANB,1,,,     
ET,1,COMBIN14,,,1    ! THREE-DIMENSIONAL TORSIONAL SPRING
ET,2,MASS21,,,3      ! TWO-DIMENSIONAL MASS WITH ROTARY INERTIA
R,1,4.8              ! REAL CONSTANT SET #1 SPRING CONSTANT
R,2,1,.30312         ! REAL CONSTANT SET #2 MASS & IZZ(J)
N,1                  ! BEGIN NODE DEFINITION
N,2,,,-1
E,1,2                ! DEFINE BEAM ELEMENT
TYPE,2               ! DEFINE ACTIVE ELEMENT TYPE AS SET 2
REAL,2               ! DEFINE ACTIVE REAL CONSTANT TYPE AS SET 2
E,2                  ! DEFINE MASS AT END OF WIRE
OUTPR,BASIC,1
D,1,ALL              ! CONSTRAIN END OF WIRE IN ALL DOF
D,2,UX,,, ,,UY,UX    ! PREVENT TRANSLATION OF THE MASS
FINISH
/SOLU    
SOLVE
*GET,FREQ,MODE,1,FREQ
*DIM,LABEL,CHAR,1,2
*DIM,VALUE,,1,3
LABEL(1,1) = '      F,'
LABEL(1,2) = ' (Hz)   '
*VFILL,VALUE(1,1),DATA,.63333 
*VFILL,VALUE(1,2),DATA,FREQ 
*VFILL,VALUE(1,3),DATA,ABS(FREQ/.63333 ) 
/COM
/OUT,vm47,vrt
/COM,------------------- VM47 RESULTS COMPARISON ---------------
/COM,
/COM,                 |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.5,'  ',F14.5,'   ',1F15.3)
/COM,-----------------------------------------------------------
/OUT

FINISH
*LIST,vm47,vrt


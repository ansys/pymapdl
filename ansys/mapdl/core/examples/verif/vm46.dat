/COM,ANSYS MEDIA REL. 150 (11/8/2013) REF. VERIF. MANUAL: REL. 150
/VERIFY,VM46
/TITLE, VM46, TWO DIMENSIONAL END NOTCHED FLEXURE PROBLEM
/COM, REFERENCE:" J.F.MANDELL, ET AL., PREDICTION OF DELAMINATION IN 
/COM,             WIND TURBINE BLADE STRUCTURAL DETAILS
/COM,             JOURNAL OF SOLAR ENERGY ENGINEERING, VOL. 25, 2003, PG: 522-530
/COM,
/OUT,SCRATCH
/PREP7
ET,1,PLANE182               ! ELEMENT TYPE 182
KEYOPT,1,3,2                ! PLANE STRAIN

!MATERIAL PROPERTIES
YOUNG = 210000              ! YOUNG'S MODULUS
NU = 0.3                    ! POISSION'S RATIO
MP,EX,1,YOUNG               !MPa
MP,PRXY,1,NU
L=30                        ! DISTANCE FROM THE SUPPORT POINT TO LOADING POINT
H=0.6                       ! THICKNESS OF BEAM
AA=10.0                     ! CRACK LENGTH
D=3                         ! THE LEFT END TO SUPPORT POINT
BB=0.2                      ! FINE MESH AREA
PP=-10                      ! LOADING
B=1                         ! SPECIMEN WIDTH
NN=1                        ! CHANGE OF MESH SIZE

K,1,0,0
K,2,2*(D+L)
K,3,2*(D+L),2*H
K,4,,2*H
K,5,,H
K,6,D+AA,H
K,7,,H
K,8,D+AA-BB,H
K,9,D+AA-BB,H+BB
K,10,D+AA+BB,H+BB
K,11,D+AA+BB,H-BB
K,12,D+AA-BB,H-BB
K,13,D+AA-BB,H
K,21,D,0
K,22,2*(D+L)-D,0
K,23,D+L,2*H
K,26,D+AA+BB,H

A,6,26,10,9,8
A,13,12,11,26,6
A,2,3,10,26,11
A,1,21,12,13,7
A,21,22,2,11,12
A,3,23,4,9,10
A,4,5,8,9

AESIZE,1,BB/10*NN   
AESIZE,2,BB/10*NN
ALLSEL
AMESH,1,2
ASEL,U,AREA,,1,2
AESIZE,ALL,BB/2*NN
AMESH,ALL
ALLSEL

R,3 
REAL,3  
ET,2,TARGE169
ET,3,CONTA172
R,3,,,1.0,0.1,0,
RMORE,,,1.0E20,0.0,1.0, 
RMORE,0.0,0,1.0,,1.0,0.5
RMORE,0,1.0,1.0,0.0,,1.0
KEYOPT,3,3,0
KEYOPT,3,4,0
KEYOPT,3,5,0
KEYOPT,3,7,0
KEYOPT,3,8,0
KEYOPT,3,9,0
KEYOPT,3,10,2   
KEYOPT,3,11,0   
KEYOPT,3,12,0   
KEYOPT,3,2,0

LSEL,S,,,5,23,18                   ! Generate the target surface   
CM,_TARGET,LINE 
TYPE,2  
NSLL,S,0
ESLN,S,0
ESURF   
ALLSEL

LSEL,S,,,9,15,6                    ! Generate the contact surface  
CM,_CONTACT,LINE
TYPE,3  
NSLL,S,0
ESLN,S,0
ESURF   
ALLSEL  

FINISH

/SOLU
TIME,1
KSEL,S,KP,,21
NSLK,S
D,ALL,UX
D,ALL,UY
ALLSEL
KSEL,S,KP,,22
NSLK,S
D,ALL,UY
ALLSEL
KSEL,S,KP,,23
NSLK,S
F,ALL,FY,PP
ALLSEL
KSEL,S,KP,,6
NSLK,S
CM,TIPP,NODE
ALLSEL,ALL

CINT,NEW,1                    ! DEFINE CRACK ID
CINT,TYPE,VCCT 
CINT,CTNC,TIPP                ! DEFINE CRACK TIP NODE COMPONENT         
CINT,SYMM,OFF                 ! SYMMETRY OFF
CINT,NORMAL                   ! DEFINE CRACK PLANE NORMAL
CINT,LIST
ALLSEL,ALL

SOLVE
FINISH

/OUT,SCRATCH
/POST1
/SHOW
/ESHAPE,1
/GRAPHICS,POWER
PRCINT,1
CRACK=NODE(13.00,0.60,0.00)
*GET,G1_ANSYS,CINT,1,,CRACK,,1,,G1
*GET,G2_ANSYS,CINT,1,,CRACK,,1,,G2
PLNSOL, U,Y, 0,1.0
*GET,UY_MIN,PLNSOL,0,MIN
CC=UY_MIN/PP                  ! specimen compliance (=center point deflection / PP)

/OUT
G_ANSYS=ABS(G1_ANSYS)+ABS(G2_ANSYS)
G_REF=(9*PP*PP*AA*AA*CC)/((2*B)*(2*L*L*L+3*AA*AA*AA))    ! equation (2) in the reference
/COM
/OUT,vm46,vrt
/COM, -------------- VM 46 RESULTS COMPARISON -------------
/COM,
/COM,             |  TARGET    |      Mechanical APDL     |  RATIO
/COM,
/COM,     G COMPUTATION FOR VCCT
/COM,
*VWRITE, 'G',  G_REF, G_ANSYS, G_ANSYS/G_REF
(1X,A8,'     ', F10.5, '       ', F14.5, '     ', F15.3)
/OUT
FINISH
*LIST, vm46,vrt

/COM,ANSYS MEDIA REL. 150 (11/8/2013) REF. VERIF. MANUAL: REL. 150
/VERIFY,VM164
/PREP7
/TITLE, VM164, DRYING OF A THICK WOODEN SLAB (DIFFUSION)
C***     HEAT, MASS AND MOMENTUM TRANS., ROHSENOW AND CHOI, 2ND. PR., PAGE 392
ANTYPE,TRANS
ET,1,LINK33
R,1,1                   ! ARBITRARY AREA
MP,KXX,1,4E-5           ! DIFFUSION COEFFICIENT D
MP,DENS,1,1             ! ARBITRARY DENSITY AND CAPACITANCE
MP,C,1,1
N,1
N,11,(1/12)
FILL
E,1,2
EGEN,10,1,1
TUNIF,30                ! INITIAL MOISTURE CONCENTRATION (THAT OF WOOD)
D,11,TEMP,5             ! FINAL MOISTURE CONCENTRATION (AMBIENT)
FINISH
/SOLU    
SOLCONTROL,0
AUTOTS,ON
OUTPR,,LAST
DELTIM,0.434
TIME,127                ! TIME AT END OF LOAD STEP
KBC,1                   ! STEP BOUNDARY CONDITIONS
/OUT,SCRATCH
SOLVE
/POST1
SET,LAST
/OUT,
*GET,T,NODE,1,TEMP
*DIM,LABEL,CHAR,1,2
*DIM,VALUE,,1,3
LABEL(1,1) = '      C '
LABEL(1,2) = ' % '
*VFILL,VALUE(1,1),DATA,10
*VFILL,VALUE(1,2),DATA,T
*VFILL,VALUE(1,3),DATA,ABS(T/10)
/COM
/OUT,vm164,vrt
/COM,------------------- VM164 RESULTS COMPARISON -------------
/COM,
/COM,                 |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.1,'  ',F13.1,'   ',1F15.3)
/COM,----------------------------------------------------------
/OUT
FINISH
*LIST,vm164,vrt

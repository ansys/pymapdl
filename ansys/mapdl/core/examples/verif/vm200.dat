/COM,ANSYS MEDIA REL. 150 (11/8/2013) REF. VERIF. MANUAL: REL. 150
/VERIFY,VM200
JPGPRF,500,100,1
/SHOW,JPEG
/PREP7
/TITLE, VM200, VISCOELASTIC SANDWICH SEAL ANALYSIS
/COM,    ----- 2-D ANALYSIS -----
/COM,    "FE CALCULATIONS OF RESIDUAL STRESSES ....",SOULES ET AL.
ET,1,PLANE183,,,1            ! AXISYMMETRIC 2-D QUADRATIC ELEMENT
KEYOPT,1,6,0     !U-P formulation; 1= mixed, 0=U only
*CREATE,MAC1
/COM,    MATERIAL ONE IS G-11 GLASS AND MATERIAL TWO IS ALUMINA
/COM,    NOTE THAT ALUMINA IS AN ELASTIC MATERIAL THEREFORE IT
/COM,     DOES NOT HAVE VISCOELASTICITY AND STRUCTURAL RELAXATION
/COM,    MATERIAL PROPERTIES OF ALUMINA
MP,EX,  2, 3.73113E5
MP,PRXY,2, 0.3
TB,PRONY,2,1,1,SHEAR
TBDATA,1,0.0,1E-7
TB,SHIFT,2,1,1,FICT
TBDATA, 1,    618,      0.0,     1.0 ! SHIFT FUNCTION PARAMETERS
TBDATA, 4,    618,      1.0,     0.0 ! FICTIVE TEMP PARAMETERS, TFI, CFI, TAUFI
TBDATA, 7,52.6E-7, 0.119E-7, -1.0E-11 ! GLASS CTE COEFFICIENTS
TBDATA,12,52.6E-7, 0.119E-7, -1.0E-11 ! LIQUID CTE COEFFICIENTS
/COM,    MATERIAL PROPERTIES OF G-11 GLASS
MP,EX,  1, 7.2548E4
MP,PRXY,1, 0.3
TB,PRONY,1,1,3,SHEAR         ! DEVIATORIC VISCOELASTIC PROPERTIES
TBDATA,1,0.422,0.0689
TBDATA,3,0.423,0.0065
TBDATA,5,0.155,0.0001
TB,SHIFT,1,1,6,FICT           ! TN TTS W/ FICTIVE TEMPERATURE
TBDATA, 1, 618, 6.45E4, 0.53  ! SHIFT FUNCTION PARAMETERS
TBDATA, 4, 618,  0.108, 3.0   ! 1ST FICTIVE TEMP
TBDATA, 7, 618,  0.443, 0.671 ! 2ND FICTIVE TEMP
TBDATA,10, 618,  0.166, 0.247 ! 3RD FICTIVE TEMP
TBDATA,13, 618,  0.161, 0.091 ! 4TH FICTIVE TEMP
TBDATA,16, 618,  0.046, 0.033 ! 5TH FICTIVE TEMP
TBDATA,19, 618,  0.076, 0.008 ! 6TH FICTIVE TEMP
TBDATA,22, 64.7E-7, 0.02E-7,  ! GLASS CTE COEFFICIENTS
TBDATA,27, 3.43E-5,           ! LIQUID  CTE COEFFICIENTS
*END
*USE,MAC1                   ! EXECUTE MACRO FOR MATERIAL PROPERTIES
/COM,   CREATE FINITE ELEMENT MODEL
N,1,
N,3,,0.00025
FILL
N,5,0,(0.00025+0.00325)
FILL
NGEN,3,10,1,5,1,.001
MAT,2
E,1,21,23,3,11,22,13,2
MAT,1
E,3,23,25,5,13,24,15,4
/COM,    APPLY BOUNDARY CONDITIONS AND COUPLING
NSEL,S,LOC,Y
DSYM,SYMM,Y
NSEL,S,LOC,X
DSYM,SYMM,X
NSEL,ALL
D,1,ALL
CP,1,UX,21,22,23,24,25
CP,2,UY,2,22
CP,3,UY,3,13,23
CPSGEN,2,2,2,3,1
FINISH
/COM    SINCE THE SOLUTION OUTPUT IS VOLUMINOUS IT IS DIVERTED TO A
/COM     SCRATCH FILE
/OUTPUT,SCRATCH
*CREATE,MAC2                   ! CREATE MACRO FOR ANALYSIS TYPE AND LOADING
/SOLU
SOLCONTROL,0
ANTYPE,STATIC
/COM,    TEMPERATURE SET UP
TREF,618
TOFFST,273
TUNIF,618
TIME,1E-5
CNVTOL,F,,,,.001             ! VERY SMALL MINIMUM ENFORCED
                               !  FOR CONVERGENCE
SOLVE
OUTRES,ESOL,1                  ! STORE RESULTS FOR EVERY SUBSTEP
NSUBST,200
TUNIF,460                      ! COOLING
TIME,3160
SOLVE
TIME,(14400+3160)              ! ISOTHERMAL HOLD
SOLVE
TUNIF,18                       ! FURTHER COOLING
TIME,(14400+12000)
SOLVE
*END
FINISH
*USE,MAC2                      ! EXECUTE ANALYSIS AND LOADING MACRO
/OUTPUT
/POST26
ESOL,2,2,,BFE,TEMP
ESOL,3,2,3,S,X,STRESS
*CREATE,MAC3                   ! MACRO FOR PROCESSING RESULTS
XVAR,2
/GRID,1
/AXLAB,X,TEMPERATURE
/AXLAB,Y,IN-PLANE STRESS (MPA)
PLVAR,3
*GET,MXSX,VARI,3,EXTREM,VMAX   ! MAXIMUM IN-PLANE STRESS
NSTORE,20                      ! STORE EVERY 20TH TIME POINT RESULTS
PRVAR,2,3
*END
*USE,MAC3                      ! EXECUTE POSTPROCESSING MACRO
*SET,P1,(MXSX)
*GET,T1,VARI,3,EXTREM,TMAX
*GET,TE,VARI,2,RTIME,T1
*DIM,LABEL,CHAR,2,2
*DIM,VALUE,,2,3
LABEL(1,1) = 'PRES MX ','TEMP '
LABEL(1,2) = 'MPa','DEG C'
*VFILL,VALUE(1,1),DATA,12.5,460
*VFILL,VALUE(1,2),DATA,P1,TE
*VFILL,VALUE(1,3),DATA,ABS(P1/12.5),ABS(TE/460)
SAVE,TABLE_1
FINISH
/CLEAR,NOSTART                 ! CLEAR THE DATABASE
/PREP7
/TITLE, VM200, VISCOELASTIC SANDWICH SEAL ANALYSIS
/COM,    ----- 3-D ANALYSIS -----
ET,1,SOLID186
KEYOPT,1,2,1                        
KEYOPT,1,6,1     !U-P formulation; 1= mixed, 0=U only
*USE,MAC1                      ! EXECUTE MACRO FOR MATERIAL PROPERTIES
/COM,   CREATE FINITE ELEMENT MODEL
N,1,
N,3,0.00025
FILL
N,5,(0.00025+0.00325)
FILL
NGEN,3,10,1,5,1,,.001
NGEN,3,100,1,25,1,,,0.001
MAT,2
E,1,3,23,21,201,203,223,221
EMORE,2,13,22,11,202,213,222,211
EMORE,101,103,123,121
EGEN,2,2,1,1,1,-1
NSLE,S
NSEL,INVE
NDELE,ALL
NSLE,S
/COM,    APPLY BOUNDARY CONDITIONS AND COUPLING
NSEL,S,LOC,Y
DSYM,SYMM,Y
NSEL,S,LOC,X
DSYM,SYMM,X
NSEL,S,LOC,Z
DSYM,SYMM,Z
NSEL,S,LOC,Y,0.002
CP,1,UY,ALL
NSEL,S,LOC,Z,0.002
CP,2,UZ,ALL
NSEL,S,LOC,X,0.00025
CP,3,UX,ALL
NSEL,S,LOC,X,0.0035
CP,4,UX,ALL
NSEL,ALL
FINISH
/COM,    SINCE THE SOLUTION OUTPUT IS VOLUMINOUS IT IS DIVERTED TO A
/COM,     SCRATCH FILE
/OUTPUT,SCRATCH
*USE,MAC2                       ! EXECUTE ANALYSIS AND LOADING MACRO
/OUTPUT
/POST26
ESOL,2,2,,BFE,TEMP
ESOL,3,2,3,S,Y,STRESS
*USE,MAC3                       ! EXECUTE POSTPROCESSING MACRO
*SET,P2,(MXSX)
*GET,T2,VARI,3,EXTREM,TMAX
*GET,TE2,VARI,2,RTIME,T2
*DIM,LABEL,CHAR,2,2
*DIM,VALUE,,2,3
LABEL(1,1) = 'PRES MX ','TEMP '
LABEL(1,2) = 'MPa','DEG C'
*VFILL,VALUE(1,1),DATA,12.5,460
*VFILL,VALUE(1,2),DATA,P2,TE2
*VFILL,VALUE(1,3),DATA,ABS(P2/12.5),ABS(TE2/460)
SAVE,TABLE_2
RESUME,TABLE_1
/COM
/OUT,vm200,vrt
/COM,------------------- VM200 RESULTS COMPARISON --------------
/COM,
/COM,                 |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
/COM,RESULTS USING VISCO88
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.1,'  ',F14.1,'   ',1F15.3)
/NOPR
RESUME,TABLE_2
/GOPR
/COM,
/COM,RESULTS USING VISCO89
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.1,'  ',F14.1,'   ',1F15.3)
/COM,-----------------------------------------------------------
/OUT
FINISH
/NOPR
/DELETE,TABLE_1
/DELETE,TABLE_2
/DELETE,MAC1
/DELETE,MAC2
/DELETE,MAC3
FINISH
*LIST,vm200,vrt

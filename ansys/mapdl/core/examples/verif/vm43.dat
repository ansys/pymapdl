/COM,ANSYS MEDIA REL. 150 (11/8/2013) REF. VERIF. MANUAL: REL. 150
/VERIFY,VM43
JPGPRF,500,100,1             ! MACRO TO SET PREFS FOR JPEG PLOTS
/SHOW,JPEG
/PREP7
SMRT,OFF
/TITLE, VM43, BENDING OF AN AXISYMMETRIC THICK PIPE UNDER GRAVITY LOADING
C***           FORMULAS FOR STRESS AND STRAIN, ROARK, 4TH ED., PAGE 112, NO. 33
ANTYPE,STATIC       ! STATIC ANALYSIS
ET,1,PLANE25,,,,,,2 ! PLANE25
MP,EX,1,30.E6       ! DEFINE MATERIAL PROPERTIES
MP,DENS,1,.00073
MP,NUXY,1,0         ! DEFINE NUXY AS 0.0
K,1,.5              ! DEFINE KEYPOINTS
K,2,.5,100
KGEN,2,1,2,1,.5     ! GENERATE 2 ADDITIONAL KEYPOINTS IN X DIRECTION
L,1,2               ! DEFINE LINES AND NUMBER OF DIVISIONS
LESIZE,1,,,12
L,2,4
LESIZE,2,,,1
L,3,4
LESIZE,3,,,12    
L,1,3
LESIZE,4,,,1     
A,3,1,2,4           ! DEFINE AREA
AMESH,1             ! MESH AREA 1
ACEL,386,,-386      ! GRAVITY AS THE SUM OF TWO HARMONICALLY VARYING LOADS
MODE,1,1            ! SYMMETRIC HARMONIC LOAD
NSEL,S,LOC,Y,0      ! SELECT NODES AT Y=0
D,ALL,ALL           ! CONSTRAIN IN ALL DOF
NSEL,S,LOC,Y,100    ! SELECT NODES AT Y=100
D,ALL,UY            ! CONSTRAIN IN Y DISPLACEMENT DOF (SYMMETRY PLANE) 
NSEL,ALL
FINISH
/SOLU    
OUTPR,BASIC,LAST    ! PRINT BASIC SOLUTION
/OUT,SCRATCH
SOLVE
FINISH
/POST1
SET,1,1,,,,0.0      ! READ IN RESULTS AT ANGLE=0.0
/VUP,1,X            ! DEFINE X AXIS AS VERTICAL AXIS FOR DISPLAYS
/WINDOW,1,-1,1,0,1  ! DEFINE AND TURN ON WINDOW 1
PLDISP,1            ! DISPLAY UNDISPLACED AND DISPLACED SHAPE OF PIPE
/OUT,
PRNSOL,U,COMP       ! PRINT DISPLACEMENTS
*GET,DEF_X,NODE,3,U,X
SET,1,1,,,,90.0     ! READ IN RESULTS AT ANGLE=90.0
/WINDOW,1,OFF       ! TURN OFF WINDOW 1
/NOERASE            ! DON'T ERASE EXISTING DISPLAY
/WINDOW,2,-1,1,-1,0 ! DEFINE AND TURN ON WINDOW 2
/VUP,2,X            ! DEFINE X AXIS AS VERTICAL AXIS FOR DISPLAYS
PLDISP,1            ! DISPLAY UNDIS. AND DISP. SHAPE AT NEW ANGLE
PRNSOL,U,COMP       ! PRINT DISPLACEMENTS
*GET,DEF_Z,NODE,3,U,Z
*DIM,LABEL,CHAR,2,2
*DIM,VALUE,,2,3
LABEL(1,1) = 'UX, IN  ','UZ, IN ('
LABEL(1,2) = '(ANG=0) ','ANG=90) '
*VFILL,VALUE(1,1),DATA,-.12524,.12524
*VFILL,VALUE(1,2),DATA,DEF_X,DEF_Z
*VFILL,VALUE(1,3),DATA,ABS(DEF_X/.12524 ) ,ABS( DEF_Z/.12524) 
/COM
/OUT,vm43,vrt
/COM,------------------- VM43 RESULTS COMPARISON ---------------
/COM,
/COM,                 |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.5,'  ',F14.5,'   ',1F15.3)
/COM,-----------------------------------------------------------
/COM,
/COM,-----------------------------------------------------------
/COM,NOTE: THERE ARE VERIFIED RESULTS IN VM43 NOT CONTAINED IN 
/COM,THIS TABLE
/COM,-----------------------------------------------------------
/OUT
FINISH
*LIST,vm43,vrt


/COM,ANSYS MEDIA REL. 150 (11/8/2013) REF. VERIF. MANUAL: REL. 150
/VERIFY,VM121
JPGPRF,500,100,1
/SHOW,JPEG
/TITLE,VM121,VOLTAGE FORCED COIL (1/8 SYMMETRY)
/VIEW,1,1,1,1
/VUP,1,Z
/COM,   REFERENCE (BENCHMARK DESCRIPTION AND EXPECTED RESULTS):
/COM,   "VOLTAGE FORCED COILS FOR 3D FINITE-ELEMENT ELECTROMAGNETIC MODELS," 
/COM,   IEEE TRANSACTIONS ON MAGNETICS, VOL. 24, NO.6 (1988)
/OUT,SCRATCH
C*** PARAMETERS (GEOMETRY, MESH SIZE)
PI=ACOS(-1)
RI=0.087				! COIL INNER RADIUS (M)
RO=0.116				! COIL OUTER RADIUS (M)
H=0.028				    ! COIL HEIGHT (M)
A=0.240				    ! PLATE EDGE LENGTH (M)
T=0.0127				! PLATE THICKNESS (M)
GAP=0.03435				! GAP BETWEEN PLATES (M)
D_DMN=0.100				! DEPTH OF SURROUNDING DOMAIN (M)
ESZ1=0.0095 			! MESH SIZE, COIL 
ESZ2=5*ESZ1				! MESH SIZE, ENCLOSURE

C*** OTHER PARAMETERS
C*** DETERMINATION OF COIL REAL CONSTANTS/RESISTIVITY
C*** SERIES RESISTOR, PLATE RESISTIVITY, EXCITATION
R_COIL=12.4				! TARGET COIL DC RESISTANCE (OHM)
TURNS=700				! COIL # OF TURNS
XC=(RI+RO)/2			! COIL AVERAGE RADIUS
CARE=(RO-RI)*H			! COIL CROSS SECTION AREA (M^2)
VOLU=H*PI*(RO**2-RI**2)		! COIL VOLUME (M^3)
RSV_PLATE=1/3.28E7		! PLATE RESISTIVITY (OHM-M)
V=20					! STEP APPLIED VOLTAGE
T_FINAL=0.07			! END OF TRANSIENT (S)

C*** CREATE GEOMETRY
/PREP7
WPCS,-1,0				! COIL CYLINDRICAL ESYS
CSWP,11,1
CSYS
VSEL,NONE				! COIL
CYLI,RI,RO,0,-H/2,0,90
VATT,3,3,3,11
CM,COIL_V,VOLU
VSEL,NONE				! PLATE
BLOCK,,A/2,,A/2,-GAP/2,-GAP/2-T
VATT,2,2,2
CM,PLATE_V,VOLU
ALLSEL,ALL				! PREPARE FOR BOOLEAN
CM,KEEP_V,VOLU
VSEL,NONE				! ENCLOSURE
BLOCK,,A/2+D_DMN,,A/2+D_DMN,0,-GAP/2-T-D_DMN
CM,SCRAP_V,VOLU
ALLSEL,ALL				! BOOLEAN
CMSEL,ALL	
VSBV,SCRAP_V,KEEP_V,,DELE,KEEP
CMSEL,U,KEEP_V
VATT,1,1,1
CM,AIR_V,VOLU
CMSEL,ALL
CMDELE,KEEP_V
CMPLOT

C*** DEFINE ATTRIBUTES AND MESH
ET,1,SOLID236			! AIR (AZ)
MP,MURX,1,1
ET,2,SOLID236,1			! PLATE (AZ,VOLT)
MP,MURX,2,1
MP,RSVX,2,RSV_PLATE
ET,3,SOLID236,2			! STRANDED COIL (AZ,VOLT,EMF)
MP,MURX,3,1
R,3,CARE,TURNS,VOLU,0,1,0
RMORE,R_COIL,8
VSEL,S,MAT,,2,3			! MESH COIL AND PLATE
ALLSEL,BELOW,VOLU
ESIZE,ESZ1
KESIZE,ALL,ESZ1
MSHAPE,1,3D
VMESH,ALL
VSEL,S,MAT,,1
ESIZE,ESZ2
VMESH,ALL
ALLSEL,ALL
EPLOT

C*** ELECTRICAL BCS
VSEL,S,MAT,,3
ALLSEL,BELOW,VOLU
CP,1,VOLT,ALL
CP,2,EMF,ALL
N_COIL=NDNEXT(0)
ALLSEL,ALL
D,N_COIL,VOLT,V       		! VOLTAGE LOAD 
ASEL,S,LOC,X	            ! CURRENT NORMAL @X=0 & Y=0 PLANES OF PLATE
ASEL,A,LOC,Y
VSEL,S,MAT,,2
ASLV,R
DA,ALL,VOLT
ALLSEL,ALL
C*** MAGNETIC BCS
ASEL,S,LOC,X	            ! X SYMMETRY PLANE: FLUX PARALLEL
DA,ALL,AZ
ASEL,S,LOC,Y	            ! Y SYMMETRY PLANE: FLUX PARALLEL
DA,ALL,AZ
ALLSEL,ALL		            ! ALL OTHER BOUNDARIES FLUX PARALLEL
ASEL,S,EXT
ASEL,U,LOC,X
ASEL,U,LOC,Y
ASEL,U,LOC,Z
DA,ALL,AZ
EPLOT
FINISH

C*** SOLVE
/COM,  COIL BETWEEN 2 CONDUCTIVE PLATES
/SOLU
ANTYPE,TRANS
TIME,T_FINAL
KBC,1
NSBS=50
NSUBST,NSBS
AUTOTS,OFF
OUTRES,ALL,ALL
KUSE,1
ALLSEL,ALL
SOLVE
FINISH

C*** POST PROCESS
/POST1
SET,FIRST
PLVE,B,,,,VECT,,ON
VSEL,S,MAT,,2
ALLS,BELO,VOLU
/OUT
PLVE,JT,,,,VECT,,ON
VSEL,S,MAT,,3
ALLS,BELO,VOLU
PLVE,JT,,,,VECT,,ON
FINI
/OUT,SCRATCH
/POST26
*DIM,VALUE,,2,3
*DIM,VALUE4,,2,3
*DIM,VALUE7,,2,3
*DIM,LABEL,CHAR,2		! PARAMETERS FOR POSTPROCESSING
LABEL(1) = 'C_PLATE'
LABEL(2) = 'C_FREE'
RFORCE,2,N_COIL,AMPS,,IC_PLATE
VGET,IC,2				! SAVE RESULTS TO ARRAY
VGET,VALUE(1,2),2,1E-2
VGET,VALUE4(1,2),2,4E-2
VGET,VALUE7(1,2),2,7E-2
FINISH

C*** CONVERT MODEL: COIL IN AIR
/COM, COIL IN AIR
/PREP7				! CONVERT PLATE ELEMENTS => AIR
ALLSEL,ALL
ET,2,236
DADELE,ALL,VOLT
FINISH
/SOLU					! SOLVE
ALLSEL,ALL
SOLVE
FINISH

C*** POST PROCESS
/POST26
RFORCE,2,N_COIL,AMPS,,IC_AIR
VPUT,IC,3,,,IC_PLATE    	! MOVE RESULTS FROM AN ARRAY
VGET,VALUE(2,2),2,1E-2
VGET,VALUE4(2,2),2,4E-2
VGET,VALUE7(2,2),2,7E-2
VALUE(1,1)=1.16			! ORIGINAL RESULTS
VALUE(2,1)=0.97
VALUE4(1,1)=1.51
VALUE4(2,1)=1.57
VALUE7(1,1)=1.58
VALUE7(2,1)=1.61
VALUE(1,3)=VALUE(1,2)/VALUE(1,1)
VALUE(2,3)=VALUE(2,2)/VALUE(2,1)
VALUE4(1,3)=VALUE4(1,2)/VALUE4(1,1)
VALUE4(2,3)=VALUE4(2,2)/VALUE4(2,1)
VALUE7(1,3)=VALUE7(1,2)/VALUE7(1,1)
VALUE7(2,3)=VALUE7(2,2)/VALUE7(2,1)
/OUT
*VLEN,2
/OUT,VM121_1,vrt
/COM
/COM,----------- VM121 RESULTS COMPARISON at Time=1e-2 Sec -------------
/COM,
/COM,          |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
*VWRITE,LABEL(1),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,'    ',F7.3,'     ',F10.3,'       ',1F15.3)
/COM,------------------------------------------------------------------
/OUT,VM121_4,vrt
/COM
/COM,----------- VM121 RESULTS COMPARISON at Time=4e-2 Sec -------------
/COM,
/COM,          |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
*VWRITE,LABEL(1),VALUE4(1,1),VALUE4(1,2),VALUE4(1,3)
(1X,A8,'    ',F7.3,'     ',F10.3,'       ',1F15.3)
/COM,------------------------------------------------------------------
/OUT,VM121_7,vrt
/COM
/COM,----------- VM121 RESULTS COMPARISON at Time=7e-2 Sec -------------
/COM,
/COM,          |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
*VWRITE,LABEL(1),VALUE7(1,1),VALUE7(1,2),VALUE7(1,3)
(1X,A8,'    ',F7.3,'     ',F10.3,'       ',1F15.3)
/COM,------------------------------------------------------------------
/OUT
/AXL,X,TIME (S)
/AXL,Y,COIL CURRENT (A)
PLVAR,2,3
PRVAR,2,3
*LIST,VM121_1,vrt
*LIST,VM121_4,vrt
*LIST,VM121_7,vrt
FINISH



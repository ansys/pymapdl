/COM,ANSYS MEDIA REL. 150 (11/8/2013) REF. VERIF. MANUAL: REL. 150
/VERIFY,VM205
JPGPRF,500,100,1             ! MACRO TO SET PREFS FOR JPEG PLOTS
/SHOW,JPEG
/PREP7
SMRT,OFF
/TITLE, VM205, ELLIPTIC MEMBRANE UNDER A UNIFORMLY-DISTRIBUTED LOAD
/COM,   NAFEMS (REF.58), TEST NO. LE1 (MODIFIED)
/COM,   USING 2-D STRUCTURAL SOLID, PLANE182
ANTYPE,STATIC   
ET,1,PLANE182,3,,3          ! DEFINE ELEMENT AS PLANE182 FOR PLANE STRESS 
                            !  WITH THICKNESS
MP,EX,1,210E9               ! DEFINE MATERIAL PROPERTIES
MP,NUXY,1,.3
R,1,0.1                     ! SET THICKNESS
LOCAL,11,1,,,,,,,0.5        ! DEFINE ELLIPTICAL COORD. SYSTEM
K,1,2,90                    ! CREATE MODEL GEOMETRY
K,2,2,0                     ! DEFINE KEYPOINTS
L,1,2                       ! DEFINE LINE SEGMENTS
LOCAL,12,1,,,,,,,0.8461585
K,3,3.25,90
K,4,3.25,0.0
L,3,4
CSYS,0
L,2,4
L,1,3
AL,2,4,1,3                  ! DEFINE AREA
DL,4,1,SYMM                 ! APPLY BOUNDARY CONDITIONS 
DL,3,1,SYMM
SFL,2,PRES,-10E6            ! APPLY LINE PRESSURE LOAD
MSHK,2                    ! MAPPED AREA MESH IF POSSIBLE
MSHA,0,2D                 ! USING QUADS
SAVE                        ! SAVE DATABASE
FINISH
ADAPT,4,7,,,1               ! USE Mechanical APDL PREDEFINED MACRO FOR ADAPTIVE MESHING
                            !  AND SOLUTION WITH NSOLN=3, STARGT=7, AND FACMX=1
*CREATE,MAC                 ! CREATE MACRO FOR POST PROCESSING 
/POST1
EPLOT                       ! PLOT ELEMENTS
PRERR                       ! PRINT THE ENERGY NORM PERCENT ERROR (SEPC)
NSEL,S,LOC,Y,0.0
NSEL,R,LOC,X,2.0
*GET,MNODE,NODE,,NUM,MAX
*GET,SY_D,NODE,MNODE,S,Y    ! GET DESIRED STRESS SY VALUE
NSEL,ALL
ESEL,ALL
*status,parm                     ! SHOW STATUS OF PARAMETERS
*END
*USE,MAC                    ! USE POST PROCESSING MACRO
*DIM,LABEL,CHAR,1,2
*DIM,VALUE,,1,3
LABEL(1,1) = 'TAN STR '
LABEL(1,2) = 'MPa'
*VFILL,VALUE(1,1),DATA,92.70
*VFILL,VALUE(1,2),DATA,(SY_D/1000000)
*VFILL,VALUE(1,3),DATA,ABS((SY_D/1000000)/92.7)
SAVE,TABLE1
FINISH
/CLEAR, NOSTART ! CLEAR DATABASE BEFORE STARTING PART 2
/COM,        USING 2-D 8-NODE STRUCTURAL SOLID, PLANE82 
/PREP7
SMRT,OFF
RESUME                      ! RESUME DATABASE
ET,1,PLANE183,,,3            ! DEFINE ELEMENT AS PLANE82 FOR PLANE STRESS 
                            !  WITH THICKNESS
FINISH
ADAPT,2,5,,,1               ! USE Mechanical APDL PREDEFINED MACRO FOR ADAPTIVE MESHING
                            !  AND SOLUTION WITH NSOLN=2, STARGT=5, AND FACMX=1
*USE,MAC                    ! USE POST PROCESSING MACRO
*DIM,VALUE,,1,3
*DIM,LABEL,CHAR,1,2
LABEL(1,1) = 'TAN STR '
LABEL(1,2) = 'MPa'
*VFILL,VALUE(1,1),DATA,92.70
*VFILL,VALUE(1,2),DATA,(SY_D/1000000)
*VFILL,VALUE(1,3),DATA,ABS((SY_D/1000000)/92.7)
SAVE,TABLE2
RESUME,TABLE1
/COM
/OUT,vm205,vrt
/COM,------------------- VM205 RESULTS COMPARISON --------------
/COM,
/COM,                  |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
/COM,PLANE182 RESULTS:
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.2,'  ',F14.2,'   ',1F18.3)
/NOPR
RESUME,TABLE2
/GOPR
/COM,
/COM,PLANE183 RESULTS:
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.2,'  ',F14.2,'   ',1F18.3)
/COM,----------------------------------------------------------
/OUT
FINISH
*LIST,vm205,vrt

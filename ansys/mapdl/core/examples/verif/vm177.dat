/COM,ANSYS MEDIA REL. 150 (11/8/2013) REF. VERIF. MANUAL: REL. 150
/VERIFY,VM177
/PREP7  
JPGPRF,500,100,1               ! MACRO TO SET PREFS FOR JPEG PLOTS
/SHOW,JPEG
/TITLE, VM177, NATURAL FREQUENCY OF SUBMERGED RING
/COM, REF. "FINITE ELEMENT SOLUTION OF FLUID STRUCTURE
/COM, INTERACTION PROBLEMS"  SCHROEDER & MARCUS
/COM, SHOCK & VIBRATION SYMPOSIUM, SAN DIEGO, 1975
/COM, USING FLUID30 AND SHELL63 AND FULL HARMONIC ANALYSIS (ANTYPE=3)
/OUT,SCRATCH

ET,1,FLUID30,,                   	! FLUID ELEMENTS INTERFACING WITH STRUCTURE
ET,2,SHELL63                   	! SHELL ELEMENTS TO MODEL STEEL RING
ET,3,FLUID30,,1                	! NON-INTERFACING FLUID ELEMENTS

R,1,1
R,2,0.25

MP,DENS,1,0.001156		 	    ! DENSITY DEFINES IN SLUGS PER CUBIC INCH
MP,SONC,1,57480.0              	! SPEED OF SOUND IN WATER, INCHES PER SECOND
MP,EX,2,30.E6			 	    ! DEFINES IN POUNDS PER SQUARE INCH
MP,DENS,2,0.0089			 	! DENSITY DEFINES IN SLUGS PER CUBIC INCH
MP,NUXY,2,0.3

PI = ACOS(-1)

CSYS,1
N,1,10.0
N,7,30.0
FILL
NGEN,9,10,1,10,1,0,(90/8)
NGEN,2,100,1,99,1,0,0,1        	! DEFINE UPPER PLANE OF NODES
E,1,101,111,11,2,102,112,12
EGEN,6,1,-1
EGEN,8,10,-6

TYPE,2
REAL,2
MAT,2
E,1,101,111,11
EGEN,8,10,-1
NSEL,S,LOC,X,10.0
ESLN
ESEL,INVE

TYPE,3
REAL,1
MAT,1
EMODIF,ALL                     	! CHANGE ELEMENT TYPE TO TYPE 3
ESEL,ALL

NSEL,S,LOC,X,10
SF,ALL,FSI                     	! COUPLE STRUCTURAL MOTION & FLUID PRESS.
NSEL,ALL

D,7,PRES,0.0,,87,10            	! SET PRESSURE AT OUTER RADIUS TO ZERO
D,107,PRES,0.0,,187,10

NSEL,S,LOC,X,10.0
NSEL,R,LOC,Y,90.0
DSYM,SYMM,X
NSEL,S,LOC,X,10.0
NSEL,R,LOC,Y,0.0
DSYM,SYMM,Y
NSEL,ALL

D,1,UZ,0.0,,7,6
D,81,UZ,0.0,,87,6

F,1,FX,1.0                     	! EXCITE THE EVEN MODES OF VIBRATION
F,81,FY,0,1.0
SAVE
FINISH


/SOLU
ANTYPE,MODAL
MODOPT,UNSYM,5
MXPAND,5,,,YES
SOLVE
FINISH


/SOLU    
ANTYPE,HARMIC                  	! FULL HARMONIC ANALYSIS
HARFRQ,10.56,10.58  	        ! SELECT FREQUENCY RANGE TO EXCITE THE 1ST MODE
						       
NSUBST,10,10,10
KBC,1
OUTRES,,1
SOLVE
FINISH

/POST26
NSOL,2,1,U,X
NSOL,3,41,U,X
NSOL,4,81,U,Y
/OUT,
PRVAR,2,3,4                    	! PRINT DISPLACEMENTS OF RING VS. FREQUENCY
/COM,
/COM, GETTING THE FREQUENCY AT WHICH RESONANCE OCCURS
/COM, EXPECT THE FREQUENCY TO BE SAME FOR ALL 3 VARIABLES
/COM,
*GET,MAXFRQ1,VARI,2,EXTREM,TMAX
*GET,MAXFRQ2,VARI,3,EXTREM,TMAX
*GET,MAXFRQ3,VARI,4,EXTREM,TMIN
FINISH

/POST1
SET,1,4
PLDISP,1
SET,1,4,,1
PLDISP,1
*GET,F1,ACTIVE,0,SET,FREQ
*DIM,LABEL,CHAR,1,2
*DIM,VALUE,,1,3
LABEL(1,1) = 'F1'
LABEL(1,2) = 'Hz'
*VFILL,VALUE(1,1),DATA,10.198
*VFILL,VALUE(1,2),DATA,(MAXFRQ1)
*VFILL,VALUE(1,3),DATA,ABS(MAXFRQ1/10.198)
FINISH
SAVE,TABLE_1
/CLEAR, NOSTART                	! CLEAR DATABASE BEFORE STARTING PART 2

JPGPRF,500,100,1               	! MACRO TO SET PREFS FOR JPEG PLOTS
/PREP7
/COM,
/COM, USING FLUID29 AND BEAM188 AND UNSYMMETRIC MATRIX MODAL ANALYSIS (ANTYPE=2)
/COM,
/OUT,SCRATCH

ET,1,FLUID29                   	! FLUID ELEMENTS INTERFACING WITH STRUCTURE
ET,2,BEAM188                   	! BEAM ELEMENTS TO MODEL STEEL RING
ET,3,FLUID29,,1                	! NON-INTERFACING FLUID ELEMENTS

MP,EX,1,1
MP,DENS,1,0.001156		 	! DENSITY DEFINES IN SLUGS PER CUBIC INCH
MP,SONC,1,57480.0              	! SPEED OF SOUND IN WATER, INCHES PER SECOND
MP,EX,2,30.E6			 	! DEFINES IN POUNDS PER SQUARE INCH
MP,DENS,2,0.0089			 	! DENSITY DEFINES IN SLUGS PER CUBIC INCH
MP,NUXY,2,0.3

R,1,1
SECT,2,BEAM,RECT
SECD,0.25,1
PI=ACOS(-1)
CSYS,1
N,1,10.0
N,7,30.0
FILL
NGEN,9,10,1,10,1,0,(90/8)
E,1,2,12,11                    	! DEFINE FLUID ELEMENTS INTERFACE WITH STRUCTURE
EGEN,6,1,-1
EGEN,8,10,-6
TYPE,2                         
MAT,2
SECN,2
E,1,11                         	! DEFINE BEAM ELEMENTS TO MODEL STEEL RING
EGEN,8,10,-1
ESEL,,TYPE,,2
NSLE
NSEL,R,LOC,Y,90
DSYM,SYMM,X                    	! APPLY SYMMETRY BOUNDARY CONDITION ON RING
NSLE
NSEL,R,LOC,Y,0
DSYM,SYMM,Y
ESEL,ALL
NSEL,S,LOC,X,10
ESLN
ESEL,INVE
TYPE,3
MAT,1
REAL,1
EMODIF,ALL                     	! DEFINE UNCOUPLED FLUID ELEMENTS
NSEL,ALL
ESEL,S,TYPE,,1                 	! DEFINE FLUID-STRUCTURE INTERFACE
NSEL,R,LOC,X,10
SF,ALL,FSI                     	! COUPLE STRUCTURAL MOTION & FLUID PRESS.
ESEL,ALL
NSEL,S,LOC,X,30
D,ALL,PRES,0.0                 	! SET PRESSURE AT OUTER RADIUS TO ZERO
NSEL,ALL
ESEL,S,TYPE,,2
NSLE
D,ALL,UZ,,,,,ROTX,ROTY
ALLS,ALL
FINISH
/SOLU
ANTYPE,MODAL                   	! MODAL ANALYSIS
MODOPT,UNSYM,4,	             	! SELECT UNSYMMETRIC MATRIX MODE EXTRACTION
MXPAND,4,,,YES             		! EXPAND THE MODES  
	                         	
SOLVE 	
FINISH
/POST1
SET,1,1
PLDISP,1
/OUT,
*GET,F1,ACTIVE,0,SET,FREQ
*DIM,LABEL,CHAR,1,2
*DIM,VALUE,,1,3
LABEL(1,1) = 'F1'
LABEL(1,2) = 'Hz'
*VFILL,VALUE(1,1),DATA,10.198
*VFILL,VALUE(1,2),DATA,F1
*VFILL,VALUE(1,3),DATA,ABS(F1/10.198)
SAVE,TABLE_2
FINISH
/CLEAR, NOSTART                	! CLEAR DATABASE BEFORE STARTING PART 3


/PREP7
JPGPRF,500,100,1               	! MACRO TO SET PREFS FOR JPEG PLOTS
/SHOW,JPEG
/COM,
/COM,    USING FLUID30 AND SHELL181 AND FULL HARMONIC ANALYSIS (ANTYPE=3)
/COM,
/OUT,SCRATCH

ET,1,FLUID30,,                  	! FLUID ELEMENTS INTERFACING WITH STRUCTURE
ET,2,SHELL181,,,2               	! SHELL ELEMENTS TO MODEL STEEL RING
ET,3,FLUID30,,1                 	! NON-INTERFACING FLUID ELEMENTS

R,1,1

SECTYPE,1,SHELL
SECDATA,0.25,2,0,3

MP,DENS,1,0.001156		 	! DENSITY DEFINES IN SLUGS PER CUBIC INCH
MP,SONC,1,57480.0              	! SPEED OF SOUND IN WATER, INCHES PER SECOND
MP,EX,2,30.E6			 	! DEFINES IN POUNDS PER SQUARE INCH
MP,DENS,2,0.0089			 	! DENSITY DEFINES IN SLUGS PER CUBIC INCH
MP,NUXY,2,0.3

PI=ACOS(-1)
CSYS,1
N,1,10.0
N,7,30.0
FILL
NGEN,9,10,1,10,1,0,(90/8)
NGEN,2,100,1,99,1,0,0,1        	! DEFINE UPPER PLANE OF NODES
E,1,101,111,11,2,102,112,12
EGEN,6,1,-1
EGEN,8,10,-6

TYPE,2
SECNUM,1
MAT,2
E,1,101,111,11
EGEN,8,10,-1
NSEL,S,LOC,X,10.0
ESLN
ESEL,INVE
TYPE,3
REAL,1
MAT,1
EMODIF,ALL                     	! CHANGE ELEMENT TYPE TO TYPE 3
ESEL,ALL

NSEL,S,LOC,X,10
SF,ALL,FSI                     	! COUPLE STRUCTURAL MOTION & FLUID PRESS.
NSEL,ALL

D,7,PRES,0.0,,87,10            	! SET PRESSURE AT OUTER RADIUS TO ZERO
D,107,PRES,0.0,,187,10

NSEL,S,LOC,X,10.0
NSEL,R,LOC,Y,90.0
DSYM,SYMM,X
NSEL,S,LOC,X,10.0
NSEL,R,LOC,Y,0.0
DSYM,SYMM,Y
NSEL,ALL

D,1,UZ,0.0,,7,6
D,81,UZ,0.0,,87,6
F,1,FX,1.0
F,81,FY,0,1.0
ALLSEL,ALL
FINISH

/SOLU
ANTYPE,MODAL
MODOPT,UNSYM,5
MXPAND,5,,,YES
SOLVE
FINISH


/SOLU    
ANTYPE,HARMIC                  	! FULL HARMONIC ANALYSIS
HARFRQ,10.64,10.66           	! SELECT FREQUENCY RANGE TO EXCITE THE 1ST MODE 
						        
NSUBST,10,10,10
KBC,1
OUTRES,,1
SOLVE
FINISH

/POST26
NSOL,2,1,U,X
NSOL,3,41,U,X
NSOL,4,81,U,Y
/OUT,
PRVAR,2,3,4                    	! PRINT DISPLACEMENTS OF RING VS. FREQUENCY
/COM,
/COM, GETTING THE FREQUENCY AT WHICH RESONANCE OCCURS
/COM, EXPECT THE FREQUENCY TO BE SAME FOR ALL 3 VARIABLES
/COM,
*GET,MAXFRQ1,VARI,2,EXTREM,TMAX
*GET,MAXFRQ2,VARI,3,EXTREM,TMAX
*GET,MAXFRQ3,VARI,4,EXTREM,TMIN

*DIM,LABEL,CHAR,1,2
*DIM,VALUE,,1,3
LABEL(1,1) = 'F1'
LABEL(1,2) = 'Hz'
*VFILL,VALUE(1,1),DATA,10.198
*VFILL,VALUE(1,2),DATA,(MAXFRQ1)
*VFILL,VALUE(1,3),DATA,ABS(MAXFRQ1/10.198)
SAVE,TABLE_3
FINISH
/CLEAR,NOSTART

/PREP7
JPGPRF,500,100,1               	! MACRO TO SET PREFS FOR JPEG PLOTS
/SHOW,JPEG
/COM,
/COM,    USING FLUID220 AND SHELL281 AND FULL HARMONIC ANALYSIS (ANTYPE=3)
/COM,
/OUT,SCRATCH

ET,1,FLUID220,,                	! FLUID ELEMENTS INTERFACING WITH STRUCTURE
ET,2,SHELL281,,,               	! SHELL ELEMENTS TO MODEL STEEL RING
ET,3,FLUID220,,1               	! NON-INTERFACING FLUID ELEMENTS

R,1,1

SECTYPE,1,SHELL
SECDATA,0.25,2,0,3

MP,DENS,1,0.001156		 	! DENSITY DEFINES IN SLUGS PER CUBIC INCH
MP,SONC,1,57480.0              	! SPEED OF SOUND IN WATER, INCHES PER SECOND
MP,EX,2,30.E6			 	! DEFINES IN POUNDS PER SQUARE INCH
MP,DENS,2,0.0089			 	! DENSITY DEFINES IN SLUGS PER CUBIC INCH
MP,NUXY,2,0.3

PI=ACOS(-1)

CSYS,1

CYLINDER,10,30,0,-1,0,90

LSEL,S,LINE,,1,3,2
LSEL,A,LINE,,6,8,2
LESIZE,ALL,,,6
LSEL,ALL

LSEL,S,LINE,,2,4,2
LSEL,A,LINE,,5,7,2
LESIZE,ALL,,,8
LSEL,ALL

LSEL,S,LINE,,9,12,1
LESIZE,ALL,,,1
LSEL,ALL

TYPE,1
MAT,1
REAL,1
VMESH,1
ALLSEL,ALL

NSEL,S,LOC,X,10.0
ESLN
ESEL,INVE
TYPE,3
REAL,1
MAT,1
EMODIF,ALL
ALLSEL,ALL

TYPE,2
SECNUM,1
MAT,2
ASEL,S,AREA,,4
NSLA,S,1
ESURF
ALLSEL,ALL

ESEL,S,TYPE,,1
NSLE,S
NSEL,R,LOC,X,10
SF,ALL,FSI
ALLSEL,ALL

NSEL,S,LOC,Y,0,90
NSEL,R,LOC,X,30
D,ALL,PRES,0
NSEL,ALL

NSEL,S,LOC,X,10
NSEL,R,LOC,Y,90
DSYM,SYMM,X
NSEL,S,LOC,X,10.0
NSEL,R,LOC,Y,0.0
DSYM,SYMM,Y
NSEL,ALL

NSEL,S,NODE,,203
NSEL,A,NODE,,174
NSEL,A,NODE,,191
NSEL,A,NODE,,175
D,ALL,UZ,0
NSEL,ALL
ALLSEL,ALL

F,203,FX,1.0
F,191,FY,0,1.0
FINI

/SOLU
ANTYPE,MODAL
MODOPT,UNSYM,5
MXPAND,5,,,YES
SOLVE
FINISH


/SOLU    
ANTYPE,HARMIC                  	! FULL HARMONIC ANALYSIS
HARFRQ,10.27,10.29           	! SELECT FREQUENCY RANGE TO EXCITE THE 1ST MODE
						
NSUBST,10,10,10
KBC,1
OUTRES,,1
SOLVE
FINISH

/POST26
NSOL,2,203,U,X
NSOL,3,211,U,X
NSOL,4,191,U,Y
/OUT,
PRVAR,2,3,4                    	! PRINT DISPLACEMENTS OF RING VS. FREQUENCY
/COM,
/COM, GETTING THE FREQUENCY AT WHICH RESONANCE OCCURS
/COM, EXPECT THE FREQUENCY TO BE SAME FOR ALL 3 VARIABLES
/COM,
*GET,MAXFRQ1,VARI,2,EXTREM,TMAX
*GET,MAXFRQ2,VARI,3,EXTREM,TMAX
*GET,MAXFRQ3,VARI,4,EXTREM,TMIN

*DIM,LABEL,CHAR,1,2
*DIM,VALUE,,1,3
LABEL(1,1) = 'F1'
LABEL(1,2) = 'Hz'
*VFILL,VALUE(1,1),DATA,10.198
*VFILL,VALUE(1,2),DATA,(MAXFRQ1)
*VFILL,VALUE(1,3),DATA,ABS(MAXFRQ1/10.198)
SAVE,TABLE_4
FINISH
/CLEAR,NOSTART

/PREP7
JPGPRF,500,100,1               	! MACRO TO SET PREFS FOR JPEG PLOTS
/SHOW,JPEG
/COM,
/COM,    USING FLUID221 AND SHELL281 AND FULL HARMONIC ANALYSIS (ANTYPE=3)
/COM,
/OUT,SCRATCH

ET,1,FLUID221,,                	! FLUID ELEMENTS INTERFACING WITH STRUCTURE
ET,2,SHELL281,,,               	! SHELL ELEMENTS TO MODEL STEEL RING
ET,3,FLUID221,,1               	! NON-INTERFACING FLUID ELEMENTS

R,1,1

SECTYPE,1,SHELL
SECDATA,0.25,2,0,3

MP,DENS,1,0.001156		 	! DENSITY DEFINES IN SLUGS PER CUBIC INCH
MP,SONC,1,57480.0           ! SPEED OF SOUND IN WATER, INCHES PER SECOND
MP,EX,2,30.E6			 	! DEFINES IN POUNDS PER SQUARE INCH
MP,DENS,2,0.0089			! DENSITY DEFINES IN SLUGS PER CUBIC INCH
MP,NUXY,2,0.3

PI=ACOS(-1)

CSYS,1

CYLINDER,10,30,0,-1,0,90

LSEL,S,LINE,,1,3,2
LSEL,A,LINE,,6,8,2
LESIZE,ALL,,,8
LSEL,ALL

LSEL,S,LINE,,2,4,2
LSEL,A,LINE,,5,7,2
LESIZE,ALL,,,10
LSEL,ALL

LSEL,S,LINE,,9,12,1
LESIZE,ALL,,,1
LSEL,ALL

TYPE,1
MAT,1
REAL,1
VMESH,1
ALLSEL,ALL

NSEL,S,LOC,X,10.0
ESLN
ESEL,INVE
TYPE,3
REAL,1
MAT,1
EMODIF,ALL
ALLSEL,ALL

TYPE,2
SECNUM,1
MAT,2
ASEL,S,AREA,,4
NSLA,S,1
ESURF
ALLSEL,ALL

ESEL,S,TYPE,,1
NSLE,S
NSEL,R,LOC,X,10
SF,ALL,FSI
ALLSEL,ALL

NSEL,S,LOC,Y,0,90
NSEL,R,LOC,X,30
D,ALL,PRES,0
NSEL,ALL

NSEL,S,LOC,X,10
NSEL,R,LOC,Y,90
DSYM,SYMM,X
NSEL,S,LOC,X,10.0
NSEL,R,LOC,Y,0.0
DSYM,SYMM,Y
NSEL,ALL

NSEL,S,NODE,,386
NSEL,A,NODE,,387
NSEL,A,NODE,,423
NSEL,A,NODE,,407
D,ALL,UZ,0
NSEL,ALL
ALLSEL,ALL

F,423,FX,1.0
F,407,FY,0,1.0
FINI

/SOLU
ANTYPE,MODAL
MODOPT,UNSYM,5
MXPAND,5,,,YES
SOLVE
FINISH

/SOLU    
ANTYPE,HARMIC                  	! FULL HARMONIC ANALYSIS
HARFRQ,10.33,10.35             	! SELECT FREQUENCY RANGE TO EXCITE THE 1ST BENDING MODE 
						
NSUBST,10,10,10
KBC,1
OUTRES,,1
SOLVE
FINISH

/POST26
NSOL,2,423,U,X
NSOL,3,433,U,X
NSOL,4,407,U,Y
/OUT,
PRVAR,2,3,4                    	! PRINT DISPLACEMENTS OF RING VS. FREQUENCY
/COM,
/COM, GETTING THE FREQUENCY AT WHICH RESONANCE OCCURS
/COM, EXPECT THE FREQUENCY TO BE SAME FOR ALL 3 VARIABLES
/COM,
*GET,MAXFRQ1,VARI,2,EXTREM,TMAX
*GET,MAXFRQ2,VARI,3,EXTREM,TMAX
*GET,MAXFRQ3,VARI,4,EXTREM,TMIN

*DIM,LABEL,CHAR,1,2
*DIM,VALUE,,1,3
LABEL(1,1) = 'F1'
LABEL(1,2) = 'Hz'
*VFILL,VALUE(1,1),DATA,10.198
*VFILL,VALUE(1,2),DATA,(MAXFRQ1)
*VFILL,VALUE(1,3),DATA,ABS(MAXFRQ1/10.198)
SAVE,TABLE_5
FINISH
/CLEAR,NOSTART
/NOPR
RESUME,TABLE_1
/COM
/OUT,vm177,vrt
/COM,------------------- VM177 RESULTS COMPARISON --------------
/COM,
/COM,                 |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
/COM,RESULTS USING FLUID30 & SHELL63
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.2,'  ',F14.2,'   ',1F15.3)
/NOPR
RESUME,TABLE_2
/GOPR
/COM,
/COM,RESULTS USING FLUID29 & BEAM188
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.2,'  ',F14.2,'   ',1F15.3)
/NOPR
RESUME,TABLE_3
/GOPR
/COM,
/COM,RESULTS USING FLUID30 & SHELL181
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.2,'  ',F14.2,'   ',1F15.3)
/NOPR
RESUME,TABLE_4
/GOPR
/COM,
/COM,RESULTS USING FLUID220 & SHELL281
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.2,'  ',F14.2,'   ',1F15.3)
/NOPR
RESUME,TABLE_5
/GOPR
/COM,
/COM,RESULTS USING FLUID221 & SHELL281
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.2,'  ',F14.2,'   ',1F15.3)
/COM,-----------------------------------------------------------
/OUT
FINISH
*LIST,vm177,vrt
/DELETE,TABLE_1
/DELETE,TABLE_2
/DELETE,TABLE_3
/DELETE,TABLE_4
/DELETE,TABLE_5

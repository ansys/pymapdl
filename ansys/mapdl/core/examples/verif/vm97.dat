/COM,ANSYS MEDIA REL. 150 (11/8/2013) REF. VERIF. MANUAL: REL. 150
/VERIFY,VM97
/PREP7
/TITLE, VM97, TEMPERATURE DISTRIBUTION ALONG A STRAIGHT FIN
C***       PRINCIPLES OF HEAT TRANSFER, KREITH, 2ND. PRINTING, PAGE 57, EX. 2-13
ANTYPE,STATIC
ET,1,SHELL131,,,2,1        ! CONDUCTING SHELL ELEMENTS
ET,2,LINK34                ! CONVECTION ELEMENTS
L=(4/12)                   ! FIN LENGTH
B=(1/12)                   ! FIN WIDTH
SECTYPE,1,SHELL            ! SECTION INFORMATION
SECD,B
R,2,B/2                    ! CROSS-SECTIONAL AREA OF CONVECTION ELEMENTS
MP,KXX,1,15                ! CONDUCTIVITY
MP,HF,1,15                 ! CONVECTION COEFFICIENT
N,1
N,11,L
FILL
N,12,L
NGEN,2,20,1,12,1,,1
E,1,2,22,21
EGEN,10,1,1
TYPE,2
REAL,2
E,11,12                    ! CONVECTION ELEMENTS AT THE TIP OF THE FIN
EGEN,2,20,11
D,12,TEMP,100,,32,20       ! DEFINE TEMPERATURE FOR CONVECTION ELEMENTS
NSEL,S,LOC,X,0
D,ALL,TEMP,1100            ! APPLY WALL TEMPERATURE
NSEL,ALL
ESEL,S,TYPE,,1
SFE,ALL,1,CONV,,15         ! H = 15 AND TBULK = 100
SFE,ALL,1,CONV,2,100
SFE,ALL,2,CONV,,15
SFE,ALL,2,CONV,2,100
ESEL,ALL
FINISH
/SOLU
SOLVE
FINISH
/POST26
ESOL,2,1,1,HEAT,,HEAT
ESOL,3,1,21,HEAT,,HEAT
STORE
*GET,HEAT,VARI,2,EXTREM,VMAX
*GET,HEAT2,VARI,3,EXTREM,VMAX
HTTOT=(ABS(HEAT+HEAT2))
/POST1
*DIM,VALUE,,12,3
*VFILL,VALUE(1,1),DATA,1100,955,835,740,660,595,535,490,460,430
*VFILL,VALUE(11,1),DATA,416,5820
*DO,I,0,1,0.1              ! PRINT NODAL TEMPERATURES FOR NODES
NSEL,S,LOC,X,(I*L)         !  ALONG INCREMENTS OF 0.1*L
PRNSOL,TEMP
NNUM = NODE (I*L,0,0)
*GET,VAL,NODE,NNUM,TEMP
*VFILL,VALUE(I*10+1,2),DATA,VAL
*VFILL,VALUE(I*10+1,3),DATA,ABS(VALUE(I*10+1,2) / VALUE(I*10+1,1) )
*ENDDO
*VFILL,VALUE(12,2),DATA,HTTOT
*VFILL,VALUE(12,3),DATA,ABS(HTTOT/5820)
NSEL,S,LOC,X,0             ! SELECT NODES TO GET HEAT DISSIPATION RATE (Q)
PRRSOL,HEAT                ! PRINT NODAL HEAT FLOW REACTIONS
*DIM,LABEL,CHAR,12,2
LABEL(1,1) = 'T,F(AT X','T,F(AT X','T,F(AT X','T,F(AT X','T,F(AT X','T,F(AT X','T,F(AT X'
LABEL(8,1) = 'T,F(AT X','T,F(AT X','T,F(AT X','T,F(AT X','q,   BTU'
LABEL(1,2) = '/L = 0.0','/L = 0.1','/L = 0.2','/L = 0.3','/L = 0.4','/L = 0.5','/L = 0.6'
LABEL(8,2) = '/L = 0.7','/L = 0.8','/L = 0.9','/L = 1.0','/hr	  '
/COM
/OUT,vm97,vrt
/COM,------------------- VM97 RESULTS COMPARISON ---------------
/COM,
/COM,                 |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.0,'  ',F14.0,'   ',1F15.3)
/COM,-----------------------------------------------------------
/OUT
FINISH
*LIST,vm97,vrt

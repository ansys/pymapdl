/COM,ANSYS MEDIA REL. 150 (11/8/2013) REF. VERIF. MANUAL: REL. 150
/VERIFY,VM197
JPGPRF,500,100,1             ! MACRO TO SET PREFS FOR JPEG PLOTS
/SHOW,JPEG
/PREP7
/TITLE, VM197, IGES WRITE/READ FOR THICK-WALLED CYLINDER WITH SPHERICAL END CAPS
/STITLE,1, REASON COMPARE DIFFERENCES ARE ACCEPTABLE
/STITLE,2, relatively small cross product moments may differ
/COM,   ANY BASIC GEOMETRY TEXT

RADIUS=20                                ! INSIDE RADIUS
THICK=10                                 ! WALL THICKNESS
LENGTH=50                                ! CYLINDER LENGTH (WITHOUT END CAPS)

CYLIND,RADIUS,RADIUS+THICK,0,LENGTH,90,270    ! CREATE HOLLOW CYLINDER 
WPROTA,,90                      ! ROTATE WORKING PLANE FOR CREATION OF END CAPS
SPHERE,RADIUS,RADIUS+THICK,180,270     ! CREATE END CAP AT ONE END OF CYLINDER
WPOFFS,,LENGTH                         ! MOVE WORKING PLANE TO OTHER END OF CYL
SPHERE,RADIUS,RADIUS+THICK,90,180      ! CREATE END CAP AT OTHER END OF CYLINDER

/VIEW,1,1,1,1           ! SET VIEW DIRECTION FOR DISPLAY
/TYPE,1,4               ! SET TYPE OF DISPLAY TO PRECISE HIDDEN
/TRIAD,OFF              ! TURN OFF COORDINATE SYSTEM TRIAD ON DISPLAYS
WPSTYL,,,,,,,OFF        ! TURN OFF WORKING PLANE TRIAD ON DISPLAYS
VPLOT

IGESOUT,VM197,IGS            ! WRITE IGES FILE NAMED VM197.IGS
FINISH  
PARSAV,ALL                   ! SAVE PARAMETERS (TO BE AVAILABLE AFTER CLEAR)
/CLEAR, NOSTART 
/AUX15
IOPTN,IGES,SMOOTH            ! SELECT SMOOTH GEOMETRY IGES IMPORT
IOPTN,MERGE,YES
IOPTN,SOLID,YES
IGESIN,VM197,IGS             ! READ IN IGES FILE NAMED VM197.IGS
FINISH
/PREP7
/OUT,SCRATCH
VSUM                         ! PERFORM VSUM FOR SUBSEQUENT *GET OF TOT VOLUME

PARRES                       ! RESUME PARAMETERS SAVED PRIOR TO CLEAR
/OUT,
*GET,VOLUME,VOLU,,VOLU       ! VOLUME = TOTAL VOLUME OF GEOMETRY THAT WAS READ
                             ! IN FROM IGES FILE (FROM VSUM)

PI=(4.0)*ATAN(1.0)                     ! HAVE Mechanical APDL CALCULATE VALUE FOR PI
CAPVOL=2/3*PI*(((RADIUS+THICK)**3)-(RADIUS**3))  ! CALC VOL OF TWO END CAPS
CYLVOL=.5*LENGTH*PI*(((RADIUS+THICK)**2)-(RADIUS**2)) ! CALC VOL OF THE CYLINDER
CALCVOL=CAPVOL+CYLVOL        ! TOTAL CALC'D VOL = CYL VOL + END CAPS VOL

NORMVOL=VOLUME/CALCVOL       ! NORMALIZE Mechanical APDL'S VOLUME BY CALCULATED VOLUME

CAPVOL=                      ! DELETE PARAMETERS USED IN CALC. OF VOLUME
CYLVOL=
PI=
RADIUS=                      ! DELETE PARAMETERS USED IN MODEL GENERATION
THICK=
LENGTH=
*DIM,VALUE,,1,3
*DIM,LABEL,CHAR,1,2
LABEL(1,1) = 'VOLUME '
LABEL(1,2) = 'NO UNTS'
*VFILL,VALUE(1,1),DATA,79063
*VFILL,VALUE(1,2),DATA,VOLUME
*VFILL,VALUE(1,3),DATA,ABS(VOLUME/79063)
/COM
/OUT,vm197,vrt
/COM,------------------- VM197 RESULTS COMPARISON --------------
/COM,
/COM,                  |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.0,'  ',F15.0,'   ',1F18.3)
/COM,-----------------------------------------------------------
/OUT
FINISH
/NOPR
/DELETE,VM197,IGS
FINISH
*LIST,vm197,vrt

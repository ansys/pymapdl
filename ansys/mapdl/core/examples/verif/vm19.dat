/COM,ANSYS MEDIA REL. 150 (11/8/2013) REF. VERIF. MANUAL: REL. 150
/VERIFY,VM19
JPGPRF,500,100,1             ! MACRO TO SET PREFS FOR JPEG PLOTS
/SHOW,JPEG
/PREP7
/TITLE, VM19, RANDOM VIBRATION ANALYSIS OF A DEEP SIMPLY-SUPPORTED BEAM
/COM REFERENCE:  NAFEMS FORCED VIBRATION BENCHMARKS TEST 5R
ET,1,BEAM188                         ! DEFINE ELEMENT TYPE
KEYOPT,1,3,3
MP,EX,1,200E9                      ! DEFINE MATERIAL PROPERTIES
MP,NUXY,1,0.3
MP,ALPX,1,0.1E-5
MP,DENS,1,8000
SECT,1,BEAM,RECT
SECD,2,2
N,1,0
N,11,10
FILL
E,1,2
EGEN,10,1,1
FINISH

/SOLU
ANTYPE,MODAL                       ! DEFINE ANALYSIS TYPE
MXPAND,9,,,YES                     ! EXPAND 9 MODES, CALC. STRESS VALUES
MODOPT,LANB,9
D,1,UX,0,0,1,1,UY,UZ,ROTX          ! APPLY CONSTRAINTS
D,11,UY,0,0,11,1,UZ
/OUT,SCRATCH
SOLVE
*GET,FREQ,MODE,1,FREQ
FINISH
!/COPY,,tri,,mode,tri

/SOLU
ANTYPE,SPECTR                      ! PREFORM SPECTRUM PSD ANALYSIS
SPOPT,PSD,9,ON                     ! CALC. STRESS RESPONSE FOR FIRST 9 MODES
PSDUNIT,1,FORCE
DMPRAT,0.02
F,1,FY,-0.5E6                      ! SCALE LOADS
F,11,FY,-0.5E6
F,2,FY,-1E6,,10,1
PSDFRQ,1,1,0.1,70.
PSDVAL,1,1,1                       ! IN N**2/HZ
PFACT,1,NODE
PSDRES,DISP,REL
PSDCOM 
SOLVE
FINISH

/POST26
/OUT,
STORE,PSD,10
NSOL,2,6,U,Y
RPSD,8,2,2,0,2,,,1.0e-3
PRTIME,42.630,42.631
PRVAR,8
*GET,P1,VARI,8,RTIME,42.63
PM=P1*1000000
FINISH

/POST26
STORE,PSD,10
ESOL,3,5,6,SMISC,37  
RPSD,9,3,3,0,2,,,1.0e-3
PRTIME,42.630,42.631
PRVAR,9
*GET,P2,VARI,9,RTIME,42.63
PM2=P2/(1E12)
*DIM,LABEL,CHAR,3,2
*DIM,VALUE,,3,3
LABEL(1,1) = 'FREQ1','PEAK d','PSD(N/mm'
LABEL(1,2) = ' (Hz)',' mm^2/Hz','^2)^2/Hz'
*VFILL,VALUE(1,1),DATA,42.65,180.9,58515.6
*VFILL,VALUE(1,2),DATA,FREQ,PM,PM2
*VFILL,VALUE(1,3),DATA,ABS(FREQ/42.65) ,ABS(PM/180.9 ),ABS(PM2/58515.6 )
FINISH
/COM
/OUT,vm19,vrt
/COM,------------------- VM19 RESULTS COMPARISON ---------------------
/COM,
/COM,                 |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.2,'  ',F14.2,'   ',1F15.2)
/COM,-----------------------------------------------------------------
/COM,
/COM,-----------------------------------------------------------------
/COM,NOTE: THERE ARE VERIFIED RESULTS IN VM19 NOT CONTAINED IN
/COM,THIS TABLE
/COM,-----------------------------------------------------------------
/OUT
*LIST,vm19,vrt


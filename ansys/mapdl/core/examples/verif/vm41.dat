/COM,ANSYS MEDIA REL. 150 (11/8/2013) REF. VERIF. MANUAL: REL. 150
/VERIFY,VM41
/PREP7
MP,PRXY,,0.3
/TITLE, VM41, SMALL DEFLECTION OF A RIGID BEAM
!COM           REFERENCE - ANY BASIC STRENGTH OF MATERIAL BOOK
!COM           USING THICK BEAM GEOMETRY
ET,1,MATRIX27,,,4          ! KEYOPT(3)=4, INPUT DATA AS 12 X 12 STIFFNESS MATRIX
ET,2,BEAM188
R,1                        ! TABLE 1 REAL CONSTANTS FOR MATRIX27 STIFFNESS MATRIX
RMODIF,1,51,10000          ! MODIFY POSITIONS 51, 57 AND 78 IN TABLE 1
RMODIF,1,78,10000          ! RMODIF USED, RATHER THAN RMORE, FOR EASIER INPUT
RMODIF,1,57,-10000
SECT,2,BEAM,ASEC           ! RIGID BEAM PROPERTIES
SECD,100,1000,1,1000,,1
MP,EX,1,30E6
N,1
N,2
N,3,10
E,1,2                      ! STIFFNESS MATRIX ELEMENT
TYPE,2
SECN,2
E,2,3                      ! BEAM ELEMENT
OUTPR,ALL,1                ! PRINT ALL ITEMS
D,1,ROTZ
D,2,UX,,,,,UY
F,3,FY,-10
D,ALL,UZ
D,ALL,ROTX
D,ALL,ROTY
FINISH
/SOLU    
/OUT,SCRATCH
SOLVE
FINISH
/POST26
/OUT,
ESOL,2,2,,SMISC,32,SBEN
STORE
*GET,STRS_BEN,VARI,2,EXTREM,VMAX
FINISH
/POST1
*GET,DEF_X,NODE,3,U,X
*GET,DEF_Y,NODE,3,U,Y
*GET,ROT_Z,NODE,3,ROT,Z
*DIM,LABEL,CHAR,4,2
*DIM,VALUE,,4,3
LABEL(1,1) = 'DEFLECTI','DEFLECTI','ROTATION','SIG_BEND'
LABEL(1,2) = 'ON_X(in)','ON_Y(in)','   (rad)','   (psi)'
*VFILL,VALUE(1,1),DATA,0,-.1,-.01,0
*VFILL,VALUE(1,2),DATA,DEF_X,DEF_Y,ROT_Z,STRS_BEN
*VFILL,VALUE(1,3),DATA,0,ABS(DEF_Y/.1 ),ABS(ROT_Z/.01),0
SAVE,TABLE_1
FINISH
/PREP7
!COM         USING CONSTRAINT EQUATIONS
SECN,2                     ! BEAM PROPERTIES
SECD,.0625,.00032552,.00001,.00032552,,1
CE,1,,3,UY,1,2,ROTZ,-10    ! CONSTRAINT EQUATION
FINISH
/SOLU    
/OUT,SCRATCH
SOLVE
FINISH
/POST26
/OUT,
ESOL,2,2,,SMISC,32,SBEN
STORE
*GET,STRS_BEN,VARI,2,EXTREM,VMAX
FINISH
/POST1
*GET,DEF_X,NODE,3,U,X
*GET,DEF_Y,NODE,3,U,Y
*GET,ROT_Z,NODE,3,ROT,Z 
LABEL(1,1) = 'DEFLECTI','DEFLECTI','ROTATION','SIG_BEND'
LABEL(1,2) = 'ON_X(in)','ON_Y(in)','   (rad)','   (psi)'
*VFILL,VALUE(1,1),DATA,0,-.1,-.01,0
*VFILL,VALUE(1,2),DATA,DEF_X,DEF_Y,ROT_Z,STRS_BEN
*VFILL,VALUE(1,3),DATA,0,ABS(DEF_Y/.1 ),ABS(ROT_Z/.01),0
SAVE,TABLE_2
RESUME,TABLE_1
/COM
/OUT,vm41,vrt
/COM,------------------- VM41 RESULTS COMPARISON ---------------
/COM,
/COM,                 |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
/COM,RESULTS FOR THICK BEAM:
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.2,'  ',F13.2,'   ',1F15.3)
/NOPR
RESUME,TABLE_2
/GOPR
/COM,
/COM,RESULTS WITH CONSTRAINT EQUATION:
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.2,'  ',F13.2,'   ',1F15.3)
/COM,-----------------------------------------------------------
/OUT
FINISH
*LIST,vm41,vrt


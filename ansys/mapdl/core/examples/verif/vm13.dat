/COM,ANSYS MEDIA REL. 150 (11/8/2013) REF. VERIF. MANUAL: REL. 150
/VERIFY,VM13
/PREP7
/TITLE, VM13, CYLINDRICAL SHELL UNDER PRESSURE
C***   STR. OF MATL., TIMOSHENKO, PART 1, 3RD ED., PAGE 45, ART. 11
C***   AND UGURAL AND FENSTER, ADV. STRENGTH AND APPL. ELAS., 1981
ANTYPE,STATIC
ET,1,SHELL208
SECTYPE,1,SHELL
SECDATA,1
SECNUM,1
MP,EX,1,30E6
MP,NUXY,1,.3
N,1,60
N,2,60,10
E,1,2
CP,1,UX,1,2            ! COUPLE RADIAL DIRECTION
D,1,UY,,,,,ROTZ
D,2,ROTZ
F,2,FY,5654866.8       ! CAP FORCE
SFE,1,1,PRES,,500      ! INTERNAL PRESSURE
FINISH
/SOLU    
OUTPR,ALL,1
SOLVE
FINISH
/POST1
ETABLE,STRS_Y,S,Y
ETABLE,STRS_Z,S,Z
*GET,STRSS_Y,ELEM,1,ETAB,STRS_Y
*GET,STRSS_Z,ELEM,1,ETAB,STRS_Z
*DIM,LABEL,CHAR,2,2
*DIM,VALUE,,2,3
LABEL(1,1) = 'STRESS,Y ','STRESS,Z'
LABEL(1,2) = ' (psi)  ',' (psi)  '
*VFILL,VALUE(1,1),DATA,15000,29749
*VFILL,VALUE(1,2),DATA,STRSS_Y,STRSS_Z
*VFILL,VALUE(1,3),DATA,ABS(STRSS_Y/15000 ) ,ABS(STRSS_Z/29749 )
/COM
/OUT,vm13,vrt
/COM,------------------- VM13 RESULTS COMPARISON ---------------------
/COM,
/COM,                 |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.0,'  ',F12.0,'   ',1F15.3)
/COM,-----------------------------------------------------------------
/OUT
FINISH
*LIST,vm13,vrt

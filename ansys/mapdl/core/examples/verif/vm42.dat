/COM,ANSYS MEDIA REL. 150 (11/8/2013) REF. VERIF. MANUAL: REL. 150
/VERIFY,VM42
/PREP7
smrt,off
/TITLE, VM42, BARREL VAULT ROOF UNDER SELF WEIGHT
/COM, REF:  COOK, CONCEPTS AND APPL. OF F.E.A., 2ND ED., 1981, PP. 284-287.
C***          USING SHELL181 ELEMENTS
ANTYPE,STATIC
ET,1,SHELL181, , ,2
SECTYPE,1,SHELL
SECDATA,0.25,1,0,5
MP,EX,1,4.32E8         ! MATERIAL PROPERTIES
MP,NUXY,1,0.0
MP,DENS,1,36.7347
CSYS,1
K,1,25,50
K,2,25,50,25           ! DEFINE KEYPOINTS AND AREA
KGEN,2,1,2,1,,40
A,1,3,4,2
ESIZE,,4
AMESH,1
CSYS,0                 ! SWITCH BACK TO GLOBAL CARTESIAN C.S.   
NSEL,S,LOC,X
DSYM,SYMM,X            ! CONSTRAIN SYMMETRY PLANES
NSEL,S,LOC,Z
DSYM,SYMM,Z
NSEL,S,LOC,Z,25
D,ALL,UX,0,,,,UY,ROTZ  ! CONSTRAIN END OF ROOF
NSEL,ALL
ACEL,,9.8
FINISH
*CREATE,SOLVIT,MAC
/out,scratch
/SOLU    
SOLVE
FINISH
/out
/POST1
NSEL,S,NODE,,1,2,1     ! SELECT NODES AT POINTS A AND B
ESLN,S                 ! SELECT ELEMENTS CONTAINING NODES
PRNSOL,U,COMP
*GET,UYA,NODE,1,U,Y
*GET,UXA,NODE,1,U,X
RSYS,1                 ! DISPLAY RESULTS IN CYLINDRICAL SYSTEM
SHELL,TOP
PRNSOL,S,COMP
*GET,SIGZ_TOP,NODE,1,S,Z
*GET,SIGY_TOP,NODE,2,S,Y
SHELL,BOT
PRNSOL,S,COMP
*GET,SIGZ_BOT,NODE,1,S,Z
*GET,SIGY_BOT,NODE,2,S,Y
*DIM,LABEL,CHAR,6,2
*DIM,VALUE,,6,3
LABEL(1,1) = 'UYA     ','UXA     ','SIGZ, TO','SIGZ, BO','SIGTH,TO','SIGTH,BO'
LABEL(1,2) = '     (m)','     (m)','P_A (PA)','T_A (PA)','P_B (PA)','T_B (PA)'
*VFILL,VALUE(1,1),DATA,-.3019,-.1593,215570,340700,191230,-218740
*VFILL,VALUE(1,2),DATA,UYA,UXA,SIGZ_TOP,SIGZ_BOT,SIGY_TOP,SIGY_BOT
*VFILL,VALUE(1,3),DATA,ABS(UYA/.3019 ),ABS(UXA/.1593 ),ABS(SIGZ_TOP/215570 )
*VFILL,VALUE(4,3),DATA,ABS(SIGZ_BOT/340700),ABS(SIGY_TOP/191230),ABS(SIGY_BOT/218740)
FINISH
*END
SOLVIT                 ! USE MACRO SOLVIT
SAVE,TABLE_1
/CLEAR, NOSTART                ! CLEAR DATABASE FOR SECOND SOLUTION
C***          USING SHELL281 ELEMENTS
/PREP7
SMRT,OFF
ANTYPE,STATIC
ET,1,SHELL281
SECTYPE,1,SHELL
SECDATA,0.25,1,0,5
MP,EX,1,4.32E8         ! MATERIAL PROPERTIES
MP,NUXY,1,0.0
MP,DENS,1,36.7347
CSYS,1
K,1,25,50
K,2,25,50,25           ! DEFINE KEYPOINTS AND AREA
KGEN,2,1,2,1,,40
A,1,3,4,2
ESIZE,,4
AMESH,1
CSYS,0                 ! SWITCH BACK TO GLOBAL CARTESIAN C.S.   
NSEL,S,LOC,X
DSYM,SYMM,X            ! CONSTRAIN SYMMETRY PLANES
NSEL,S,LOC,Z
DSYM,SYMM,Z
NSEL,S,LOC,Z,25
D,ALL,UX,0,,,,UY,ROTZ  ! CONSTRAIN END OF ROOF
NSEL,ALL
ACEL,,9.8
FINISH
SOLVIT                 ! USE MACRO SOLVIT
SAVE,TABLE_2
/NOPR
RESUME,TABLE_1
/COM
/OUT,vm42,vrt
/COM,------------------- VM42 RESULTS COMPARISON ---------------
/COM,
/COM,                 |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
/COM,SHELL181
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F12.4,'  ',F15.4,'   ',1F15.3)
/NOPR
RESUME,TABLE_2
/COM,
/COM,SHELL281
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F12.4,'  ',F15.4,'   ',1F15.3)
/NOPR
/COM,-----------------------------------------------------------
/OUT
FINISH
*LIST,vm42,vrt

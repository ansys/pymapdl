/COM,ANSYS MEDIA REL. 150 (11/8/2013) REF. VERIF. MANUAL: REL. 150
/VERIFY,VME2
JPGPRF,500,100,1             ! MACRO TO SET PREFS FOR JPEG PLOTS
/SHOW,JPEG

/title,VME2, Drop Analysis Of A Block Onto A Spring Scale
/stitle,1,Reason COMPARE differences are acceptable:
/stitle,2,  Leading zero before decimals, Accuracy
! Beer and Johnson, Vector Mechanics for Engineers, pg 635 
/PREP7
ET,1,164
R,1
MP,EX,1,207E9
MP,NUXY,1,.29
MP,DENS,1,60
BLOCK,-.5,.5,8.25,8.75,-.5,.5,
VMESH,1
CM,BLOCK,NODE

ET,2,164
R,2
MP,EX,2,207E9
MP,NUXY,2,.29
MP,DENS,2,10
EDMP,RIGID,2,6,7
TYPE,2
REAL,2
MAT,2
BLOCK,-1,1,6,6.25,-1,1,
VMESH,2
ET,3,165
R,3
MP,EX,3,207E9
MP,NUXY,3,.29
MP,DENS,3,10
TB,DISC,3,,,0
TBDATA,1,20000

TYPE,3
REAL,3
MAT,3
N,1000
E,143,1000
NSEL,S,NODE,,1000
D,ALL,ALL
ALLS

NSEL,S,LOC,Y,6.25
CM,N1,NODE
NSEL,S,LOC,Y,8.25
CM,N2,NODE
EDCGEN,NTS,N2,N1
ALLS

*DIM,TIME,ARRAY,2
*DIM,ACCL,ARRAY,2
TIME(1)=0
TIME(2)=1.5
ACCL(1)=9.81
ACCL(2)=9.81
EDLOAD,ADD,ACLY,,BLOCK,TIME(1),ACCL(1)
/VIEW,1,1,1,1
/ANG,1
/AUTO,1
EPLOT
FINI

/SOLU
TIME,.75
EDRST,10
EDHT,50
NSEL,S,NODE,,143
CM,SCALE,NODE
EDHIST,SCALE
ALLS
SAVE
/COM  &COMPARE,NOCOMPARE
SOLVE
/COM  &COMPARE,NORMAL

/POST26
FILE,,his
NSOL,2,143,U,Y,DISPY
PLVAR,2
PRVAR,2
*GET,RES1,VARI,2,EXTREM,VMIN, ,
*DIM,LABEL,CHAR,1
*DIM,RES,,1,3
LABEL(1) = 'MAX Uy'
*VFILL,RES(1,1),DATA,0.225
*VFILL,RES(1,2),DATA,ABS(RES1)
*VFILL,RES(1,3),DATA,ABS(RES(1,2)/RES(1,1))
/OUT,vme2,vrt
/COM,
/COM,-------------------- VME2 DYNA RESULTS COMPARISION --------------------
/COM,
/COM,         |  TARGET  |  ANSYS  |  RATIO  |
/COM,
*VWRITE,LABEL(1),RES(1,1),RES(1,2),RES(1,3)
(1X,A8,'   ',F5.3,'      ',F5.3,'     ',F5.3)
/COM,
/COM,-----------------------------------------------------------------------
/OUT
*LIST,vme2,vrt
*DELETE,vme2,db
FINISH

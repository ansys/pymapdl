/COM,ANSYS MEDIA REL. 150 (11/8/2013) REF. VERIF. MANUAL: REL. 150
/VERIFY,VM60
/PREP7
SMRT,OFF
/TITLE,VM60: NATURAL FREQUENCY OF A CROSS-PLY LAMINATED SPHERICAL SHELL
C*** THEORETICAL SOLUTION IS FROM J.N. REDDY
C*** ASCE JOURNAL OF ENGINEERING MECHANICS VOL 110 #5  MAY,1984 PP794-809
C*** USING SHELL281
/PREP7
SMRT,OFF
ET,1,SHELL281
KEYOPT,1,8,1              ! WRITE LAYER RESULTS
SECTYPE,1,SHELL
SECDATA,0.5,1,0           ! LAYER 1: 0.5 THK, THETA 0 
SECDATA,0.5,1,90          ! LAYER 2: 0.5 THK, THETA 90
MP,EX,1,25E6
MP,EY,1,1E6
MP,EZ,1,25E6
MP,NUXY,1,.01
MP,NUYZ,1,.01
MP,NUXZ,1,.01
MP,GXY,1,.5E6
MP,GYZ,1,.2E6
MP,GXZ,1,.5E6
MP,DENS,1,1
CSYS,2                     ! SPHERICAL COORDINATES
K,1,300
K,2,300,19.0986
K,3,300,19.0986,19.0986
K,4,300,,19.0986
ESIZE,,4
A,1,2,3,4
AMESH,ALL
NROTAT,ALL
FINISH
/out,scratch
/SOLU    
ANTYPE,MODAL
MODOPT,LANB,5              ! EXTRACT 1ST 5 MODES
NSEL,R,LOC,Y,0
NSEL,A,LOC,Y,19,20
D,ALL,UX,,,,,ROTX,UZ,ROTY
NSEL,S,LOC,Z,0
NSEL,A,LOC,Z,19,20
D,ALL,UX,,,,,ROTX,UY,ROTZ
NSEL,ALL
SOLVE
/out
*GET,FREQ,MODE,1,FREQ
*DIM,LABEL,CHAR,1,2
*DIM,VALUE,,1,3
LABEL(1,1) = '      f,'
LABEL(1,2) = ' (Hz)   '
*VFILL,VALUE(1,1),DATA,.73215 
*VFILL,VALUE(1,2),DATA,FREQ
*VFILL,VALUE(1,3),DATA,ABS(FREQ/.73215)
SAVE,TABLE_1
FINISH
RESUME,TABLE_1
/COM
/OUT,vm60,vrt
/COM,------------------- VM60 RESULTS COMPARISON --------------
/COM,
/COM,                 |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
/COM,SHELL281
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.5,'  ',F14.5,'   ',1F15.3)
/COM,----------------------------------------------------------
/OUT
FINISH
*LIST,vm60,vrt
/DELETE,TABLE_1

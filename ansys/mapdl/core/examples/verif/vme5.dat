/COM,ANSYS MEDIA REL. 150 (11/8/2013) REF. VERIF. MANUAL: REL. 150
/VERIFY,vme5
JPGPRF,500,100,1             ! MACRO TO SET PREFS FOR JPEG PLOTS
/SHOW,JPEG
/TITLE,VME5, Pinned Bar Under Gravity Loading
/com, Ref: Maclean and Nelson, page 336

/PREP7          			! ENTER PREPROCESSOR
*AFUN,DEG				! SET MODE TO DEGREE
N,1,0.25*COS(120),0.25*SIN(120)		! DEFINE NODES
N,8,0,0,0
FILL,1,8
N,29,0.75*COS(-60),0.75*SIN(-60)
FILL,8,29
N,30,1
ET,1,161				! SET ELEMENT TYPE TO EXPLICIT BEAM
R,1,5/6,0.1,0.1,0.1,0.1			! REAL CONSTANT SET
E,1,2,30				! DEFINE ELEMENTS
*REPEAT,28,1,1,0
MP,EX,1,10E6				! DEFINE YOUNG'S MODULUS
MP,DENS,1,1.0				! DEFINE DENSITY
MP,NUXY,1,0.3				! DEFINE POISSON'S RATIO
D,8,UX					! CONSTRAIN PIVOT ALONG
D,8,UY					! UX AND UY
D,ALL,UZ				! CONTSRAIN ALL NODES ALONG UZ
NSEL,S,NODE,,8   			
NSEL,A,NODE,,30
NSEL,INVE
CM,GNODES,NODE				! CREATE NODAL COMPONENT
ALLSEL
FINISH
/SOLUTION				! ENTER SOLUTION
EDRST,100				! SPECIFY OUTPUT INTERVAL
EDHTIME,100				! SPECIFY TIME-HISTORY OUTPUT INTERVAL
NSEL,S,NODE,,29				! SELECT THE NODE AT THE END OF THE BEAM
CM,NODE29,NODE				! CREATE NODAL COMPONENT
ALLSEL
EDHIST,NODE29				! SPECIFY TIME-HISTORY OUTPUT FOR NODE 29
EDCTS,,0.4				! DEFINE SCALE FATOR FOR COMPUTED TIME STEP
*DIM,T,ARRAY,2				! DEFINE TIME ARRAY
*DIM,G,ARRAY,2				! DEFINE GRAVITY ARRAY
T(1)=0,0.5				! INITIALIZE THE TIME ARRAY
G(1)=9.8,9.8				! INITIALIZE THE GRAVITY ARRAY
EDLOAD,ADD,ACLY,,GNODES,T,G   		! SPECIFY THE LOAD
TIME,0.5                      		! SPECIFY THE SOLUTION TIME
/COM  &COMPARE,NOCOMPARE
SOLVE					! SOLVE THE PROBLEM
/COM  &COMPARE,NORMAL
FINISH
/POST26     				! ENTER TIME-HISTORY POSTPROCESSOR
FILE,vme5,his			! READ THE HISTORY FILE
NSOL,2,29,U,X				! DEFINE VARIABLE 2 AS UX AT NODE 29
DERIV,3,2,1,,VX				! DEFINE VARIABLE 3 AS VELOCITY AT NODE 29
NSOL,4,29,U,Y				! DEFINE VARIABLE 4 AS UY AT NODE 29
EXTREM,3,4
*GET,TEMP1,VARI,3,EXTREM,VMIN, ,
RES1 = ABS(TEMP1/0.75)
*DIM,RES,,1,3
*VFILL,RES(1,1),DATA,2.121
*VFILL,RES(1,2),DATA,ABS(RES1)
*VFILL,RES(1,3),DATA,ABS(RES(1,2)/RES(1,1))
/COM,
/OUT,vme5,vrt
/COM,-------------------- VME5 DYNA RESULTS COMPARISION --------------------
/COM,
/COM,                 TARGET  |  ANSYS  |  RATIO 
/COM,
/COM, |w| THTA = 0 deg 
/COM,
*VWRITE,RES(1,1),RES(1,2),RES(1,3)
('                ','   ',F5.3,'      ',F5.3,'     ',F5.3)
/COM,
/COM,-----------------------------------------------------------------------
/OUT
*LIST,vme5,vrt
FINISH






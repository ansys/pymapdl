/COM,ANSYS MEDIA REL. 150 (11/8/2013) REF. VERIF. MANUAL: REL. 150
/VERIFY,VM31
/PREP7
/TITLE, VM31, CABLE SUPPORTING HANGING LOADS
C***VECTOR MECHANICS FOR ENGINEERS, BEER AND JOHNSTON, 1962, PAGE 260, PROB. 7.8
ANTYPE,STATIC
ET,1,LINK180
SECTYPE,1,LINK   
SECDATA,0.1         ! AREA = .1
MP,EX,1,20E6
N,1                 ! DEFINE NODES
N,2,20,-5.56
N,3,30,-5
N,4,45,5.83
N,5,60,20
E,1,2               ! DEFINE ELEMENTS
EGEN,4,1,1
INIS,SET,CSYS,-2    ! ARBITRARY SMALL INITIAL STRAIN 
INIS,SET,DTYP,EPEL
INIS,DEFINE,,,,,1E-7
NSUBST,3
OUTPR,,3
OUTPR,NLOAD,3       ! PRINT NODAL FORCES
KBC,1               ! STEP CHANGE B.C.'S
D,1,ALL,,,5,4       ! BOUNDARY CONDITIONS AND LOADS
D,2,UZ,,,4
F,2,FY,-6
F,3,FY,-12
F,4,FY,-4
FINISH
/SOLU 
NLGEOM,ON           ! LARGE DEFLECTION TURNED ON   
/OUT,SCRATCH
SOLVE
FINISH
/POST26
RFORCE,2,1,F,X 
RFORCE,3,1,F,Y 
STORE
/OUT,
*GET,AX,VARI,2,EXTREM,VMAX
*GET,AY,VARI,3,EXTREM,VMAX
FINISH
/POST1
ETABLE,MFX,SMISC,1
*GET,MX_FOR_X,ELEM,4,ETAB,MFX

*DIM,LABEL,CHAR,3,2
*DIM,VALUE,,3,3
LABEL(1,1) = 'AX, (kip','AY, (kip','MAX TENS'
LABEL(1,2) = 's)      ','s)      ',' (kips) '
*VFILL,VALUE(1,1),DATA,-18.000,5.0000,24.762
*VFILL,VALUE(1,2),DATA,AX,AY,MX_FOR_X
*VFILL,VALUE(1,3),DATA,ABS(AX/18.000) ,ABS(AY/5.000),ABS(MX_FOR_X/24.762)
/COM
/OUT,vm31,vrt
/COM,------------------- VM31 RESULTS COMPARISON ---------------
/COM,
/COM,                 |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.4,'  ',F14.4,'   ',1F15.3)
/COM,-----------------------------------------------------------

/OUT
FINISH
*LIST,vm31,vrt


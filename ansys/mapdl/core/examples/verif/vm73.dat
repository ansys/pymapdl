/COM,ANSYS MEDIA REL. 150 (11/8/2013) REF. VERIF. MANUAL: REL. 150
/VERIFY,VM73
/PREP7                       
JPGPRF,500,100,1             ! MACRO TO SET PREFS FOR JPEG PLOTS
/SHOW,JPEG
/TITLE, VM73, FREE VIBRATION WITH COULOMB DAMPING
C***           MECHANICAL VIBRATIONS, TSE, MORSE, AND HINKLE, PAGE 175, CASE 1
ET,1,COMBIN40,,,,,,2         ! MASS AT NODE J OF ELEMENT
R,1,1E4,,(10/386),,1.875,30
N,1
N,2
E,1,2
FINISH
/SOLU    
SOLCONTROL,0
ANTYPE,TRANS                 ! NONLINEAR TRANSIENT DYNAMIC ANALYSIS
TRNOPT, , , , , ,HHT
D,1,UX
IC,2,UX,-1,0                 ! STRETCH SPRING
KBC,1                        ! STEP BOUNDARY CONDITION
CNVTOL,F,1,0.001             ! FORCE CONVERGENCE CRITERIA
TIME,.2025
NSUBST,404                   ! TO COMPLETE CIRCLE
OUTRES,,1
SOLVE
FINISH
/POST26
NSOL,2,2,U,X,UX              ! STORE UX DISPLACEMENT OF NODE 2 
ESOL,3,1,,SMISC,1,F1         ! STORE FORCE F1 OF ELEMENT 1 AS VARIABLE 3
PRVAR,2,3                    ! PRINT VARIABLES 2 AND 3
/GRID,1                      ! TURN GRID ON
/AXLAB,Y,DISP                ! Y AXIS LABEL AS DISP
/GTHK,CURVE,2                ! CURVE LINES THICKNESS RATIO OF 2
PLVAR,2                      ! DISPLAY VARIABLE 2
/AXLAB,Y,FORCE               ! Y AXIS LABEL AS FORCE
PLVAR,3                      ! DISPLAY VARIABLE 3
*GET,U1,VARI,2,RTIME,.09
*GET,U2,VARI,2,RTIME,.102
*GET,U3,VARI,2,RTIME,.183
*DIM,LABEL,CHAR,3,2
*DIM,VALUE,,3,3
LABEL(1,1) = 'U,IN(T=0','U,IN(T=0','U,IN(T=0'
LABEL(1,2) = '.09sec) ','.102sec)','.183sec)'
*VFILL,VALUE(1,1),DATA,.87208,.83132,-.74874
*VFILL,VALUE(1,2),DATA,U1,U2,U3
*VFILL,VALUE(1,3),DATA,ABS(U1/.87208 ),ABS(U2/.83132 ),ABS(U1/.87208 )
/COM
/OUT,vm73,vrt
/COM,------------------- VM73 RESULTS COMPARISON --------------
/COM,
/COM,                 |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.5,'  ',F14.5,'   ',1F15.3)
/COM,----------------------------------------------------------
/OUT
FINISH
*LIST,vm73,vrt

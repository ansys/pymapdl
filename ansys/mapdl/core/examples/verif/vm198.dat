/COM,ANSYS MEDIA REL. 150 (11/8/2013) REF. VERIF. MANUAL: REL. 150
/VERIFY,VM198
JPGPRF,500,100,1             ! MACRO TO SET PREFS FOR JPEG PLOTS
/SHOW,JPEG
EL='PLANE182'
*DIM,A,CHAR,2              ! DEFINE AND DIMENSION THE CHARACTER PARAMETER A 
A(1)='UX','UY'             ! SET CHARACTER STRINGS AS VALUES OF CHARACTER 
!                             PARAMETER A 
/PREP7
/TITLE, VM198, LARGE STRAIN IN-PLANE TORSION TEST (%EL%)
!       "SOME COMPUTATIONAL ASPECTS ....",NAGTEGAAL ET AL.
ET,1,PLANE182,,,2		   ! 2-D 4 NODE PLANE ELEMENT
MP,EX,1,7200
MP,NUXY,1,0.33
TB,BISO,1                  ! BILINEAR ISOTROPIC HARDENING
TBDATA,1,10                ! YIELD STRESS
TBDATA,2,40                ! TANGENT MODULUS
SAVE                       ! SAVE DATABASE FOR LATER USE
CSYS,1
N,1,10
N,11,20
FILL
NGEN,2,100,1,11,1,,3
E,1,2,102,101
EGEN,10,1,1,1
!         APPLY BOUNDARY CONDITIONS
NSEL,S,LOC,X,20
D,ALL,ALL                  ! CLAMP OUTER SURFACE
NSEL,ALL
!         ROTATE APPROPRIATE NODES AND APPLY COUPLING
LOCAL,11,0,,,,3          
NROTAT,101,111
CSYS,0
CP,1,A(1),2,102            ! USE A(1) VALUE FOR DOF LABEL
CP,2,A(2),2,102            ! USE A(2) VALUE FOR DOF LABEL
CPSGEN,9,1,1,2,1
FINISH
/OUTPUT,SCRATCH            ! DIVERT VOLUMINOUS SOLUTION OUTPUT
/SOLU
!     CREATE SOLUTION OPTIONS AND LOADING MACRO FOR MULTIPLE USE
*CREATE,SOLD,MAC
   SOLCONTROL,0
   ANTYPE,STATIC
   NLGEOM,ON                  ! LARGE STRAIN OPTION ACTIVATED
   NEQIT,100                  ! 100 EQUILIBRIUM ITERATIONS ALLOWED
  CUTC,PLSLIMIT,0.5          ! RESET MAXIMUM PLASTIC INCREMENTAL STRAIN 
  NSUBS,100,1000,10
  D,ARG1,ARG2,,,ARG3,ARG4,ARG5
   SOLVE
   OUTRES,ESOL,1              ! STORE RESULTS FOR EVERY SUBSTEP
   NSTP = 10                  ! NO. OF LOAD STEPS USED
   T1   = 60/NSTP             ! ROTATION PER LOAD STEP
   T2   = 3.1415927/180       ! PARAMETER FOR FURTHER CALCULATIONS
   T33  = T1*T2               ! DEGREES TO RADIANS CONVERSION
   *DO,I,1,NSTP               ! USE DO LOOP FOR LOADING
       T3   = (I*T33)         ! CURRENT ANGLE
       T4   = (10*SIN(T3))    ! UY DISPLACEMENT
       T5   = (10*COS(T3))    
       T5   = (T5-10)         ! UX DISPLACEMENT
       D,ARG1,ARG2,T5,,ARG3,ARG4
       D,ARG1,ARG5,T4,,ARG3,ARG4
       SOLVE
   *ENDDO
*END
SOLD,1,A(1),101,100,A(2)      ! USE A(1) AND A(2) AS ARG2 AND ARG5 VALUES FOR 
FINISH                        !  DOF LABELS  
/OUTPUT
!         CREATE POST PROCESSING MACRO FOR MULTIPLE USE
*CREATE,POSP,MAC
   !COM      PLOT THE FINAL DISPLACED GEOMETRY USING POST1
   /POST1
   SET,NSTP+1
   /DSCALE,1,1
   /DIST,1,13
   PLDISP,1
   /DIST,1
   FINISH
   !         OBTAIN MAXIMUM SHEAR STRESS USING POST26
   /POST26
   ESOL,2,1,1,S,1
   ESOL,3,1,1,S,3
   FILLDATA,5,1,,,0,6
   ADD,4,2,3,,SHEAR,,,-1/2,1/2         ! COMPUTE MAX. SHEAR USING
                                       !  PRINCIPAL STRESSES
   PRVAR,4,5
   /GRID,1
   /XRANGE,0,60
   /YRANGE,-60,0
   /AXLAB,X,ROTATION (DEGREES)
   /AXLAB,Y,SHEAR STRESS (PSI)
   XVAR,5
   PLVAR,4
   *GET,P1,VARI,4,RTIME,11
   *DIM,LABEL,CHAR,1,2
   *DIM,VALUE,,1,3
   LABEL(1,1) = 'PRS MAX '
   LABEL(1,2) = 'psi'
   *VFILL,VALUE(1,1),DATA,-48
   *VFILL,VALUE(1,2),DATA,P1
   *VFILL,VALUE(1,3),DATA,ABS(P1/48)
   FINISH
*END
POSP
SAVE,TABLE_1
/CLEAR, NOSTART 

!         ANALYZE THE SAME PROBLEM WITH ELEMENT SOLID185
/PREP7
RESUME
EL='SOLID185'
/OUT,SCRATCH
/STATUS,TITLE              ! DISPLAY TITLE WITH NEW VALUE OF PARAMETER EL     
/OUT,
ET,1,SOLID185	           ! 3D 8 NODE SOLID ELEMENT
CSYS,1
N,1,10
N,11,20
FILL
NGEN,2,100,1,11,1,,3
NGEN,2,200,1,111,1,,,1
E,1,2,102,101,201,202,302,301
EGEN,10,1,1,1
!          APPLY BOUNDARY CONDITIONS
D,ALL,UZ
NSEL,S,LOC,X,20
D,ALL,ALL                  ! CLAMP OUTER SURFACE
NSEL,ALL
!          ROTATE APPROPRIATE NODES AND APPLY COUPLING
LOCAL,11,0,,,,3
NROTAT,101,111
NROTAT,301,311
CSYS,0
CP,1,A(1),2,102,202,302
CP,2,A(2),2,102,202,302
CPSGEN,9,1,1,2,1
FINISH
/OUTPUT,SCRATCH            ! DIVERT VOLUMINOUS SOLUTION OUTPUT
/SOLU
SOLD,1,A(1),301,100,A(2)
FINISH
/OUTPUT
POSP                       ! POSTPROCESS RESULTS USING POSP MACRO
SAVE,TABLE_2

!         ANALYZE THE SAME PROBLEM WITH ELEMENT PLANE183 
/PREP7
RESUME
EL='PLANE183'
/OUT,SCRATCH
/STATUS,TITLE              ! DISPLAY TITLE WITH NEW VALUE OF PARAMETER EL     
/OUT,
et,1,PLANE183,,,2
N,1,10
N,21,20
FILL
CSYS,1
NGEN,3,30,1,21,1,,1.5
E,1,3,63,61,2,33,62,31
EGEN,10,2,1,1,1
NSLE 
NSEL,INVE
NDELE,ALL
!         APPLY BOUNDARY CONDITIONS
NSEL,S,LOC,X,20
D,ALL,ALL
!         ROTATE APPROPRIATE NODES AND APPLY COUPLING
NSEL,S,NODE,,61,81
LOCAL,11,0,,,,3
NROTAT,ALL
NSEL,S,NODE,,31,51,2
LOCAL,12,0,,,,1.5
NROTAT,ALL
NSEL,ALL
CSYS,0
CP,1,A(1),3,33,63
CP,2,A(2),3,33,63
CPSGEN,9,2,1,2,1
CP,21,A(1),2,62
CP,22,A(2),2,62
CPSGEN,10,2,21,22,1
FINISH

/OUT,SCRATCH
/SOLU
!     CREATE SOLUTION OPTIONS AND LOADING MACRO FOR MULTIPLE USE
*CREATE,SOLD1,MAC
   SOLCONTROL,0
   ANTYPE,STATIC
   NLGEOM,ON                  ! LARGE STRAIN OPTION ACTIVATED
   NEQIT,100                  ! 100 EQUILIBRIUM ITERATIONS ALLOWED
   CUTC,PLSLIMIT,0.5          ! RESET MAXIMUM PLASTIC INCREMENTAL STRAIN 
   NSUBS,500,10000,100
   cnvtol,f,1,1.0e-3
   D,ARG1,ARG2,,,ARG3,ARG4,ARG5
   SOLVE
   OUTRES,ESOL,1              ! STORE RESULTS FOR EVERY SUBSTEP
   NSTP = 10                  ! NO. OF LOAD STEPS USED
   T1   = 60/NSTP             ! ROTATION PER LOAD STEP
   T2   = 3.1415927/180       ! PARAMETER FOR FURTHER CALCULATIONS
   T33  = T1*T2               ! DEGREES TO RADIANS CONVERSION
   *DO,I,1,NSTP               ! USE DO LOOP FOR LOADING
       T3   = (I*T33)         ! CURRENT ANGLE
       T4   = (10*SIN(T3))    ! UY DISPLACEMENT
       T5   = (10*COS(T3))    
       T5   = (T5-10)         ! UX DISPLACEMENT
       D,ARG1,ARG2,T5,,ARG3,ARG4
       D,ARG1,ARG5,T4,,ARG3,ARG4
       SOLVE
   *ENDDO
*END
SOLD1,1,A(1),61,30,A(2)      ! USE A(1) AND A(2) AS ARG2 AND ARG5 VALUES FOR 
FINISH  

/OUTPUT
POSP                       ! POSTPROCESS RESULTS USING POSP MACRO
SAVE,TABLE_3
RESUME,TABLE_1
/COM
/OUT,vm198,vrt
/COM,------------------- VM198 RESULTS COMPARISON --------------
/COM,
/COM,                 |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
/COM,RESULTS USING PLANE182
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.1,'  ',F15.1,'   ',1F15.3)
/NOPR
RESUME,TABLE_2
/GOPR
/COM,
/COM,RESULTS USING SOLID185
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.1,'  ',F15.1,'   ',1F15.3)
/NOPR
RESUME,TABLE_3
/GOPR
/COM,
/COM,RESULTS USING PLANE183
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.1,'  ',F15.1,'   ',1F15.3)
/COM,-----------------------------------------------------------
/OUT
FINISH
/NOPR
/DELETE,POSP,MAC
/DELETE,SOLD,MAC
/DELETE,SOLD1,MAC
/DELETE,TABLE_1
/DELETE,TABLE_2
/DELETE,TABLE_3
FINISH
*LIST,vm198,vrt

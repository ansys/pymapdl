/COM,ANSYS MEDIA REL. 150 (11/8/2013) REF. VERIF. MANUAL: REL. 150
/VERIFY,VM17
JPGPRF,500,100,1         ! MACRO TO SET PREFS FOR JPEG PLOTS
/SHOW,JPEG
/PREP7
smrt,off
/TITLE, VM17, SNAP-THROUGH BUCKLING OF A HINGED SHELL
:COM    CHANG, C.C.,"PERIODICALLY RESTARTED QUASI-NEWTON UPDATES IN
:COM    IN CONSTANT ARC-LENGTH METHOD", COMPUTERS AND STRUCTURES,
:COM    VOL. 41, NO. 5, PP. 963-972, 1991.
C***    USING SHELL63 ELEMENTS
ANTYPE,STATIC            ! STATIC ANALYSIS
ET,1,SHELL63,,1
R,1,6.350                ! SHELL THICKNESS
MP,EX,1,3102.75
MP,NUXY,1,0.3
:COM  CREATE FINITE ELEMENT MODEL
R1 = 2540                ! SHELL MID-SURFACE RADIUS
L  = 254                 ! HALF THE LENGTH
PI = 4*ATAN(1)           ! VALUE OF PI COMPUTED
THETA = 0.1*180/PI       ! 0.1 RADIANS CONVERTED TO DEGREES
CSYS,1                   ! CYLINDRICAL CO-ORDINATE SYSTEM
N,1,R1,90                ! NODES 1 AND 2 ARE CREATED AT POINTS
N,2,R1,90,L              ! A AND B RESPECTIVELY.
K,1,R1,90
K,2,R1,(90-THETA)
K,3,R1,90,L
K,4,R1,(90-THETA),L
ESIZE,,2                 ! TWO DIVISION ALONG THE REGION BOUNDARY
A,1,3,4,2
AMESH,1
NUMMRG,NODE           
FINISH
*CREATE,SOLVIT,MAC
/PREP7
:COM  APPLY BOUNDARY CONDITIONS
NSEL,S,LOC,Z,0
DSYM,SYMM,Z
NSEL,S,LOC,Y,90
DSYM,SYMM,X
NSEL,S,LOC,Y,(90-THETA)
D,ALL,UX,,,,,UY,UZ
NSEL,ALL
FINISH
:COM   SOLUTION PHASE
:COM SINCE THE SOLUTION OUTPUT IS SUBSTANTIAL IT IS DIVERTED TO A 
:COM  SCRATCH FILE
/OUTPUT,SCRATCH
/SOLUTION
NLGEOM,ON                ! LARGE DEFLECTION TURNED ON
OUTRES,,1                ! WRITE SOLUTION ON RESULTS FILE FOR EVERY SUBSTEP 
F,1,FY,-250              ! 1/4 TH OF THE TOTAL LOAD APPLIED DUE TO SYMMETRY
NSUBST,30                ! BEGIN WITH 30 SUBSTEPS
ARCLEN,ON,5              ! ARC-LENGTH SOLUTION TECHNIQUE TURNED ON WITH
                         !  MAX. ARC-LENGTH KEPT AT 5 TO COMPUTE AND STORE
			 !  SUFFICIENT INTERMEDIATE SOLUTION INFORMATION
SOLVE
FINISH
/OUTPUT
:COM   POSTPROCESSING PHASE
/POST26
NSOL,2,1,U,Y             ! STORE UY DISPLACEMENT OF NODE 1
NSOL,3,2,U,Y             ! STORE UY DISPLACEMENT OF NODE 2
PROD,4,1,,,LOAD,,,4*250  ! TOTAL LOAD IS 4*250 DUE TO QUARTER SYMMETRY
PROD,5,2,,,,,,-1         ! CHANGE SIGNS OF THE DISPLACEMENT VALUES
PROD,6,3,,,,,,-1
*GET,UY1,VARI,2,EXTREM,VMIN
*GET,UY2,VARI,3,EXTREM,VMIN
PRVAR,2,3,4              ! PRINT STORED INFORMATION
/AXLAB,X, DEFLECTION (MM)
/AXLAB,Y, TOTAL LOAD (N)
/GRID,1
/XRANGE,0,35
/YRANGE,-500,1050
XVAR,5   
PLVAR,4                  ! PLOT LOAD WITH RESPECT TO -UY OF NODE 1
/NOERASE
XVAR,6
PLVAR,4                  ! PLOT LOAD WITH RESPECT TO -UY OF NODE 2
/ERASE
*DIM,LABEL,CHAR,2,2
*DIM,VALUE,,2,3
LABEL(1,1) = 'UY @A ','UY @B '
LABEL(1,2) = 'mm ','mm '
*VFILL,VALUE(1,1),DATA,-30,-26 
*VFILL,VALUE(1,2),DATA,UY1,UY2
*VFILL,VALUE(1,3),DATA,ABS(UY1/30) ,ABS(UY2/26 )
FINISH
*END
SOLVIT
SAVE,TABLE_1
/CLEAR, NOSTART          ! CLEAR DATABASE FOR NEXT SOLUTION
/TITLE, VM17, SNAP-THROUGH BUCKLING OF A HINGED SHELL
C***    USING SHELL181 ELEMENTS
/PREP7
SMRT,OFF
ANTYPE,STATIC            ! STATIC ANALYSIS
ET,1,SHELL181
SECT,1,SHELL
SECD,6.350,1             ! SHELL THICKNESS
MP,EX,1,3102.75
MP,NUXY,1,0.3
:COM  CREATE FINITE ELEMENT MODEL
R1 = 2540                ! SHELL MID-SURFACE RADIUS
L  = 254                 ! HALF THE LENGTH
PI = 4*ATAN(1)           ! VALUE OF PI COMPUTED
THETA = 0.1*180/PI       ! 0.1 RADIANS CONVERTED TO DEGREES
CSYS,1                   ! CYLINDRICAL CO-ORDINATE SYSTEM
N,1,R1,90                ! NODES 1 AND 2 ARE CREATED AT POINTS
N,2,R1,90,L              ! A AND B RESPECTIVELY.
K,1,R1,90
K,2,R1,(90-THETA)
K,3,R1,90,L
K,4,R1,(90-THETA),L
ESIZE,,2                 ! TWO DIVISION ALONG THE REGION BOUNDARY
A,1,3,4,2
AMESH,1
NUMMRG,NODE           
FINISH
SOLVIT
SAVE,TABLE_2
/CLEAR, NOSTART          ! CLEAR DATABASE FOR NEXT SOLUTION
/TITLE, VM17, SNAP-THROUGH BUCKLING OF A HINGED SHELL
C***    USING SHELL281 ELEMENTS
/PREP7
SMRT,OFF
ANTYPE,STATIC            ! STATIC ANALYSIS
ET,1,SHELL281
SECT,1,SHELL
SECD,6.350,1             ! SHELL THICKNESS
MP,EX,1,3102.75
MP,NUXY,1,0.3
:COM  CREATE FINITE ELEMENT MODEL
R1 = 2540                ! SHELL MID-SURFACE RADIUS
L  = 254                 ! HALF THE LENGTH
PI = 4*ATAN(1)           ! VALUE OF PI COMPUTED
THETA = 0.1*180/PI       ! 0.1 RADIANS CONVERTED TO DEGREES
CSYS,1                   ! CYLINDRICAL CO-ORDINATE SYSTEM
N,1,R1,90                ! NODES 1 AND 2 ARE CREATED AT POINTS
N,2,R1,90,L              ! A AND B RESPECTIVELY.
K,1,R1,90
K,2,R1,(90-THETA)
K,3,R1,90,L
K,4,R1,(90-THETA),L
ESIZE,,2                 ! TWO DIVISION ALONG THE REGION BOUNDARY
A,1,3,4,2
AMESH,1
NUMMRG,NODE           
FINISH
SOLVIT
SAVE,TABLE_3
/NOPR
RESUME,TABLE_1
/COM
/OUT,vm17,vrt
/COM,------------------- VM17 RESULTS COMPARISON ---------------------
/COM,
/COM,                 |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
/COM,SHELL63
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.1,'  ',F14.1,'   ',1F15.3)
/NOPR
RESUME,TABLE_2
/GOPR
/COM,
/COM,SHELL181
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.1,'  ',F14.1,'   ',1F15.3)
/NOPR
RESUME,TABLE_3
/GOPR
/COM,
/COM,SHELL281
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.1,'  ',F14.1,'   ',1F15.3)
/COM,-------------------------------------------------------------
/OUT
FINISH
*LIST,vm17,vrt
/DELETE,TABLE_1
/DELETE,TABLE_2
/DELETE,TABLE_3
/DELETE,SOLVIT,MAC

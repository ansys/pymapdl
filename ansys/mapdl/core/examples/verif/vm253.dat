/COM,ANSYS MEDIA REL. 150 (11/8/2013) REF. VERIF. MANUAL: REL. 150
/VERIFY,vm253
JPGPRF,500,100,1
/SHOW,JPEG
/TITLE,VM253,GURSON HYDROSTATIC TENSION BENCHMARK
/COM, REFERENCE:
/COM, N. ARAVAS, "ON THE NUMERICAL INTEGRATION OF A CLASS OF PRESSURE
/COM, DEPENDENT PLASTICITY MODELS." INT. J. FOR NUMERICAL METHODS IN
/COM, ENGINEERING.  VOLUME. 24, PP. 1395-1416 (1987)
/COM, SECTION 5.2, FIGURE 7.
! DEFINED CONSTANTS
PI=3.141592654
UAPP=0.15                    ! APPLIED DISPLACEMENT
YOUNG=1000000                ! YOUNG'S MODULUS
NU=0.3                       ! POISSON RATIO
! GURSON COEFFICIENTS
Q1=1.5                       ! FIRST TVERGAARD CONSTANT
Q2=1                         ! SECOND TVERGAARD CONSTANT
Q3=Q1*Q1                     ! THIRD TVERGAARD CONSTANT
SIGMA_Y=YOUNG/300.0          ! YIELD STRESS
YIELD=1.0D0/SIGMA_Y/PI       ! YIELD STRENGTH
F_0=0.04                     ! INITIAL POROSITY
F_N=0.04                     ! VOLUME FRACTION OF VOID NUCLEATING PARTICLES
S_N=0.1                      ! STANDARD DEVIATION OF MEAN STRAIN FOR NUCLEA.
STRAIN_N=0.3                 ! MEAN STRAIN FOR NUCLEATIONS
POWER_N=0.1                  ! POWER FOR ELASTIC MODEL
/PREP7
MP,EX,1,YOUNG
MP,NUXY,1,NU
TB,NLISO,1,1,2,5             ! ELASTIC MODEL DEFINITION
TBDATA,1,SIGMA_Y,POWER_N

TB,GURS,1,,5,BASE		! BASE DEFINED
TBDATA,1,SIGMA_Y,F_0,Q1,Q2,Q3

TB,GURS,1,,3,SNNU		! SNNU DEFINED
TBDATA,1,F_N,STRAIN_N,S_N

BLOCK,,1,,1,,1               ! GEOMETRY AND MESHING
SAVE,MODEL
ET,1,SOLID185
VMESH,ALL
NSEL,S,LOC,X,0               ! BOUNDARY CONDITIONS
D,ALL,UX,0
NSEL,S,LOC,Y,0
D,ALL,UY,0
NSEL,S,LOC,Z,0
D,ALL,UZ,0
ALLS
FINISH
/SOLU
NSEL,S,LOC,X,1
D,ALL,UX,UAPP
NSEL,S,LOC,Y,1
D,ALL,UY,UAPP
NSEL,S,LOC,Z,1
D,ALL,UZ,UAPP
ALLS
NLGEOM,ON                    ! NON-LINEAR SOLUTION
NROPT,UNSYM
AUTOTS,OFF
SOLCONTROL,ON
CNVTOL,F,1.0,1E-5
NSUBST,100,100,100
OUTRES,ALL,ALL
/OUTPUT,SCRATCH
SOLVE
/OUT
FINISH
*CREATE,MACRO,MAC            ! MACRO FOR PLOTTING AND DATA COLLECTION
/POST26                      ! VOLUMETRIC PRESSURE VS. VOLUMETRIC STRAIN
NSOL,2,NODE(1,0,0),U,X
EXP,3,2,,,ONE,,,1E-10,1.0
ADD,4,2,3
NLOG,5,4,,,EPSV,,,1.0,3.0    ! X-AXIS ADJUSTMENT
ESOL,6,1,,NL,HPRES
PROD,7,6,,,P_SIGMAY,,,1/SIGMA_Y
XVAR,5
/AXLAB,X,VOLUMETRIC STRAIN
/AXLAB,Y,PRESSURE
PLVAR,7
*GET,EPSV10,VARI,7,RTIME,0.23
*GET,EPSV24,VARI,7,RTIME,0.55
*GET,EPSV40,VARI,7,RTIME,0.93
*DIM,TARGET,ARRAY,3,1
*DIM,ANSYS,ARRAY,3,1
*DIM,RATIO,ARRAY,3,1
*DIM,LABEL,CHAR,10
LABEL(1) = '0.10','0.24','0.40'
*VFILL,TARGET(1,1),DATA,1.62,1.00,0.62
*VFILL,ANSYS(1,1),DATA,EPSV10,EPSV24,EPSV40
R1=ANSYS(1,1)/TARGET(1,1)
R2=ANSYS(2,1)/TARGET(2,1)
R3=ANSYS(3,1)/TARGET(3,1)
*VFILL,RATIO(1,1),DATA,R1,R2,R3
*END
MACRO
SAVE,TABLE_1
RESUME,MODEL
/PREP7
ET,1,SOLID186
VMESH,ALL
NSEL,S,LOC,X,0               ! BOUNDARY CONDITIONS
D,ALL,UX,0
NSEL,S,LOC,Y,0
D,ALL,UY,0
NSEL,S,LOC,Z,0
D,ALL,UZ,0
ALLS
FINISH
/SOLU
NSEL,S,LOC,X,1
D,ALL,UX,UAPP
NSEL,S,LOC,Y,1
D,ALL,UY,UAPP
NSEL,S,LOC,Z,1
D,ALL,UZ,UAPP
ALLS
NLGEOM,ON                    ! NON-LINEAR SOLUTION
NROPT,UNSYM
AUTOTS,OFF
SOLCONTROL,ON
CNVTOL,F,1.0,1E-5
NSUBST,100,100,100
OUTRES,ALL,ALL
/OUTPUT,SCRATCH
SOLVE
/OUT
FINISH
MACRO
SAVE,TABLE_2
RESUME,TABLE_1
/COM,
/OUT,vm253,vrt
/COM,--------------- VM253 RESULTS COMPARISON -------------------
/COM,
/COM, EPSV  |  TARGET PRESSURE  |  Mechanical APDL |  RATIO  |
/COM,
/COM,SOLID185 RESULTS COMPARISON
*VWRITE,LABEL(1),TARGET(1,1),ANSYS(1,1),RATIO(1,1)
(1X,A8,'   ',F10.4,'   ',F14.4,'   ',1F14.4)
/NOPR
RESUME,TABLE_2
/GOPR
/COM,
/COM,SOLID186 RESULTS COMPARISON
*VWRITE,LABEL(1),TARGET(1,1),ANSYS(1,1),RATIO(1,1)
(1X,A8,'   ',F10.4,'   ',F14.4,'   ',1F14.4)
/COM,------------------------------------------------------------
/OUT
FINISH
*LIST,vm253,vrt
/DELETE,TABLE_1
/DELETE,TABLE_2

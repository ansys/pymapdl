/COM,ANSYS MEDIA REL. 150 (11/8/2013) REF. VERIF. MANUAL: REL. 150
/VERIFY,VM16
/SHOW,JPEG
/PREP7
/TITLE, VM16, BENDING OF A SOLID BEAM (PLANE ELEMENTS)
C***           FORMULAS FOR STRESS AND STRAIN, ROARK, 4TH ED.,PG. 104,106
C***           USING PLANE42 ELEMENTS
ANTYPE,STATIC
ET,1,PLANE42,,,,,,2     ! PLANE42 WITH SURFACE PRINTOUT FOR FACES 1 AND 3
MP,EX,1,30E6
MP,NUXY,1,0.0
N,1  
N,6,10  
FILL
NGEN,2,10,1,6,1,,2
E,1,2,12,11  
EGEN,5,1,1
/COM, VM16, CASE 1 - END MOMENT, ROARK, PAGE 106, NO. 9
D,1,ALL,,,11,10
F,6,FX,1000
F,16,FX,-1000
OUTPR,NSOL,1
OUTPR,ESOL,1
FINISH
/SOLU  
/OUT,SCRATCH
SOLVE
/POST1
SET,LAST
*GET,U1,NODE,16,U,Y
/GRAPHICS,POWER
/ESHAPE,1
/VIEW,1,1,1,1
PLNSOL,S,X			!BENDING STRESS
/OUT,
*GET,BEND_STRESS1,PLNSOL,0,MAX
FINI
/SOLU
/COM, VM16, CASE 2 - END LOAD, ROARK, PAGE 104, NO. 1
F,6,FX,,,16,10
F,6,FY,150,,16,10
/OUT,SCRATCH
SOLVE
FINISH
/POST1
SET,LAST
/OUT,
*GET,U2,NODE,16,U,Y
/GRAPHICS,POWER
/ESHAPE,1
/VIEW,1,1,1,1
PLNSOL,S,X			!BENDING STRESS
*GET,BEND_STRESS2,PLNSOL,0,MAX

*DIM,LABEL,CHAR,2,2
*DIM,VALUE,,2,3
*DIM,VALUE2,,2,3
LABEL(1,1) = 'DEFL','BEND_STRESS'
LABEL(1,2) = ' (in)  ',' (psi)  '
*VFILL,VALUE(1,1),DATA,.00500,3000
*VFILL,VALUE(1,2),DATA,U1,BEND_STRESS1
*VFILL,VALUE(1,3),DATA,ABS(U1/.005),ABS(BEND_STRESS1/3000)
SAVE,TABLE_1
*VFILL,VALUE2(1,1),DATA,.00500,4050
*VFILL,VALUE2(1,2),DATA,U2,BEND_STRESS2
*VFILL,VALUE2(1,3),DATA,ABS(U2/.005),ABS(BEND_STRESS2/4050)
SAVE,TABLE_2
FINI
/CLEAR,NOSTART



!*********************************************************************


/PREP7
/SHOW,JPEG
C***           USING PLANE182 ELEMENTS
ANTYPE,STATIC
ET,1,PLANE182
KEYOPT,1,1,3			!SIMPLIFIED ENHANCED STRAIN FORMULATION
MP,EX,1,30E6
MP,NUXY,1,0.0
N,1  
N,6,10  
FILL
NGEN,2,10,1,6,1,,2
E,1,2,12,11  
EGEN,5,1,1
/COM, VM16, CASE 1 - END MOMENT, ROARK, PAGE 106, NO. 9
D,1,ALL,,,11,10
F,6,FX,1000
F,16,FX,-1000
OUTPR,NSOL,1
OUTPR,ESOL,1
FINISH
/SOLU 
/OUT,SCRATCH
SOLVE
/POST1
SET,LAST
/GRAPHICS,POWER
/ESHAPE,1
/VIEW,1,1,1,1
/OUT,
*GET,U2,NODE,16,U,Y
PLNSOL,S,X			!BENDING STRESS
*GET,BEND_STRESS1,PLNSOL,0,MAX
*GET,U1,NODE,16,U,Y
FINI
/SOLU

/COM, VM16, CASE 2 - END LOAD, ROARK, PAGE 104, NO. 1
F,6,FX,,,16,10
F,6,FY,150,,16,10
/OUT,SCRATCH
SOLVE
FINISH
/POST1
SET,LAST
/GRAPHICS,POWER
/ESHAPE,1
/VIEW,1,1,1,1
/OUT,
*GET,U2,NODE,16,U,Y
PLNSOL,S,X			!BENDING STRESS
*GET,BEND_STRESS2,PLNSOL,0,MAX

*DIM,LABEL,CHAR,2,2
*DIM,VALUE,,2,3
*DIM,VALUE2,,2,3
LABEL(1,1) = 'DEFL','BEND_STRESS'
LABEL(1,2) = ' (in)  ',' (psi)  '
*VFILL,VALUE(1,1),DATA,.00500,3000
*VFILL,VALUE(1,2),DATA,U1,BEND_STRESS1
*VFILL,VALUE(1,3),DATA,ABS(U1/.005),ABS(BEND_STRESS1/3000)
SAVE,TABLE_3
*VFILL,VALUE2(1,1),DATA,.00500,4050
*VFILL,VALUE2(1,2),DATA,U2,BEND_STRESS2
*VFILL,VALUE2(1,3),DATA,ABS(U2/.005),ABS(BEND_STRESS2/4050)
SAVE,TABLE_4
FINI
/CLEAR,NOSTART



/NOPR
/COM
/COM
/OUT,vm16,vrt
/COM,------------------- VM16 RESULTS COMPARISON ---------------------
/COM,
/COM, USING PLANE42
RESUME,TABLE_1
/COM,CASE 1:          |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.5,'  ',F13.5,'   ',1F15.3)
RESUME,TABLE_2
/COM,CASE 2:
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE2(1,1),VALUE2(1,2),VALUE2(1,3)
(1X,A8,A8,'   ',F10.5,'  ',F13.5,'   ',1F15.3)
/COM, USING PLANE182
RESUME,TABLE_3
/COM,CASE 1:          |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.5,'  ',F13.5,'   ',1F15.3)
/NOPR
RESUME,TABLE_4
/COM,CASE 2:
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE2(1,1),VALUE2(1,2),VALUE2(1,3)
(1X,A8,A8,'   ',F10.5,'  ',F13.5,'   ',1F15.3)
/COM,-----------------------------------------------------------------
/COM,
/OUT
FINISH
/DELETE,TABLE_1
/DELETE,TABLE_2
/DELETE,TABLE_3
/DELETE,TABLE_4
FINISH
*LIST,vm16,vrt





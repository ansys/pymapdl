/COM,ANSYS MEDIA REL. 150 (11/8/2013) REF. VERIF. MANUAL: REL. 150
/VERIFY,VM93
/PREP7
/TITLE, VM93, TEMPERATURE DEPENDENT CONDUCTIVITY
C***        PRINCIPLES OF HEAT TRANSFER, KREITH, 2ND. PRINTING, PAGE 25, EX. 2-2
ANTYPE,STATIC
ET,1,LINK33
MP,KXX,1,.031,31E-6      ! TEMPERATURE-DEPENDENT CONDUCTIVITY
R,1,1                    ! AREA = 1
N,1
N,2,.25
E,1,2
OUTPR,ALL,1
OUTPR,VENG,NONE
KBC,1                    ! STEP BOUNDARY CONDITIONS
D,1,TEMP,300
D,2,TEMP,100
FINISH
/SOLU    
SOLVE
FINISH
/POST26
ESOL,2,1,2,HEAT,,HEAT
STORE
*GET,HEAT,VARI,2,EXTREM,VMAX
*DIM,LABEL,CHAR,1,2
*DIM,VALUE,,1,3
LABEL(1,1) = 'Q, BTU/h'
LABEL(1,2) = 'r       '
*VFILL,VALUE(1,1),DATA,29.760 
*VFILL,VALUE(1,2),DATA,HEAT
*VFILL,VALUE(1,3),DATA,ABS(HEAT/29.760 ) 
/COM
/OUT,vm93,vrt
/COM,------------------- VM93 RESULTS COMPARISON --------------
/COM,
/COM,                 |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.3,'  ',F14.3,'   ',1F15.3)
/COM,----------------------------------------------------------
/OUT
FINISH
*LIST,vm93,vrt

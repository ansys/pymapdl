/COM,ANSYS MEDIA REL. 150 (11/8/2013) REF. VERIF. MANUAL: REL. 150
/VERIFY,VM172
/PREP7
smrt,off
JPGPRF,500,100,1             ! MACRO TO SET PREFS FOR JPEG PLOTS
/SHOW,JPEG
/TITLE, VM172, STRESS ANALYSIS OF A LONG, THICK, ISOTROPIC SOLENOID
/COM,   MAGNETO-SOLID MECHANICS, MOON, PG. 275, 2D ANALYSIS
ANTYPE,STATIC                       ! COUPLED FIELD ANALYSIS
ET,1,PLANE13,,,1                    ! PLANE13, AZ DOF, AXISYMMETRIC OPTION
ET,2,PLANE13,4,,1                   ! PLANE13, AZ,UX,UY DOF, AXISYMMETRIC OPTION
MP,EX,2,10.76E10                    ! SOLENOID MODULUS OF ELASTICITY
MP,NUXY,2,.35                       ! SOLENOID POISSON RATIO
EMUNIT,MKS                          ! MKS UNITS
MP,MURX,1,1                         ! RELATIVE PERMEABILITY=1.0 
MP,MURX,2,1                         
K,1
K,2,1E-2
K,3,2E-2
L,1,2
LESIZE,1,,,5
L,2,3
LESIZE,2,,,20
LGEN,2,ALL,,,,2E-3
A,1,2,5,4                           ! AREA 1=AIR
A,2,3,6,5                           ! AREA 2=SOLENOID
ASEL,S,AREA,,2
AATT,2,,2
ASEL,ALL
ESIZE,,1
MSHK,2                    ! MAPPED AREA MESH IF POSSIBLE
MSHA,0,2D                 ! USING QUADS 
AMESH,ALL
ESEL,S,MAT,,2
NSLE,S
NSEL,R,LOC,Y,2E-3
CP,1,UY,ALL                         ! COUPLE SOLENOID NODAL UY DISP.
NSEL,S,LOC,X,2E-2                      ! SELECT NODES AT OUTER RADIUS
CP,2,AZ,ALL                         ! COUPLE AZ TO ENSURE FLUX-PARALLEL B.C.
FINISH
/SOLU
NSEL,S,LOC,X,0
D,ALL,AZ,0                          ! SET FLUX PARALLEL B.C.
ESEL,S,MAT,,2
BFE,ALL,JS,,,,1E+6                    ! APPLY CURRENT DENSITY LOAD
NSLE
NSEL,R,LOC,Y,0
DSYM,SYMM,2                         ! APPLY STRUCTURAL SYMMETRY B.C.
NSEL,ALL
ESEL,ALL
KBC,1                               ! STEP BOUNDARY CONDITIONS
OUTRES,,LAST
SOLVE
FINISH
/POST1
SET,1
ESEL,S,MAT,,2                       ! SELECT SOLENOID NODES AND ELEMENTS 
NSLE
/AXLAB,X,DISTANCE
/AXLAB,Y,STRESS - 2-D MODEL
/GTHK,AXIS,2
!/YRANGE,0,150
PATH,COIL1,2,,48                     ! DEFINE PATH WITH NAME = "COIL1"
PPATH,1,2                           ! DEFINE PATH POINTS BY NODE
PPATH,2,13
PDEF,SZ,S,Z     
PDEF,BY,B,Y
!/YRANGE,500,1500
PLPATH,SZ                           ! DISPLAY CIRCUM STRESS THRU SOLENOID
!/YRANGE,0,125
/AXLAB,Y,FLUX DENSITY - 2-D MODEL
PLPATH,BY                           ! DISPLAY AXIAL FLUX DENSITY THRU SOLENOID
NSEL,S,LOC,X,1e-2
NSEL,A,LOC,X,1.3e-2
NSEL,A,LOC,X,1.7e-2
PRNSOL,B,COMP                       ! PRINT AXIAL FLUX DENSITY
PRNSOL,S,COMP                       ! PRINT COMPONENT STRESSES
*GET,B1,NODE,7,B,SUM
*GET,B2,NODE,19,B,SUM
*GET,B3,NODE,27,B,SUM
*GET,S1,NODE,7,S,Z
*GET,S2,NODE,19,S,Z
*GET,S3,NODE,27,S,Z
*DIM,LABEL,CHAR,6,2
*DIM,VALUE,,6,3
LABEL(1,1) = 'B, T ','B, T ','B, T ','PRS ','PRS ','PRS '
LABEL(1,2) = 'R=1E-2','R=1.3E-2','R=1.7E-2','R=1E-2','R=1.3E-2','R=1.7E-2'
*VFILL,VALUE(1,1),DATA,0.01257,8.796E-3,3.77E-3,146.7,97.79,62.44
*VFILL,VALUE(1,2),DATA,B1,B2,B3,S1,S2,S3
V1=B1/0.01257
V2=B2/8.796E-3
V3=B3/3.77E-3
V4=S1/146.7
V5=S2/97.79
V6=S3/62.44
*VFILL,VALUE(1,3),DATA,V1,V2,V3,V4,V5,V6
SAVE,TABLE_1
FINISH
RESUME,TABLE_1
/COM
/OUT,vm172,vrt
/COM,------------------- VM172 RESULTS COMPARISON --------------
/COM,
/COM,                 |   TARGET   |   ANSYS   |   RATIO
/COM,
/COM,PRESSURES HAVE UNITS OF N/M**2
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',e12.5,'  ',e12.5,'   ',1F5.3)
/COM,
/COM,-----------------------------------------------------------
/COM,
/OUT
FINISH
*LIST,vm172,vrt
/DELETE,TABLE_1


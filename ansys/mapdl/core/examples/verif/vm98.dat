/COM,ANSYS MEDIA REL. 150 (11/8/2013) REF. VERIF. MANUAL: REL. 150
/VERIFY,VM98
/PREP7
/TITLE, VM98, TEMPERATURE DISTRIBUTION ALONG A TAPERED FIN
C***       PRINCIPLES OF HEAT TRANSFER, KREITH, 2ND. PRINTING, PAGE 57, EX. 2-13
C***       USING PLANE55 ELEMENTS
ANTYPE,STATIC
ET,1,PLANE55
MP,KXX,1,15
L=(4/12)                      ! FIN LENGTH
B=(1/12)                      ! FIN HEIGHT AT WALL 
N,1,,-(B/2)
N,11,L
FILL
N,21,,(B/2)
N,31,L
FILL
E,21,1,2,22
EGEN,9,1,1
E,30,10,11,11
NSEL,S,LOC,X,0  
D,ALL,TEMP,1100
NSEL,S,NODE,,1,11
SF,ALL,CONV,15,100
NSEL,S,NODE,,21,30
NSEL,A,NODE,,11
SF,ALL,CONV,15,100
NSEL,ALL
FINISH
/SOLU    
SOLVE
FINISH
/POST26
ESOL,2,1,1,HEAT,,HEAT
ESOL,3,1,21,HEAT,,HEAT
STORE
*GET,HEAT,VARI,2,EXTREM,VMAX
*GET,HEAT2,VARI,3,EXTREM,VMAX
HTTOT=(ABS(HEAT+HEAT2))
/POST1
*DIM,VALUE,,12,3
*VFILL,VALUE(1,1),DATA,1100,970,850,750,655,575,495,430,370,315
*VFILL,VALUE(11,1),DATA,265,5050
*DO,I,0,1,.1                  ! CREATE DO LOOP TO PRINT TEMPS IN
NSEL,S,LOC,X,(I*L)            ! INCREMENTS OF 0.1*L
PRNSOL,TEMP
NNUM = NODE (I*L,(-(B/2)),0)
*GET,VAL,NODE,NNUM,TEMP
*VFILL,VALUE(I*10+1,2),DATA,VAL
*VFILL,VALUE(I*10+1,3),DATA,ABS(VALUE(I*10+1,2) / VALUE(I*10+1,1) )
*ENDDO
*VFILL,VALUE(12,2),DATA,HTTOT
*VFILL,VALUE(12,3),DATA,ABS(HTTOT/5050)
NSEL,ALL
PRNLD,HEAT                    ! PRINT NODAL HEAT FLOW RATES
*DIM,LABEL,CHAR,12,2
LABEL(1,1) = 'T,F(AT X','T,F(AT X','T,F(AT X','T,F(AT X','T,F(AT X','T,F(AT X','T,F(AT X'
LABEL(8,1) = 'T,F(AT X','T,F(AT X','T,F(AT X','T,F(AT X','q,   BTU'
LABEL(1,2) = '/L = 0.0','/L = 0.1','/L = 0.2','/L = 0.3','/L = 0.4','/L = 0.5','/L = 0.6'
LABEL(8,2) = '/L = 0.7','/L = 0.8','/L = 0.9','/L = 1.0','/hr	  '
/COM
/OUT,vm98,vrt
/COM,------------------- VM98 RESULTS COMPARISON ---------------
/COM,
/COM,                 |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.0,'  ',F14.0,'   ',1F15.3)
/COM,-----------------------------------------------------------
/OUT
FINISH
*LIST,vm98,vrt

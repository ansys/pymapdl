/COM,ANSYS MEDIA REL. 150 (11/8/2013) REF. VERIF. MANUAL: REL. 150
/VERIFY,VM199
/PREP7
/TITLE, VM199, VISCOPLASTIC ANALYSIS OF A BODY UNDERGOING SHEAR DEFORMATION
!              " AN IMPLICIT STRESS UPDATE ....", LWO ET AL.
!           PLANE182 ELEMENT
ET,1,182	  	                ! 2-D, 4-NODE ELEMENT
KEYOPT,1,3,2					! PLANE STRAIN
MP,EX,1,60.6E9
MP,NUXY,1,0.4999
TB,RATE,1,,9,ANAND              ! VISCOPLASTIC MODEL BY ANAND
TBDATA,1,29.7E6
TBDATA,2,21.08999E3
TBDATA,3,1.91E7
TBDATA,4,7.0
TBDATA,5,0.23348
TBDATA,6,1115.6E6
TBDATA,7,18.92E6
TBDATA,8,0.07049
TBDATA,9,1.3
SAVE                            ! SAVE DATABASE FOR LATER USE
N,1
N,2,1E-2
N,3,1E-2,1E-2
N,4,,1E-2
E,1,2,3,4
D,1,ALL,,,2
FINISH
/OUTPUT,SCRATCH                 ! DIVERT VOLUMINOUS SOLUTION OUTPUT
/SOLU
SOLCONTROL,0
ANTYPE,STATIC                   
NLGEOM,ON                       ! LARGE DEFORMATION ACTIVATED
OUTRES,RSOL,ALL                 ! STORE REACTION RESULTS FOR ALL SUBSTEPS
BFUNIF,TEMP,673                 ! UNIFORM TEMPERATURE OF 673 K
D,3,ALL,0.0,,4
TIME,0.000001                   ! NEAR ZERO TIME FOR FIRST LOAD STEP
SOLVE
NSUBST,20
D,3,UX,0.2E-2,,4
TIME,20
SOLVE
FINISH
/OUTPUT
/POST26
RFORCE,2,3,F,X
RFORCE,3,4,F,X
ADD,4,2,3,,LOAD,,,(1/100),(1/100)    
!         THE FX FORCE IS DIVIDED BY 100 BECAUSE THE DEFAULT OUT-
!          OF-PLANE THICKNESS IS 1 METER WHILE PROBLEM CONSIDERED 
!          HAS OUT-OF-PLANE THICKNESS OF 0.01 METER WHICH IS 100 
!          TIMES LESS THAN 1 METER.
PRVAR,4
*GET,F1,VARI,4,RTIME,20
*DIM,LABEL,CHAR,1,2
*DIM,VALUE,,1,3
LABEL(1,1) = 'Fx '
LABEL(1,2) = 'N'
*VFILL,VALUE(1,1),DATA,845
*VFILL,VALUE(1,2),DATA,F1
*VFILL,VALUE(1,3),DATA,ABS(F1/845)
SAVE,TABLE1
FINISH
/CLEAR, NOSTART 
!         PLANE183 ELEMENT TYPE
/PREP7
RESUME
ET,1,183	                   ! 2-D, 8-NODE  ELEMENT
KEYOPT,1,3,2					! PLANE STRAIN
N,1
N,2,1E-2
N,3,1E-2,1E-2
N,4,,1E-2
N,5,0.5E-2
N,6,1E-2,0.5E-2
N,7,0.5E-2,1E-2
N,8,,0.5E-2
E,1,2,3,4,5,6,7,8
D,1,ALL
D,2,ALL
D,5,ALL
FINISH
/OUTPUT,SCRATCH                 ! DIVERT VOLUMINOUS SOLUTION OUTPUT
/SOLU
SOLCONTROL,0
NLGEOM,ON                       ! LARGE DEFORMATION ACTIVATED
OUTRES,RSOL,ALL                 ! STORE REACTION RESULTS FOR ALL SUBSTEPS
BFUNIF,TEMP,673                 ! UNIFORM TEMPERATURE OF 673 K
D,3,ALL,0.0,,8
TIME,0.000001                   ! NEAR ZERO TIME FOR FIRST LOAD STEP
SOLVE
NSUBST,20
D,3,UX,0.2E-2,,4
D,7,UX,0.2E-2
D,6,UX,0.1E-2,,8,2
TIME,20
SOLVE
FINISH
/OUTPUT
/POST26
RFORCE,2,3,F,X
RFORCE,3,4,F,X
RFORCE,4,7,F,X
ADD,5,2,3,4,LOAD,,,(1/100),(1/100),(1/100)    
PRVAR,5
*GET,F1,VARI,5,RTIME,20
*DIM,LABEL,CHAR,1,2
*DIM,VALUE,,1,3
LABEL(1,1) = 'Fx '
LABEL(1,2) = 'N'
*VFILL,VALUE(1,1),DATA,845
*VFILL,VALUE(1,2),DATA,F1
*VFILL,VALUE(1,3),DATA,ABS(F1/845)
SAVE,TABLE2
FINISH

/CLEAR, NOSTART 
!         SOLID185 ELEMENT
/PREP7
RESUME
ET,1,SOLID185    		        !3-D, 8-NODE VISCOPLASTIC ELEMENT
N,1
N,2,1E-2
N,3,1E-2,1E-2
N,4,,1E-2
NGEN,2,4,1,4,1,,,1E-2
E,1,2,3,4,5,6,7,8
D,1,ALL,,,2
D,5,ALL,,,6
FINISH
/OUTPUT,SCRATCH                 ! DIVERT VOLUMINOUS SOLUTION OUTPUT
/SOLU
SOLCONTROL,0
NLGEOM,ON                       ! LARGE DEFORMATION ACTIVATED
OUTRES,RSOL,ALL                 ! STORE REACTION RESULTS FOR ALL SUBSTEPS
BFUNIF,TEMP,673                 ! UNIFORM TEMPERATURE OF 673 K
D,3,ALL,0.0,,8
TIME,0.000001                   ! NEAR ZERO TIME FOR FIRST LOAD STEP
SOLVE
NSUBST,20
D,3,UX,0.2E-2,,4
D,7,UX,0.2E-2,,8
TIME,20
SOLVE
FINISH
/OUTPUT
/POST26
RFORCE,2,3,F,X
RFORCE,3,4,F,X
RFORCE,4,7,F,X
RFORCE,5,8,F,X
ADD,6,2,3,4
ADD,7,6,5,,LOAD
PRVAR,7
*GET,F1,VARI,7,RTIME,20
*DIM,LABEL,CHAR,1,2
*DIM,VALUE,,1,3
LABEL(1,1) = 'Fx '
LABEL(1,2) = 'N'
*VFILL,VALUE(1,1),DATA,845
*VFILL,VALUE(1,2),DATA,F1
*VFILL,VALUE(1,3),DATA,ABS(F1/845)
/COM
/OUT,vm199,vrt
/COM,------------------- VM199 RESULTS COMPARISON --------------
/COM,
/COM,                  |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
/COM,PLANE182 RESULTS:
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.2,'  ',F15.2,'   ',1F18.3)
/NOPR
RESUME,TABLE1
/GOPR
/COM,
/COM,PLANE183 RESULTS:
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.2,'  ',F15.2,'   ',1F18.3)
/NOPR
RESUME,TABLE2
/GOPR
/COM,
/COM,SOLID185 RESULTS:
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.2,'  ',F15.2,'   ',1F18.3)
/COM,----------------------------------------------------------
/OUT
FINISH
*LIST,vm199,vrt

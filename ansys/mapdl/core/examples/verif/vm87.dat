/COM,ANSYS MEDIA REL. 150 (11/8/2013) REF. VERIF. MANUAL: REL. 150
/VERIFY,VM87
/PREP7
/TITLE, VM87, EQUIVALENT STRUCTURAL DAMPING
C***         VIBRATION THEORY AND APPLICATIONS, THOMSON, 2ND. PRINTING, PAGE 72,
C***         ART. 3.9, AND PAGE 56, EX. 3.1-2
ANTYPE,HARMIC            ! HARMONIC RESPONSE ANALYSIS
HROPT,FULL               ! FULL HARMONIC RESPONSE
HROUT,OFF                ! PRINT COMPLEX DISP. AS AMPLITUDES AND PHASE ANGLES
ET,1,COMBIN40,,,3,,,2
BETAD,.03                ! EQUIVALENT STRUCTURAL DAMPING
R,1,200,,.5              ! SPRING STIFFNESS = 200, C = 0, M = 0.5
N,1
N,2
E,1,2
OUTPR,BASIC,1
HARFRQ,,3.1831           ! HARMONIC FREQUENCY RANGE
D,1,UZ
F,2,FZ,10
FINISH
/SOLU    
/OUT,SCRATCH
SOLVE
FINISH
/POST26
FILE,,rst
/OUT,
NSOL,2,2,U,Z,2UX
PRVAR,2
*GET,A,VARI,2,ITIME,3.1831
*DIM,LABEL,CHAR,1,2
*DIM,VALUE,,1,3
LABEL(1,1) = 'AMPLITUD'
LABEL(1,2) = 'E, in   '
*VFILL,VALUE(1,1),DATA,.0833
*VFILL,VALUE(1,2),DATA,ABS(A)
*VFILL,VALUE(1,3),DATA,ABS(A/.0833)
/COM
/OUT,vm87,vrt
/COM,------------------- VM87 RESULTS COMPARISON ---------------
/COM,
/COM,                 |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.4,'  ',F14.4,'   ',1F15.3)
/COM,-----------------------------------------------------------
/COM,
/COM,-----------------------------------------------------------
/COM,NOTE: THERE ARE VERIFIED RESULTS IN VM87  NOT CONTAINED IN
/COM,THIS TABLE
/COM,-----------------------------------------------------------
/OUT
FINISH
*LIST,vm87,vrt

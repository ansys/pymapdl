/COM,ANSYS MEDIA REL. 150 (11/8/2013) REF. VERIF. MANUAL: REL. 150
/VERIFY,VM223
JPGPRF,500,100,1             ! MACRO TO SET PREFS FOR JPEG PLOTS
/SHOW,JPEG
/TITLE,VM223, ELECTRO-THERMAL-COMPLIANT MICROACTUATOR
/stitle,1,Reason COMPARE differences are acceptable:
/stitle,2,Convergence Difference
/COM,
/COM, REFERENCE:
/COM, N.D. MANKAME AND G.K. ANANTHASURESH, "COMPREHENSIVE THERMAL MODELLING
/COM, AND CHARACTERIZATION OF AN ELECTRO-THERMAL-COMPLIANT MICROACTUATOR,"
/COM, J. MICROMECH. MICROENG., V.11 (2001), PP. 452-462
/COM,
/NOPR
D1=40E-6                 ! MICROACTUATOR DIMENSIONS, m
D2=255E-6
D3=40E-6
D4=330E-6
D5=1900E-6
D6=90E-6
D7=75E-6
D8=352E-6
D9=352E-6
D11=20E-6
VLT=15                 ! APPLIED VOLTAGE, VOLT
TBLK=300               ! BULK TEMPERATURE, K
/PREP7
ET,1,SOLID227,111      ! STRUCTURAL-THERMOELECTRIC TETRAHEDRON
MP,EX,1,169E9
MP,PRXY,1,0.3
MP,RSVX,1,4.2E-4
MPTEMP,1,300,400,500,600,700,800
MPTEMP,7,900,1000,1100,1200,1300,1400
MPTEMP,13,1500
MPDATA,ALPX,1,1,2.568E-6,3.212E-6,3.594E-6,3.831E-6,3.987E-6,4.099E-6
MPDATA,ALPX,1,7,4.185E-6,4.258E-6,4.323E-6,4.384E-6,4.442E-6,4.5E-6
MPDATA,ALPX,1,13,4.556E-6
MPDATA,KXX,1,1,146.4,98.3,73.2,57.5,49.2,41.8
MPDATA,KXX,1,7,37.6,34.5,31.4,28.2,27.2,26.1
MPDATA,KXX,1,13,25.1
TREF,TBLK             ! REFERENCE TEMPERATURE
/COM, === SOLID MODEL
K,1,0,0               ! DEFINE KEYPOINTS
K,2,0,D9
K,3,D8,D9
K,4,D8,D1
K,5,D8+D4+D5,D1
K,6,D8+D4+D5,-(D7+D2)
K,7,D8+D4,-(D7+D2)
K,8,D8+D4,-(D7+D3)
K,9,D8,-(D7+D3)
K,10,D8,-(D7+D9)
K,11,0,-(D7+D9)
K,12,0,-D7
K,13,D8+D4+D5-D6,-D7
K,14,D8+D4+D5-D6,0
A,1,2,3,4,5,6,7,8,9,10,11,12,13,14
VEXT,1,,,,,D11

/COM, === FINITE ELEMENT MODEL
LSEL,S,LINE,,31,42   ! ELEMENT SIZE ALONG OUT-OF-PLANE DIMENSION
LESIZE,ALL,D11
LSEL,S,LINE,,1,3     ! ELEMENT SIZE ALONG ANCHOR SIDES
LSEL,A,LINE,,9,11
LSEL,A,LINE,,15,17
LSEL,A,LINE,,23,25
LESIZE,ALL,D9/2
LSEL,S,LINE,,5       ! ELEMENT SIZE ALONG SIDE WALLS
LSEL,A,LINE,,19
LESIZE,ALL,(D1+D2+D7)/6
LSEL,S,LINE,,13      ! ELEMENT SIZE ALONG THE END CONNECTION
LSEL,A,LINE,,27
LESIZE,ALL,D7/3
LSEL,S,LINE,,8       ! ELEMENT SIZE ALONG THE FLEXURE
LSEL,A,LINE,,22
LESIZE,ALL,D4/6
LSEL,S,LINE,,4       ! ELEMENT SIZE ALONG THE THIN ARM
LSEL,A,LINE,,18
LESIZE,ALL,(D4+D5)/30
LSEL,S,LINE,,14
LSEL,A,LINE,,28
LESIZE,ALL,(D8+D4+D5-D6)/40
LSEL,S,LINE,,7       ! ELEMENT SIZE ALONG THE WIDE ARM
LSEL,A,LINE,,21
LESIZE,ALL,D2/5
LSEL,S,LINE,,12
LSEL,A,LINE,,26
LESIZE,ALL,(D8+D4+D5-D6)/35
LSEL,S,LINE,,6
LSEL,A,LINE,,20
LESIZE,ALL,D5/25
LSEL,ALL
VMESH,1              ! MESH THE VOLUME

/COM, === DOF CONSTRAINTS ON THE ANCHORS
NSEL,S,LOC,X,0,D8
NSEL,R,LOC,Z,0       ! BOTTOM SURFACE
D,ALL,UX,0,,,,UY,UZ
D,ALL,TEMP,TBLK
NSEL,ALL

NSEL,S,LOC,X,0,D8
NSEL,R,LOC,Y,-(D7+D9),-D7
CP,1,VOLT,ALL
N_GR=NDNEXT(0)
D,N_GR,VOLT,0
NSEL,S,LOC,X,0,D8
NSEL,R,LOC,Y,0,D9
CP,2,VOLT,ALL
N_VLT=NDNEXT(0)
D,N_VLT,VOLT,VLT
NSEL,ALL

/COM, === RADIOSITY BOUNDARY CONDITIONS
SF,ALL,RDSF,0.7,1    ! SURFACE-TO-SURFACE RADIATION LOAD
SPCTEMP,1,TBLK       ! AMBIENT TEMPERATURE
STEF,5.6704E-8       ! STEFAN-BOLTZMAN RADIATION CONSTANT, J/(K)4(M)2(S)

/COM, === TEMPERATURE DEPENDENT CONVECTION BOUNDARY CONDITIONS
MPTEMP               ! INITIALIZE TEMPERATURE TABLE
/COM, TEMPERATURE TABLE FOR THERMAL LOADING
MPTEMP,1,300,500,700,900,1100,1300
MPTEMP,7,1500
/COM, === UPPER FACE
ASEL,S,AREA,,2       ! THIN ARM AND FLEXURE
NSLA,S,1
NSEL,R,LOC,X,D8,D8+D4+D5-D6
NSEL,R,LOC,Y,0,D1
SF,ALL,CONV,-1,TBLK
NSLA,S,1
NSEL,R,LOC,X,D8,D8+D4
NSEL,R,LOC,Y,-(D3+D7),-D7
SF,ALL,CONV,-1,TBLK
MPDATA,HF,1,1,17.8,60.0,65.6,68.9,71.1,72.6
MPDATA,HF,1,7,73.2
NSLA,S,1              ! WIDE ARM
NSEL,R,LOC,X,D8+D4,D8+D4+D5-D6
NSEL,R,LOC,Y,-(D2+D7),-D7
SF,ALL,CONV,-2,TBLK
MPDATA,HF,2,1,11.2,37.9,41.4,43.4,44.8,45.7
MPDATA,HF,2,7,46.0
NSLA,S,1              ! END CONNECTION
NSEL,R,LOC,X,D8+D4+D5-D6,D8+D4+D5
SF,ALL,CONV,-3,TBLK
MPDATA,HF,3,1,15.,50.9,55.5,58.2,60.,61.2
MPDATA,HF,3,7,62.7
NSLA,S,1              ! ANCHORS
NSEL,R,LOC,X,0,D8
SF,ALL,CONV,-4,TBLK
MPDATA,HF,4,1,10.3,35.0,38.2,40.,41.3,42.1
MPDATA,HF,4,7,42.5
/COM, === BOTTOM FACE
ASEL,S,AREA,,1
NSLA,S,1             ! THIN ARM AND FLEXURE
NSEL,R,LOC,X,D8,D8+D4+D5-D6
NSEL,R,LOC,Y,0,D1
SF,ALL,CONV,-5,TBLK
NSLA,S,1
NSEL,R,LOC,X,D8,D8+D4
NSEL,R,LOC,Y,-(D3+D7),-D7
SF,ALL,CONV,-5,TBLK
MPDATA,HF,5,1,22.4,69.3,76.1,80.5,83.7,86.0
MPDATA,HF,5,7,87.5
NSLA,S,1             ! WIDE ARM
NSEL,R,LOC,X,D8+D4,D8+D4+D5-D6
NSEL,R,LOC,Y,-(D2+D7),-D7
SF,ALL,CONV,-6,TBLK
MPDATA,HF,6,1,13.,39.6,43.6,46.,47.6,49.
MPDATA,HF,6,7,50.1
NSLA,S,1             ! END CONNECTION
NSEL,R,LOC,X,D8+D4+D5-D6,D8+D4+D5
SF,ALL,CONV,-7,TBLK
MPDATA,HF,7,1,24.,73.8,81.,85.7,89.2,91.6
MPDATA,HF,7,7,93.2
NSEL,ALL
ASEL,ALL
/COM, === SIDE WALLS (ANCHORS AND AREA BETWEEN THE THIN AND WIDE
/COM, ARMS ARE EXCLUDED)
ASEL,S,AREA,,6,16
ASEL,U,AREA,,11,16
SFA,ALL,,CONV,-8,TBLK
ASEL,ALL
MPDATA,HF,8,1,929,1193,1397,1597,1791,1982
MPDATA,HF,8,7,2176
FINISH

/SOLU
ANTYPE,STATIC
CNVTOL,F,1,1.E-4     ! DEFINE CONVERGENCE TOLERANCES
CNVTOL,HEAT,1,1.E-5
CNVTOL,AMPS,1,1.E-5
NLGEOM,ON            ! LARGE DEFLECTION ANALYSIS
/OUT,SCRATCH
SOLVE
FINISH

/POST1
/DSCALE,1,10
PLNSOL,U,SUM         ! PLOT DISPLACEMENT VECTOR SUM
PLNSOL,TEMP          ! PLOT TEMPERATURE
/OUT,
/COM,
/COM, TARGET RESULTS:
/COM, FIGURE 5 (P. 458) TIP TRANSVERSE DISPLACEMENT VS. VOLTAGE CURVE:
/COM,           VOLTAGE = 15.0 (V), UY = 27E-6 (m)
/COM,
UYANALYT=27.0E-6
NPOST=NODE(0.25820E-02,-0.33000E-03,0.20000E-04)
*GET,UYANSYS,NODE,NPOST,U,SUM
RATIO=ABS(UYANSYS/UYANALYT)
*DIM,LABEL,CHAR,1
LABEL(1) = 'UY (15V)'

/GOPR
/OUT,vm223,vrt
/COM
/COM,------------------- VM223  RESULTS COMPARISON ---------------------
/COM,
/COM,               TARGET   |   Mechanical APDL   |   RATIO
/COM,
*VWRITE,LABEL(1),UYANALYT,UYANSYS,RATIO
(1X,A10,'     ',E10.4,'    ',E14.4,'     ',1F15.3)
/COM,-------------------------------------------------------------------
/OUT
FINISH
*LIST,vm223,vrt

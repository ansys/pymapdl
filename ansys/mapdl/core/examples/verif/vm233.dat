/COM,ANSYS MEDIA REL. 150 (11/8/2013) REF. VERIF. MANUAL: REL. 150
/VERIFY,VM233
/TITLE,VM233, STATIC FORCE COMPUTATION OF A 3D SOLENOID ACTUATOR
/STITLE,1,Reason COMPARE differences are acceptable:
/STITLE,2,  No of nodes/elements affect accuracy
/COM, REFERENCE: ANALYSIS OF BENCHMARK PROBLEM TEAM20 WITH VARIOUS
/COM,            FORMULATION, PROCEEDINGS OF TEAM WORKSHOP,COMPUMAF RIO
/COM,		     PG 18-20,1997.
/COM,		     IEEE TRANS. ON MAG, VOL. 34. NO. 5. PG 2481-84,1998
/COM,		     IEEE TRANS. ON MAG, VOL. 35. NO. 3. PG 1406-84,1998
/COM,		     ANALYSIS SOLUTIONS, VOL.1. ISSUE 2,WINTER 1997-98,PG 10-11. 
/COM
/PREP7
/NOPR
/OUT,SCRATCH
ET,1,SOLID98,10
MP,MURX,1,1
MP,MURX,4,1

TB,BH,2,,40
TBPT,,355,.7
,,405,.8
,,470,.9
,,555,1.
,,673,1.1
,,836,1.2
,,1065,1.3
,,1220,1.35
,,1420,1.4
,,1720,1.45
,,2130,1.5
,,2670,1.55
,,3480,1.6
,,4500,1.65
,,5950,1.70
,,7650,1.75
,,10100,1.8
,,13000,1.85
,,15900,1.9
,,21100,1.95
,,26300,2.
,,32900,2.05
,,42700,2.1
,,61700,2.15
,,84300,2.2
,,110000,2.25
,,135000,2.3
,,200000,2.41
,,400000,2.69
,,800000,3.22

TBCOPY,BH,2,3
XINF=100.
YINF=100.
ZINF=175.
TCUR=5000						! CURRENT
N,1,0,0,75/1000           			! PATH FOR POST PROCESSING
N,2,63.5/1000,0,75/1000
BLOCK,0,63.5,0,25/2,0,25  			! POLE
BLOCK,38.5,63.5,0,25/2,25,125
BLOCK,13.5,63.5,0,25/2,125,150
VGLUE,ALL
BLOCK,0,12.5,0,5,26.5,125  			! ARMATURE
BLOCK,0,13,0,5.5,26,(125+.5) 			! AIR REGION
VOVLAP,1,2
NUMCMP,VOLU

BLOCK,39/2,75/2,0,14.5,(25+1.7),(125-1.7)
BLOCK,0,14.5,39/2,75/2,(25+1.7),(125-1.7)
LOCAL,11,1,14.5,14.5,25+1.7
WPCSYS,11
CYL4,,,5,0,23,90,(125-1.7)-(25+1.7)
VGLUE,6,8
NUMCMP,VOLU

CSYS,0
WPCSYS,0
CYL4,,,0,0,100,90,175
VOVLAP,ALL
NUMCMP,ALL

VSEL,S,VOLU,,1
VATT,3,1,1              
VSEL,S,VOLU,,3,5
VATT,2,1,1              
VSEL,S,VOLU,,6  
VATT,4,2,1              			! COIL +Y
VSEL,S,VOLU,,7
VATT,4,4,1              			! COIL -X
VSEL,S,VOLU,,8
ESYS,11
VATT,4,3,1              			! COIL +Y THETA 

ALLSEL,ALL
SMRT,10
MSHAPE,1,3D
MSHMID,1
MSHKEY,0
VMESH,ALL

ESEL,S,MAT,,3      				! ARMATURE
CM,ARM,ELEM
FMAGBC,'ARM'
ALLSEL,ALL

VLSCALE,ALL,,,.001,.001,.001,,0,1     	! SCALE TO METERS

LOCAL,12,0,0,0,75/1000
WPCSYS,-1
RACE,.0285,.0285,.014,TCUR,.018,.0966     ! CREATE COIL
SAVE
FINISH  
/SOLU
NSLE,S
*GET,NMIN,NODE,,NUM,MIN
D,NMIN,MAG,0
NSEL,ALL
EQSLV,JCG
MAGSOLV,3,,,,,1
FINISH

/POST1
/OUT
*MSG,NOTE,TCUR
%/RESULTS FOR CURRENT = %G (MULTIPLY ORCE BY 4 FOR SYMMETRY)
FMAGSUM,'ARM'
*GET,FVWZ,SSUM, ,ITEM,FVW_Z     		! EXTRACT VIRTUAL FORCE Z DIRECTION
FZ = 4*FVWZ                     		! SCALE FORCE FOR SYMMETRY
ESEL,S,MAT,,2,3
NSLE,S
NSEL,A,NODE,,1,2
LPATH,1,2
PDEF,BZ,B,Z
PRPATH,BZ
*GET,BZPOLE,PATH,0,LAST,BZ      		! EXTRACT BZ AT POLE
*GET,BZARM,PATH,0,MAX,BZ        		! EXTRACT BZ at ARM
*DIM,LABEL,CHAR,3
*DIM,VALUE,,3,3
LABEL(1) = 'FVW(Z) ','POLE(BZ) ','ARM(BZ) '
*VFILL,VALUE(1,1),DATA,80.1,0.46,2.05
*VFILL,VALUE(1,3),DATA,ABS(FZ/80.1),ABS(BZPOLE/0.46),ABS(BZARM/2.05)
*VFILL,VALUE(1,2),DATA,ABS(FZ),ABS(BZPOLE),ABS(BZARM)
/OUT,vm233,vrt,,APPEND
/COM
/COM,------------------- VM233 RESULTS COMPARISON ---------------------
/COM,
/COM,          |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
*VWRITE,LABEL(1),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,'    ',F7.3,'     ',F14.3,'       ',1F15.3)
/COM,----------------------------------------------------------------
/OUT
FINISH   
*LIST,vm233,vrt

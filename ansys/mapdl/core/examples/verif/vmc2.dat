/COM,ANSYS MEDIA REL. 140 (11/26/2011) REF. VERIF. MANUAL: REL. 140
/VERIFY,VMC2
/NOPR
*DIM,NARAY,TABLE,12,6          ! 2-D NARAY FOR RESULTS INFO
*DO,I,1,12
  *DO,J,1,6
    NARAY(I,J)=0.              ! INITIALIZE NARAY
  *ENDDO
*ENDDO
*CREATE,base,
  PARSAV,ALL
  /clear, nostart
  JPGPRF,500,100,1             ! MACRO TO SET PREFS FOR JPEG PLOTS
  /SHOW,JPEG
  PARRES,CHANGE
  /PREP7 $smrt,off
  /TITLE, VMC2, ELLIPTIC MEMBRANE UNDER A UNIFORMLY-DISTRIBUTED LOAD
  /COM,   SEE "SELECTED FE BENCHMARKS FOR STRUCTURAL AND THERMAL ANALYSIS",
  /COM,   NAFEMS REPORT NO. FEBSTA, REV. 1, OCT. 1986, TEST NO. LE1
  /COM,
  ET,1,ETYP,,,3                  ! DEFINE ELEMENT TYPE (PARAMETRICALLY)
  MP,EX,1,210E9                  ! DEFINE MATERIAL PROPERTIES
  MP,NUXY,1,.3
  R,1,0.1                        ! SET THICKNESS
  LOCAL,11,1,,,,,,,0.5           ! DEFINE ELLIPTICAL COORD. SYSTEM
  K,1,2,90
  K,4,2,0                        ! CREATE MODEL GEOMETRY
  K,5,1.165,20
  KMOVE,5,0,1.165,U,0.0,11,2.0,U,0.0
  K,8,2.0,5.0
  KMOVE,8,0,U,0.453,0.0,11,E,U,0.0
  L,1,5
  L,5,8
  L,8,4
  LOCAL,12,1,,,,,,,0.8461585
  K,2,3.25,90
  K,3,3.25,0.0
  K,6,3.25,67
  KMOVE,6,0,1.783,U,0.0,12,E,U,0.0
  K,7,3.25,25
  KMOVE,7,0,U,1.348,0.0,12,E,U,0.0
  L,2,6
  L,6,7
  L,7,3
  LESIZE,ALL,,,ARG1
  CSYS,0
  L,1,2,
  L,4,3,
  LSEL,S,LINE,,7,8
  LESIZE,ALL,,,ARG2
  LSEL,ALL
  A,4,3,7,8
  A,8,7,6,5
  A,5,6,2,1
  ESIZE,,ARG2
  ESHAPE,2
  AMESH,ALL                      ! MESH ALL AREAS
  *GET,MAXN,NODE,,NUM,MAX        ! GET MAX NODE NUMBER
  NARAY(ARG3,2)=ARG1             ! STORE N1
  NARAY(ARG3,3)=ARG2             ! STORE N2
  NARAY(ARG3,4)=MAXN*2           ! CALCULATE NO. DEGREES OF FREEDOM
  NSEL,S,LOC,Y,0
  DSYM,SYMM,Y,1                  ! APPLY BOUNDARY CONDITIONS
  NSEL,S,LOC,X,0
  DSYM,SYMM,Y,1
  CSYS,12
  NSEL,S,LOC,X,3.25
  SF,ALL,PRES,-10E6              ! APPLY PRESSURE LOAD
  NSEL,ALL

  /VIEW,1,,,1
  /ANG,1
  /AUTO,1
  /DEVICE,VECTOR,1
  /DEVICE,DITHER,1
  /TYPE,ALL,4
  /COLOR,NUM,BLUE,1
  *IF,ETYP,EQ,42,THEN
  *IF,ARG1,EQ,1,THEN
   *IF,ARG2,EQ,2,THEN
    /TITLE,VMC2 - QUADRILATERAL MESH: (N1 = 1, N2 = 2)
    EPLOT
   *ENDIF
  *ENDIF
  *ENDIF
  *IF,ETYP,EQ,42,THEN
  *IF,ARG1,EQ,3,THEN
   *IF,ARG2,EQ,4,THEN
    /TITLE,VMC2 - QUADRILATERAL MESH: (N1 = 3, N2 = 4)
    EPLOT
   *ENDIF
  *ENDIF
  *ENDIF
   *IF,ETYP,EQ,42,THEN
   *IF,ARG1,EQ,8,THEN
   *IF,ARG2,EQ,10,THEN
    /TITLE,VMC2 - QUADRILATERAL MESH: (N1 = 8, N2 = 10)
    EPLOT
   *ENDIF
  *ENDIF
  *ENDIF
  /SHOW,GRPH
  /TITLE, VMC2, ELLIPTIC MEMBRANE UNDER A UNIFORMLY-DISTRIBUTED LOAD

  OUTRES,STRS
  OUTPR,BASIC
  FINISH
  /SOLU
  SOLVE
  FINISH
  /POST1
  NSEL,S,LOC,Y,0,.1
  NSEL,R,LOC,X,2.0
  NSORT,S,Y
  *GET,MAXN,NODE,,NUM,MAX         ! GET MAX NODE NUMBER
  *GET,SYM,NODE,MAXN,S,Y          ! GET DESIRED SY STRESS VALUE
  TARG=92.7E6                     ! TARGET SY VALUE
  NARAY(ARG3,6)=SYM/TARG          ! NORMALIZED SY VALUE
  *GET,NARAY(ARG3,5),PRERR,,SEPC  ! STORE PERCENT ENERGY ERROR NORM
  PARSAV,,PARAM
  FINISH
*END
  *DO,I,1,6                       ! INITIALIZE COLUMN 1 WITH ELEMENT TYPES
   NARAY(I,1)=42
   NARAY(I+6,1)=82
  *ENDDO
  *DO,I,1,2
    *IF,I,EQ,1,THEN
      ETYP=42
      JINDX=0
    *ELSEIF,I,EQ,2,THEN
      ETYP=82
      JINDX=6
    *ENDIF
    /COM, COMMENT:  CALL MACRO TO LOOP THROUGH DIFFERENT ELEMENT TYPES
    *DO,J,1,6
      *IF,J,EQ,1,THEN
        *USE,base,1,2,JINDX+J
      *ELSEIF,J,EQ,2,THEN
        *USE,base,2,3,JINDX+J
      *ELSEIF,J,EQ,3,THEN
        *USE,base,3,4,JINDX+J
      *ELSEIF,J,EQ,4,THEN
        *USE,base,5,7,JINDX+J
      *ELSEIF,J,EQ,5,THEN
        *USE,base,8,10,JINDX+J
      *ELSEIF,J,EQ,6,THEN
        *USE,base,10,12,JINDX+J
      *ENDIF
    *ENDDO
  *ENDDO
  SAVE,temp,db
  /OUT
  /SHOW,,GRPH
  /GRID,1
  /AXLAB,X,No. DOF'S
  /AXLAB,Y,% ERROR IN ENERGY NORM
  /GTHK,AXIS,2
  /GTHK,CURVE,3
  /GROPT,LOGX,ON
  /GROPT,LOGY,ON
  /XRANGE,10,1E5
  /YRANGE,0,1.25
  *VLEN,6,1
  *VPLOT,NARAY(1,4),NARAY(1,5)
  *VLEN,6,1
  *VPLOT,NARAY(7,4),NARAY(7,5)
  /AXLAB,Y,SY RATIO
  /GROPT,LOGY,OFF
  *VLEN,6,1
  *VPLOT,NARAY(1,4),NARAY(1,6)
  *VLEN,6,1
  *VPLOT,NARAY(7,4),NARAY(7,6)
finish
/delete,PARAM
/delete,base

/VERIFY,VMC2
/NOPR
*DIM,NARAY,TABLE,12,6          ! 2-D NARAY FOR RESULTS INFO
*DO,I,1,12
  *DO,J,1,6
    NARAY(I,J)=0.              ! INITIALIZE NARAY
  *ENDDO
*ENDDO
*CREATE,base,
  PARSAV,ALL
  /clear, nostart
  JPGPRF,500,100,1             ! MACRO TO SET PREFS FOR JPEG PLOTS
  /SHOW,JPEG
  PARRES,CHANGE
  /PREP7 $smrt,off
  /TITLE, VMC2, ELLIPTIC MEMBRANE UNDER A UNIFORMLY-DISTRIBUTED LOAD
  /COM,   SEE "SELECTED FE BENCHMARKS FOR STRUCTURAL AND THERMAL ANALYSIS",
  /COM,   NAFEMS REPORT NO. FEBSTA, REV. 1, OCT. 1986, TEST NO. LE1
  /COM,
  ET,1,ETYP,,,3                  ! DEFINE ELEMENT TYPE (PARAMETRICALLY)
  MP,EX,1,210E9                  ! DEFINE MATERIAL PROPERTIES
  MP,NUXY,1,.3
  R,1,0.1                        ! SET THICKNESS
  LOCAL,11,1,,,,,,,0.5           ! DEFINE ELLIPTICAL COORD. SYSTEM
  K,1,2,90
  K,4,2,0                        ! CREATE MODEL GEOMETRY
  K,5,1.165,20
  KMOVE,5,0,1.165,U,0.0,11,2.0,U,0.0
  K,8,2.0,5.0
  KMOVE,8,0,U,0.453,0.0,11,E,U,0.0
  L,1,5
  L,5,8
  L,8,4
  LOCAL,12,1,,,,,,,0.8461585
  K,2,3.25,90
  K,3,3.25,0.0
  K,6,3.25,67
  KMOVE,6,0,1.783,U,0.0,12,E,U,0.0
  K,7,3.25,25
  KMOVE,7,0,U,1.348,0.0,12,E,U,0.0
  L,2,6
  L,6,7
  L,7,3
  LESIZE,ALL,,,ARG1
  CSYS,0
  L,1,2,
  L,4,3,
  LSEL,S,LINE,,7,8
  LESIZE,ALL,,,ARG2
  LSEL,ALL
  A,4,3,7,8
  A,8,7,6,5
  A,5,6,2,1
  ESIZE,,ARG2
  ESHAPE,2
  AMESH,ALL                      ! MESH ALL AREAS
  *GET,MAXN,NODE,,NUM,MAX        ! GET MAX NODE NUMBER
  NARAY(ARG3,2)=ARG1             ! STORE N1
  NARAY(ARG3,3)=ARG2             ! STORE N2
  NARAY(ARG3,4)=MAXN*2           ! CALCULATE NO. DEGREES OF FREEDOM
  NSEL,S,LOC,Y,0
  DSYM,SYMM,Y,1                  ! APPLY BOUNDARY CONDITIONS
  NSEL,S,LOC,X,0
  DSYM,SYMM,Y,1
  CSYS,12
  NSEL,S,LOC,X,3.25
  SF,ALL,PRES,-10E6              ! APPLY PRESSURE LOAD
  NSEL,ALL

  /VIEW,1,,,1
  /ANG,1
  /AUTO,1
  /DEVICE,VECTOR,1
  /DEVICE,DITHER,1
  /TYPE,ALL,4
  /COLOR,NUM,BLUE,1
  *IF,ETYP,EQ,182,THEN
  *IF,ARG1,EQ,1,THEN
   *IF,ARG2,EQ,2,THEN
    /TITLE,VMC2 - QUADRILATERAL MESH: (N1 = 1, N2 = 2)
    EPLOT
   *ENDIF
  *ENDIF
  *ENDIF
  *IF,ETYP,EQ,183,THEN
  MSHAPE,1,2D
  *IF,ARG1,EQ,2,THEN
   *IF,ARG2,EQ,3,THEN
    /TITLE,VMC2 - TRIANGLE MESH: (N1 = 2, N2 = 3)
    EPLOT
   *ENDIF
  *ENDIF
  *ENDIF
  *IF,ETYP,EQ,182,THEN
  *IF,ARG1,EQ,3,THEN
   *IF,ARG2,EQ,4,THEN
    /TITLE,VMC2 - QUADRILATERAL MESH: (N1 = 3, N2 = 4)
    EPLOT
   *ENDIF
  *ENDIF
  *ENDIF
  *IF,ETYP,EQ,183,THEN
  MSHAPE,1,2D
  *IF,ARG1,EQ,5,THEN
   *IF,ARG2,EQ,7,THEN
    /TITLE,VMC2 - TRIANGLE MESH: (N1 = 5, N2 = 7)
    EPLOT
   *ENDIF
  *ENDIF
  *ENDIF
   *IF,ETYP,EQ,182,THEN
   *IF,ARG1,EQ,8,THEN
   *IF,ARG2,EQ,10,THEN
    /TITLE,VMC2 - QUADRILATERAL MESH: (N1 = 8, N2 = 10)
    EPLOT
   *ENDIF
  *ENDIF
  *ENDIF
  *IF,ETYP,EQ,183,THEN
  MSHAPE,1,2D
  *IF,ARG1,EQ,10,THEN
   *IF,ARG2,EQ,12,THEN
    /TITLE,VMC2 - TRIANGLE MESH: (N1 = 10, N2 = 12)
    EPLOT
   *ENDIF
  *ENDIF
  *ENDIF
  /SHOW,GRPH
  /TITLE, VMC2, ELLIPTIC MEMBRANE UNDER A UNIFORMLY-DISTRIBUTED LOAD

  OUTRES,STRS
  OUTPR,BASIC
  FINISH
  /SOLU
  SOLVE
  FINISH
  /POST1
  NSEL,S,LOC,Y,0,.1
  NSEL,R,LOC,X,2.0
  NSORT,S,Y
  *GET,MAXN,NODE,,NUM,MAX         ! GET MAX NODE NUMBER
  *GET,SYM,NODE,MAXN,S,Y          ! GET DESIRED SY STRESS VALUE
  TARG=92.7E6                     ! TARGET SY VALUE
  NARAY(ARG3,6)=SYM/TARG          ! NORMALIZED SY VALUE
  *GET,NARAY(ARG3,5),PRERR,,SEPC  ! STORE PERCENT ENERGY ERROR NORM
  PARSAV,,PARAM
  FINISH
*END
  *DO,I,1,6                       ! INITIALIZE COLUMN 1 WITH ELEMENT TYPES
   NARAY(I,1)=182
   NARAY(I+6,1)=183
  *ENDDO
  *DO,I,1,2
    *IF,I,EQ,1,THEN
      ETYP=182
      JINDX=0
    *ELSEIF,I,EQ,2,THEN
      ETYP=183
      JINDX=6
    *ENDIF
    /COM, COMMENT:  CALL MACRO TO LOOP THROUGH DIFFERENT ELEMENT TYPES
    *DO,J,1,6
      *IF,J,EQ,1,THEN
        *USE,base,1,2,JINDX+J
      *ELSEIF,J,EQ,2,THEN
        *USE,base,2,3,JINDX+J
      *ELSEIF,J,EQ,3,THEN
        *USE,base,3,4,JINDX+J
      *ELSEIF,J,EQ,4,THEN
        *USE,base,5,7,JINDX+J
      *ELSEIF,J,EQ,5,THEN
        *USE,base,8,10,JINDX+J
      *ELSEIF,J,EQ,6,THEN
        *USE,base,10,12,JINDX+J
      *ENDIF
    *ENDDO
  *ENDDO
  SAVE,temp2,db
  /SHOW,,GRPH
  /GRID,1
  /AXLAB,X,No. DOF'S
  /AXLAB,Y,% ERROR IN ENERGY NORM
  /GTHK,AXIS,2
  /GTHK,CURVE,3
  /GROPT,LOGX,ON
  /GROPT,LOGY,ON
  /XRANGE,10,1E5
  /YRANGE,0,1.25
  *VLEN,6,1
  *VPLOT,NARAY(1,4),NARAY(1,5)
  *VLEN,6,1
  *VPLOT,NARAY(7,4),NARAY(7,5)
  /AXLAB,Y,SY RATIO
  /GROPT,LOGY,OFF
  *VLEN,6,1
  *VPLOT,NARAY(1,4),NARAY(1,6)
  *VLEN,6,1
  *VPLOT,NARAY(7,4),NARAY(7,6)
  finish
  RESUME,temp,db
  /OUT,
  /OUT,vmc2,vrt  
  /COM
  /COM,------------------- VMC2 RESULTS LISTING ------------------------------------
  /COM,
  /COM,| ETYP | N1  | N2   |   DOF  | %ERR NM  | SY RAT |
  /COM,
  *VWRITE,naray(1,1),naray(1,2),naray(1,3),NARAY(1,4),NARAY(1,5),NARAY(1,6)
  (F5.0,'  ',F5.0,'  ',F5.0,' ',F10.0,'  ',F7.3,'     ',F5.3)
  /COM,-----------------------------------------------------------------------------
  /OUT
  RESUME,temp2,db
  /OUT,vmc2,vrt,,append
  /COM
  /COM,------------------- VMC2 RESULTS LISTING ------------------------------------
  /COM,
  /COM,| ETYP | N1  | N2   |   DOF  | %ERR NM  | SY RAT |
  /COM,
  *VWRITE,naray(1,1),naray(1,2),naray(1,3),NARAY(1,4),NARAY(1,5),NARAY(1,6)
  (F5.0,'  ',F5.0,'  ',F5.0,' ',F10.0,'  ',F7.3,'     ',F5.3)
  /COM,-----------------------------------------------------------------------------
  /OUT
  *LIST,vmc2,vrt
/delete,PARAM
/delete,base
finish

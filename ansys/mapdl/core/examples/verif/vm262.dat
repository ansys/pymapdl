/COM,ANSYS MEDIA REL. 150 (11/8/2013) REF. VERIF. MANUAL: REL. 150
/VERIFY,VM262
JPGPRF,500,100,1
/SHOW,JPEG
/COM,VERFICATION MANUAL FOR TWO DIMENSIONAL THERMAL FRACTURAL PROBLEM
/TITLE,VM262,TWO DIMENSIONAL FRACTURAL PROBLEM UNDER THERMAL LOAD
/COM, REFERENCE: "W.K.WILSON, ET AL., THE USE OF THE J-INTEGRAL IN THERMAL STRESS CRACK PROBLEMS
/COM,             INTERNATIONAL JOURNAL OF FRACTURE, 1979,PG:377-387
/COM,
/OUT,SCRATCH
/PREP7
A=1                    ! CRACK LENGTH
W=2*A                  ! WIDTH
L=4*W                  ! LENGTH
L2=L/2                 ! HALF OF LENGTH
E=1E5                  ! YOUNG'S MODULUS 
NU=0.3                 ! POISSONS RATIO
T0=10                  ! TEMPERATURES AT THE RIGHT SIDE
ALPHA=1e-4             ! SECANT COEFFICIENTS OF THERMAL EXPANSION 

MP,EX,1,E
MP,NUXY,1,NU
MP,ALPX,1,ALPHA        ! THERMAL MATERIAL PROPERTIES 
MP,REFT,1,0            ! REFERENCE TEMPERATURE FOR ELEMENTs

K,1,
K,2,W/2
K,3,W/2,A
K,4,W/2,L2
K,5,-W/2,L2
K,6,-W/2,A
K,7,-W/2
A,1,2,3,6,7
A,3,4,5,6

ET,1,PLANE182
KEYOPT,1,3,2            ! PLANE STRAIN

ESIZE,A/6
KSCON,1,A/12,1,8
AMESH,1
ESIZE,W/6
AMESH,2

NSEL,S,LOC,X,0,W/2      ! SYMMETRICAL CONDITIONS
NSEL,R,LOC,Y,0
D,ALL,UY,0
NSEL,ALL

NSEL,S,LOC,Y,L2         ! TOP EDGE FIXED
D,ALL,UY,0
D,ALL,UX,0
NSEL,ALL
FINISH

/SOLU
ANTYPE,STATIC
OUTRES,CINT,ALL
CNVTOL,F,1,0.001
CNVTOL,U,1,0.001
TIME,1.0

NSEL,S,LOC,X,0
NSEL,R,LOC,Y,0
CM,CRACK1,NODE
ALLSEL

*GET,NNODE,NODE,0,COUNT
*DO,I,1,NNODE
  TN=2*T0/W*NX(I)
  BF,I,TEMP,TN          ! APPLY TEMPERATURE LOADING
*ENDDO
ALLSEL

CINT,NEW,1            
CINT,NAME,CRACK1        !CRACK ID   
CINT,NCON,6             !NUMBER OF COUNTOURS  
CINT,SYMM,ON            !SYMMETRICAL CONDITION
CINT,NORM,,
CINT,LIST
ALLSEL
/OUT,
SOLVE
FINISH

/OUT,SCRATCH
/POST1
PRCINT,1
*GET,J1,CINT,1,CTIP,1,,1
*GET,J2,CINT,1,CTIP,1,,2
*GET,J3,CINT,1,CTIP,1,,3
*GET,J4,CINT,1,CTIP,1,,4
*GET,J5,CINT,1,CTIP,1,,5
*GET,J6,CINT,1,CTIP,1,,6
JC1=ABS(J2+J3+J4+J5+J6)/5      
K1=SQRT(JC1*E/(1-NU*NU))
/COM, Mechanical APDL RESULTS
*STAT,K1
/COM, EXPECTED RESULTS FROM REFERENCE PAPER
S0=E*ALPHA*T0/(1-NU)
K1_EXPECT=0.5*S0*SQRT(3.1416*A)
*STAT,K1_EXPECT
*DIM,LABEL,CHAR,1,1
*DIM,VALUE,,1,3
LABEL(1,1) = 'K1'
*VFILL,VALUE(1,1),DATA,K1_EXPECT
*VFILL,VALUE(1,2),DATA,K1
*VFILL,VALUE(1,3),DATA,ABS(K1/K1_EXPECT)
SAVE,TABLE_1
FINISH
RESUME,TABLE_1
/COM,
/OUT,vm262,vrt
/COM,
/COM,
/COM, ----------------------VM262 RESULTS COMPARISON------------------------
/COM,
/COM,           |  TARGET  |   Mechanical APDL  |  RATIO
/COM,
/COM,   STRESS-INTENSITY
/COM,  --------------------
/COM,
*VWRITE,LABEL(1,1),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,'   ',F10.4,'  ',F14.4,'   ',F15.3)
/COM,
/COM, ------------------------------------------------------------------------
/OUT,
*LIST,vm262,vrt
FINISH

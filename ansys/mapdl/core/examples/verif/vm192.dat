/COM,ANSYS MEDIA REL. 150 (11/8/2013) REF. VERIF. MANUAL: REL. 150
/VERIFY,VM192
/PREP7
SMRT,OFF
/TITLE, VM192, COOLING OF A BILLET BY RADIATION
/COM    THERMAL RADIATION HEAT TRANSFER, SIEGEL AND HOWELL, 2ND EDITION,
/COM    PG. 229, PROBLEM NO. 21.
ET,1,SOLID70            ! 3-D THERMAL SOLID ELEMENT
ET,2,SURF152,,,,1,1     ! 3-D THERMAL SURFACE EFFECT ELEMENTS
KEYOPT,2,9,1            ! RADIATION OPTION
R,2,1,0.1712E-8         ! FORM FACTOR = 1, STEFAN-BOLTZMANN CONSTANT
MP,KXX,1,10000          ! ARBITRARY CONDUCTIVITY
MP,C,1,0.11            
MP,DENS,1,487.5
MP,EMIS,2,1             ! BLACK BODY EMISSIVITY
BLOCK,,2,,2,,4
ESIZE,,1
VMESH,1                 ! MESH WITH A SINGLE SOLID70 ELEMENT
TYPE,2
REAL,2
MAT,2
N,100,5,5,5             ! EXTRA "SPACE" NODE FOR RADIATION
ESURF,100               ! GENERATE SURF152 ELEMENTS
FINISH

/SOLU
SOLCONTROL,0
ANTYPE,TRANS            ! TRANSIENT ANALYSIS
D,100,TEMP,530          ! SPECIFY SURROUNDING ABSOLUTE TEMPERATURE
TUNIF,2000              ! INITIAL BILLET ABSOLUTE TEMPERATURE
AUTOTS,ON               
KBC,1                   ! STEP SURROUNDING TEMPERATURE IN FIRST TIME STEP
DELTIM,0.005            ! INITIAL (MINIMUM) INTEGRATION TIME STEP
OUTRES,,ALL
OUTPR,NSOL,LAST
TIME,3.7                ! TRANSIENT TIME SPAN
/OUT,SCRATCH
SOLVE
FINISH

/POST26
/OUT,
NSOL,2,1,TEMP,,TEMP     
PRVAR,2                 ! PRINT TEMPERATURE HISTORY OF BILLET
*GET,T,VARI,2,RTIME,3.7
*DIM,VALUE,,1,3
*DIM,LABEL,CHAR,1,2
LABEL(1,1) = 'TEMP (B) '
LABEL(1,2) = 'DEG R'
*VFILL,VALUE(1,1),DATA,1000
*VFILL,VALUE(1,2),DATA,T
*VFILL,VALUE(1,3),DATA,ABS(T/1000)
/COM
/OUT,vm192,vrt
/COM,------------------- VM192 RESULTS COMPARISON --------------
/COM,
/COM,  LOAD STP 4      |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.1,'  ',F15.1,'   ',1F18.3)
/COM,-----------------------------------------------------------
/OUT
FINISH
*LIST,vm192,vrt

/COM,ANSYS MEDIA REL. 150 (11/8/2013) REF. VERIF. MANUAL: REL. 150
/VERIFY,VM38
/PREP7
/TITLE, VM38, PLASTIC LOADING OF A THICK-WALLED CYLINDER UNDER PRESSURE
C***      STR. OF MATLS., TIMOSHENKO, PART 2, 3RD ED., PAGE 388, ART. 70

C***     USING PLANE182
ET,1,PLANE182,3,,1            ! AXISYMMETRIC SOLID,SIMPLIFIED ENHANCED STRAIN FORMULATION
ET,2,SURF153,,,1,1           ! AXISYMMETRIC 2-D SURFACE EFFECT ELEMENT WITHOUT
                             !  MIDSIDE NODES
REAL,2
R,2,,,,,,,
RMORE,1                      ! UNIT THICKNESS
MP,EX,1,30E6
MP,NUXY,1,.3
TB,BKIN,1,1                  ! BILINEAR KINEMATIC HARDENING
TBTEMP,70
TBDATA,1,30000,0             ! YIELD STRESS AND ZERO TANGENT MODULUS
N,1,4                        ! DEFINE NODES
N,6,8
FILL
NGEN,2,10,1,6,1,,1
E,11,1,2,12                  ! DEFINE ELEMENTS
EGEN,5,1,1
CPNGEN,1,UY,11,16            ! COUPLE NODES
TYPE,2                       ! CREATE SURF153 TO APPLY SURFACE PRESSURE LOADING
REAL,2
NSEL,S,LOC,X,4
ESURF
NSEL,ALL
TREF,70                      ! BOUNDARY CONDITIONS AND LOADING
D,1,UY,,,6
FINISH
/SOLU    
ESEL,S,TYPE,,2               ! SELECT SURF153 ELEMENTS TO APPLY SURFACE PRESSURE 
                             !  LOADING FOR ELASTIC ANALYSIS
SFE,ALL,1,PRES,,12990
ESEL,ALL
OUTPR,BASIC,1
/OUT,SCRATCH
SOLVE
FINISH
/POST1
/OUT,
ETABLE,STRS_R,S,X
ETABLE,STRS_T,S,Z
*GET,SIGR_I,ELEM,1,ETAB,STRS_R
*GET,SIGT_I,ELEM,1,ETAB,STRS_T
*GET,SIGR_O,ELEM,5,ETAB,STRS_R
*GET,SIGT_O,ELEM,5,ETAB,STRS_T

*DIM,LABEL,CHAR,4,2
*DIM,VALUE,,4,3
LABEL(1,1) = 'SIGR LFT','SIGT LFT','SIGR RT_','SIGT RT_'
LABEL(1,2) = '_END psi','_END psi','END psi ','END psi '
*VFILL,VALUE(1,1),DATA,-9984,18645,-468,9128
*VFILL,VALUE(1,2),DATA,SIGR_I,SIGT_I,SIGR_O,SIGT_O
*VFILL,VALUE(1,3),DATA,ABS(SIGR_I/9984),ABS(SIGT_I/18645)
*VFILL,VALUE(3,3),DATA,ABS(SIGR_O/468),ABS(SIGT_O/9128)
SAVE,TABLE_1


/PREP7
C*** USING SOLID185
EDELE,ALL
NDELE,ALL                    ! REMOVE PREVIOUS MODEL GEOMETRY
ET,1,SOLID185,,3               ! 3-D SOLID ELEMENT, SIMPLIFIED ENHANCED STRAIN FORMULATION 
ET,2,SURF154,,,,1            ! 3-D SURFACE EFFECT ELEMENT WITHOUT
                             !  MIDSIDE NODE
REAL,2
R,2,,,,,,,
RMORE,1                      ! UNIT THICKNESS                             
CSYS,1
N,1,4,-2.5                   ! DEFINE NODES
N,6,8,-2.5
FILL
NGEN,2,6,1,6,1,,5
NGEN,2,12,1,12,1,,,1
NUMCMP,ELEM
NUMSTR,ELEM,1
TYPE,1
MAT,1
E,1,2,8,7,13,14,20,19        ! DEFINE ELEMENTS
EGEN,5,1,-1
TYPE,2                       ! CREATE SURF154 TO APPLY SURFACE PRESSURE LOADING
REAL,2
NSEL,S,NODE,,1,7,6
NSEL,A,NODE,,13,19,6
ESURF                        
NSEL,ALL
NROTAT,ALL                   ! ROTATE ALL NODES INTO CYLINDRICAL COORDINATES
CPDELE,1,1,1                 ! REMOVE NODAL COUPLING
SFDELE,ALL,PRES              ! REMOVE NODAL PRESSURES
D,ALL,UY,0.0                 ! CONSTRAIN ALL NODES IN TANGENTIAL DIRECTION
NSEL,S,LOC,Z,1               ! SELECT NODES AT Z = 1
CP,1,UZ,ALL                  ! COUPLE SELECTED NODES IN UZ DIRECTION TO 
                             !  SIMULATE GENERALIZED 3-D PLANE STRAIN BEHAVIOR
NSEL,S,LOC,Z,0               ! CONSTRAIN NODES AT Z = 0 IN UZ DIRECTION
D,ALL,UZ
NSEL,ALL
FINISH
/SOLU    
ESEL,S,TYPE,,2               ! SELECT SURF154 ELEMENTS TO APPLY SURFACE PRESSURE 
                             !  LOADING FOR ELASTIC ANALYSIS
SFE,ALL,1,PRES,,12990
ESEL,ALL
OUTPR,BASIC,1
/OUT,SCRATCH
SOLVE
FINISH

/POST1
/OUT,
ETABLE,STRS_R,S,X
ETABLE,STRS_T,S,Y
*GET,SIGR_I,ELEM,1,ETAB,STRS_R
*GET,SIGT_I,ELEM,1,ETAB,STRS_T
*GET,SIGR_O,ELEM,5,ETAB,STRS_R
*GET,SIGT_O,ELEM,5,ETAB,STRS_T

LABEL(1,1) = 'SIGR LFT','SIGT LFT','SIGR RT_','SIGT RT_'
LABEL(1,2) = '_END psi','_END psi','END psi ','END psi '
*VFILL,VALUE(1,1),DATA,-9984,18645,-468,9128
*VFILL,VALUE(1,2),DATA,SIGR_I,SIGT_I,SIGR_O,SIGT_O
*VFILL,VALUE(1,3),DATA,ABS(SIGR_I/9984),ABS(SIGT_I/18645)
*VFILL,VALUE(3,3),DATA,ABS(SIGR_O/468),ABS(SIGT_O/9128)
SAVE,TABLE_2
FINISH


RESUME,TABLE_1
/COM
/OUT,vm38,vrt
/COM,------------------- VM38 RESULTS COMPARISON ---------------
/COM,
/COM,                 |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
/COM,FULLY ELASTIC, PLANE182 RESULTS:
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.0,'  ',F14.0,'   ',1F15.3)
/NOPR
RESUME,TABLE_2
/GOPR
/COM,
/COM,FULLY ELASTIC, SOLID185 RESULTS:
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.0,'  ',F14.0,'   ',1F15.3)
/COM,-----------------------------------------------------------
/COM,
/COM,-----------------------------------------------------------
/COM,-----------------------------------------------------------
/OUT
FINISH
*LIST,vm38,vrt


/COM,ANSYS MEDIA REL. 150 (11/8/2013) REF. VERIF. MANUAL: REL. 150
/VERIFY,VM142
/FILNAM,vm142         ! DEFINE JOBNAME FOR THE COARSE MODEL 
JPGPRF,500,100,1             ! MACRO TO SET PREFS FOR JPEG PLOTS
/SHOW,JPEG
/GRAPH,POWER
/PREP7
smrt,off
/TITLE, VM142, STRESS CONCENTRATION AT A HOLE IN A PLATE
C*** ROARK 4TH EDITION, PAGE 384.
/NOPR
ANTYPE,STATIC         ! STATIC ANALYSIS
ET,1,PLANE183
MP,EX,1,30E6
MP,NUXY,1,0.3
K,1,6                 ! KEYPOINTS
K,2,6,6
K,3,,6
K,4,,.5
K,5,.5
K,6
L,1,2                 ! LINE SEGMENTS
L,2,3
L,3,4
LESIZE,3,,,4,.25      ! DEFINE NO. OF DIVISONS AND SPACING FOR LINE 3
LARC,4,5,6,0.5
LESIZE,4,,,6          ! DEFINE NO. OF DIVISONS FOR LINE 4
L,5,1
LESIZE,5,,,4,4        ! DEFINE NO. OF DIVISONS AND SPACING FOR LINE 5
AL,1,2,3,4,5          ! AREA DEFINED BY 5 BOUNDING LINES
ESIZE,,4              ! 4 DIVISIONS PER LINE
MSHAPE,1,2D
MSHKEY,0
AMESH,ALL
LSEL,S,LINE,,3,5,2
DL,ALL,,SYMM
LSEL,S,LINE,,1
NSLL,,1
SF,ALL,PRES,-1000.    ! APPLY TENSION ON PLATE
LSEL,ALL
NSEL,ALL
CSYS,1
FINISH
/SOLU    
SOLVE
FINISH
SAVE                  ! SAVE FILE AS VM142.DB
/POST1
SET,1,1
NSORT,S,X,,,3         ! SORT BASED ON SX, RETAIN ONLY THE HIGHEST 3
PRNSOL,S,COMP
PLESOL,S,X
*GET,CRSESTR,NODE,18,S,X
*status,parm
*DIM,LABEL,CHAR,1,2
*DIM,VALUE,,1,3
LABEL(1,1) = 'MX STR '
LABEL(1,2) = 'CRS MODEL'
*VFILL,VALUE(1,1),DATA,3018
*VFILL,VALUE(1,2),DATA,CRSESTR
*VFILL,VALUE(1,3),DATA,ABS(CRSESTR/3018)
SAVE,TABLE_1
FINISH
/CLEAR, NOSTART ! CLEAR THE DATABASE
/FILNAM,SUBMODEL      ! DEFINE JOBNAME FOR THE SUBMODEL
/PREP7
smrt,off
/NOPR
/TITLE, VM142, STRESS CONCENTRATION AT A HOLE IN A PLATE
C*** BOUNDARY INTERPOLATION, MODIFIED SECTION
ANTYPE,STATIC
ET,1,PLANE182,2
MP,EX,1,30E6
MP,NUXY,1,0.3
CSYS,1
K,10,.5,45            ! DEFINE KEYPOINTS FOR SUBMODEL
K,11,.5,90
K,12,1.5,45
K,13,1.5,90
A,10,12,13,11
ESIZE,,8              ! 8 DIVISIONS 
MSHK,1                    ! MAPPED AREA MESH
MSHA,0,2D                 ! USING QUADS 
AMESH,1
LSEL,S,LINE,,1,2
NSLL,,1               ! SELECT NODES OF CUT BOUNDARY
NWRITE                ! WRITE GEOMETRY TO SUBMODEL.NODE
LSEL,ALL
NSEL,ALL
FINISH
SAVE                  ! SAVE SUBMODEL DATA IN FILE SUBMODEL.DB
/POST1
RESUME,vm142,db       ! RESUME FROM FILE VM142.DB 
FILE,vm142,rst        ! DEFINE RESULTS FILE NAME
CBDOF,,,,,,,0,,0      ! ACTIVATE CUT BOUNDARY INTERPOLATION
FINISH
/PREP7
smrt,off
RESUME                ! RESUME SUBMODEL FROM FILE SUBMODEL.DB
/NOPR
/INPUT,,cbdo,,:cb1    ! READ IN INTERPOLATED B.C.'S FROM SUBMODEL.CBDO
/GOPR
LSEL,S,LINE,,3        ! APPLY REMAINING BOUNDARY CONDITIONS
DL,ALL,,SYMM
FINISH
/SOLU    
SOLVE
FINISH
/POST1
SET,1,1
NSORT,S,X,,,3
PRNSOL,S,COMP
PLESOL,S,X
*GET,SUBSTR,NODE,18,S,X
*DIM,LABEL,CHAR,1,2
*DIM,VALUE,,1,3
LABEL(1,1) = 'MAX STRS'
LABEL(1,2) = ' SUBMOD'
*VFILL,VALUE(1,1),DATA,3018
*VFILL,VALUE(1,2),DATA,SUBSTR
*VFILL,VALUE(1,3),DATA,ABS(SUBSTR/3018 )
SAVE,TABLE_2
FINISH
/CLEAR,NOSTART
RESUME,TABLE_1
/OUT,vm142,vrt 
/COM,-------------------(VM142)RESULTS COMPARISON---------------
/COM,
/COM,                 |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
/COM,PLANE183
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.0,'  ',F12.0,'   ',1F15.3)
/NOPR
RESUME,TABLE_2
/GOPR
/COM,
/COM,PLANE182
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.0,'  ',F12.0,'   ',1F15.3)
/COM,-----------------------------------------------------------
/COM,
/OUT
FINISH
/DEL,TABLE_1
/DEL,TABLE_2
*LIST,vm142,vrt

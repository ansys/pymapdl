/COM,ANSYS MEDIA REL. 150 (11/8/2013) REF. VERIF. MANUAL: REL. 150
/VERIFY,VM154
/PREP7
/TITLE, VM154, VIBRATION OF A FLUID COUPLING
C***            FRITZ, ASME J. OF ENG. FOR INDUST., VOL. 94, 1972, PP 167-173.
C***            USING FLUID COUPLING ELEMENTS (FLUID38)
ANTYPE,MODAL          ! MODE-FREQUENCY ANALYSIS
ET,1,FLUID38
ET,2,COMBIN14,,1      ! ELEMENT WITH UX DEGREE OF FREEDOM
R,1,8,7,1             ! GEOMETRIC PROPERTIES OF FLUID38
R,2,10                ! SPRING STIFFNESS
MP,DENS,1,934E-7
N,1
N,2
E,1,2
REAL,2
TYPE,2
E,1,2                 ! TYPE 2 ELEMENT WITH REAL CONSTANT 2
OUTPR,,1
D,1,UZ
D,2,ALL
FINISH
/SOLU    
MODOPT,LANB,1
MXPAND,1
SOLVE
FINISH
/POST1
*GET,F1,MODE,1,FREQ
*DIM,LABEL,CHAR,1,2
*DIM,VALUE,,1,3
LABEL(1,1) = 'f FLD38 '
LABEL(1,2) = 'Hz'
*VFILL,VALUE(1,1),DATA,1.5293
*VFILL,VALUE(1,2),DATA,F1
*VFILL,VALUE(1,3),DATA,ABS(F1/1.5293)
SAVE,TABLE_1
FINISH
RESUME,TABLE_1
/COM
/OUT,vm154,vrt
/COM,------------------- VM154 RESULTS COMPARISON -------------
/COM,
/COM,                 |    TARGET   |    Mechanical APDL   |    RATIO
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F11.4,'  ',F16.4,'   ',1F16.3)
/COM,
/COM,----------------------------------------------------------
FINISH
/DELETE,TABLE_1
/OUT
*LIST,vm154,vrt
FINISH

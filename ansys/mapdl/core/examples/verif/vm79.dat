/COM,ANSYS MEDIA REL. 150 (11/8/2013) REF. VERIF. MANUAL: REL. 150
/VERIFY,VM79
/PREP7
/TITLE, VM79, TRANSIENT RESPONSE OF A BI-LINEAR SPRING ASSEMBLY
C***VIBRATION THEORY AND APPLICATIONS, THOMSON, 2ND PRINTING, PAGE 150,FIG 5.6-1
ET,1,COMBIN40,,,2,,,2    ! Y DOF ELEMENTS, MASS AT NODE J
R,1,200,,.5              ! K1 = 200; M = .5
N,1
N,2
E,1,2
GP,3,2,FY,-200,.75       ! GAP CONDITION
D,1,UY,,,3,2             ! CONSTRAIN UY DOF AT NODES 1 AND 3
FINISH

/SOLUTION
ANTYPE,MODAL
MODOPT,LANB,1           ! PERFORM MODAL SOLVE USING LANB EIGENSOLVER
MXPAND,1
SOLVE
FINISH

/SOLU    
ANTYPE,TRANSIENT
TRNOPT,MSUP,1            ! PERFORM MODE SUPERPOSITION TRANSIENT SOLVE
DELTIM,25E-4             ! INTEGRATION TIME STEP
KBC,1                    ! STEP BOUNDARY CONDITIONS
F,2,FY
OUTPR,BASIC,1
SOLVE                    ! STATIC LOAD STEP

TIME,25E-4               ! TIME AT END OF LOAD STEP
F,2,FY,-4E3              ! APPLY 4000 LB. LOAD
SOLVE

TIME,.105
F,2,FY,0                 ! REMOVE LOAD
SOLVE
FINISH

/POST26
FILE,,rdsp
NSOL,2,2,U,Y,2UY          ! STORE UY DISPLACEMENTS OF APPROPRIATE NODES
PRVAR,2
*GET,Y1,VARI,2,RTIME,.09
*GET,Y2,VARI,2,RTIME,.04
*GET,Y3,VARI,2,RTIME,.07
*GET,Y4,VARI,2,RTIME,.085
*GET,Y5,VARI,2,RTIME,.105
*DIM,LABEL_1,CHAR,3,2
*DIM,VALUE_1,,3,4
LABEL_1(1,1) = 'TIME    ','Y, in   ','Y, in   '
LABEL_1(1,2) = 'sec     ','linear ','bilinear'
*VFILL,VALUE_1(1,1),DATA,.040,-.68122,Y2
*VFILL,VALUE_1(1,2),DATA,.070,-.97494,Y3
*VFILL,VALUE_1(1,3),DATA,.085,-.99604,Y4
*VFILL,VALUE_1(1,4),DATA,.105,-.88666,Y5
*DIM,LABEL_2,CHAR,1,2
*DIM,VALUE_2,,1,3
LABEL_2(1,1) = 'Y, in   '
LABEL_2(1,2) = 'MAX     '
*VFILL,VALUE_2(1,1),DATA,-1.0417
*VFILL,VALUE_2(1,2),DATA,Y1
*VFILL,VALUE_2(1,3),DATA,ABS(Y1/1.0417)
/COM
/OUT,vm79,vrt
/COM,------------------- VM79 RESULTS COMPARISON ----------------- 
/COM,
/COM,                 |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
/COM,TIME=.09 sec
/COM,
*VWRITE,LABEL_2(1,1),LABEL_2(1,2),VALUE_2(1,1),VALUE_2(1,2),VALUE_2(1,3)
(1X,A8,A8,'   ',F10.5,'  ',F10.5,'   ',1F5.3)
/COM,
/COM,COMPARISON OF Mechanical APDL LINEAR (VM74) AND BILINEAR SPRING RESULTS
/COM,
*VWRITE,LABEL_1(1,1),LABEL_1(1,2),VALUE_1(1,1),VALUE_1(1,2),VALUE_1(1,3),VALUE_1(1,4)
(1X,A8,A8,'   ',F8.5,'  ',F8.5,'  ',F15.5,'  ',F15.5)
/COM,
/COM,-------------------------------------------------------------
/OUT
FINISH
*LIST,vm79,vrt

/COM,ANSYS MEDIA REL. 150 (11/8/2013) REF. VERIF. MANUAL: REL. 150
/VERIFY,VM248
/SHOW,JPEG
JPGPRF,500,100,1           !* MACRO TO SET PREFS FOR JPEG PLOTS
/TITLE, VM248, DELAMINATION OF DOUBLE CANTILEVER BEAM - 2D PLANE STRAIN
C*** USING INTER202
/COM,   REF: ALFANO, G. AND CRISFIELD, M. A.,
/COM,   "FINITE ELEMENT INTERFACE MODELS FOR THE DELAMINATION ANALYSIS
/COM,   OF LAMINATED COMPOSITES: MECHANICAL AND COMPUTATIONAL ISSUES"
/COM,   INT. J. NUMER. METH. ENGNG 2001, 50:1701-1736.
/PREP7
ET,1,182                   !* 2D 4-NODE STRUCTURAL SOLID ELEMENT
KEYOPT,1,1,2               !* ENHANCE STRAIN FORMULATION
KEYOPT,1,3,2               !* PLANE STRAIN
ET,2,182
KEYOPT,2,1,2
KEYOPT,2,3,2
ET,3,202                   !* 2D 4-NODE COHESIVE ZONE ELEMENT
KEYOPT,3,3,2               !* PLANE STRAIN
MP,EX,4,1.353E5            !* E11 = 135.3 GPA
MP,EY,4,9.0E3              !* E22 =   9.0 GPA
MP,EZ,4,9.0E3              !* E33 =   9.0 GPA
MP,GXY,4,5.2E3             !* G12 =   5.2 GPA
MP,PRXY,4,0.24
MP,PRXZ,4,0.24
MP,PRYZ,4,0.46
GMAX = 0.004
TNMAX = 25                 !* TENSILE STRENGTH
TB,CZM,5,,,EXPO            !* COHESIVE ZONE MATERIAL
TBDATA,1,TNMAX,GMAX,1000.0
RECTNG,0,100,0,1.5         !* DEFINE AREAS
RECTNG,0,100,0,-1.5
LSEL,S,LINE,,2,8,2         !* DEFINE LINE DIVISION
LESIZE,ALL,0.75
LSEL,INVE
LESIZE,ALL, , ,200
ALLSEL,ALL
TYPE,1                     !* MESH AREA 2
MAT,4
LOCAL,11,0,0,0,0
ESYS,11
AMESH,2
CSYS,0
TYPE,2                     !* MESH AREA 1
ESYS,11
AMESH,1
CSYS,0
NSEL,S,LOC,X,30,100
NUMMRG,NODES
ESLN
TYPE,3
MAT,5
CZMESH,,,1,Y,0,            !* GENERATE INTERFACE ELEMENTS
ALLSEL,ALL
NSEL,S,LOC,X,100           !* APPLY CONSTRAINTS
D,ALL,ALL
NSEL,ALL
FINISH
/SOLU
ESEL,S,TYPE,,2
NSLE,S
NSEL,R,LOC,X
NSEL,R,LOC,Y,1.5           !* APPLY DISPLACEMENT LOADING ON TOP
D,ALL,UY,10
NSEL,ALL
ESEL,ALL
ESEL,S,TYPE,,1
NSLE,S
NSEL,R,LOC,X
NSEL,R,LOC,Y,-1.5          !* APPLY DISPLACEMENT LOADING ON BOTTOM
D,ALL,UY,-10
NSEL,ALL
ESEL,ALL
NLGEOM,ON
AUTOTS,ON
TIME,1
NSUBST,40,40,40
OUTRES,ALL,ALL
/OUT,SCRATCH
SOLVE                      !* PERFORM SOLUTION
FINISH
/POST26
NSEL,S,LOC,Y,1.5
NSEL,R,LOC,X,0
*GET,NTOP,NODE,0,NUM,MAX
NSEL,ALL
NSOL,2,NTOP,U,Y,UY
RFORCE,3,NTOP,F,Y,FY
PROD,4,3, , ,RF, , ,20
/TITLE,VM248, DCB: REACTION AT TOP NODE VERSES PRESCRIBED DISPLACEMENT
/AXLAB,X,DISP U (mm)
/AXLAB,Y,REACTION FORCE R (N)
/YRANGE,0,60
XVAR,2
PLVAR,4
PRVAR,UY,RF
*GET,TMAX,VARI,4,EXTREM,TMAX   !* TIME CORRESPONDING TO MAX RFORCE
FINISH
/POST1
SET, , , , ,TMAX               !* RETRIEVE RESULTS AT TMAX
NSEL,S,NODE, ,NTOP             !* SELECT NODE NTOP
/OUT,
*GET,RF_NTOP,NODE,NTOP,RF,FY   !* FY RFORCE AT NODE NTOP
*GET,UY_NTOP,NODE,NTOP,U,Y     !* DISP AT NODE NTOP CORRESPONDING TO RFORCE
RF_MAX = RF_NTOP*20            !* PLANE STRAIN OPTION AND WIDTH = 20 mm
SET,LAST                       !* RETRIEVE RESULTS AT LAST SUBSTEP
*GET,RF_END,NODE,NTOP,RF,FY    !* FY RFORCE AT NODE NTOP AT LAST SUBSTEP
*GET,UY_END,NODE,NTOP,U,Y      !* DISP AT NODE NTOP CORRESPONDING TO RFORCE
RF_END = RF_END*20             !* PLANE STRAIN OPTION AND WIDTH = 20 mm
*DIM,LABEL,CHAR,2,2
*DIM,VALUE,,2,3
*DIM,VALUE2,,2,3
LABEL(1,1) = 'RFORCE','DISP '
LABEL(1,2) = 'FY (N)','UY (mm)'
*VFILL,VALUE(1,1),DATA,60.0,1.0
*VFILL,VALUE(1,2),DATA,RF_MAX,UY_NTOP
*VFILL,VALUE(1,3),DATA,ABS(RF_MAX/60.0),ABS(UY_NTOP/1.0)
*VFILL,VALUE2(1,1),DATA,24,10.0
*VFILL,VALUE2(1,2),DATA,RF_END,UY_END
*VFILL,VALUE2(1,3),DATA,ABS(RF_END/24.0),ABS(UY_END/10.0)
SAVE,INF1
FINI
/CLEAR,NOSTART
/OUT,SCRATCH



/TITLE, VM248, DELAMINATION OF DOUBLE CANTILEVER BEAM - 2D PLANE STRAIN
C*** USING INTER203
/PREP7
ET,1,183                   !* 2D 8-NODE STRUCTURAL SOLID ELEMENT
KEYOPT,1,3,2               !* PLANE STRAIN
ET,2,183
KEYOPT,2,3,2
ET,3,203                   !* 2D 6-NODE COHESIVE ZONE ELEMENT
KEYOPT,3,3,2               !* PLANE STRAIN
MP,EX,4,1.353E5            !* E11 = 135.3 GPA
MP,EY,4,9.0E3              !* E22 =   9.0 GPA
MP,EZ,4,9.0E3              !* E33 =   9.0 GPA
MP,GXY,4,5.2E3             !* G12 =   5.2 GPA
MP,PRXY,4,0.24
MP,PRXZ,4,0.24
MP,PRYZ,4,0.46
GMAX = 0.004
TNMAX = 25                 !* TENSILE STRENGTH
TB,CZM,5,,,EXPO            !* COHESIVE ZONE MATERIAL
TBDATA,1,TNMAX,GMAX,1000.0
RECTNG,0,100,0,1.5         !* DEFINE AREAS
RECTNG,0,100,0,-1.5
LSEL,S,LINE,,2,8,2         !* DEFINE LINE DIVISION
LESIZE,ALL,1.5
LSEL,INVE
LESIZE,ALL, , ,200
ALLSEL,ALL
TYPE,1                     !* MESH AREA 2
MAT,4
LOCAL,11,0,0,0,0
ESYS,11
AMESH,2
CSYS,0
TYPE,2                     !* MESH AREA 1
ESYS,11
AMESH,1
CSYS,0
NSEL,S,LOC,X,30,100
NUMMRG,NODES
ESLN
TYPE,3
MAT,5
CZMESH,,,1,Y,0,            !* GENERATE INTERFACE ELEMENTS
ALLSEL,ALL
NSEL,S,LOC,X,100           !* APPLY CONSTRAINTS
D,ALL,ALL
NSEL,ALL
FINISH
/SOLU
ESEL,S,TYPE,,2
NSLE,S
NSEL,R,LOC,X
NSEL,R,LOC,Y,1.5           !* APPLY DISPLACEMENT LOADING ON TOP
D,ALL,UY,10
NSEL,ALL
ESEL,ALL
ESEL,S,TYPE,,1
NSLE,S
NSEL,R,LOC,X
NSEL,R,LOC,Y,-1.5          !* APPLY DISPLACEMENT LOADING ON BOTTOM
D,ALL,UY,-10
NSEL,ALL
ESEL,ALL
NLGEOM,ON
AUTOTS,ON
TIME,1
NSUBST,40,40,40
OUTRES,ALL,ALL
/OUT,SCRATCH
SOLVE                      !* PERFORM SOLUTION
FINISH
/POST26
NSEL,S,LOC,Y,1.5
NSEL,R,LOC,X,0
*GET,NTOP,NODE,0,NUM,MAX
NSEL,ALL
NSOL,2,NTOP,U,Y,UY
RFORCE,3,NTOP,F,Y,FY
PROD,4,3, , ,RF, , ,20
/TITLE,VM248, DCB: REACTION AT TOP NODE VERSES PRESCRIBED DISPLACEMENT
/AXLAB,X,DISP U (mm)
/AXLAB,Y,REACTION FORCE R (N)
/YRANGE,0,60
XVAR,2
PLVAR,4
PRVAR,UY,RF
*GET,TMAX,VARI,4,EXTREM,TMAX   !* TIME CORRESPONDING TO MAX RFORCE
FINISH
/POST1
SET, , , , ,TMAX               !* RETRIEVE RESULTS AT TMAX
NSEL,S,NODE, ,NTOP             !* SELECT NODE NTOP
/OUT,
*GET,RF_NTOP,NODE,NTOP,RF,FY   !* FY RFORCE AT NODE NTOP
*GET,UY_NTOP,NODE,NTOP,U,Y     !* DISP AT NODE NTOP CORRESPONDING TO RFORCE
RF_MAX = RF_NTOP*20            !* PLANE STRAIN OPTION AND WIDTH = 20 mm
SET,LAST                       !* RETRIEVE RESULTS AT LAST SUBSTEP
*GET,RF_END,NODE,NTOP,RF,FY    !* FY RFORCE AT NODE NTOP AT LAST SUBSTEP
*GET,UY_END,NODE,NTOP,U,Y      !* DISP AT NODE NTOP CORRESPONDING TO RFORCE
RF_END = RF_END*20             !* PLANE STRAIN OPTION AND WIDTH = 20 mm
*DIM,LABEL,CHAR,2,2
*DIM,VALUE,,2,3
*DIM,VALUE2,,2,3
LABEL(1,1) = 'RFORCE','DISP '
LABEL(1,2) = 'FY (N)','UY (mm)'
*VFILL,VALUE(1,1),DATA,60.0,1.0
*VFILL,VALUE(1,2),DATA,RF_MAX,UY_NTOP
*VFILL,VALUE(1,3),DATA,ABS(RF_MAX/60.0),ABS(UY_NTOP/1.0)
*VFILL,VALUE2(1,1),DATA,24,10.0
*VFILL,VALUE2(1,2),DATA,RF_END,UY_END
*VFILL,VALUE2(1,3),DATA,ABS(RF_END/24.0),ABS(UY_END/10.0)
SAVE,INF2
FINI
/CLEAR,NOSTART
/OUT,SCRATCH



/TITLE, VM248, DELAMINATION OF DOUBLE CANTILEVER BEAM - 2D PLANE STRAIN
C*** USING INTER205
/PREP7
ET,1,185		   !* 3D 8-NODE STRUCTURAL SOLID ELEMENT
KEYOPT,1,2,2               !* PLANE STRAIN
ET,2,185
KEYOPT,2,2,2
ET,3,205                   !* 3D 8-NODE COHESIVE ZONE ELEMENT
MP,EX,4,1.353E5            !* E11 = 135.3 GPA
MP,EY,4,9.0E3              !* E22 =   9.0 GPA
MP,EZ,4,9.0E3              !* E33 =   9.0 GPA
MP,GXY,4,5.2E3             !* G12 =   5.2 GPA
MP,GYZ,4,5.2E3
MP,GXZ,4,3.08E3
MP,PRXY,4,0.24
MP,PRXZ,4,0.24
MP,PRYZ,4,0.46
GMAX = 0.004
TNMAX = 25                 !* TENSILE STRENGTH
TB,CZM,5,,,EXPO            !* COHESIVE ZONE MATERIAL
TBDATA,1,TNMAX,GMAX,1000.0
BLC4,0,0,100,1.5,1         !* DEFINE VOLUMES
BLC4,0,0,100,-1.5,1
LSEL,S,LINE,,10,11,1       !* DEFINE LINE DIVISION
LSEL,A,LINE,,22,23,1
LESIZE,ALL,,,1
LSEL,S,LINE,,7,17,10
LESIZE,ALL,,,200
LSEL,S,LINE,,6,18,12
LESIZE,ALL,1.5
ALLSEL,ALL
TYPE,1                     !* MESH VOLUME 2
MAT,4
LOCAL,11,0,0,0,0
ESYS,11
VMESH,2
CSYS,0
TYPE,2                     !* MESH VOLUME 1
ESYS,11
VMESH,1
CSYS,0
NSEL,S,LOC,X,30,100
NUMMRG,NODES
ESLN
TYPE,3
MAT,5
CZMESH,,,1,Y,0,            !* GENERATE INTERFACE ELEMENTS
ALLSEL,ALL
NSEL,S,LOC,X,100           !* APPLY CONSTRAINTS
D,ALL,ALL
NSEL,ALL
D,ALL,UZ,0

FINISH
/SOLU
ESEL,S,TYPE,,2
NSLE,S
NSEL,R,LOC,X
NSEL,R,LOC,Y,1.5           !* APPLY DISPLACEMENT LOADING ON TOP
D,ALL,UY,10
NSEL,ALL
ESEL,ALL
ESEL,S,TYPE,,1
NSLE,S
NSEL,R,LOC,X
NSEL,R,LOC,Y,-1.5          !* APPLY DISPLACEMENT LOADING ON BOTTOM
D,ALL,UY,-10
NSEL,ALL
ESEL,ALL
NLGEOM,ON
AUTOTS,ON
TIME,1
NSUBST,40,40,40
OUTRES,ALL,ALL
/OUT,SCRATCH
SOLVE                      !* PERFORM SOLUTION
FINISH
/POST26
NSEL,S,LOC,Y,1.5
NSEL,R,LOC,X,0
NSEL,R,LOC,Z,0
*GET,NTOP,NODE,0,NUM,MAX
NSEL,ALL
NSOL,2,NTOP,U,Y,UY
RFORCE,3,NTOP,F,Y,FY
PROD,4,3, , ,RF, , ,20
/TITLE,VM248, DCB: REACTION AT TOP NODE VERSES PRESCRIBED DISPLACEMENT
/AXLAB,X,DISP U (mm)
/AXLAB,Y,REACTION FORCE R (N)
/YRANGE,0,60
XVAR,2
PLVAR,4
PRVAR,UY,RF
*GET,TMAX,VARI,4,EXTREM,TMAX   !* TIME CORRESPONDING TO MAX RFORCE
FINISH
/POST1
SET, , , , ,TMAX               !* RETRIEVE RESULTS AT TMAX
NSEL,S,NODE, ,NTOP             !* SELECT NODE NTOP
/OUT,
*GET,RF_NTOP,NODE,NTOP,RF,FY   !* FY RFORCE AT NODE NTOP
*GET,UY_NTOP,NODE,NTOP,U,Y     !* DISP AT NODE NTOP CORRESPONDING TO RFORCE
RF_MAX = RF_NTOP*40 	       !* PLANE STRAIN OPTION AND WIDTH = 20 mm
SET,LAST                       !* RETRIEVE RESULTS AT LAST SUBSTEP
*GET,RF_END,NODE,NTOP,RF,FY    !* FY RFORCE AT NODE NTOP AT LAST SUBSTEP
*GET,UY_END,NODE,NTOP,U,Y      !* DISP AT NODE NTOP CORRESPONDING TO RFORCE
RF_END = RF_END*40 	       !* PLANE STRAIN OPTION AND WIDTH = 20 mm
*DIM,LABEL,CHAR,2,2
*DIM,VALUE,,2,3
*DIM,VALUE2,,2,3
LABEL(1,1) = 'RFORCE','DISP '
LABEL(1,2) = 'FY (N)','UY (mm)'
*VFILL,VALUE(1,1),DATA,60.0,1.0
*VFILL,VALUE(1,2),DATA,RF_MAX,UY_NTOP
*VFILL,VALUE(1,3),DATA,ABS(RF_MAX/60.0),ABS(UY_NTOP/1.0)
*VFILL,VALUE2(1,1),DATA,24,10.0
*VFILL,VALUE2(1,2),DATA,RF_END,UY_END
*VFILL,VALUE2(1,3),DATA,ABS(RF_END/24.0),ABS(UY_END/10.0)
SAVE,INF3
/OUT,SCRATCH

RESUME,INF1
/COM
/OUT,vm248,vrt
/COM,------------------- VM248 RESULTS COMPARISON --------------
/COM,
/COM,                 |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
/COM,MAX RFORCE AND CORRESPONDING DISP USING INTER202:
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.3,'  ',1F14.3,'   ',1F15.3)
/COM,
/COM,RFORCE CORRESPONDING TO DISP U = 10.0 USING INTER202:
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE2(1,1),VALUE2(1,2),VALUE2(1,3)
(1X,A8,A8,'   ',F10.3,'  ',1F14.3,'   ',1F15.3)
/COM,
/NOPR
RESUME,INF2
/COM,MAX RFORCE AND CORRESPONDING DISP USING INTER203:
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.3,'  ',1F14.3,'   ',1F15.3)
/COM,
/COM,RFORCE CORRESPONDING TO DISP U = 10.0 USING INTER203:
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE2(1,1),VALUE2(1,2),VALUE2(1,3)
(1X,A8,A8,'   ',F10.3,'  ',1F14.3,'   ',1F15.3)
/COM,
RESUME,INF3
/COM,MAX RFORCE AND CORRESPONDING DISP USING INTER205:
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.3,'  ',1F14.3,'   ',1F15.3)
/COM,
/COM,RFORCE CORRESPONDING TO DISP U = 10.0 USING INTER205:
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE2(1,1),VALUE2(1,2),VALUE2(1,3)
(1X,A8,A8,'   ',F10.3,'  ',1F14.3,'   ',1F15.3)
/COM,-----------------------------------------------------------
/OUT
FINI
*LIST,vm248,vrt
/DELETE,INF1
/DELETE,INF2
/DELETE,INF3

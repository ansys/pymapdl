/COM,ANSYS MEDIA REL. 150 (11/8/2013) REF. VERIF. MANUAL: REL. 150
/verify,vm277
/title,vm277,Hall Plate in a Uniform Magnetic Field
/COM,
/COM, Reference: Meijer,G. "Smart Sensor Systems"
/COM, John Wiley & Sons, Ltd.2008,pp 252
/COM,

JPGPRF,500,100,1
/SHOW,JPEG

/NOPR

! *** DEFINED PARAMETERS
A=3E-3				! HALF PLATE LENGTH, M
B=3E-3				! HALF PLATE HEIGHT, M
C=0.4E-3			! PLATE WIDTH, M
! *** MATERIAL PROPERTIES FOR N-TYPE INDIUM ARSENIDE 
RH=-0.0001			! HALL COEFFICIENT, M^3/C
RHO=1.6e-3			! RESISTIVITY, OHM*M
MU=1				! RELATIVE MAGNETIC PERMEABILITY
V=3				    ! APPLIED VOLTAGE, V
BZ=0.8				! APPLIED MAGNETIC FIELD, T

/PREP7
ET,1,SOLID236,1			! ELECTROMAGNETIC ANALYSIS, AZ+VOLT
MP,RH,1,RH
MP,RSVX,1,RHO
MP,MURX,1,MU
BLOCK,-A,A,-B,B,0,C		! HALL PLATE
BLOCK,-A,-A+A/8,B,B-B/8,C/2,C	! SENSOR CONNECTIONS
BLOCK,-A,-A+A/8,-B,-B+B/8,C/2,C
BLOCK,A,A-A/8,B,B-B/8,C/2,C
BLOCK,A,A-A/8,-B,-B+B/8,C/2,C
ESIZE,C
MSHA,1,3D			! TETRAHEDRAL
VOVLAP,ALL
VMESH,ALL

W1=NODE(-A,B,C)			! NODES FOR WIRE MODELING
W2=NODE(A,-B,C)
*GET,N_MAX,NODE,,NUM,MAX	! GET MAX NODE NUMBER
N,N_MAX+1,-2*A,2*B,C		! DEFINE NODES FOR ELEMENTS
N,N_MAX+2,-2*A,,C
N,N_MAX+3,-2*A,-2*B,C
N,N_MAX+4,-A,2*B,C
N,N_MAX+5,A,-2*B,C
ET,2,CIRCU124,4			! INDEPENDENT VOLTAGE SOURCE FROM CIRCU124
R,2,V
TYPE,2
REAL,2
E,N_MAX+1,N_MAX+3,N_MAX+2
/ICSCALE,,0.1
ET,3,200,0,0,0,0		! WIRE FROM MESH200
TYPE,3
E,N_MAX+1,N_MAX+4
E,N_MAX+4,W1
E,N_MAX+3,N_MAX+5
E,N_MAX+5,W2

! *** BOUNDARY CONDITIONS AND LOADS
VSEL,S,VOLU,,6
NSLV
NSEL,A,NODE,,N_MAX+1
CP,1,VOLT,ALL			! VOLTAGE SUPPLY
ND1=NDNEXT(0)			! MASTER NODE
VSEL,S,VOLU,,9
NSLV
NSEL,A,NODE,,N_MAX+3
D,ALL,VOLT,0			! GROUND
ND2=NDNEXT(0)			! MASTER NODE
VSEL,S,VOLU,,7
NSLV
CP,2,VOLT,ALL			! COUPLE FOR OUTPUT VOLTAGE
ND3=NDNEXT(0)			! MASTER NODE
VSEL,S,VOLU,,8
NSLV
CP,3,VOLT,ALL			! COUPLE FOR OUTPUT VOLTAGE
ND4=NDNEXT(0)			! MASTER NODE
ALLS

DFLX,ALL,,,0			! SET B-FIELD IN THE +Z-DIFECTION TO B=0 T

/VIEW,,1,2,3
EPLOT
FINISH

/SOLU
OUTRES,ALL,ALL
SOLVE
VBASE=VOLT(ND4)-VOLT(ND3)	! OFFSET VOLTAGE = OUTPUT VOLTAGE AT B=0 T
DFLX,ALL,,,BZ			! UNIFORM B-FIELD BZ IN THE +Z-DIRECTION
ALLS
SOLVE
FINISH

/POST1
SET,LAST,LAST
VSEL,S,VOLU,,6
NSLV
FSUM
*GET,_I,FSUM,,ITEM,AMPS		! CURRENT THROUGH PLATE
ALLS

PLVECT,B,,,,VECT,ELEM,ON,0
/VIEW,,,,1
PLNSOL,VOLT
*DIM,VALUE,,1,3
*VFILL,VALUE(1,1),DATA,VOLT(ND4)-VOLT(ND3)-VBASE
*VFILL,VALUE(1,2),DATA,(RH*_I*BZ)/(C)
*VOPER,VALUE(1,3),VALUE(1,1),DIV,VALUE(1,2)
/OUT,vm277,vrt
/COM,
/COM,  ----------------------- RESULTS COMPARISON ---------------------------
/COM,
/COM,   ***** HALL VOLTAGE (V) AT B=%BZ% T *****
/COM,
/COM,       |      TARGET      |     MECHANICAL APDL     |     RATIO
/COM,
*VWRITE,VALUE(1,2),VALUE(1,1),VALUE(1,3)
('         ',G14.5,'         ',G14.5,'          ',F8.3)
/COM,
/COM, ------------------------------------------------------------------------
/OUT,
*list,vm277,vrt
FINISH


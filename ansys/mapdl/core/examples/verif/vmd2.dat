/COM,ANSYS MEDIA REL. 150 (11/8/2013) REF. VERIF. MANUAL: REL. 150
/VERIFY,VMD2
JPGPRF,500,100,1             ! MACRO TO SET PREFS FOR JPEG PLOTS
/SHOW,JPEG
/DEVICE,VECTOR,ON
/TITLE, VMD2, BARREL VAULT ROOF UNDER SELF-WEIGHT

/COM
/COM    COOK, CONCEPTS AND APPL OF F.E.A., 2ND ED., 1981, PP. 284-287.
/COM
/NOPR
/OUT,SCRATCH

*DIM,R,,5,5                        ! ARRAY PARAMETER FOR RESULTS
*DO,I,1,3

*IF,I,EQ,1,THEN                    ! DEFINE ELEMENT TYPE
  ETYP=63
*ELSEIF,I,EQ,2,THEN
  ETYP=181
*ELSEIF,I,EQ,3,THEN
  ETYP=281
*ENDIF

*DO,J,1,5
*IF,J,EQ,1,THEN                    ! DEFINE SKEW ANGLE
  BETA=65.0
*ELSEIF,J,EQ,2,THEN
  BETA=77.5
*ELSEIF,J,EQ,3,THEN
  BETA=90.0
*ELSEIF,J,EQ,4,THEN
  BETA=110.0
*ELSEIF,J,EQ,5,THEN
  BETA=130.0
*ENDIF

/PREP7 $smrt,off
ANTYPE,STATIC                      ! STATIC ANALYSIS
ET,1,ETYP                          ! DEFINE ELEMENT TYPE PARAMETRICALLY
*IF,ETYP,EQ,181,THEN
KEYOPT,1,3,2
*ENDIF
MP,EX,1,4.32E8                     ! DEFINE MATERIAL PROPERTIES
MP,NUXY,1,0.0
MP,DENS,1,36.7347
R,1,0.25
CSYS,1                             ! DEFINE CYLINDRICAL C.S.
A=6.25
K,1,25,50 $ K,2,25,60              ! DEFINE KEYPOINTS
K,3,25,70 $ K,4,25,80
K,5,25,90 $ KGEN,5,1,5,1,,,A
GAM=(135-(BETA/2))                 ! CALCULATE SKEWED KEYPOINT COORDINATES
DEGR=(3.14159/180)
TGAM=TAN((GAM*DEGR))
*IF,BETA,GE,90.0,THEN
  ZZ=((A*TGAM)/(1+TGAM))
  TAU=ATAN((A-ZZ)/((2*A)-ZZ))
  TAU=TAU/DEGR
  ALPA=(90-(2*TAU))                ! CALCULATE ANGLE ALPHA (ALPA)
*ELSE
  GAM=(180-GAM)
  TGAM=TAN((GAM*DEGR))
  ZZ=((A*TGAM)/(TGAM-1))
  TAU=ATAN((ZZ-A)/((2*A)-ZZ))
  TAU=TAU/DEGR
  ALPA=(90+(2*TAU))                ! CALCULATE ANGLE ALPHA (ALPA)
*ENDIF
R(J,4)=BETA                        ! STORE SKEW ANGLES
R(J,5)=ALPA
RAT=ZZ/A
Z1=ZZ                              ! Z COORDINATES OF SKEWED KEYPOINTS
Z2=((2*A)-Z1)
Z3=((2*A)+Z1)
Z4=((4*A)-Z1)
ANG=(RAT*10)                       ! CALCULATE ANGLE OF SKEWED KEYPOINTS
K,7,25,(ANG+50),Z1                 ! DEFINE SKEWED KEYPOINT
K,9,25,(ANG+70),Z2
K,17,25,(70-ANG),Z3
K,19,25,(90-ANG),Z4
A,1,2,7,6     $ *REPEAT,4,1,1,1,1  ! GENERATE AREAS
A,6,7,12,11   $ *REPEAT,4,1,1,1,1
A,11,12,17,16 $ *REPEAT,4,1,1,1,1
A,16,17,22,21 $ *REPEAT,4,1,1,1,1
ESIZE,,1                           ! DEFINE ELEMENT SIZE
ESHAPE,2                           ! MAPPED MESHING
AMESH,ALL                          ! MESH ALL AREAS
/VIEW,1,1,1,1
EPLOT
NSEL,S,LOC,Z,0 $ NSEL,R,LOC,Y,50   ! SELECT NODE OF INTEREST
*GET,N1,NODE,,NUM,MAX              ! GET NODE NUMBER
NSEL,S,LOC,Z,0 $ NSEL,R,LOC,Y,90   ! SELECT NODE OF INTEREST
*GET,N2,NODE,,NUM,MAX              ! GET NODE NUMBER
NSEL,ALL
CSYS,0                             ! SWITCH TO GLOBAL CARTESIAN C.S.
NSEL,S,LOC,Z,0
DSYMM,SYMM,Z                       ! APPLY SYMMETRY B.C.
NSEL,S,LOC,X,0
DSYMM,SYMM,X
NSEL,S,LOC,Z,25
D,ALL,UX,0,,,,UY,ROTZ              ! CONSTRAIN MODEL EDGE
NSEL,ALL
ACEL,,9.8                          ! DEFINE GRAVITATIONAL ACCELERATION
WSORT,Z                            ! REORDER ELEMENTS
CHECK                              ! CHECK ELEMENTS FOR EXCESSIVE WARPING
FINISH
/SOLU
SOLVE
FINISH
/POST1
SET,1,1
UY1T=-.3016                        ! TARGET DISPLACEMENT SOLUTION
SZ1T=358420                        ! TARGET SZ STRESS
SY2T=-213400                       ! TARGET SY STRESS
SHELL,MID                          ! PROCESS MIDDLE STRESSES
*GET,UY1,NODE,N1,U,Y               ! GET UY AT NODE N1
UYN=UY1/UY1T                       ! CALCULATE NORMALIZED DISPLACEMENT
R(J,1)=UYN
RSYS,1                             ! ACTIVATE CYLINDRICAL RESULTS C.S.
SHELL,BOT                          ! PROCESS BOTTOM STRESSES
*GET,SZ1B,NODE,N1,S,Z              ! GET AXIAL (Z) STRESS AT BOTTOM
SZN=SZ1B/SZ1T                      ! CALCULATE NORMALIZED SZ STRESS
R(J,2)=SZN
*GET,SY2B,NODE,N2,S,Y              ! GET CIRCUMFERENTIAL (Y) STRESS AT BOTTOM
SYN=SY2B/SY2T                      ! CALCULATE NORMALIZED SY STRESS
R(J,3)=SYN
FINISIH
PARSAV,ALL                         ! SAVE LOOP PARAMETERS
/CLEAR, NOSTART ! CLEAR DATABASE
PARRES
*ENDDO
/GOPR
/OUT
/COM
/COM
*MSG,INFO,ETYP
 ELEMENT: SHELL %I
/COM
/COM     SKEW ANGLE                     RATIO
/COM   BETA     ALPHA  |   UY(1)      S AXIAL(1)  S THETA(2)
/COM --------------------------------------------------------
*VWRITE,R(1,4),R(1,5),R(1,1),R(1,2),R(1,3)
(2X,F5.1,5X,F5.1,5X,3(F6.3,6X))
/NOPR
/OUT,SCRATCH
*ENDDO

/COM,ANSYS MEDIA REL. 150 (11/8/2013) REF. VERIF. MANUAL: REL. 150
/VERIFY,VM71
/PREP7                    
JPGPRF,500,100,1             ! MACRO TO SET PREFS FOR JPEG PLOTS
/SHOW,JPEG
/TITLE, VM71, TRANSIENT RESPONSE OF A SPRING, MASS, DAMPING SYSTEM
C*** VIBRATION THEORY AND APPLICATIONS, THOMSON, 2ND PRINTING, PAGE 41, EX 2.2-1
ET,1,COMBIN40,,,2         ! Y-DOF ELEMENT
R,1,30,3.52636,.02590673  ! VARIOUS SPRING CONST., DAMPING COEFFICIENTS, MASS
R,2,30,1.76318,.02590673
R,3,30,.352636,.02590673
R,4,30,0,.02590673
N,1
N,8
FILL
E,1,2
EGEN,4,2,1,1,1,,,1        ! GENERATE ELEMENTS WITH INCREMENTING REAL CONSTANT SET
D,2,UY,,,8,2
FINISH

/SOLU       
ANTYPE,MODAL              ! PERFORM MODAL SOLVE
MODOPT,QRDAMP,4           ! USE QRDAMP EIGENSOLVER
MXPAND,4,,,YES
/OUT,SCRATCH
SOLVE
FINISH


/SOLU
ANTYPE,TRANSIENT
TRNOPT,MSUP,4,,,          ! PERFORM MSUP TRANSIENT SOLVE    
DELTIM,1E-3
OUTRES,NSOL,1
OUTPR,NSOL,1
F,1,FY,30,,7,2            ! APPLY INITIAL FORCE
TIME,1E-3
KBC,1
SOLVE

TIME,95E-3                ! TIME TO COVER ABOUT 1/2 THE PERIOD
F,1,FY,,,7,2              ! REMOVE FORCE
SOLVE
FINISH


/POST26
FILE,,rdsp
/OUT,
NSOL,2,1,U,Y,1UY          ! STORE UY DISPLACEMENTS OF APPROPRIATE NODES
NSOL,3,3,U,Y,3UY
NSOL,4,5,U,Y,5UY
NSOL,5,7,U,Y,7UY
NPRINT,10                 ! PRINT EVERY 10 POINTS
PRVAR,2,3,4,5             ! PRINT VARIABLES 2,3,4,5
/GRID,1                   ! TURN GRID ON
/AXLAB,Y,DISP             ! Y-AXIS LABEL DISP
PLVAR,2,3,4,5             ! DISPLAY VARIABLES 2,3,4,5
*GET,U_DAMP2,VARI,2,RTIME,.09
*GET,U_DAMP1,VARI,3,RTIME,.09
*GET,U_DAMP02,VARI,4,RTIME,.09
*GET,U_DAMP0,VARI,5,RTIME,.09
*DIM,LABEL,CHAR,4,2
*DIM,VALUE,,4,3
LABEL(1,1) = 'U,in  DA','U,in  DA','U,in  DA','U,in  DA'
LABEL(1,2) = 'MPRAT=2 ','MPRAT=1 ','MPRAT=.2','MPRAT=0 '
*VFILL,VALUE(1,1),DATA,.4742,.18998,-.52108,-.99688
*VFILL,VALUE(1,2),DATA,U_DAMP2,U_DAMP1,U_DAMP02,U_DAMP0
*VFILL,VALUE(1,3),DATA,U_DAMP2/.4742,U_DAMP1/.18998,ABS(U_DAMP02/.52108)
*VFILL,VALUE(4,3),DATA,ABS(U_DAMP0/.99688)
/COM
/OUT,vm71,vrt
/COM,------------------- VM71 RESULTS COMPARISON --------------
/COM,
/COM,                 |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.5,'  ',F14.5,'   ',1F15.3)
/COM,----------------------------------------------------------
/OUT
FINISH
*LIST,vm71,vrt

/COM,ANSYS MEDIA REL. 150 (11/8/2013) REF. VERIF. MANUAL: REL. 150
/VERIFY,VM178
/TITLE, VM178, TWO DIMENSIONAL DOUBLE CANTILEVER BEAM PROBLEM
/COM, REFERENCE:" J.F.MANDELL, ET AL., PREDICTION OF DELAMINATION IN 
/COM,             WIND TURBINE BLADE STRUCTURAL DETAILS
/COM,             JOURNAL OF SOLAR ENERGY ENGINEERING, VOL. 25, 2003, PG: 522-530
/COM,
/OUT,SCRATCH
/PREP7
DL=200                      ! WIDTH OF BEAM
DH=10                       ! THICKNESS OF BEAM
PP=10                       ! LOADING
A=DL*0.3                    ! CRACK LENGTH
ET,1,PLANE182               ! 2D 4-NODE STRUCTURAL SOLID ELEMENT
KEYOPT,1,3,2                ! PLANE STRAIN

!MATERIAL PROPERTIES
YOUNG = 210000              ! YOUNG'S MODULUS
NU = 0.3                    ! POISSION'S RATIO
MP,EX,1,YOUNG               !MPa
MP,PRXY,1,NU

RECTNG,0,DL,DH/2            !DEFINE AREAS
RECTNG,0,DL,0,-DH/2
LSEL,S,LINE,,2,8,2          !DEFINE LINE DIVISION
LESIZE,ALL,DH/4
LSEL,INVE
LESIZE,ALL, , ,200
ALLSEL,ALL
TYPE,1                      !MESH AREAS
AMESH,1,2
NSEL,S,LOC,X,A,DL
NUMMRG,NODES
ALLSEL

NSEL,S,LOC,X,DL             !APPLY CONSTRAINTS
D,ALL,ALL
NSEL,ALL

NSEL,S,LOC,X,A
NSEL,R,LOC,Y,0
CM,CRACK1,NODE
ALLS
FINISH

/OUT,SCRATCH
/SOLU
NSEL,S,LOC,X
NSEL,R,LOC,Y,DH/2           !APPLY LOADING 
F,ALL,FY,PP
NSEL,S,LOC,X
NSEL,R,LOC,Y,-DH/2          !APPLY LOADING 
F,ALL,FY,-PP
NSEL,ALL

CINT,NEW,1                  ! DEFINE CRACK ID
CINT,TYPE,VCCT 
CINT,CTNC,CRACK1            ! DEFINE CRACK TIP NODE COMPONENT         
CINT,SYMM,OFF               ! SYMMETRY OFF
CINT,NORMAL                 ! DEFINE CRACK PLANE NORMAL
CINT,LIST
ALLSEL,ALL

SOLVE
FINISH

/OUT,SCRATCH
/POST1
/SHOW
/ESHAPE,1
/GRAPHICS,POWER
PRCINT,1
CRACK=NODE(A,0.0,0.0)
*GET,G1_ANSYS,CINT,1,,CRACK,,1,,G1
*GET,G2_ANSYS,CINT,1,,CRACK,,1,,G2
PLNSOL, U,Y, 0,1.0
*GET,UY_MIN,PLNSOL,0,MIN
*GET,UY_MAX,PLNSOL,0,MAX
CC=UY_MAX-UY_MIN            ! Displacement between cantilever arms at critical load

/OUT
G_ANSYS=ABS(G1_ANSYS)+ABS(G2_ANSYS)
G_REF=(3*PP*CC)/(2*A*1)          ! equation (1) in the reference, B=1 for this case
/COM
/OUT,vm178,vrt
/COM, -------------- VM 178 RESULTS COMPARISON -------------
/COM,
/COM,             |  TARGET    |      Mechanical APDL     |  RATIO
/COM,
/COM,     G COMPUTATION FOR VCCT
/COM,
*VWRITE, 'G',  G_REF, G_ANSYS, G_ANSYS/G_REF
(1X,A8,'     ', F10.5, '       ', F14.5, '     ', F15.3)
/OUT
FINISH
*LIST, vm178,vrt

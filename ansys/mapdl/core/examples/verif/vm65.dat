/COM,ANSYS MEDIA REL. 150 (11/8/2013) REF. VERIF. MANUAL: REL. 150
/VERIFY,VM65
/PREP7                 
JPGPRF,500,100,1             ! MACRO TO SET PREFS FOR JPEG PLOTS
/SHOW,JPEG
/TITLE, VM65, TRANSIENT RESPONSE OF A BALL IMPACTING A FLEXIBLE SURFACE
C***        VIBRATION THEORY AND APPLICATIONS, THOMSON, 2ND. PRINTING, PAGE 110,
C***        EX. 4.6-1, USING NON-LINEAR TRANSIENT DYNAMIC ANALYSIS
C***
/PREP7                 
ANTYPE,TRANS           ! NONLINEAR TRANSIENT DYNAMIC ANALYSIS
ET,1,MASS21, , ,4      ! 2-D MASS WITHOUT ROTARY INERTIA
ET,2,CONTA175,,1       ! 2-D NODE-TO-SURFACE CONTACT ELEMENT
ET,3,TARGE169          ! 2-D TARGET SEGMENT
R,1,.5                 ! MASS
R,2,,,-1973.92088      ! CONTACT SURFACE WITH STIFFNESS = 1973.92088
RMOD,2,6,-1.2          ! PIN BALL REGION
N,1,0,1
TYPE,1
REAL,1
E,1                    ! MASS ELEMENT
TYPE,2     
REAL,2
E,1                    ! CONTACT ELEMENT
N,2,-1
N,3,1
TYPE,3
REAL,2
TSHAP,LINE
E,3,2                  ! TARGET ELEMENT
ALLSEL,ALL
FINISH
/SOLU    
SOLCONTROL,0
NSUBST,10
ACEL,,386
KBC,1                  ! STEP ACCELERATION CHANGE
CNVTOL,F,1,1E-5        ! FORCE CONVERGENCE CRITERIA
TIME,1E-3              ! RELEASE MASS USING SMALL TIME STEP
/OUT,SCRATCH
SOLVE
OUTRES,,1
NSUBST,109             ! INTEGRATION TIME STEP OF 0.001 IN SECOND LOAD STEP
TIME,.11               ! TIME TO ALLOW THE MASS TO REACH ITS LARGEST DEFLECTION
SOLVE
FINISH
/POST26
/OUT,
NSOL,2,1,U,Y,UY
DERIV,3,2,1,,VEL1UY
PROD,4,3,3,,K.E.,,,.5,.5  ! CALCULATE K.E. BY 1/2(MV**2)
PLVAR,2,3,4
PRVAR,2,3,4               ! PRINT DISP., VELOCITY AND KINETIC ENERGY
*GET,DISP,VARI,2,RTIME,.072
*GET,VELO,VARI,3,RTIME,.072
*GET,KENG,VARI,4,RTIME,.072
*GET,MAXY,VARI,2,EXTREM,VMIN
*GET,TMAX,VARI,2,EXTREM,TMIN
FINISH
/POST1
SET,,,,,0.072          ! DEFINE DATA SET AT TIME = 0.072
ETABLE,KENE,KENE       ! RETRIEVE KINETIC ENERGY
PRETAB,KENE            ! PRINT KINETIC ENERGY
*DIM,LABEL2,CHAR,4,2
*DIM,VALUE2,,4,3
LABEL2(1,1) = 'TIME,  s','Y DISP, ','Y VEL, i','K ENRG, '
LABEL2(1,2) = 'ec      ','in      ','n/sec   ','lb-in   '
*VFILL,VALUE2(1,1),DATA,.07198,-1,-27.79,193
*VFILL,VALUE2(1,2),DATA,.072,DISP,VELO,KENG
*VFILL,VALUE2(1,3),DATA,ABS(.072/.07198),ABS(DISP/1),ABS(VELO/27.79),ABS(KENG/193)
*DIM,LABEL,CHAR,2,2
*DIM,VALUE,,2,3
LABEL(1,1) = 'TIME,  s','MAXY DISP, '
LABEL(1,2) = 'ec      ',' in    '
*VFILL,VALUE(1,1),DATA,.10037,-1.5506
*VFILL,VALUE(1,2),DATA,TMAX,MAXY
*VFILL,VALUE(1,3),DATA,ABS(TMAX/.10037),ABS(MAXY/1.5506)
FINISH
/COM
/OUT,vm65,vrt
/COM,=================== VM65 RESULTS COMPARISON ==============
/COM,
/COM,
/COM,AT IMPACT         |  TARGET   |   Mechanical APDL   |   RATIO
/COM,
*VWRITE,LABEL2(1,1),LABEL2(1,2),VALUE2(1,1),VALUE2(1,2),VALUE2(1,3)
(1X,A8,A8,'   ',F10.5,'  ',F14.5,'   ',1F15.3)
/COM,
/COM,
/COM,AT "ZERO" VELOCITY |  TARGET   |   Mechanical APDL   |   RATIO
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.5,'  ',F14.5,'   ',1F15.3)
/COM,==========================================================
/OUT
FINISH
/NOPR
*LIST,vm65,vrt

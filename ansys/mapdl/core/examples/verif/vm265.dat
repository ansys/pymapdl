/COM,ANSYS MEDIA REL. 150 (11/8/2013) REF. VERIF. MANUAL: REL. 150
/VERIFY,VM265
/PREP7
JPGPRF,500,100,1             ! MACRO TO SET PREFS FOR JPEG PLOTS
/SHOW,JPEG
/TITLE, VM265, ELASTIC ROD IMPACTING A RIGID WALL
/stitle,1,Reason COMPARE differences are acceptable:
/stitle,2,Convergence Difference
C***     N.J. CARPENTER, R.L. TAYLOR AND M.G.KATONA,   
C***     "LAGRANGE CONSTRAINTS FOR TRANSIENT FINITE ELEMENT SURFACE CONTACT"   
C***     INTERNATIONAL JOURNAL FOR NUMERICAL METHODS IN ENGINEERING, VOL.32,103-128 (1991)
/PREP7 
ANTYPE,TRANS                 ! NONLINEAR TRANSIENT DYNAMIC ANALYSIS                
ET,1,SHELL181                ! 4-NODE STRUCTURAL SHELL
ET,3,CONTA177                ! 3D LINE-TO-SURFACE CONTACT
KEYOPT,3,2,4                 ! PURE LAGRANGE MULTIPLIER ON CONTACT NORMAL AND TANGENT
KEYOPT,3,7,4                 ! IMPACT CONSTRAINTS
ET,4,TARGE170                ! 3D TARGET SEGMENT
R,3                          ! REAL CONSTANT CONTACT PAIR
SECTYPE,1,SHELL              ! SHELL SECTION TYPE
SECDATA,1                    ! SHELL THICKNESS
MP,EX,1,3.0E7                ! YOUNG'S MODULUS [PSI]
MP,NUXY,1,0.3                ! POISSON'S RATIO
MP,DENS,1,0.73E-3            ! DENSITY  [LBF S^2/IN^4]
BLC4,-10.01,0.0,10.0,1.0     ! ROD GEOMETRY
TYPE,1
SECNUM,1
MAT,1
LESIZE,1,,,20                ! 20 ELEMENTS PER LENGTH
LESIZE,2,,,1
LESIZE,3,,,20
LESIZE,4,,,1
AMESH,1
N,1001,0,-0.5,-1.5           ! RIGID WALL
N,1002,0,-0.5,1.5
N,1003,0,1.5,1.5
N,1004,0,1.5,-1.5
REAL,3           
TYPE,4
TSHAP,QUAD
E,1001,1002,1003,1004        ! TARGET ELEMENT
REAL,3
TYPE,3
ESEL,S,TYPE,,1
NSLE
NSEL,R,LOC,X,-0.01
ESURF                        ! CONTACT ON THE RIGHT SHELL EDGE
ALLSEL,ALL
NSEL,S,LOC,Y,0.0             ! BOUNDARY CONDITIONS
NSEL,U,LOC,X,-0.01           ! AVOID OVER CONSTRAINTS ON CONTACT NODE 
D,ALL,UY
NSEL,ALL
NSEL,S,LOC,Y,1.0
NSEL,U,LOC,X,-0.01           ! AVOID OVER CONSTRAINTS ON CONTACT NODE 
D,ALL,UY
NSEL,ALL
NSEL,U,LOC,X,-0.01           ! AVOID OVER CONSTRAINTS ON CONTACT NODE 
D,ALL,UZ
D,ALL,ROTX
D,ALL,ROTY
D,ALL,ROTZ
NSEL,ALL
ESEL,S,TYPE,,1               
NSLE
IC,ALL,UX,,202.2             ! INITIAL VELOCITY
ALLSEL,ALL
FINISH
/SOLU
NLGEOM,ON                    ! INLCUDE LARGE DEFLECTION EFFECTS
TRNOPT,FULL, , , , ,HHT      ! FULL METHOD WITH HHT ALGORITHM
TINTP,0.0	                 ! NO NUMERICAL DAMPING
TIME,2.0E-4                  ! TIME TO ALLOW IMPACT AND RELEASE OF THE ROD
NSUB,89,89,89                ! UNIFORM TIME STEP INCREMENT OF 0.2226E-5 SEC
OUTRES,ALL,ALL
/OUT,SCRATCH
SOLVE
FINISH
/POST26
NUMVAR,20
NSOL,2,22,U,X,UX22           ! NODE ON THE ROD CONTACT SURFACE
NSOL,3,22,VEL,X,VX22
ESOL,4,22,2,SMISC,1,FORCE1  
ESOL,5,22,22,SMISC,2,FORCE2 
ADD,6,4,5,,FORCE             ! NORMAL CONTACT FORCE
ENERSOL,7,KENE               ! KINETIC ENERGY
ENERSOL,8,SENE               ! STRAIN ENERGY DENSITY
ADD,9,7,8,,TOTALENE          ! TOTAL ENERGY
/OUT,
PRVAR,2,3,6
PRVAR,7,8,9
/TITLE,CONTACT SURFACE DISPLACEMENT VERSUS TIME
/AXLAB,Y,AXIAL DISPLACEMENT
PLVAR,2                      ! COMPARE THIS PLOT WITH FIGURE 3a IN REFERENCE
/TITLE,CONTACT SURFACE VELOCITY VERSUS TIME
/AXLAB,Y,AXIAL VELOCITY
PLVAR,3                      ! COMPARE THIS PLOT WITH FIGURE 3b IN REFERENCE
/TITLE,NORMAL CONTACT FORCE VERSUS TIME
/AXLAB,Y,NORMAL CONTACT FORCE
PLVAR,6                      ! COMPARE THIS PLOT WITH FIGURE 3c IN REFERENCE
/TITLE,KINETIC ENERGY,STRAIN ENERGY AND TOTAL ENERGY VERSUS TIME
/AXLAB,Y,ENERGIES
PLVAR,7,8,9
FINI
/POST1
*GET,NSET,ACTIVE,0,SET,NSET  ! NUMBER OF SUBSTEPS
*DIM,AFORCE,ARRAY,NSET       ! ARRAY FOR NORMAL CONTACT FORCE
*DIM,ATIME,ARRAY,NSET        ! ARRAY FOR TIME
*DIM,ADISP,ARRAY,NSET        ! ARRAY FOR AXIAL DISPLACEMENT
*DIM,AVEL,ARRAY,NSET         ! ARRAY FOR AXIAL CENTER OF MASS VELOCITY
*DIM,VELN,ARRAY,42
*DO,I,1,NSET
 SET,1,I
 *GET,FORCE1,ELEM,22,SMISC,1
 *GET,FORCE2,ELEM,22,SMISC,2
 AFORCE(I)=FORCE1+FORCE2
 *GET,ATIME(I),ACTIVE,0,SET,TIME
 ADISP(I)=UX(22)
 *DO,J,1,42
  VELN(J)=VX(J)
 *ENDDO
 AVEL(I)=0
 *DO,J,3,21 
  AVEL(I)=AVEL(I)+VELN(J)+VELN(J+21)
 *ENDDO
 AVEL(I)=AVEL(I)+0.5*VELN(1)+0.5*VELN(2)
 AVEL(I)=(AVEL(I)+0.5*VELN(22)+0.5*VELN(23))/40
*ENDDO
IFOUND=0
*DO,I,1,NSET
 *IF,AFORCE(I),NE,0,AND,IFOUND,EQ,0,THEN
  TIMP=ATIME(I-1)            ! IMPACT TIME
  UIMP=ADISP(I-1)            ! CONTACT SURFACE DISPLACEMENT AT IMPACT
  VIMP=AVEL(I-1)             ! CONTACT SURFACE VELOCITY AT IMPACT
  IMP_FORCE=AFORCE(I-1)      ! NORMAL CONTACT FORCE AT IMPACT
  IFOUND=1
 *ENDIF
 *IF, AFORCE(I),EQ,0,AND,IFOUND,EQ,1,THEN
  *IF, AFORCE(I+1),EQ,0,THEN
   TREL=ATIME(I)              ! RELEASE TIME
   UREL=ADISP(I)              ! CONTACT SURFACE DISPLACEMENT AT RELEASE
   VREL=AVEL(I)               ! CONTACT SURFACE VELOCITY AT RELEASE
   REL_FORCE=AFORCE(I)        ! NORMAL CONTACT FORCE AT RELEASE
   IFOUND=2
  *ENDIF
 *ENDIF
*ENDDO
*DIM,LABEL2,CHAR,4,2
*DIM,VALUE2,,4,3
LABEL2(1,1) = 'TIME,  ','X DISP, ','X VEL, ','FORCE, '
LABEL2(1,2) = 'sec      ','in      ','in/sec   ','lb   '
*VFILL,VALUE2(1,1),DATA,.494E-4,0.01,202.2,0
*VFILL,VALUE2(1,2),DATA,TIMP,UIMP,VIMP,IMP_FORCE
*VFILL,VALUE2(1,3),DATA,ABS(TIMP/.494E-4),ABS(UIMP/0.01),ABS(VIMP/202.2),1
*DIM,LABEL,CHAR,4,2
*DIM,VALUE,,4,3
LABEL(1,1) = 'TIME,  ','X DISP, ','X VEL, ','FORCE, '
LABEL(1,2) = 'sec      ','in      ','in/sec   ','lb   '
*VFILL,VALUE(1,1),DATA,0.148E-3,0.01,-202.2,0
*VFILL,VALUE(1,2),DATA,TREL,UREL,VREL,REL_FORCE
*VFILL,VALUE(1,3),DATA,ABS(TREL/.148E-3),ABS(UREL/0.01),ABS(VREL/(-202.2)),1
FINISH
/COM
/OUT,vm265,vrt
/COM,----------------- VM265 RESULTS COMPARISON -------------------
/COM,
/COM,
/COM,AT IMPACT         |  TARGET   |   Mechanical APDL   |   RATIO
/COM,
*VWRITE,LABEL2(1,1),LABEL2(1,2),VALUE2(1,1),VALUE2(1,2),VALUE2(1,3)
(1X,A8,A8,'   ',F10.5,'  ',F14.5,'   ',1F15.3)
/COM,
/COM,
/COM,AT RELEASE        |  TARGET   |   Mechanical APDL   |   RATIO
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.5,'  ',F14.5,'   ',1F15.3)
/COM,----------------------------------------------------------------
/OUT
FINISH
*LIST,vm265,vrt





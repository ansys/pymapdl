/COM,ANSYS MEDIA REL. 150 (11/8/2013) REF. VERIF. MANUAL: REL. 150
/VERIFY,VM115
/PREP7
/TITLE, VM115, THERMAL RESPONSE OF A HEAT GENERATING SLAB
C***    CONDUCTION HEAT TRANSFER, SCHNEIDER, 2ND. PRINTING, PAGE 283, EQN 11-21
ANTYPE,TRANS
ET,1,LINK33        ! HEAT CONDUCTING BAR
R,1,1
MP,KXX,1,20
MP,DENS,1,500
MP,C,1,.2
K,1                ! SURFACE KEYPOINT
K,2,.5             ! CENTERLINE KEYPOINT
L,1,2              ! UNIFORM MESH
LESIZE,1,,,5
LMESH,1
AUTOTS,ON
OUTPR,,LAST
TIME,(12/60)       ! CONVERT 12 MIN TO HRS
DELTIM,0.01
BFUNIF,TEMP,60     ! INITIAL UNIFORM TEMPERATURE
KBC,1              ! APPLY STEP LOADS
DK,1,TEMP,32       ! SURFACE TEMPERATURE APPLIED TO KEYPOINT 1
BFK,ALL,HGEN,4E4   ! HEAT GENERATION SPECIFIED AT KEYPOINTS 1 & 2
FINISH
/SOLU    
SOLVE
/POST1
*GET,TN1,NODE,1,TEMP
*GET,TN3,NODE,3,TEMP
*GET,TN4,NODE,4,TEMP
*GET,TN5,NODE,5,TEMP
*GET,TN6,NODE,6,TEMP
*GET,TN2,NODE,2,TEMP
*DIM,LABEL,CHAR,6,2
*DIM,VALUE,,6,3
LABEL(1,1) = 'T,F (NOD','T,F (NOD','T,F (NOD','T,F (NOD','T,F (NOD','T,F (NOD'
LABEL(1,2) = 'E 1)    ','E 3)    ','E 4)    ','E 5)    ','E 6)    ','E 2)    '
*VFILL,VALUE(1,1),DATA,32,75.75,103.99,120.80,129.46,132.1
*VFILL,VALUE(1,2),DATA,TN1,TN3,TN4,TN5,TN6,TN2
*VFILL,VALUE(1,3),DATA,ABS(TN1/32 ),ABS(TN3/75.75),ABS(TN4/103.99),ABS(TN5/120.80)
*VFILL,VALUE(5,3),DATA,ABS(TN6/129.46),ABS(TN2/132.1)
/COM
/OUT,vm115,vrt
/COM,------------------- VM115 RESULTS COMPARISON --------------
/COM,
/COM,                 |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.2,'  ',F12.2,'   ',1F15.3)
/COM,-----------------------------------------------------------
/OUT
FINISH
*LIST,vm115,vrt


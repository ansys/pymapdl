/COM,ANSYS MEDIA REL. 150 (11/8/2013) REF. VERIF. MANUAL: REL. 150
/SHOW,JPEG
JPGPRF,500,100,1             ! MACRO TO SET PREFS FOR JPEG PLOTS
/VERIFY,VM100
/PREP7      
/TITLE, VM100, HEAT CONDUCTION ACROSS A CHIMNEY SECTION
C***       PRINCIPLES OF HEAT TRANSFER, KREITH, 2ND. PRINTING, PAGE 102, EX. 3-4
ANTYPE,STATIC                ! THERMAL ANALYSIS
ET,1,PLANE55
MP,KXX,1,1
N,1
N,3,1
FILL
NGEN,5,3,1,5,1,,.5
E,4,1,2,5
E,3,6,5,2
EGEN,2,3,1,2,1
E,11,7,8,8
E,9,12,11,8
E,12,15,11,11
OUTPR,ALL,1
OUTPR,VENG,NONE
NSEL,S,LOC,X
SF,ALL,CONV,12,100           ! INNER CONVECTION SURFACE
NSEL,S,LOC,X,1
SF,ALL,CONV,3,0              ! OUTER CONVECTION SURFACE
NSEL,ALL
FINISH
/SOLU    
SOLVE
FINISH
/POST1
PRNSOL,TEMP                  ! PRINT NODAL TEMPERATURES
ESEL,S,ELEM,,1,3,2           ! SELECT ELEMENTS 1 AND 3
FSUM                         ! PERFORM FORCE AND MOMENT SUMMATIONS
*GET,HT13,FSUM,,ITEM,HEAT    ! ADD HEAT FLOW RATES OF ELEMENTS 1 AND 3
HEAT=HT13*8                  ! COMPUTE TOTAL HEAT FLOW RATE
ESEL,ALL
/CLABEL,,1                   ! LABEL CONTOUR LINES
/CONTOUR,,20                 ! USE 20 CONTOUR LINES
PLNSOL,TEMP                  ! DISPLAY TEMPERATURE CONTOURS
*DIM,VALUE,,13,3
*VFILL,VALUE(1,1),DATA,93.7,56.3,22.2,93.2,54.6,21.4,87.6,47.5,18.3
*VFILL,VALUE(10,1),DATA,29.6,11.7,4.7,775.2
*DO,I,1,9,1
*GET,TN,NODE,I,TEMP
*VFILL,VALUE(I,2),DATA,TN 
*VFILL,VALUE(I,3),DATA,ABS(VALUE(I,2)/VALUE(I,1))
*ENDDO
*VFILL,VALUE(13,2),DATA,HEAT
*VFILL,VALUE(13,3),DATA,ABS(HEAT/775.2)
*GET,TN11,NODE,11,TEMP
*GET,TN12,NODE,12,TEMP
*GET,TN15,NODE,15,TEMP
*VFILL,VALUE(10,2),DATA,TN11,TN12,TN15
*VFILL,VALUE(10,3),DATA,ABS(TN11/29.6),ABS(TN12/11.7),ABS(TN15/4.7)
*DIM,LABEL,CHAR,13,2
LABEL(1,1) = 'T,F (AT ','T,F (AT ','T,F (AT ','T,F (AT ','T,F (AT ','T,F (AT ','T,F (AT '
LABEL(8,1) = 'T,F (AT ','T,F (AT ','T,F (AT ','T,F (AT ','T,F (AT ','q,   BTU'
LABEL(1,2) = 'NODE 1) ','NODE 2) ','NODE 3) ','NODE 4) ','NODE 5) ','NODE 6) ','NODE 7) '
LABEL(8,2) = 'NODE 8) ','NODE 9) ','NODE 11)','NODE 12)','NODE 15)','/hr     '  
/COM
/OUT,vm100,vrt
/COM,------------------- VM100 RESULTS COMPARISON --------------
/COM,
/COM,                 |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.1,'  ',F10.1,'   ',1F5.2)
/COM,-----------------------------------------------------------
/OUT
FINISH
*LIST,vm100,vrt


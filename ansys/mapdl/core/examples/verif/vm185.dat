/COM,ANSYS MEDIA REL. 150 (11/8/2013) REF. VERIF. MANUAL: REL. 150
/VERIFY,VM185
/PREP7
SMRT,OFF
/TITLE, VM185, AC ANALYSIS OF A SLOT EMBEDDED CONDUCTOR
C***    KONRAD, A., "INTEGRODIFFERENTIAL FINITE ELEMENT FORMULATION
C***    OF TWO-DIMENSIONAL STEADY-STATE SKIN EFFECT PROBLEMS",
C***    IEEE TRANS. MAGNETICS, VOL. MAG-18, NO. 1, JAN. 1982
C***    PP. 284-292.
C***
ET,1,PLANE13                        ! PLANE13, AZ DOF, (FOR AIR)
ET,2,PLANE13,6                      ! PLANE13, AZ VOLT DOF, (FOR CONDUCTOR)
EMUNIT,MKS                          ! DEFINE SYSTEM UNITS
MP,MURX,1,1                         ! RELATIVE PERMEABILITY
MP,MURX,2,1
RES=1.724E-8                        ! DEFINE RESISTIVITY OF CONDUCTOR
MP,RSVZ,2,RES                       ! CONDUCTOR RESISTIVITY
A=6.45E-3                           ! DEFINE GEOMETRY IN TERMS OF PARAMETERS
B=8.55E-3
C=8.45E-3
D=18.85E-3
E=8.95E-3
F=(D-E)/2

PTXY,0,0,D,0,D,C,D-F,C              ! CREATE POLYGON AREA OF CONDUCTOR
PTXY,D-F,B+C,F,B+C,F,C,0,C
POLY
RECTNG,F,F+E,B+C,B+C+A              ! CREATE AIR AREA
AGLUE,1,2                           ! GLUE AREAS TOGETHER

ASEL,S,AREA,,3
AATT,1,,1                           ! SET ATTRIBUTES FOR AIR
ASEL,S,AREA,,1   
AATT,2,,2                           ! SET ATTRIBUTES FOR CONDUCTOR
ASEL,ALL
ESIZE,D/15                          ! SET ELEMENT EDGE LENGTH
MSHK,0                              ! FREE AREA MESH
MSHA,1,2D                           ! USING TRIS 
AMESH,ALL                           ! MESH AREAS
ESEL,S,MAT,,2                       ! SELECT ALL NODES IN CONDUCTOR
NSLE,S  
CP,1,VOLT,ALL                       ! COUPLE ALL NODES IN VOLT
I=1.0                               ! DEFINE TOTAL CURRENT
ASUM                                ! CALCULATE AREA ATTRIBUTES
*GET,AREA,AREA,1,AREA               ! GET AREA OF CONDUCTOR
*GET,N1,NODE,,NUM,MIN               ! SELECT A NODE IN THE CONDUCTOR
F,N1,AMPS,I                         ! APPLY 1 AMP TOTAL CURRENT
ESEL,ALL
NSEL,S,LOC,Y,.02345                 ! SELECT NODES AT TOP PLANE
D,ALL,AZ,0                          ! SET FLUX PARALLEL B.C.
NSEL,ALL
FINISH
/SOLU    
ANTYPE,HARMIC                       ! HARMONIC ANALYSIS
HARFRQ,45                           ! SET OPERATING FREQUENCY
SOLVE
FINISH
/POST1
SET,1,1                             ! RETRIEVE REAL SOLUTION
ETABLE,JT,NMISC,7                   ! STORE TOTAL CURRENT DENSITY
ETABLE,JS,SMISC,1                   ! STORE SOURCE CURRENT DENSITY
ETABLE,JE,NMISC,6                   ! STORE EDDY CURRENT DENSITY
/PNUM,MAT,1
/EDGE,1,1
/NUM,1
/SHOW,JPEG
/GFILE,500
JPEG,QUAL,100
/TRIAD,OFF
/PLOPTS,LOGO,0
/PLOPTS,INFO,2
/PLOPTS,WP,0
/RGB,INDEX,100,100,100,0
/RGB,INDEX,80,80,80,13
/RGB,INDEX,60,60,60,14
/RGB,INDEX,0,0,0,15
PLNSOL,A,Z                          ! DISPLAY FLUX LINES
/NUM,0
ESEL,MAT,2                          ! SELECT COPPER ONLY
PLETAB,JT,1                         ! DISPLAY TOTAL CURRENT DENSITY
PLETAB,JE,1                         ! DISPLAY EDDY CURRENT DENSITY
*GET,JSR,ELEM,1,ETAB,JS             ! GET REAL COMPONENT OF JS
ACRE=JSR*RES/I                      ! CALCULATE AC RESISTANCE/LENGTH
SET,1,1,,1                          ! READ IN IMAGINARY DATA
ETABLE,REFL                         ! REFILL ELEMENT TABLE WITH IMAG. DATA
*GET,JSI,ELEM,1,ETAB,JS             ! GET IMAGINARY COMPONENT OF JS
ACRA=JSI*RES/I                      ! CALCULATE AC REACTANCE/LENGTH
DCRE=RES/AREA                       ! CALCULATE DC RESISTANCE/LENGTH
RAT=ACRE/DCRE                       ! AC/DC LOSS RATIO
/OUTPUT
/GOPR
*STATUS,PARM                             ! SHOW PARAMETER STATUS
*DIM,LABEL,CHAR,5,2
*DIM,VALUE,,5,3
LABEL(1,1) = 'JS ','JS ','Impedance','Impedance','LOSS '
LABEL(1,2) = '(RE)','(IM)','(RE)','(IM)','RATIO'
*VFILL,VALUE(1,1),DATA,10183,27328,0.175e-3,0.471e-3,2.33
*VFILL,VALUE(1,2),DATA,JSR,JSI,ACRE,ACRA,RAT
*VFILL,VALUE(1,3),DATA,ABS(JSR/10183),ABS(JSI/27328),ABS(ACRE)/0.175e-3,ABS(ACRA)/0.471e-3,ABS(RAT/2.33)
/COM
/OUT,vm185,vrt
/COM,------------------- VM185 RESULTS COMPARISON -------------------------
/COM,
/COM,                 |     TARGET     |   Mechanical APDL   |   RATIO
/COM,PLANE13
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A9,'   ',F12.6,'  ',F15.6,'   ',F12.3)
/OUT
FINISH

/CLEAR,NOSTART
/NOPR
/PREP7
SMRT,OFF
/TITLE, VM185, AC ANALYSIS OF A SLOT EMBEDDED CONDUCTOR
ET,1,PLANE233                       ! PLANE233, AZ DOF, (FOR AIR)
ET,2,PLANE233,1                     ! PLANE233, AZ VOLT DOF, (FOR CONDUCTOR)
EMUNIT,MKS                          ! DEFINE SYSTEM UNITS
MP,MURX,1,1                         ! RELATIVE PERMEABILITY
MP,MURX,2,1
RES=1.724E-8                        ! DEFINE RESISTIVITY OF CONDUCTOR
MP,RSVZ,2,RES                       ! CONDUCTOR RESISTIVITY
A=6.45E-3                           ! DEFINE GEOMETRY IN TERMS OF PARAMETERS
B=8.55E-3
C=8.45E-3
D=18.85E-3
E=8.95E-3
F=(D-E)/2

PTXY,0,0,D,0,D,C,D-F,C              ! CREATE POLYGON AREA OF CONDUCTOR
PTXY,D-F,B+C,F,B+C,F,C,0,C
POLY
RECTNG,F,F+E,B+C,B+C+A              ! CREATE AIR AREA
AGLUE,1,2                           ! GLUE AREAS TOGETHER

ASEL,S,AREA,,3
AATT,1,,1                           ! SET ATTRIBUTES FOR AIR
ASEL,S,AREA,,1   
AATT,2,,2                           ! SET ATTRIBUTES FOR CONDUCTOR
ASEL,ALL
ESIZE,D/15                          ! SET ELEMENT EDGE LENGTH
MSHK,0                              ! FREE AREA MESH
MSHA,1,2D                           ! USING TRIS 
AMESH,ALL                           ! MESH AREAS
ESEL,S,MAT,,2                       ! SELECT ALL NODES IN CONDUCTOR
NSLE,S  
CP,1,VOLT,ALL                       ! COUPLE ALL NODES IN VOLT
I=1.0                               ! DEFINE TOTAL CURRENT
ASUM                                ! CALCULATE AREA ATTRIBUTES
*GET,AREA,AREA,1,AREA               ! GET AREA OF CONDUCTOR
*GET,N1,NODE,,NUM,MIN               ! SELECT A NODE IN THE CONDUCTOR
F,N1,AMPS,I                         ! APPLY 1 AMP TOTAL CURRENT
ESEL,ALL
NSEL,S,LOC,Y,.02345                 ! SELECT NODES AT TOP PLANE
D,ALL,AZ,0                          ! SET FLUX PARALLEL B.C.
NSEL,ALL
FINISH

/SOLU    
ANTYPE,HARMIC                       ! HARMONIC ANALYSIS
HARFRQ,45                           ! SET OPERATING FREQUENCY
SOLVE
FINISH

/POST1
/PNUM,MAT,1
/EDGE,1,1
/NUM,1
/SHOW,JPEG
/GFILE,500
JPEG,QUAL,100
/TRIAD,OFF
/PLOPTS,LOGO,0
/PLOPTS,INFO,2
/PLOPTS,WP,0
/RGB,INDEX,100,100,100,0
/RGB,INDEX,80,80,80,13
/RGB,INDEX,60,60,60,14
/RGB,INDEX,0,0,0,15
/NUM,0
SET,1,1                             ! RETRIEVE REAL SOLUTION
ETABLE,JT,NMISC,1                   ! STORE TOTAL CURRENT DENSITY
ESEL,MAT,2                          ! SELECT COPPER ONLY
PLETAB,JT,1                         ! DISPLAY TOTAL CURRENT DENSITY
JSR=VOLT(N1)/RES
ACRE=JSR*RES/I                      ! CALCULATE AC RESISTANCE/LENGTH

SET,1,1,,1                          ! RETRIEVE IMAG SOLUTION
ETABLE,JT,NMISC,1                   ! STORE TOTAL CURRENT DENSITY
ESEL,MAT,2                          ! SELECT COPPER ONLY
PLETAB,JT,1                         ! DISPLAY TOTAL CURRENT DENSITY
JSI=VOLT(N1)/RES
ACRA=JSI*RES/I                      ! CALCULATE AC REACTANCE/LENGTH

DCRE=RES/AREA                       ! CALCULATE DC RESISTANCE/LENGTH
RAT=ACRE/DCRE                       ! AC/DC LOSS RATIO
/GOPR
*DIM,LABEL,CHAR,5,2
*DIM,VALUE,,5,3
LABEL(1,1) = 'JS ','JS ','Impedance','Impedance','LOSS '
LABEL(1,2) = '(RE)','(IM)','(RE)','(IM)','RATIO'
*VFILL,VALUE(1,1),DATA,10183,27328,0.175e-3,0.471e-3,2.33
*VFILL,VALUE(1,2),DATA,JSR,JSI,ACRE,ACRA,RAT
*VFILL,VALUE(1,3),DATA,ABS(JSR/10183),ABS(JSI/27328),ABS(ACRE/0.175e-3),ABS(ACRA/0.471e-3),ABS(RAT/2.33)
/COM
/OUT,vm185,vrt,,APPEND
/COM,PLANE233
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A9,'   ',F12.6,'  ',F15.6,'   ',F12.3)
/COM,---------------------------------------------------------------------
/COM,
/OUT
FINISH
*LIST,vm185,vrt

/COM,ANSYS MEDIA REL. 150 (11/8/2013) REF. VERIF. MANUAL: REL. 150
/VERIFY,VM80
/PREP7    
JPGPRF,500,100,1             ! MACRO TO SET PREFS FOR JPEG PLOTS
/SHOW,JPEG
/TITLE, VM80, PLASTIC RESPONSE TO A SUDDENLY APPLIED CONSTANT FORCE
C***           INTRODUCTION TO STRUCT. DYNAMICS, BIGGS, PAGE 69, ART. 2.7
ANTYPE,TRANS        ! FULL TRANSIENT DYNAMIC ANALYSIS
ET,1,LINK180
ET,2,MASS21,,,4     ! TWO-DIMENSIONAL MASS
SECTYPE,1,LINK   
SECDATA,.278		    ! AREA (A)
R,2,0.0259          ! MASS
MP,EX,1,30E3
TB,BKIN,1,1         ! BILINEAR KINEMATIC HARDENING STRESS-STRAIN CURVE
TBTEMP,0
TBDATA,1,162.9,0    ! YIELD STRESS AND TANGENT MODULUS
N,1
N,2,,-100
E,1,2
D,ALL,UZ
TYPE,2
REAL,2
E,1
FINISH
/SOLU    
SOLCONTROL,0
KBC,1               ! STEP BOUNDARY CONDITIONS
TIME,4E-3           ! TIME AT THE END OF LOAD STEP 1
D,1,UX,,,2
D,2,UY
F,1,FY,30           ! APPLY F1
NSUBST,10           ! 10 SUBSTEPS FOR  TIME STEP OF .0004
OUTPR,BASIC,1       ! PRINT BASIC SOLUTION FOR EACH SUBSTEP
OUTRES,NSOL,1       ! STORE NODAL SOLUTION FOR EACH SUBSTEP
/OUT,SCRATCH
SOLVE
TIME,.14            ! FINAL TIME SLIGHTLY MORE THAN 1 CYCLE OF VIBRATION
NSUBST,68           ! 68 REPEATS FOR TIME STEP OF 0.002 
OUTPR,BASIC,8       ! PRINT BASIC SOLUTION FOR EVERY 8TH SUBSTEP
SOLVE
FINISH
/POST26
/OUT,
NSOL,2,1,U,Y,UY       ! STORE UY DISPLACEMENTS OF NODE 1 AGAINST TIME
PRVAR,2               ! PRINT VARIABLE 2 (DISPLACEMENT UY OF NODE 1) V/S TIME
/GRID,1               ! TURN THE GRID ON FOR DISPLAY
/AXLAB,Y,DISPLACEMENT ! MAKE Y-AXIS LABEL AS DISP FOR DISPLAY
PLVAR,2               ! DISPLAY VARIABLE 2 (DISPLACEMENT UY OF NODE 1) V/S TIME
*GET,YMAX,VARI,2,EXTREM,VMAX
*GET,TMAX,VARI,2,EXTREM,TMAX
*GET,YMIN,VARI,2,RTIME,.122 
TMIN = .122
*DIM,LABEL,CHAR,4,2
*DIM,VALUE,,4,3
LABEL(1,1) = 'Y_MAX, i','TIME, se','Y_MIN, i','TIME, se'
LABEL(1,2) = 'n       ','c       ','n       ','c       '
*VFILL,VALUE(1,1),DATA,.806,.0669,.438,.122
*VFILL,VALUE(1,2),DATA,YMAX,TMAX,YMIN,TMIN
*VFILL,VALUE(1,3),DATA,ABS(YMAX/.806),ABS(TMAX/.0669),ABS(YMIN/.438),ABS(TMIN/.122)
/COM
/OUT,vm80,vrt
/COM,------------------- VM80 RESULTS COMPARISON --------------
/COM,
/COM,                 |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.4,'  ',F14.4,'   ',1F15.3)
/COM,----------------------------------------------------------
/OUT
FINISH
*LIST,vm80,vrt

/COM,ANSYS MEDIA REL. 150 (11/8/2013) REF. VERIF. MANUAL: REL. 150
/VERIFY,VMD1
JPGPRF,500,100,1             ! MACRO TO SET PREFS FOR JPEG PLOTS
/SHOW,JPEG
/DEVICE,VECTOR,ON
! ----------------------------------------------------------------------
! PARAMETER KEY:
!
! ATYP  - ANALYSIS TYPE (2=2D, 3=3D)
! ETYP  - ELEMENT TYPE NUMBER
! QUAD  - '1' IF QUADRILATERAL ELEMENT
! TRI   - '1' IF TRIANGULAR HIGHER ORDER ELEMENT
! BRICK - '1' IF BRICK ELEMENT
! TET   - '1' IF HIGHER ORDER TET ELEMENT
! LOW   - '1' IF LOWER ORDER QUAD OR BRICK ELEMENT
! HIGH  - '1' IF HIGHER ORDER QUAD OR BRICK ELEMENT
!
! NOTE: FOR QUAD, TRI, BRICK, & TET KEYS: ONLY ONE MAY BE SET TO
!       '1' (ACTIVE).  LOW & HIGH ARE APPLICABLE ONLY TFOR EITHER
!       QUAD OR BRICK ELEMENT TYPES.  SET LOW OR HIGH TO ZERO FOR
!       TRI OR TET ELEMENTS.
! ----------------------------------------------------------------------
/TITLE, VMD1, STRAIGHT BEAM UNDER VARIOUS LOADS
/COM
/COM    MACNEAL, R.H., AND HARDER R.L., "A PROPOSED STANDARD SET OF
/COM    PROBLEMS TO TEST FINITE ELEMENT ACCURACY
/COM
/NOPR
/OUT,SCRATCH
*CREATE,MAC1
 ARG1=(ARG1*3.141592654)/180.           ! ARG1=DISTORTION ANGLE (DEG.)
 DELX=(0.1*TAN(ARG1))
 NMODIF,ARG2,(1+DELX)
 *REPEAT,3,4,2
 NMODIF,ARG3,(2-DELX)
 *REPEAT,2,4,2
 NMODIF,ARG4,(1-DELX)
 *REPEAT,3,4,2
 NMODIF,ARG5,(2+DELX)
 *REPEAT,2,4,2
*END
*CREATE,MAC2
 ARG1=(ARG1*3.141592654)/180.
 DELX=(0.1*TAN(ARG1))
 NMODIF,ARG2,(1-DELX)
 *REPEAT,5,2,1
 NMODIF,ARG3,(1+DELX)
 *REPEAT,5,2,1
*END
*CREATE,MAC3                                   ! MACRO - EXTRACT DISP
  *DO,I,1,7
    *GET,X1,NODE,13+(I-1)*200,U,ARG1
    *GET,X2,NODE,53+(I-1)*200,U,ARG1
    DR(ARG2,I)=(ABS(X1)+ABS(X2))/2
  *ENDDO
*END
*CREATE,MAC4                                   ! MACRO - EXTRACT ERROR
  SET,ARG1
  *DO,J,1,7
    ESEL,S,,,EINC*(J-1)+1,EINC*(J-1)+EINC
    PRERR
 *GET,ER(ARG1,J),PRERR,,SEPC
  *ENDDO
*END
*DO,L,1,6
*IF,L,EQ,1,THEN
  ETYP=183
 ATYP=2
 QUAD=0
 TRI=1
 BRICK=0
 TET=0
 LOW=0
 HIGH=0
  ELAB='PLANE183 tri'
*ELSEIF,L,EQ,2,THEN
  ETYP=182
 ATYP=2
 QUAD=1
 TRI=0
 BRICK=0
 TET=0
 LOW=1
 HIGH=0
  ELAB='PLANE182'
*ELSEIF,L,EQ,3,THEN
  ETYP=183
 ATYP=2
 QUAD=1
 TRI=0
 BRICK=0
 TET=0
 LOW=0
 HIGH=1
  ELAB='PLANE183'
*ELSEIF,L,EQ,4,THEN
  ETYP=185
 ATYP=3
 QUAD=0
 TRI=0
 BRICK=1
 TET=0
 LOW=1
 HIGH=0
  ELAB='SOLID185'
*ELSEIF,L,EQ,5,THEN
  ETYP=187
 ATYP=3
 QUAD=0
 TRI=0
 BRICK=0
 TET=1
 LOW=0
 HIGH=0
  ELAB='SOLID187'
*ELSEIF,L,EQ,6,THEN
  ETYP=186
 ATYP=3
 QUAD=0
 TRI=0
 BRICK=1
 TET=0
 LOW=0
 HIGH=1
  ELAB='SOLID186'
*ENDIF
*DIM,DR,,4,7                            ! ARRAY - DISPLACEMENT RATIO
*DIM,ER,,4,7                            ! ARRAY - ENERY NORM ERROR
*DIM,LAB,CHAR,4
LAB(1)='EXTEND'
 LAB(2)='IP SHEAR'
 LAB(3)='OP SHEAR'
 LAB(4)='TWIST'

*IF,TRI,EQ,1,THEN                       ! DEFINE ELEMENTS PER MODEL
  EINC=12
*ELSEIF,TET,EQ,1,THEN
  EINC=30
*ELSE
  EINC=6
*ENDIF
/PREP7

*IF,BRICK,EQ,1,AND,LOW,EQ,1,THEN
  ET,1,ETYP,,3				!SIMPLIFIED ENHANCED STRAIN FORMULATION
*ELSEIF,BRICK,EQ,1,AND,HIGH,EQ,1,THEN
  ET,1,ETYP,,1				!FULL INTEGRATION 
*ELSEIF,QUAD,EQ,1,AND,LOW,EQ,1,THEN
  ET,1,ETYP,3,,3			!SIMPLIFIED ENHANCED STRAIN FORMULATION	
*ELSEIF,TET,EQ,1,THEN
  ET,1,ETYP
*ELSEIF,TRI,EQ,1,THEN
  ET,1,ETYP,1,,3
*ELSE,
  ET,1,ETYP,,,3
*ENDIF
R,1,0.1
MP,EX,1,1E7
MP,NUXY,1,0.3
N,1
 N,13,6,0
FILL,1,13,5,3,2
NGEN,3,20,1,13,2,0,0.1,0                ! DEFINE CORNER NODES
NGEN,4,200,1,53,2,0,0,0                 ! DEFINE NODES FOR TRAPIZOIDAL ELEMENTS
*USE,MAC1,15,203,205,243,245            ! MODIFY NODE LOCATIONS
*USE,MAC1,30,403,405,443,445
*USE,MAC1,45,603,605,643,645
NGEN,2,800,1,53,2,0,0,0                 ! DEFINE NODES PARALLELOGRAM ELEMENTS
NGEN,3,200,801,853,2
*USE,MAC2,15,803,843                    ! MODIFY NODE LOCATIONS
*USE,MAC2,30,1003,1043
*USE,MAC2,45,1203,1243
*IF,QUAD,EQ,1,THEN                      ! QUAD ELEMENT GENERATION
  E,1,3,43,41
  EGEN,6,2,-1
  EGEN,4,200,-6
  EGEN,4,200,-6
  *IF,HIGH,EQ,1,THEN
    EMID
  *ENDIF
  /VIEW,1,1,2,3
  EPLOT
*ENDIF
*IF,TRI,EQ,1,THEN                       ! HIGHER ORDER TRI ELEMENT GENERATION
  E,1,3,41
  E,3,43,41
  EGEN,6,2,-2
  EGEN,4,200,-12
  EGEN,4,200,-12
  EMID
  /VIEW,1,1,2,3
  EPLOT
*ENDIF
*IF,BRICK,EQ,1,THEN                     ! BRICK ELEMENT GENERATION
  NGEN,2,1300,1,1353,1,,,.1
  E,1,3,43,41,1301,1303,1343,1341
  EGEN,6,2,-1
  EGEN,4,200,-6
  EGEN,4,200,-6
  *IF,HIGH,EQ,1,THEN
    EMID
  *ENDIF
  /VIEW,1,1,2,3
  EPLOT
*ENDIF
*IF,TET,EQ,1,THEN                       ! TET ELEMENT GENERATION
  NGEN,2,1300,1,1353,1,,,.1
  E,1,1301,1303,1341
  E,1,1303,3,43
  E,1303,1343,43,1341
  E,1,1303,43,1341
  E,1,43,1341,41
  E,3,1303,5,43
  E,1303,1305,5,1345
  E,1303,5,43,1345
  E,5,43,1345,45
  E,1303,1345,43,1343
  EGEN,3,4,-10
  EGEN,4,200,-30
  EGEN,4,200,-30
  EMID
  /VIEW,1,1,2,3
  EPLOT
*ENDIF
NSLE,S
NSEL,INVERT
NDELE,ALL
NSEL,ALL
NSEL,S,LOC,X,0
D,ALL,ALL                               ! FIX LEFT END
NSEL,ALL
WSORT,x
FINISH
/SOLU
*IF,QUAD,EQ,1,THEN                      ! QUAD ELEMENT LOADS
  *IF,LOW,EQ,1,THEN
    /TITLE, STRAIGHT BEAM -  EXTENSION (DISP = 3E-5)
    F,13,FX,.50,,53,40
 *REPEAT,7,200,,,,200
    SOLVE
    /TITLE, STRAIGHT BEAM -  IN-PLANE LOAD (DISP = .1081)
    FDEL,ALL
    F,13,FY,.50,,53,40
 *REPEAT,7,200,,,,200
    SOLVE
  *ELSE
    /TITLE, STRAIGHT BEAM -  EXTENSION (DISP = 3E-5)
    NSEL,S,LOC,X,6
    F,ALL,FX,(1/6)
    NSEL,R,LOC,Y,.1
    F,ALL,FX,(2/3)
    NSEL,ALL
    SOLVE
    /TITLE, STRAIGHT BEAM -  IN-PLANE LOAD (DISP = .1081)
    FDEL,ALL
    NSEL,S,LOC,X,6
    F,ALL,FY,(1/6)
    NSEL,R,LOC,Y,.1
    F,ALL,FY,(2/3)
    NSEL,ALL
    SOLVE
  *ENDIF
*ENDIF
*IF,TRI,EQ,1,THEN                       ! HIGHER ORDER TRAINGLE LOADS
  /TITLE, STRAIGHT BEAM -  EXTENSION (DISP = 3E-5)
  NSEL,S,LOC,X,6
  F,ALL,FX,(1/6)
  NSEL,R,LOC,Y,.1
  F,ALL,FX,(2/3)
  NSEL,ALL
  SOLVE
  /TITLE, STRAIGHT BEAM -  IN-PLANE LOAD (DISP = .1081)
  FDEL,ALL
  NSEL,S,LOC,X,6
  F,ALL,FY,(1/6)
  NSEL,R,LOC,Y,.1
  F,ALL,FY,(2/3)
  NSEL,ALL
  SOLVE
*ENDIF
*IF,BRICK,EQ,1,THEN                     ! BRICK ELEMENT LOADS
  *IF,LOW,EQ,1,THEN
    /TITLE, STRAIGHT BEAM -  EXTENSION (DISP = 3E-5)
    F,13,FX,.25,,53,40
 *REPEAT,7,200,,,,200
    F,1313,FX,.25,,1353,40
 *REPEAT,7,200,,,,200
    SOLVE
    /TITLE, STRAIGHT BEAM -  IN-PLANE LOAD (DISP = .1081)
    FDEL,ALL
    F,13,FY,.25,,53,40
 *REPEAT,7,200,,,,200
    F,1313,FY,.25,,1353,40
 *REPEAT,7,200,,,,200
    SOLVE
    /TITLE, STRAIGHT BEAM -  OUT-OF-PLANE LOAD (DISP. = .4321)
    FDEL,ALL
    F,13,FZ,.25,,53,40
 *REPEAT,7,200,,,,200
    F,1313,FZ,.25,,1353,40
 *REPEAT,7,200,,,,200
    SOLVE
    /TITLE, STRAIGHT BEAM -  TWIST LOAD (ROT = .03208)
    FDEL,ALL
    F,13,FZ,2.5,,1213,200
    F,53,FZ,-2.5,,1253,200  *UNIT TWISTING MOMENT (0.2*5)
    F,1313,FZ,2.5,,2513,200
    F,1353,FZ,-2.5,,2553,200
    SOLVE	
  *ELSE
    /TITLE, STRAIGHT BEAM -  EXTENSION (DISP = 3E-5)
    NSEL,S,LOC,X,6
    F,ALL,FX,(1/3)
    NSEL,R,LOC,Z,0
 NSEL,R,LOC,Y,0
    F,ALL,FX,(-1/12)
    NSEL,S,LOC,X,6
 NSEL,R,LOC,Z,0
 NSEL,R,LOC,Y,.2
    F,ALL,FX,(-1/12)
    NSEL,S,LOC,X,6
 NSEL,R,LOC,Z,.1
 NSEL,R,LOC,Y,0
    F,ALL,FX,(-1/12)
    NSEL,S,LOC,X,6
 NSEL,R,LOC,Z,.1
 NSEL,R,LOC,Y,.2
    F,ALL,FX,(-1/12)
    NSEL,ALL
    SOLVE
    /TITLE, STRAIGHT BEAM -  IN-PLANE LOAD (DISP = .1081)
    FDEL,ALL
    NSEL,S,LOC,X,6
    F,ALL,FY,(1/3)
    NSEL,R,LOC,Z,0
 NSEL,R,LOC,Y,0
    F,ALL,FY,(-1/12)
    NSEL,S,LOC,X,6
 NSEL,R,LOC,Z,0
 NSEL,R,LOC,Y,.2
    F,ALL,FY,(-1/12)
    NSEL,S,LOC,X,6
 NSEL,R,LOC,Z,.1
 NSEL,R,LOC,Y,0
    F,ALL,FY,(-1/12)
    NSEL,S,LOC,X,6
 NSEL,R,LOC,Z,.1
 NSEL,R,LOC,Y,.2
    F,ALL,FY,(-1/12)
    NSEL,ALL
    SOLVE
    /TITLE, STRAIGHT BEAM -  OUT-OF-PLANE LOAD (DISP. = .4321)
    FDEL,ALL
    NSEL,S,LOC,X,6
    F,ALL,FZ,(1/3)
    NSEL,R,LOC,Z,0
 NSEL,R,LOC,Y,0
    F,ALL,FZ,(-1/12)
    NSEL,S,LOC,X,6
 NSEL,R,LOC,Z,0
 NSEL,R,LOC,Y,.2
    F,ALL,FZ,(-1/12)
    NSEL,S,LOC,X,6
 NSEL,R,LOC,Z,.1
 NSEL,R,LOC,Y,0
    F,ALL,FZ,(-1/12)
    NSEL,S,LOC,X,6
 NSEL,R,LOC,Z,.1
 NSEL,R,LOC,Y,.2
    F,ALL,FZ,(-1/12)
    NSEL,ALL
    SOLVE
    /TITLE, STRAIGHT BEAM -  TWIST LOAD (ROT = .03208)
    FDEL,ALL
    NSEL,S,LOC,X,6
 NSEL,R,LOC,Z,0
 NSEL,R,LOC,Y,.1
    F,ALL,FY,-5
    NSEL,S,LOC,X,6
 NSEL,R,LOC,Z,.05
 NSEL,R,LOC,Y,0
    F,ALL,FZ,5
    NSEL,S,LOC,X,6
 NSEL,R,LOC,Z,.1
 NSEL,R,LOC,Y,.1
    F,ALL,FY,5
    NSEL,S,LOC,X,6
 NSEL,R,LOC,Z,.05
 NSEL,R,LOC,Y,.2
    F,ALL,FZ,-5
    NSEL,S,LOC,X,6
 NSEL,R,LOC,Z,0
 NSEL,R,LOC,Y,0
    F,ALL,FZ,(-15/12)
    NSEL,S,LOC,X,6
 NSEL,R,LOC,Z,0
 NSEL,R,LOC,Y,.2
    F,ALL,FZ,(15/12)
    NSEL,S,LOC,X,6
 NSEL,R,LOC,Z,.1
 NSEL,R,LOC,Y,0
    F,ALL,FZ,(-15/12)
    NSEL,S,LOC,X,6
 NSEL,R,LOC,Z,.1
 NSEL,R,LOC,Y,.2
    F,ALL,FZ,(15/12)
    NSEL,ALL
    SOLVE
  *ENDIF
*ENDIF
*IF,TET,EQ,1,THEN                     ! HIGHER ORDER TET ELEMENT LOADS
  /TITLE, STRAIGHT BEAM -  EXTENSION (DISP = 3E-5)
  NSEL,S,LOC,X,6
 NSEL,R,LOC,Z,.05
  F,ALL,FX,(1/6)
  NSEL,S,LOC,X,6
 NSEL,R,LOC,Y,.1
  F,ALL,FX,(1/6)
  NSEL,S,LOC,X,6
 NSEL,R,LOC,Z,.05
 NSEL,R,LOC,Y,.1
  F,ALL,FX,(1/3)
  NSEL,ALL
  SOLVE
  /TITLE, STRAIGHT BEAM -  IN-PLANE LOAD (DISP = .1081)
  FDEL,ALL
  NSEL,S,LOC,X,6
 NSEL,R,LOC,Z,.05
  F,ALL,FY,(1/6)
  NSEL,S,LOC,X,6
 NSEL,R,LOC,Y,.1
  F,ALL,FY,(1/6)
  NSEL,S,LOC,X,6
 NSEL,R,LOC,Z,.05
 NSEL,R,LOC,Y,.1
  F,ALL,FY,(1/3)
  NSEL,ALL
  SOLVE
  /TITLE, STRAIGHT BEAM -  OUT-OF-PLANE LOAD (DISP. = .4321)
  FDEL,ALL
  NSEL,S,LOC,X,6
 NSEL,R,LOC,Z,.05
  F,ALL,FZ,(1/6)
  NSEL,S,LOC,X,6
 NSEL,R,LOC,Y,.1
  F,ALL,FZ,(1/6)
  NSEL,S,LOC,X,6
 NSEL,R,LOC,Z,.05
 NSEL,R,LOC,Y,.1
  F,ALL,FZ,(1/3)
  NSEL,ALL
  SOLVE
  /TITLE, STRAIGHT BEAM -  TWIST LOAD (ROT = .03208)
  FDEL,ALL
  NSEL,S,LOC,X,6
 NSEL,R,LOC,Z,0
 NSEL,R,LOC,Y,.1
  F,ALL,FY,-(1/.3)
  NSEL,S,LOC,X,6
 NSEL,R,LOC,Z,.05
 NSEL,R,LOC,Y,0
  F,ALL,FZ,(1/.3)
  NSEL,S,LOC,X,6
 NSEL,R,LOC,Z,.1
 NSEL,R,LOC,Y,.1
  F,ALL,FY,(1/.3)
  NSEL,S,LOC,X,6
 NSEL,R,LOC,Z,.05
 NSEL,R,LOC,Y,.2
  F,ALL,FZ,-(1/.3)
  NSEL,ALL
  SOLVE
*ENDIF
FINISH
/POST1
! --  DISPLACEMENT RATIOS --
LCDEF,1,1
 LCFACT,1,(1/3E-5)
 LCASE,1          ! LOAD CASE 1
NSEL,S,LOC,X,6
 NSEL,R,LOC,Z,0
PRNSOL,U,X
*USE,MAC3,'X',1
LCDEF,2,2,$ LCFACT,2,(1/0.1081)
 LCASE,2        ! LOAD CASE 2
NSEL,S,LOC,X,6
 NSEL,R,LOC,Z,0
PRNSOL,U,Y
*USE,MAC3,'Y',2
*IF,ATYP,EQ,3,THEN
  LCDEF,3,3
 LCFACT,3,(1/0.4321)
 LCASE,3      ! LOAD CASE 3
  NSEL,S,LOC,X,6
 NSEL,R,LOC,Z,0
  PRNSOL,U,Z
  *USE,MAC3,'Z',3
  LCDEF,4,4
 LCFACT,4,(1/0.003208)
 LCASE,4    ! LOAD CASE 4
  NSEL,S,LOC,X,6
 NSEL,R,LOC,Z,0
  *USE,MAC3,'Z',4
*ENDIF
! -- ENERGRY ERROR NORMS --
*USE,MAC4,1
*USE,MAC4,2
*IF,ATYP,EQ,3,THEN
  *USE,MAC4,3
  *USE,MAC4,4
*ENDIF
/OUT
/COM
/COM =================================================================
/COM
*MSG,INFO,ELAB
ELEMENT TYPE: %C
/COM
/COM DISPLACEMENT RATIO
/COM
/COM  LOADING  | RECT   TRAP15  TRAP30  TRAP45  PARL15  PARL30  PARL45
/COM -----------------------------------------------------------------
*VWRITE,LAB(1),DR(1,1),DR(1,2),DR(1,3),DR(1,4),DR(1,5),DR(1,6),DR(1,7)
(2X,A8,2X,7(F5.3,3X))
/COM
/COM ENERGY NORM ERROR
/COM
/COM  LOADING  | RECT   TRAP15  TRAP30  TRAP45  PARL15  PARL30  PARL45
/COM -----------------------------------------------------------------
*VWRITE,LAB(1),ER(1,1),ER(1,2),ER(1,3),ER(1,4),ER(1,5),ER(1,6),ER(1,7)
(2X,A8,3X,7(F4.0,4X))
/OUT,SCRATCH
FINISH
PARSAV                                          ! SAVE LOOP PARAMETERS
/clear, nostart
/NOPR
PARRES
*ENDDO



/COM,ANSYS MEDIA REL. 150 (11/8/2013) REF. VERIF. MANUAL: REL. 150
/VERIFY,VM279
/COM,   VERIFICATION MANUAL FOR FRACTURE MECHANCIS,
/TITLE,VM279 T-STRESS FOR A CRACK IN A 2D PLATE
/COM    REFERENCE: STRESS INTENSITY FACTORS, T-STRESSES, WEIGHT 
/COM    FUNCTIONS BY THEO FETT,INSTITUTE OF CERAMICS IN
/COM    MECHANICAL ENGINEERING (IKM),UNIVERSITY OF KARLSRUHE(TH)
/COM    ****CRACK IN 2-DIMENSIONS USING 2-D PLANE183 ELEMENT***
/OUT,SCRATCH
/PREP7
E=207000        	           ! YOUNGS MODULUS
NU=0.3     		      ! POISSONS RATIO
SIG=100      	           ! SURFACE LOAD

! DIMENSIONS.
A = 10			                  ! HALF CRACK LENGTH
ALPHA = 0.2                             ! ALPHA = A / W
HEIGHT_WIDTH_RATIO = 0.75               ! H / W
W = A / ALPHA                           ! HALF PLATE WIDTH
H = HEIGHT_WIDTH_RATIO*W                ! HALF PLATE HEIGHT
TSTRESS_REFERENCE_NORMALIZED = -0.88    ! TSTRESS TARGET VALUE 
! NORMALIZED AS: T/SIG*(1-ALPHA)
! TSTRESS TARGET VALUE CORRESPONDING TO THE GIVEN DIMENSIONS:
TSTRESS_REFERENCE = (TSTRESS_REFERENCE_NORMALIZED*SIG) / (1 - ALPHA)

ET,1,PLANE183               ! PLANE 183 ELEMENT
KEYOPT,1,3,2                ! PLANE STRAIN
MP,EX,1,E
MP,NUXY,1,NU

K,1,0,0                     ! DEFINE KEYPOINTS AND LINE SEGMENTS
K,2,A,0                     ! CRACK TIP
K,3,W,0
K,4,W,H/2
K,5,W,H
K,6,0,H
K,7,0,H/2

L,1,2
*REP,6,1,1
L,7,1
L,4,7
AL,1,2,3,8,7
AL,4,5,6,8
ESIZE,1
KSCON,2,1,1,4,0.75          !CRACK TIP ELEMENTS
AMESH,1
ESIZE,2
AMESH,2
FINI

/SOLU
OUTRES,ALL,ALL
NSEL,S,LOC,Y,0
NSEL,R,LOC,X,A,W
D,ALL,UY,0                  ! APPLY MODEL BOUNDARY CONDITIONS
NSEL,S,LOC,X,0
NSEL,R,LOC,Y,0,H
D,ALL,UX,0
NSEL,ALL
LSEL,S,LINE,,5
SFL,ALL,PRESS,-SIG

LSEL,ALL		
NSEL,S,LOC,Y,0
NSEL,R,LOC,X,A
CM,CRACK1,NODE              ! DEFINE THE CRACK TIP NODE COMPONENT
NSEL,ALL

CINT,NEW,1
CINT,TYPE,TSTR              ! CALCULATE T-STRESS
CINT,CTNC,CRACK1            ! CRACK ID
CINT,NCON,4                 ! NUMBER OF COUNTOURS
CINT,SYMM,ON                ! SYMMETRY ON
CINT,NORM,0,2         
CINT,LIST
ALLSEL,ALL
SOLVE
FINISH

/POST1
/OUT,
PRCINT,1,,TSTR                     ! GET THE T-STRESS VALUES
TIP_NODE_NUMBER = NODE(A,0,0)
*GET,TSTRESS_1,CINT,1,CTIP,TIP_NODE_NUMBER,CONTOUR,1,DTYPE,TSTR
*GET,TSTRESS_2,CINT,1,CTIP,TIP_NODE_NUMBER,CONTOUR,2,DTYPE,TSTR
*GET,TSTRESS_3,CINT,1,CTIP,TIP_NODE_NUMBER,CONTOUR,3,DTYPE,TSTR
*GET,TSTRESS_4,CINT,1,CTIP,TIP_NODE_NUMBER,CONTOUR,4,DTYPE,TSTR

TSTRESS = (TSTRESS_2+ TSTRESS_3 + TSTRESS_4) / 3
*STAT,TSTRESS
/OUT,SCRATCH
TSTRESS_RATIO = ABS(TSTRESS/TSTRESS_REFERENCE)
! STORE DATA
*DIM,LABEL,CHAR,1,5
*DIM,VALUE,,1,3
LABEL(1,1) = 'T-STRESS'
*VFILL,VALUE(1,1),DATA,TSTRESS_REFERENCE
*VFILL,VALUE(1,2),DATA,TSTRESS
*VFILL,VALUE(1,3),DATA,TSTRESS_RATIO
SAVE,TABLE_1
FINISH
/CLEAR, NOSTART                ! CLEAR DATABASE FOR 2ND SOLUTION
/OUT,


/COM ***** CRACK IN 3D PLATE USING SOLID 185 ELEMENT ********
/OUT,SCRATCH 
/PREP7
E=207000        	            ! YOUNGS MODULUS
NU=0.3     		       ! POISSONS RATIO
SIG=100      	            ! SURFACE LOAD
PI=3.141593
TK=1

! DIMENSIONS.
A = 10			                  ! HALF CRACK LENGTH
ALPHA = 0.2                             ! ALPHA = A / W
HEIGHT_WIDTH_RATIO = 0.75               ! H / W
W = A / ALPHA                           ! HALF PLATE WIDTH
H = HEIGHT_WIDTH_RATIO*W                ! HALF PLATE HEIGHT
TSTRESS_REFERENCE_NORMALIZED = -0.88    ! TSTRESS TARGET VALUE 
! NORMALIZED AS: T/SIG*(1-ALPHA)
! TSTRESS TARGET VALUE CORRESPONDING TO THE GIVEN DIMENSIONS:
TSTRESS_REFERENCE = (TSTRESS_REFERENCE_NORMALIZED*SIG) / (1 - ALPHA)

ET,1,PLANE182               ! PLANE 182 ELEMENT
ET,2,SOLID185               ! SOLID185 ELEMENT
MP,EX,1,E
MP,NUXY,1,NU

K,1,0,0                     ! DEFINE KEYPOINTS AND LINE SEGMENTS
K,2,A,0 
K,3,W,0
K,4,W,H/2
K,5,W,H
K,6,0,H
K,7,0,H/2

L,1,2
*REP,6,1,1
L,7,1
L,4,7
AL,1,2,3,8,7
AL,4,5,6,8
ESIZE,1
KSCON,2,1,0,4,0.75          !CRACK TIP ELEMENTS
AMESH,1
ESIZE,2
AMESH,2
ALLSEL,ALL
TYPE,2
ESIZE,,5
VEXT,ALL,,,0,0,TK
ALLSEL,ALL
ESEL,S,ENAME,,182
ACLEAR,1,2,,,
ALLSEL
FINI

/SOLU
OUTRES,ALL,ALL
NSEL,S,LOC,Y,0
NSEL,R,LOC,X,A,W
D,ALL,UY,0
NSEL,S,LOC,X,0
NSEL,R,LOC,Y,0,H
D,ALL,UX,0
NSEL,ALL
ASEL,S,,,11
SFA,ALL,,PRESS,-SIG
ALLSEL,ALL

D,ALL,UZ,0
ALLSEL
		
NSEL,S,LOC,Y,0
NSEL,R,LOC,X,A
CM,CRACK1,NODE              ! DEFINE THE CRACK FRONT NODES COMPONENT
NSEL,ALL

CINT,NEW,1
CINT,TYPE,TSTR              ! CALCULATE T-STRESS
CINT,CTNC,CRACK1            ! CRACK ID
CINT,NCON,4                 ! NUMBER OF COUNTOURS
CINT,SYMM,ON                ! SYMMETRY ON
CINT,NORM,0,2         
CINT,LIST
ALLSEL,ALL
SOLVE
FINISH

/POST1
/OUT,
PRCINT,1,,TSTR                     ! GET THE T-STRESS VALUES
TIP_NODE_NUMBER = NODE(A,0,0)
*GET,TSTRESS_1,CINT,1,CTIP,TIP_NODE_NUMBER,CONTOUR,1,DTYPE,TSTR
*GET,TSTRESS_2,CINT,1,CTIP,TIP_NODE_NUMBER,CONTOUR,2,DTYPE,TSTR
*GET,TSTRESS_3,CINT,1,CTIP,TIP_NODE_NUMBER,CONTOUR,3,DTYPE,TSTR
*GET,TSTRESS_4,CINT,1,CTIP,TIP_NODE_NUMBER,CONTOUR,4,DTYPE,TSTR

TSTRESS = (TSTRESS_2+ TSTRESS_3 + TSTRESS_4) / 3
*STAT,TSTRESS
/OUT,SCRATCH
TSTRESS_RATIO = ABS(TSTRESS/TSTRESS_REFERENCE)
! STORE DATA
*DIM,LABEL,CHAR,1,5
*DIM,VALUE,,1,3
LABEL(1,1) = 'T-STRESS'
*VFILL,VALUE(1,1),DATA,TSTRESS_REFERENCE
*VFILL,VALUE(1,2),DATA,TSTRESS
*VFILL,VALUE(1,3),DATA,TSTRESS_RATIO
SAVE,TABLE_2
FINISH
/CLEAR, NOSTART                ! CLEAR DATABASE FOR 2ND SOLUTION
/OUT,


/COM ***** CRACK IN 3D PLATE USING SOLID 186 ELEMENT ******** 
/OUT,SCRATCH
/PREP7
E=207000        	            ! YOUNGS MODULUS
NU=0.3     		       ! POISSONS RATIO
SIG=100      	            ! SURFACE LOAD
PI=3.141593
TK=1

! DIMENSIONS.
A = 10			                  ! HALF CRACK LENGTH
ALPHA = 0.2                             ! ALPHA = A / W
HEIGHT_WIDTH_RATIO = 0.75               ! H / W
W = A / ALPHA                           ! HALF PLATE WIDTH
H = HEIGHT_WIDTH_RATIO*W                ! HALF PLATE HEIGHT
TSTRESS_REFERENCE_NORMALIZED = -0.88    ! TSTRESS TARGET VALUE 
! NORMALIZED AS: T/SIG*(1-ALPHA)
! TSTRESS TARGET VALUE CORRESPONDING TO THE GIVEN DIMENSIONS:
TSTRESS_REFERENCE = (TSTRESS_REFERENCE_NORMALIZED*SIG) / (1 - ALPHA)

ET,1,PLANE183               ! PLANE 182 ELEMENT
ET,2,SOLID186               ! SOLID185 ELEMENT
MP,EX,1,E
MP,NUXY,1,NU

K,1,0,0                     ! DEFINE KEYPOINTS AND LINE SEGMENTS
K,2,A,0 
K,3,W,0
K,4,W,H/2
K,5,W,H
K,6,0,H
K,7,0,H/2

L,1,2
*REP,6,1,1
L,7,1
L,4,7
AL,1,2,3,8,7
AL,4,5,6,8
ESIZE,1
KSCON,2,1,0,4,0.75          !CRACK TIP ELEMENTS
AMESH,1
ESIZE,2
AMESH,2
ALLSEL,ALL
TYPE,2
ESIZE,,5
VEXT,ALL,,,0,0,TK
ALLSEL,ALL
ESEL,S,ENAME,,183
ACLEAR,1,2,,,
ALLSEL
FINI

/SOLU
OUTRES,ALL,ALL
NSEL,S,LOC,Y,0
NSEL,R,LOC,X,A,W
D,ALL,UY,0
NSEL,S,LOC,X,0
NSEL,R,LOC,Y,0,H
D,ALL,UX,0
NSEL,ALL
ASEL,S,,,11
SFA,ALL,,PRESS,-SIG
ALLSEL,ALL

D,ALL,UZ,0
ALLSEL
		
NSEL,S,LOC,Y,0
NSEL,R,LOC,X,A
CM,CRACK1,NODE              ! DEFINE THE CRACK FRONT NODES COMPONENT
NSEL,ALL

CINT,NEW,1
CINT,TYPE,TSTR              ! CALCULATE T-STRESS
CINT,CTNC,CRACK1            ! CRACK ID
CINT,NCON,4                 ! NUMBER OF COUNTOURS
CINT,SYMM,ON                ! SYMMETRY ON
CINT,NORM,0,2         
CINT,LIST
ALLSEL,ALL
SOLVE
FINISH

/POST1
/OUT,
PRCINT,1,,TSTR                     ! GET THE T-STRESS VALUES
TIP_NODE_NUMBER = NODE(A,0,0)
*GET,TSTRESS_1,CINT,1,CTIP,TIP_NODE_NUMBER,CONTOUR,1,DTYPE,TSTR
*GET,TSTRESS_2,CINT,1,CTIP,TIP_NODE_NUMBER,CONTOUR,2,DTYPE,TSTR
*GET,TSTRESS_3,CINT,1,CTIP,TIP_NODE_NUMBER,CONTOUR,3,DTYPE,TSTR
*GET,TSTRESS_4,CINT,1,CTIP,TIP_NODE_NUMBER,CONTOUR,4,DTYPE,TSTR

TSTRESS = (TSTRESS_2+ TSTRESS_3 + TSTRESS_4) / 3
*STAT,TSTRESS
/OUT,SCRATCH
TSTRESS_RATIO = ABS(TSTRESS/TSTRESS_REFERENCE)
! STORE DATA
*DIM,LABEL,CHAR,1,5
*DIM,VALUE,,1,3
LABEL(1,1) = 'T-STRESS'
*VFILL,VALUE(1,1),DATA,TSTRESS_REFERENCE
*VFILL,VALUE(1,2),DATA,TSTRESS
*VFILL,VALUE(1,3),DATA,TSTRESS_RATIO
SAVE,TABLE_3
/OUT,
FINISH
RESUME,TABLE_1
/COM
/OUT,vm279,vrt
/COM,------------------- VM279 RESULTS COMPARISON --------------
/COM,
/COM,           |  TARGET  |   Mechanical APDL  |  RATIO
/COM,
/COM, ****************************************
/COM,  USING PLANE 183 ELEMENT (2-D ANALYSIS)
/COM, *****************************************
/COM,
*VWRITE,LABEL(1,1),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,'   ',F10.4,'  ',F14.4,'   ',F15.3)
/NOPR
RESUME,TABLE_2
/GOPR
/COM,
/COM, *****************************************
/COM,  USING SOLID 185 ELEMENT (3-D ANALYSIS)
/COM, *****************************************
/COM,
*VWRITE,LABEL(1,1),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,'   ',F10.4,'  ',F14.4,'   ',F15.3)
/NOPR
RESUME,TABLE_3
/GOPR
/COM,
/COM, ********************************************************
/COM,  USING SOLID 186 ELEMENT (3-D ANALYSIS)
/COM, ********************************************************
/COM,
*VWRITE,LABEL(1,1),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,'   ',F10.4,'  ',F14.4,'   ',F15.3)
/NOPR
/COM,
/COM,-----------------------------------------------------------
/OUT,
FINISH
*LIST,vm279,vrt

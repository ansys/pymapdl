/COM,ANSYS MEDIA REL. 150 (11/8/2013) REF. VERIF. MANUAL: REL. 150
/VERIFY,VM124
/PREP7
/TITLE, VM124, DISCHARGE OF WATER FROM A RESERVOIR
C***    ELEMENTARY THEORETICAL FLUID MECHANICS, BRENKERT, PAGE 224, PROB. 4
ET,1,FLUID116,2      ! FLOW PIPE WITH LOSS COEFFICIENTS
KEYO,1,7,3
KEYO,1,8,1
DENS = 1.94
MP,DENS,1,DENS
MP,MU,1,.025
MP,VISC,1,2.36E-5
TB,FCON              ! NON-LINEAR FRICTION FACTOR TABLE   
TBPT,,1e5,0.028
TBPT,,3e5,0.028
TBPT,,5e5,0.028
TBPT,,7e5,0.028
TBPT,,9e5,0.028
TBPT,,1e6,0.028
ACELY = 32.2
R,1,.25
RMORE
RMORE,10*ACELY*DENS  ! INCLUDE PUMP HEAD
R,2,.25
RMORE
RMORE,,.5            ! INCLUDE SHARP-EDGE LOSS
R,3,.25
RMORE
RMORE,,.9            ! INCLUDE ELBOW LOSS
R,4,.25
RMORE
RMORE,,.9            ! INCLUDE ELBOW LOSS
N,1,-.01,10
N,2,    ,10
N,3,20,  10
N,4,20
N,5,90
E,1,2
EGEN,4,1,-1,,,,,1    ! INCREMENT REAL CONSTANTS
ACEL,,ACELY          ! GRAVITY LOAD
D,1,PRES,,,5,4       ! WATER SURFACE AND PIPE OUTLET AT ZERO PRESSURE
CNVTOL,FLOW,1,.0001  ! SET CONVERGENCE VALUE FOR FLUID FLOW WITH 
                     !  TOLERANCE LIMIT
OUTPR,,1             ! PRINT BASIC SOLUTION QUANTITIES OF SUBSTEP 1
FINISH
/SOLU    
SOLVE
FINISH
/POST1
ETABLE,R,NMISC,2
ESORT,R
*GET,RE,SORT,,MAX
ETABLE,FL,NMISC,3
ESORT,FL
*GET,FLOW,SORT,,MAX
*status,parm
*DIM,LABEL,CHAR,2,2
*DIM,VALUE,,2,3
LABEL(1,1) = 'FLOW RT. ','Re'
LABEL(1,2) = 'lb/sec '
*VFILL,VALUE(1,1),DATA,0.898,1.94E5
*VFILL,VALUE(1,2),DATA,FLOW,RE
*VFILL,VALUE(1,3),DATA,ABS(FLOW/0.898),ABS(RE/1.94E5)
/OUT,vm124,vrt
/COM
/COM,------------------- VM124 RESULTS COMPARISON -------------
/COM,
/COM,                 |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,' ',F12.3,F17.3,'   ',1F15.3)
/COM,----------------------------------------------------------
/OUT
FINISH
*LIST,vm124,vrt


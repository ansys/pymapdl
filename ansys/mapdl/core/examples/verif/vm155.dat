/COM,ANSYS MEDIA REL. 150 (11/8/2013) REF. VERIF. MANUAL: REL. 150
/VERIFY,VM155
/TITLE,VM155,VERIFY CHABOCHE RATE-DEPENDENT PLASTICITY MATERIAL MODEL
/COM, REFERENCE:"R.C.Lin,ET AL.,MODELING OF FINITE STRAIN VISCOPLASTICITY BASED
/COM,            ON THE LOGARITHMIC COROTATIONAL DESCRIPTION
/COM,            ARCH APPL MECH, 2006(75),PG: 693-708
/COM,
/OUT,SCRATCH
/PREP7
ET,1,PLANE182                    ! 2D 8 NODE ELEMENT
KEYOPT,1,3,0                     ! PLANE STRESS

MP,EX,1,149650e6
MP,NUXY,1,0.33
TB,CHAB,1,,1                     ! DEFINE CHABOCHE MATERIAL DATA
TBDATA,1,1.53e8,62511e6,1.1/311e6*62511e6
TB,RATE,1,,6,CHABOCHE            ! DEFINE RATE DEPENDENT MATERIAL DATA
TBDATA,1,1.53e8,0,-1.53e8,317,1/7.7,1150e6

N, 1, 0.0, 0.0, 0.0    
N, 2, 0.0, 1.0, 0.0     
N, 3, 1.0, 0.0, 0.0   
N, 4, 1.0, 1.0, 0.0
E,1,3,4,2

NSEL,S,LOC,X,0                    ! BOUNDARY CONDITIONS
D,ALL,UX,0
NSEL,S,LOC,Y,0
D,ALL,UY,0 
ALLSEL
FINISH

/SOLU  
OUTRES,ALL,NONE
NSUB,30,300,10
TIME,910/89
NSEL,S,LOC,X,1
D,ALL,UX,-0.01
NSEL,ALL
SOLVE

*DO,I,1,42
  NSEL,S,LOC,X,1
  D,ALL,UX,0.01
  NSEL,ALL
  TIME,910/89*(2*I+1)
  SOLVE

  TIME,910/89*(2*I+3)
  NSEL,S,LOC,X,1
  D,ALL,UX,-0.01
  NSEL,ALL
  SOLVE
*ENDDO

NSUB,30,300,30
OUTRES,ALL,LAST
NSEL,S,LOC,X,1
D,ALL,UX,0.01
NSEL,ALL
TIME,910
SOLVE
TIME,940
SOLVE

NSEL,S,LOC,X,1
D,ALL,UX,-0.01
NSEL,ALL
TIME,960
SOLVE
TIME,990
SOLVE

NSEL,S,LOC,X,1
D,ALL,UX,0
NSEL,ALL
TIME,1000
SOLVE
FINI

/POST1
*DIM,VALUE,,5,3
*DIM,LABEL,CHAR,1,5
LABEL(1,1)='910s'
LABEL(1,2)='940s'
LABEL(1,3)='960s'
LABEL(1,4)='990s'
LABEL(1,5)='1000s'
VALUE(1,1)=0.681E9
VALUE(2,1)=0.501E9
VALUE(3,1)=-0.692E9
VALUE(4,1)=-0.502E9
VALUE(5,1)=0.466E9
SET,FIRST
ETABLE,SXX,S,X
*GET,SS,ELEM,1,ETAB,SXX
VALUE(1,2)=SS
VALUE(1,3)=ABS(VALUE(1,1)/VALUE(1,2))
*DO,I,2,5
   SET,NEXT
   ETABLE,SXX,S,X
  *GET,SS,ELEM,1,ETAB,SXX
   VALUE(I,2)=SS
   VALUE(I,3)=ABS(VALUE(I,1)/VALUE(I,2))
*ENDDO

/COM
/OUT,vm155,vrt
/COM,----- VM155 RESULTS COMPARISON -------------------------
/COM,
/COM,  TIME  |  TARGET  |   Mechanical APDL   |  RATIO
/COM,
*VWRITE,LABEL(1,1),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A5,' ',E10.4,'  ',E16.4,'   ',F15.3)
*VWRITE,LABEL(1,2),VALUE(2,1),VALUE(2,2),VALUE(2,3)
(1X,A5,' ',E10.4,'  ',E16.4,'   ',F15.3)
*VWRITE,LABEL(1,3),VALUE(3,1),VALUE(3,2),VALUE(3,3)
(1X,A5,' ',E10.4,'  ',E16.4,'   ',F15.3)
*VWRITE,LABEL(1,4),VALUE(4,1),VALUE(4,2),VALUE(4,3)
(1X,A5,' ',E10.4,'  ',E16.4,'   ',F15.3)
*VWRITE,LABEL(1,5),VALUE(5,1),VALUE(5,2),VALUE(5,3)
(1X,A5,' ',E10.4,'  ',E16.4,'   ',F15.3)
/COM,-----------------------------------------------------------------------
/OUT
*LIST,vm155,vrt
FINISH


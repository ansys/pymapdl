/COM,ANSYS MEDIA REL. 150 (11/8/2013) REF. VERIF. MANUAL: REL. 150
/VERIFY,VM179
/PREP7
/TITLE, VM179, DYNAMIC DOUBLE ROTATION OF A JOINTED BEAM
C***   REFERENCE -- ANY BASIC MECHANICS TEXT
ANTYPE,TRANS                ! FULL TRANSIENT DYNAMIC ANALYSIS
NLGEOM,ON                   ! LARGE DEFLECTION
TRNOPT,FULL,,,,,HHT         ! USE HHT METHOD
TINTP,.1                    ! 10 PERCENT NUMERICAL DAMPING
ET,1,MPC184,6,,,0           ! REVOLUTE JOINT
ET,2,BEAM188,,,3
ET,3,MASS21
R,1,,,,,.25,.25
SECT,2,BEAM,RECT
SECD,1,1
MP,EX,1,70E9                ! ALUMINUM MATERIAL PROPERTIES
MP,DENS,1,1E-6
MP,NUXY,1,.35
N,1,0
N,2,1.0
N,3,1.0
N,4,2.0
TYPE,3
REAL,1
E,2
E,3
TYPE,2
SECNUM,2
MAT,1
E,1,2                       ! BEAM ELEMENTS
E,3,4
SECTYPE,1,JOINT,REVO
LOCAL,11,0,1,0,0,90,0,0
SECJOINT,,11,11
SECLOCK,4,-0.08727,0.08727  ! JOINT LOCK-UP WHEN STOP ENGAGED
TYPE,1
SECN,1
E,2,3                       ! JOINT ELEMENT
D,1,UX,0,,,,UY,UZ,ROTX,ROTY ! ALLOW ONLY Z-ROTATION AT PINNED END
KBC,1
FINISH
/OUTPUT,SCRATCH             ! SEND SUBSEQUENT OUTPUT TO SCRATCH FILE
/SOLU    
PRED,ON
CNVTOL,M
NSUBST,100,1000,10
F,1,MZ,.7854                ! BEGIN ROTZ WITH NON-ZERO ACCELERATION
TIME,.05
SOLVE
NSUBST,60
TIME,1               ! DELTA THETA-Z = 45 DEG. ( AT 1 SEC )
SOLVE
F,1,MZ,-.7854        ! SLOW ROTZ W/ "INSTANTANEOUS" REVERSAL OF APPLIED MOMENT
TIME,1.05
SOLVE
NSUBST,60
TIME,2               ! DELTA THETA-Z = 90 DEG. ( AT 2 SEC )
SOLVE
FINISH
/POST1
SET,2
PRNSOL,DOF
*GET,DX1,NODE,4,U,X
*GET,DY1,NODE,4,U,Y
*GET,RZ1,NODE,4,ROT,Z
ESEL,S,TYPE,,1
ETABLE,ROT_STAT,NMISC,1
ETABLE,ROTATE,NMISC,9
ESEL,ALL
PRETAB,ROTATE,ROT_STAT
SET,4
PRNSOL,DOF
*GET,DX2,NODE,4,U,X
*GET,DY2,NODE,4,U,Y
*GET,RZ2,NODE,4,ROT,Z
ESEL,S,TYPE,,1
ETABLE,REFL
ESEL,ALL
PRETAB,ROTATE,ROT_STAT
FINISH
PARSAV
/COPY,,rdb,,rest,rdb          ! SAVE THE FILES NEEDED FOR RESTART (NOT NEEDED
/COPY,,ldhi,,rest,ldhi        ! FOR STRAIGHT-THRU RUN)
/COPY,,r001,,rest,r001
/COPY,,rst,,rest,rst          ! NEEDED FOR CONTINUITY OF THE RESULTS FILE
/CLEAR,NOSTART                ! CLEAR THE DATABASE (TO SIMULATE RESTART)
/COM,    
/COM,        -----     RESTART  ANALYSIS    -----
/COPY,rest,rdb,,file,rdb      ! COPY THE FILES NEEDED FOR RESTART (NOT NEEDED
/COPY,rest,ldhi,,file,ldhi    ! FOR STRAIGHT-THRU RUN)
/COPY,rest,r001,,file,r001
/COPY,rest,rst,,file,rst
/SOLU    
ANTYPE,,REST         ! USE RESTART ANALYSIS TO DEFINE MORE LOADSTEPS
F,1,MZ,0             ! REMOVE M1, ALLOW ROTZ MOTION TO STABILIZE
TIME,2.05
SOLVE
NSUBST,60
FJ,5,MX,-0.5         ! DELTA THETA-X = 5 DEG., THEN STOP ( AT 3 SEC )
TIME,3
SOLVE
FINISH
PARRES               ! RESTORE PARAMETERS FROM INITIAL RUN
/POST1
SET,LAST
PRNSOL,DOF
*GET,DX3,NODE,4,U,X
*GET,DY3,NODE,4,U,Y
*GET,DZ3,NODE,4,U,Z
*GET,RX3,NODE,4,ROT,X
*GET,RZ3,NODE,4,ROT,Z
ESEL,S,TYPE,,1
ETABLE,ROT_STAT,NMISC,1
ETABLE,ROTATE,NMISC,9
ESEL,ALL
PRETAB,ROTATE,ROT_STAT
*status,parm
*DIM,LABEL,CHAR,11,2
*DIM,VALUE,,11,3
LABEL(1,1) = 'DX T1 ','DY T1 ','AZ T1 ','DX T2 ','DY T2 '
LABEL(6,1) = 'AZ T2 ','DX T3 ','DY T3 ','DZ T3 ','AX T3 ','AZ T3 '
LABEL(1,2) = 'in','in','rad','in','in','rad','in','in','in'
LABEL(10,2) = 'RAD','RAD'
*VFILL,VALUE(1,1),DATA,-.5858,1.4142,.7854,-2,2,1.5708,-2,1.9962,.08716
*VFILL,VALUE(10,1),DATA,.08727,1.5708
*VFILL,VALUE(1,2),DATA,DX1,DY1,RZ1,DX2,DY2,RZ2,DX3,DY3,DZ3
*VFILL,VALUE(10,2),DATA,RX3,RZ3
V1 = ABS(DX1/.5858)
V2 = (DY1/1.4142) 
V3 = (RZ1/.7854) 
V4 = ABS(DX2/2) 
V5 = (DY2/2) 
V6 = (RZ2/1.5708) 
V7 = ABS(DX3/2)
V8 = (DY3/1.9962)
*VFILL,VALUE(1,3),DATA,V1,V2,V3,V4,V5,V6,V7,V8
*VFILL,VALUE(9,3),DATA,(DZ3/.08716),(RX3/.08727),(RZ3/1.5708)
/COM
/OUT,vm179,vrt
/COM,------------------- VM179 RESULTS COMPARISON -------------
/COM,
/COM,                 |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.5,'  ',F14.5,'   ',1F15.3)
/COM,----------------------------------------------------------
/OUT

FINISH
*LIST,vm179,vrt

/COM,ANSYS MEDIA REL. 150 (11/8/2013) REF. VERIF. MANUAL: REL. 150
/VERIFY,VM96
JPGPRF,500,100,1             ! MACRO TO SET PREFS FOR JPEG PLOTS
/SHOW,JPEG
/PREP7
smrt,off
MOPT,VMESH,MAIN
/TITLE, VM96, TEMPERATURE DISTRIBUTION IN A SHORT SOLID CYLINDER
C***      CONDUCTION HEAT TRANSFER, SCHNEIDER, 2ND. PRINTING, PAGE 134, FIG. 6-7
ANTYPE,STATIC
ET,1,SOLID87
MP,KXX,1,1
CSYS,1                ! CYLINDRICAL COORDINATE SYSTEM
K,1
K,2,.5,-22.5
K,3,.5,22.5
KGEN,2,ALL,,,,,.5
L,1,4
LESIZE,ALL,,,4        ! SET LINE SEGMENT DIVISIONS TO FOUR
V,1,2,3,1,4,5,6,4     ! DEFINE VOLUME
ESIZE,,6
MSHK,0
MSHA,1
VMESH,1
NSEL,S,LOC,Z
NSEL,A,LOC,X,.5       ! SELECT BOTTOM AND WALL NODES
D,ALL,TEMP
NSEL,S,LOC,Z,.5       ! SELECT NODES AT TOP OF CYLINDER
D,ALL,TEMP,40
NSEL,ALL
FINISH
/SOLU    
SOLVE
FINISH
/POST1
/VIEW,1,.2,-1,.4
/ANGLE,1,-29,ZS
/CTYPE,1 
/EDGE,1,1
/COM                  *** THE FOLLOWING ANNOTATION COMMANDS ARE ***
/COM                  *** TYPICALLY GENERATED INTERACTIVELY *** 
/ANUM,1,1,-.78943,-.82532        ! ANNOTATION NUMBER, TYPE AND HOT SPOT
/TSPEC,15,1.000,2,0,0            ! TEXT ATTRIBUTES
/TLABEL,-.911,-.826,THERMAL      ! ANNOTATION LOCATION AND TEXT 
/ANUM,2,1,-.72083,-.89575    
/TLABEL,-.913,-.896,ISOSURFACES 
/ANUM,3,4,-.54504,-.60828    
/LSPEC,15,0,1.000                ! LINE ATTRIBUTES
/LINE,-.599,-.667,-.491,-.550    ! ANNOTATION LINE DEFINITION
/ANUM,4,4,-.55957,-.76672    
/LINE,-.520,-.723,-.599,-.811
/ANUM,5,4,-.56409,-.69934    
/LINE,-.541,-.697,-.587,-.703
/ANUM,6,8,-.53174,-.71106    
/LARC,-.532,-.711,.016,315,468   ! ANNOTATION ARC DEFINITION
/ANUM,7,8,-.59338,-.68469    
/LARC,-.593,-.685,.016,111,281
/ANUM,8,11,-.49353,-.55261    
/LSYMBOL,-.494,-.553,48,1,1.000  ! ANNOTATION SYMBOL DEFINITION - ARROW
PLNSOL,TEMP           ! PLOT NODAL TEMPERATURES AS ISOSURFACES
CSYS,1
NSEL,S,LOC,X          ! SELECT CENTERLINE NODES
NSEL,U,LOC,Z,.0625    ! UNSELECT MIDSIDE NODES
NSEL,U,LOC,Z,.1875
NSEL,U,LOC,Z,.3125
NSEL,U,LOC,Z,.4375
NSORT,Z               ! SORT RESULTS BASED ON Z COORDINATES
NLIST,ALL
PRNSOL,TEMP           ! PRINT NODAL TEMPERATURES FOR CENTERLINE NODES (R=0)
N0 = node(0,0,0.000)
N1 = node(0,0,0.125)
N2 = node(0,0,0.250)
N3 = node(0,0,0.375)
N4 = node(0,0,0.500)
*GET,TA,NODE,N0,TEMP
*GET,TB,NODE,N1,TEMP
*GET,TC,NODE,N2,TEMP
*GET,TD,NODE,N3,TEMP
*GET,TE,NODE,N4,TEMP
*DIM,LABEL,CHAR,5,2
*DIM,VALUE,,5,3
LABEL(1,1) = 'Z=0.0 ft','Z=.125ft','Z=.25 ft','Z=.375ft','Z=0.5 ft'
LABEL(1,2) = ' T, F   ',' T, F   ',' T, F   ',' T, F   ',' T, F   '
*VFILL,VALUE(1,1),DATA,0,6.8,15.6,26.8,40.0
*VFILL,VALUE(1,2),DATA,TA,TB,TC,TD,TE
*VFILL,VALUE(1,3),DATA,0,ABS(TB/6.8),ABS(TC/15.6),ABS(TD/26.8),ABS(TE/40.0)
/COM
/OUT,vm96,vrt
/COM,------------------- VM96 RESULTS COMPARISON --------------
/COM,
/COM,                 |   TARGET   |   Mechanical APDL   |   RATIO
/COM,
*VWRITE,LABEL(1,1),LABEL(1,2),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(1X,A8,A8,'   ',F10.1,'  ',F14.1,'   ',1F15.3)
/COM,----------------------------------------------------------
/OUT
FINISH
*LIST,vm96,vrt

/COM,ANSYS MEDIA REL. 150 (11/8/2013) REF. VERIF. MANUAL: REL. 150
/VERIFY,VM209
/TITLE,VM209,DOUBLE BELLOWS AIR SPRING
/COM, REFERENCE: BERRY,DALE T. AND YANG,HENRY T.Y.,"FORMULATION AND EXPERIMENTAL
/COM, VERIFICATION OF A PNEUMATIC FINITE ELEMENT" - IJNME, VOL. 39, PG:1097-1114 (1996)
/COM, 
/PREP7
ET,1,208				!AXISYMMETRIC SHELL
MP,EX,1,1000				!YOUNG'S MODULUS (PSI)
MP,NUXY,1,0.49				!POISSON'S RATIO

ET,2,241				!2D HYDROSTATIC FLUID ELEMENT
KEYOPT,2,3,1				!AXISYMMETRIC KEY OPTION
TB,FLUID,2,1,1,GAS
TBDATA,1,4.4256E-5			!DENSITY IN LB/IN^3 FOR AIR

MP,EX,3,400000				!YOUNG'S MODULUS FOR POLYESTER CORDS (PSI)
MP,NUXY,3,0.37				!POISSON'S RATIO FOR POLYESTER CORDS

MP,EX,4,3.046E7				!YOUNG'S MODULUS (PSI) FOR RIGID PLATE
MP,NUXY,4,0.3				!POISSON'S RATIO FOR RIGID PLATE

MP,REFT,2,20
TOFFST,274

SECT,1,SHELL       			!SECTION DEFINITION FOR BELLOW SHELL
SECD,0.1	

RB=2.2                 			!RADIUS OF THE BELLOW (IN)
RP=4                   			!RADIUS OF THE PLATE (IN)
H=9.6/4                			!TOTAL HEIGHT (IN)
HP=0.2                 			!PLATE HEIGHT (IN)
F=1000
PI=3.14159	       			!VALUE OF PI

N,1,0,0    				!PRESSURE NODE LOCATION

K,1,0,0
K,2,RP,0
K,3,RP+RB,0
K,4,RP,RB
K,5,RP,RB+HP
K,6,0,H

LARC,3,4,2,2.2, 
L,4,5
L,5,6

TYPE,1
SECN,1
MAT,1
LESIZE,1,,,10
LMESH,1					!MESH THE BELLOWS

TYPE,1
SECN,1
MAT,4
LESIZE,2,,,1
LMESH,2					!MESH RIGID PLATE
LESIZE,3,,,1
LMESH,3					!MESH RIGID PLATE
ALLSEL,ALL

TYPE,2
REAL,2
R,2,,14.7				!INITIAL AIR PRESSURE (ATMOSPHERIC)=14.7 PSI
MAT,2

LSEL,S,LINE,,1,3
NSLL,S,1
ESURF,1					!GENERATE HYDROSTATIC FLUID ELEMENTS
ALLSEL,ALL

SECTYPE,11,REINF,SMEAR
SECDATA,3,1.96E-3,0.05,,,EDGO,1,0.5	!CROSS-SECTIONAL AREA/PLACEMENT (IN) OF CORDS
LSEL,S,LINE,,1
ESLL,S
ESEL,R,TYPE,,1
SECN,11
EREINF					!REINFORCE BELLOWS WITH POLYESTER CORDS
ALLSEL,ALL

NSEL,S,LOC,Y,H				!SET BOUNDARY CONDITIONS
D,ALL,ALL
NSEL,S,LOC,X,0.
D,ALL,UX,0.
NSEL,S,LOC,Y,0
D,ALL,UY,0
ALLSEL,ALL
FINISH

/SOLU
NLGEOM,ON	
TIME,1
CNVTOL,F,1
CNVTOL,M,1
CNVTOL,DVOL,1
CNVTOL,U,1
CNVTOL,ROT,1
CNVTOL,HDSP,1
OUTRES,ALL,ALL
NSUB,1,100,1
BF,1,TEMP,20				!BRING EVERYTHING TO REFERENCE TEMPERATURE
KBC,1
/OUT,SCRATCH
SOLVE
TIME,2
KBC,0
D,1,HDSP,20				!INCREASE THE PRESSURE TO DESIRED
NSUB,10,100,1
SOLVE
DDEL,1,HDSP
NSEL,S,LOC,Y,H
D,ALL,UY,-1.5				!MOVE THE RIGID PLATE DOWNWARD
ALLSEL,ALL
NSUB,30,30,30
TIME,3
SOLVE
FINISH

/POST26
/OUT,
NSOL,2,1,HDSP,,PRES
NSOL,3,14,U,Y,UY
RFOR,4,13,F,Y,FY13
RFOR,5,14,F,Y,FY14
ADD,6,4,5,,RFOR,,,-1,-1			!SWITCH THE SIGNS OF RFOR RESULTS

/OUT
/RESET
AA=
*DIM,AA,TABLE,7,6
AA(1,0) = 10,9,8,7,6,5			!VALUES FOR DISPLACEMENT
AA(0,1) = 0,990,1231,1692,2230,2769,3384,4230
AA(0,2) = 0,869.659,1226.55,1609.8,2050.35,2581.33,3246.09,4109.82
AA(0,3) = 0, 1850,2640,3350,4050,5000,6000,7333
AA(0,4) = 0,1750.69,2405.88,3089.12,3849.41,4738.9,5825.32,7209.64
AA(0,5) = 0,2667,3875,4650,6000,7200,8750
AA(0,6) = 0,2644.49,3596.3,4578.78,5658.43,6906.07,8413.2
/TITLE,ANALYSIS OF DOUBLE BELLOWS AIR SPRING
/GCOLUMN,1,EXP 20 PSI			!ASSIGNS AA(0,1) AS EXPERIMENTAL DATA AT 20 PSI
/GMARKER,1,1
/GCOLUMN,2,ANS 20 PSI			!ASSIGNS AA(0,2) AS Mechanical APDL DATA AT 20 PSI (GAS)
/GMARKER,2,2
/GCOLUMN,3,EXP 40 PSI			!ASSIGNS AA(0,3) AS EXPERIMENTAL DATA AT 40 PSI
/GMARKER,3,1
/GCOLUMN,4,ANS 40 PSI			!ASSIGNS AA(0,4) AS Mechanical APDL DATA AT 40 PSI (GAS)
/GMARKER,4,2
/GCOLUMN,5,EXP 60 PSI			!ASSIGNS AA(0,5) AS EXPERIMENTAL DATA AT 60 PSI
/GMARKER,5,1
/GCOLUMN,6,ANS 60 PSI			!ASSIGNS AA(0,6) AS Mechanical APDL DATA AT 60 PSI (GAS)
/GMARKER,6,2
/XRANGE,5,10
/YRANGE,0,10000
/GROPT,DIVX,5				!5 DIVISIONS ALONG THE X AXIS
/GROPT,REVX,1				!PLOTS X AXIS VALUES IN REVERSE ORDER
/AXLAB,X,HEIGHT BETWEEN END PLATES (IN)	!X AXIS LABEL
/AXLAB,Y,COMPRESSIVE LOAD (LBS)		!Y AXIS LABEL
*VPLOT,AA(1,0),AA(1),2,3,4,5,6		!COMPARE THIS PLOT WITH FIGURE 8 IN REFERENCE

*DIM,LABEL2,CHAR,6,6
*DIM,VALUE2,,6,6
LABEL2(1,1) ='0.25','0.50','0.75','1.00','1.25','1.50'
*VFILL,VALUE2(1,1),DATA,1231,1692,2230,2769,3384,4230
*VFILL,VALUE2(1,2),DATA,1226.55,1609.8,2050.35,2581.33,3246.09,4109.82
*VFILL,VALUE2(1,3),DATA,ABS(1226.55/1231),ABS(1609.8/1692),ABS(2050.35/2230),ABS(2581.33/2769),ABS(3246.09/3384),ABS(4109.82/4230)

*DIM,LABEL,CHAR,6,6
*DIM,VALUE,,6,6
LABEL(1,1) ='0.25','0.50','0.75','1.00','1.25','1.50'
*VFILL,VALUE(1,1),DATA,2640,3350,4050,5000,6000,7333
*VFILL,VALUE(1,2),DATA,2405.88,3089.12,3849.41,4738.9,5825.32,7209.64
*VFILL,VALUE(1,3),DATA,ABS(2405.88/2640),ABS(3089.12/3350),ABS(3849.41/4050),ABS(4738.9/5000),ABS(5825.32/6000),ABS(7209.64/7333)

*DIM,LABEL3,CHAR,5,5
*DIM,VALUE3,,5,5
LABEL3(1,1) ='0.25','0.50','0.75','1.00','1.25'
*VFILL,VALUE3(1,1),DATA,3875,4650,6000,7200,8750
*VFILL,VALUE3(1,2),DATA,3596.3,4578.78,5658.43,6906.07,8413.20
*VFILL,VALUE3(1,3),DATA,ABS(3596.3/3875),ABS(4578.78/4650),ABS(5658.43/6000),ABS(6906.07/7200),ABS(8413.20/8750)
FINISH
SAVE,TABLE_1
/CLEAR,NOSTART
C***  PERFORM ANALYSIS USING PVDATA
/PREP7  
ET,1,208				!AXISYMMETRIC SHELL 
MP,EX,1,1000				!YOUNG'S MODULUS (PSI)  
MP,NUXY,1,0.49				!POISSON'S RATIO  
      
ET,2,241				!2D HYDROSTATIC FLUID ELEMENT   
KEYOPT,2,3,1				!AXISYMMETRIC KEY OPTION

TB,FLUID,2,1,7,PVDATA			
TBTEMP,20
TBPT,,34.7,238.931			!PV POINTS FOR 20 PSI
TBPT,,44.7,185.4788747
TBPT,,54.7,151.5704881
TBPT,,74.7,110.9893668
TBPT,,94.7,87.54916262
TBPT,,414.7,19.99253846
TBPT,,1014.7,8.170795013

TB,FLUID,5,1,12,PVDATA
TBTEMP,20
TBPT,,34.7,377.9675418			!PV POINTS FOR 40 PSI
TBPT,,44.7,293.4110447
TBPT,,54.7,239.771
TBPT,,74.7,175.5752838
TBPT,,94.7,138.4949704
TBPT,,114.7,114.345891
TBPT,,314.7,41.67611598
TBPT,,414.7,31.62641355
TBPT,,514.7,25.48178298
TBPT,,714.7,18.35101959
TBPT,,914.7,14.3385522
TBPT,,1014.7,12.9254693 

TB,FLUID,6,1,25,PVDATA
TBTEMP,20
TBPT,,34.7,517.9960634			!PV POINTS FOR 60 PSI
TBPT,,44.7,402.7732752
TBPT,,54.7,328.6007934
TBPT,,74.7,240.622
TBPT,,84.7,212.2132633
TBPT,,94.7,189.8042598
TBPT,,104.7,171.6758682
TBPT,,164.7,109.1345683
TBPT,,214.7,83.71897252
TBPT,,264.7,67.9050374
TBPT,,314.7,57.11618494
TBPT,,364.7,49.28561393
TBPT,,414.7,43.3432925
TBPT,,464.7,38.67971465
TBPT,,514.7,34.92221372
TBPT,,564.7,31.8301105
TBPT,,614.7,29.24103367
TBPT,,664.7,27.04146743
TBPT,,714.7,25.14966196
TBPT,,764.7,23.50524833
TBPT,,814.7,22.06267755
TBPT,,864.7,20.78693852
TBPT,,914.7,19.65066514
TBPT,,964.7,18.63217933
TBPT,,1014.7,17.71406662

MP,EX,3,400000				!YOUNG'S MODULUS FOR POLYESTER CORDS (PSI)
MP,NUXY,3,0.37				!POISSON'S RATIO FOR POLYESTER CORDS  
      
MP,EX,4,3.046E7				!YOUNG'S MODULUS (PSI) FOR RIGID PLATE   
MP,NUXY,4,0.3				!POISSON'S RATIO FOR RIGID PLATE   
      
MP,REFT,2,20
TOFFST,274  
      
SECT,1,SHELL       			!SECTION DEFINITION FOR BELLOW SHELL  
SECD,0.1	   
      
RB=2.2                 			!RADIUS OF THE BELLOW (IN)
RP=4                   			!RADIUS OF THE PLATE (IN) 
H=9.6/4                			!TOTAL HEIGHT (IN)
HP=0.2                 			!PLATE HEIGHT (IN)
F=1000  
PI=3.14159	       			!VALUE OF PI   
      
N,1,0,0    				!PRESSURE NODE LOCATION  
      
K,1,0,0 
K,2,RP,0
K,3,RP+RB,0 
K,4,RP,RB   
K,5,RP,RB+HP
K,6,0,H 
      
LARC,3,4,2,2.2, 
L,4,5   
L,5,6   
      
TYPE,1  
SECN,1  
MAT,1   
LESIZE,1,,,10   
LMESH,1					!MESH THE BELLOWS   
     
TYPE,1  
SECN,1  
MAT,4   
LESIZE,2,,,1
LMESH,2					!MESH RIGID PLATE   
LESIZE,3,,,1
LMESH,3					!MESH RIGID PLATE   
ALLSEL,ALL  
      
TYPE,2  
REAL,2  
R,2,,14.7				!INITIAL AIR PRESSURE (ATMOSPHERIC)=14.7 PSI   
MAT,2   				!CHANGE MATERIAL TO USE DESIRED PRESSURE AND PV POINTS
      
LSEL,S,LINE,,1,3
NSLL,S,1
ESURF,1					!GENERATE HYDROSTATIC FLUID ELEMENTS
ALLSEL,ALL  
      
SECTYPE,11,REINF,SMEAR  
SECDATA,3,1.96E-3,0.05,,,EDGO,1,0.5	!CROSS-SECTIONAL AREA/PLACEMENT (IN) OF CORDS   
LSEL,S,LINE,,1  
ESLL,S  
ESEL,R,TYPE,,1  
SECN,11 
EREINF					!REINFORCE BELLOWS WITH POLYESTER CORDS  
ALLSEL,ALL  
      
NSEL,S,LOC,Y,H				!SET BOUNDARY CONDITIONS  
D,ALL,ALL   
NSEL,S,LOC,X,0. 
D,ALL,UX,0. 
NSEL,S,LOC,Y,0  
D,ALL,UY,0  
ALLSEL,ALL  
FINISH  
    
/SOLU   
NLGEOM,ON	  
TIME,1  
CNVTOL,F,1  
CNVTOL,DVOL,1   
CNVTOL,U,1  
CNVTOL,HDSP,1   
OUTRES,ALL,ALL  
NSUB,1,100,1
BF,1,TEMP,20				!BRING EVERYTHING TO REFERENCE TEMPERATURE  
KBC,1   
/OUT,SCRATCH
SOLVE   
TIME,2  
KBC,0   
D,1,HDSP,20				!INCREASE THE PRESSURE TO DESIRED PSI 
NSUB,10,100,1   
SOLVE  
DDEL,1,HDSP 
NSEL,S,LOC,Y,H  
D,ALL,UY,-1.5				!MOVE THE RIGID PLATE DOWNWARD 
ALLSEL,ALL  
NSUB,30,30,30   
TIME,3  
SOLVE   
FINISH  
     
/POST26 
/OUT,
NSOL,2,1,HDSP,,PRES 
NSOL,3,14,U,Y,UY
RFOR,4,13,F,Y,FY13  
RFOR,5,14,F,Y,FY14  
ADD,6,4,5,,RFOR,,,-1,-1			!SWITCH THE SIGNS OF RFOR RESULTS 
    
/OUT
/RESET
AA=
*DIM,AA,TABLE,7,6
AA(1,0) = 10,9,8,7,6,5			!VALUES FOR DISPLACEMENT
AA(0,1) = 0,990,1231,1692,2230,2769,3384,4230
AA(0,2) = 0,869.659,1249.79,1650.75,2077.76,2613.12,3263.09,4238.18
AA(0,3) = 0, 1850,2640,3350,4050,5000,6000,7333
AA(0,4) = 0,1750.69,2455.05,3185.58,3950.18,4742.42,5923.76,7251.99
AA(0,5) = 0,2667,3875,4650,6000,7200,8750
AA(0,6) = 0,2644.49,3613.49,4580.66,5674.34,6922.04,8780.85
/TITLE,ANALYSIS OF DOUBLE BELLOWS AIR SPRING
/GCOLUMN,1,EXP 20 PSI			!ASSIGNS AA(0,1) AS EXPERIMENTAL DATA AT 20 PSI
/GMARKER,1,1
/GCOLUMN,2,ANS 20 PSI			!ASSIGNS AA(0,2) AS Mechanical APDL DATA AT 20 PSI (PVDATA)
/GMARKER,2,2
/GCOLUMN,3,EXP 40 PSI			!ASSIGNS AA(0,3) AS EXPERIMENTAL DATA AT 40 PSI
/GMARKER,3,1
/GCOLUMN,4,ANS 40 PSI			!ASSIGNS AA(0,4) AS Mechanical APDL DATA AT 40 PSI (PVDATA)
/GMARKER,4,2
/GCOLUMN,5,EXP 60 PSI			!ASSIGNS AA(0,5) AS EXPERIMENTAL DATA AT 60 PSI
/GMARKER,5,1
/GCOLUMN,6,ANS 60 PSI			!ASSIGNS AA(0,6) AS Mechanical APDL DATA AT 60 PSI (PVDATA)
/GMARKER,6,2 
/XRANGE,5,10
/YRANGE,0,10000
/GROPT,DIVX,5				!5 DIVISIONS ALONG THE X AXIS
/GROPT,REVX,1				!PLOTS X AXIS VALUES IN REVERSE ORDER
/AXLAB,X,HEIGHT BETWEEN END PLATES (IN)	!X AXIS LABEL
/AXLAB,Y,COMPRESSIVE LOAD (LBS)		!Y AXIS LABEL
*VPLOT,AA(1,0),AA(1),2,3,4,5,6		!COMPARE THIS PLOT WITH FIGURE 8 IN REFERENCE

*DIM,LABEL2,CHAR,6,6
*DIM,VALUE2,,6,6
LABEL2(1,1) ='0.25','0.50','0.75','1.00','1.25','1.50'  
*VFILL,VALUE2(1,1),DATA,1231,1692,2230,2769,3384,4230   
*VFILL,VALUE2(1,2),DATA,1249.79,1650.75,2077.76,2613.12,3263.09,4238.18 
*VFILL,VALUE2(1,3),DATA,ABS(1249.79/1231),ABS(1650.75/1692),ABS(2077.76/2230),ABS(2613.12/2769),ABS(3263.09/3384),ABS(4238.18 /4230)  
     
*DIM,LABEL,CHAR,6,6 
*DIM,VALUE,,6,6 
LABEL(1,1) ='0.25','0.50','0.75','1.00','1.25','1.50'   
*VFILL,VALUE(1,1),DATA,2640,3350,4050,5000,6000,7333
*VFILL,VALUE(1,2),DATA,2455.05,3185.58,3950.18,4742.42,5923.76,7251.99
*VFILL,VALUE(1,3),DATA,ABS(2455.05/2640),ABS(3185.58/3350),ABS(3950.18/4050),ABS(4742.42/5000),ABS(5923.76/6000),ABS(7251.99/7333)   
    
*DIM,LABEL3,CHAR,5,5
*DIM,VALUE3,,5,5
LABEL3(1,1) ='0.25','0.50','0.75','1.00','1.25' 
*VFILL,VALUE3(1,1),DATA,3875,4650,6000,7200,8750 
*VFILL,VALUE3(1,2),DATA,3613.49,4580.66,5674.34,6922.04,8780.85 
*VFILL,VALUE3(1,3),DATA,ABS(3613.49/3875),ABS(4580.66/4650),ABS(5674.34/6000),ABS(6922.04/7200),ABS(8780.85/8750) 
FINISH  
SAVE,TABLE_2
RESUME, TABLE_1
/COM
/OUT,vm209,vrt
/COM,----------------- VM209 RESULTS COMPARISON -------------------
/COM,
/COM,
/COM,20 PSI         |  TARGET   |   Mechanical APDL   |   RATIO
/COM,
*VWRITE,LABEL2(1,1),VALUE2(1,1),VALUE2(1,2),VALUE2(1,3)
(6X,A8,' ',F10.4,'  ',F14.4,'   ',F15.3)
/COM,
/COM,
/COM,40 PSI         |  TARGET   |   Mechanical APDL   |   RATIO
/COM,
*VWRITE,LABEL(1,1),VALUE(1,1),VALUE(1,2),VALUE(1,3)
(6X,A8,' ',F10.4,'  ',F14.4,'   ',F15.3)
/COM,
/COM,
/COM,60 PSI         |  TARGET   |   Mechanical APDL   |   RATIO
/COM,
*VWRITE,LABEL3(1,1),VALUE3(1,1),VALUE3(1,2),VALUE3(1,3)
(6X,A8,' ',F10.4,'  ',F14.4,'   ',F15.3)
/NOPR
RESUME,TABLE_2
/GOPR/COM,   
/COM,20 PSI (PVDATA)|  TARGET   |   Mechanical APDL   |   RATIO 
/COM,   
*VWRITE,LABEL2(1,1),VALUE2(1,1),VALUE2(1,2),VALUE2(1,3) 
(6X,A8,' ',F10.4,'  ',F14.4,'   ',F15.3)   
/COM,   
/COM,   
/COM,40 PSI (PVDATA)|  TARGET   |   Mechanical APDL   |   RATIO 
/COM,   
*VWRITE,LABEL(1,1),VALUE(1,1),VALUE(1,2),VALUE(1,3) 
(6X,A8,' ',F10.4,'  ',F14.4,'   ',F15.3)  
/COM,   
/COM,   
/COM,60 PSI (PVDATA)|  TARGET   |   Mechanical APDL   |   RATIO 
/COM,   
*VWRITE,LABEL3(1,1),VALUE3(1,1),VALUE3(1,2),VALUE3(1,3) 
(6X,A8,' ',F10.4,'  ',F14.4,'   ',F15.3) 
/COM,----------------------------------------------------------------
/OUT
FINISH
*LIST,vm209,vrt
/DELETE,TABLE_1
/DELETE,TABLE_2


